         MACRO                                                          MADC0130
&NAME   $DETAPCE  &ID,&DOC=NO                                           MADC0140
         GBLC  &SRVRTN                                                  MADC0150
         LCLC  &R,&T,&NAME1                                             MADC0160
         LCLA  &N                                                       MADC0170
         AIF   ('&DOC' NE 'YES').NODOC                                  MADC0180
         SPACE 1                                                        MADC0190
*        CORRECT FORM IS :-                                             MADC0200
*        $DETAPCE <'ID' | ID | LABEL | (REG)> NAME OF APCE REQUIRED     MADC0210
*                                  THE NAME OF THE APCE TO DETACH MAY   MADC0220
*                                  BE A CHARACTER STRING IN QUOTES, A   MADC0230
*                                  NAME OF A FIELD CONTAINING THE NAME, MADC0240
*                                  A CHARACTER STRING WITHOUT QUOTES,   MADC0250
*                                  OR REGISTER NOTATION, IN WHICH CASE  MADC0260
*                                  THE REGISTER CONTAINS THE ADDRESS    MADC0270
*                                  OF THE APCE TO BE DETACHED.          MADC0280
*                                  OPTIONALLY, THE NAME MAY BE 'ALL',   MADC0290
*                                  IN WHICH CASE ALL SECONDARY APCE'S   MADC0300
*                                  ARE DETACHED (STILL CHAINED TOGETHER MADC0310
*              <DOC=NO | YES>      REQUEST FOR DOCUMENTATION            MADC0320
         SPACE 1                                                        MADC0330
*        THE PURPOSE OF THIS MACRO IS TO DETACH THE SECONDARY APCE      MADC0340
*         ( CHAINED OFF TTHIS PRIMARY APCE ) WITH THE SPECIFIED NAME.   MADC0350
*         THE MACRO INVOKES THE SRVICE ROUTINE 'DETAPCE' TO LOCATE THE  MADC0360
*         SPECIFIED APCE, DECHAIN IT FROM THE CURRENT PRIMARY, AND      MADC0370
*         RETURN THE ADDRESS OF THE DETACHED SECONDARY APCE IN REG1.    MADC0380
*         IF 'ALL' IS SPECIFIED, ALL SECONDARY APCE'S CHAINED TO THE    MADC0390
*         CURRENT PRIMARY APCE ARE DETACHED, BUT ARE LEFT CHAINED       MADC0400
*         TOGETHER, AND THE ADDRESS OF THE FIRST APCE IN THE CHAIN IS   MADC0410
*         RETURNED IN REG1.                                             MADC0420
         SPACE 1                                                        MADC0430
*        A SECONDARY APCE MAY BE DETACHED PRIOR TO ISSUING A $CBAL      MADC0440
*         TO PASS CONTROL TO ANOTHER MODIFICATION ROUTINE.  IF THE ADDR MADC0450
*         OF THE DETACHED APCE IS PLACED IN A REGISTER PRIOR TO ISSUING MADC0460
*         THE $CBAL, THEN THE INVOKED ROUTINE MAY USE THE $ATTAPCE MACR MADC0470
*         TO ATTACH THE APCE AS ITS OWN SECONDARY.  THIS SUPPLIES AN    MADC0480
*         ELEGANT MECHANISM FOR PASSING INFORMATION FROM ONE ROUTINE    MADC0490
*         TO A SUB-ORDINATE ROUTINE.                                    MADC0500
         SPACE 1                                                        MADC0510
.NODOC   ANOP                                                           MADC0520
&NAME1   SETC  '&NAME'                                                  MADC0530
         AIF   ('&ID' NE '').A01                                        MADC0540
         MNOTE 8,'NO APCE NAME SPECIFIED'                               MADC0550
         MEXIT                                                          MADC0560
.A01     ANOP                                                           MADC0570
&N       SETA  N'&ID                                                    MADC0580
         AIF   (&N EQ 1).NOK                                            MADC0590
         MNOTE 8,'INCORRECT PARAMETER LIST: TOO MANY OPERANDS'          MADC0600
         MEXIT                                                          MADC0610
.NOK     AIF   ('&ID'(1,1) NE '(').NOTREG                               MADC0620
&R       SETC  '&ID(1)'                                                 MADC0630
&NAME1   LCR   R1,&R               COMPLEMENT APCE ADDRESS IN R1        MADC0640
&NAME1   SETC  ' '                                                      MADC0650
         AGO   .SERV                                                    MADC0660
.NOTREG  ANOP                                                           MADC0670
&T       SETC  T'&ID                                                    MADC0680
         AIF   ('&T' EQ 'U').UNDEF                                      MADC0690
         AIF   ('&T' EQ 'N').SELFDEF                                    MADC0700
         AIF   ('&T' EQ 'C').CHAR                                       MADC0710
         MNOTE 8,'PARAMETER HAS TYPE ATTRIBUTE OF &T - INVALID'         MADC0720
         MEXIT                                                          MADC0730
.CHAR    ANOP                                                           MADC0740
&NAME    LA    R1,&ID              POINT R1 AT PARAMETER LIST           MADC0750
&NAME1   SETC  ' '                                                      MADC0760
         AGO   .SERV                                                    MADC0770
.SELFDEF ANOP                                                           MADC0780
&NAME1   BAL   R1,*+8              POINT R1 AT PARAMETER LIST           MADC0790
         DC    CL4&ID              NAME OF APCE TO BE DETACHED          MADC0800
&NAME1   SETC  ' '                                                      MADC0810
         AGO   .SERV                                                    MADC0820
.UNDEF   ANOP                                                           MADC0830
&NAME1   BAL   R1,*+8              POINT R1 AT NAME OF APCE TO DETACH   MADC0840
         DC    CL4'&ID'            NAME OF APCE TO BE DETACHED          MADC0850
.SERV    ANOP                                                           MADC0860
         AIF   ('&SRVRTN' EQ 'YES').SRVYES                              MADC0870
      $SRVSAVE SAVE=LINK           PRESERVE LINK REGISTER               MADC0880
         AGO   .ASKALL                                                  MADC0890
.SRVYES $SRVSAVE SAVE=(LINK,BASE3) PRESERVE BASE3 AND LINK ADDRESS      MADC0900
.ASKALL  ANOP                                                           MADC0910
         AIF   ('&ID' EQ 'ALL').TSTALL                                  MADC0920
         AIF   ('&ID' EQ '''ALL''').TSTALL                              MADC0930
         AGO   .CONT                                                    MADC0940
.TSTALL  TM    APCEFLG1,APF1SEC    HAS THIS APCE ANY SECONDARIES        MADC0950
         BZ    *+4+4+4+4+2         BYPASS DETACHING THEM IF NOT THERE   MADC0960
.CONT    ANOP                                                           MADC0970
         L     R9,HCT$AHCT         PICK UP AHCT ADDRESS                 MADC0980
         LA    R9,(DETAPCE*MRCBSIZE)+MRCBSTRT(,R9) GET ROUTINE MRCB     MADC0990
         L     BASE3,MRCBRADR-MRCBDSCT(,R) GET ROUTINE ENTRY POINT      MADC1000
         BALR  LINK,BASE3          PERFORM SERVICE ROUTINE 'DETAPCE'    MADC1010
      $SRVREST ,                   RESTORE PRESERVED REGISTERS          MADC1020
         MEND                                                           MADC1030
