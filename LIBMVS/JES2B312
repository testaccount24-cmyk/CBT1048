JES2B312 TITLE '$P COMMAND PROCESSOR - CONTROL BLOCK GENERATION MACRO DCB3120130
               EFINITIONS'                                              B3120140
*                                                                       B3120150
**** $HASPCB ****                  GENERATE HASP CONTROL BLOCKS         B3120160
*                                                                       B3120170
         MACRO                                                          B3120180
         $HASPCB &DOC=NO,&LIST=NO                                       B3120190
         GBLC  &PRINT,&GEN,&DATA                                        B3120200
         PUSH  PRINT                                                    B3120210
         PRINT &PRINT                                                   B3120220
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT              B3120230
         $CBPR DOC=&DOC            GENERATE MODS CBPR DSECT             B3120240
         $AHCT DOC=&DOC            GENERATE MODS AHCT DSECT             B3120250
         $DCT  DOC=&DOC            GENERATE HASP DCT DSECT              B3120260
         $ADCT DOC=&DOC            GENERATE MODS ADCT DSECT             B3120270
         $APCE DOC=&DOC            GENERATE MODS APCE DSECT             B3120280
         $APCE DOC=NO,TYPE=SPCE    GENERATE MODS SECONDARY APCE DSECT   B3120290
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT              B3120300
         $COMWORK DOC=&DOC         GENERATE HASP COMWORK DSECT          B3120310
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT           B3120320
         $CMB  DOC=&DOC            GENERATE HASP CMB DSECT              B3120330
         $IOT  DOC=&DOC            GENERATE HASP IOT DSECT              B3120340
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT              B3120350
         $JOE  DOC=&DOC            GENERATE HASP JOE DSECT              B3120360
         $JQE  DOC=&DOC            GENERATE HASP JQE DSECT              B3120370
         $TAB  DOC=&DOC            GENERATE HASP TAB DSECT              B3120380
         $UCM  LIST=&LIST          GENERATE OS UCM DSECT                B3120390
         SPACE 1                                                        B3120400
         POP   PRINT                                                    B3120410
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS           B3120420
         MEND                                                           B3120430
         TITLE 'JES2B312 - AUXILIARY COMMAND PROCESSOR'                 B3120440
JES2B312 CSECT                     AUXILIARY COMMAND PROCESSOR          B3120450
         SPACE 2                                                        B3120460
*********************************************************************** B3120470
*                                                                     * B3120480
*        PROGRAM ID. :             JES2B312                           * B3120490
*                                                                     * B3120500
*        DATE WRITTEN :            APRIL 1977                         * B3120510
*                                                                     * B3120520
*        AUTHOR :                  D.H.CARTWRIGHT.                    * B3120530
*                                                                     * B3120540
*        LOCATION :                MONSANTO MISD/TSG BRUSSELS         * B3120550
*                                                                     * B3120560
*********************************************************************** B3120570
         EJECT                                                          B3120580
*********************************************************************** B3120590
*                                                                     * B3120600
*        PURPOSE                                                      * B3120610
*       ---------                                                     * B3120620
*                                                                     * B3120630
*        THIS IS A MONSANTO JES2 MODIFICATION MODULE.                 * B3120640
*        IT IT LOADED AT JES2 INITIALISATION UPON REQUEST VIA         * B3120650
*        JES2B001,THE MONSANTO INITIALISATION MODULE,AND IS           * B3120660
*        ENTERED VIA A $CBAL IN HASPCOMM AT K2696550,AT THE START OF  * B3120670
*        HASPCJB2 CPJ (PURGE JOB) ROUTINE. THE ROUTINE TESTS FOR      * B3120680
*        A REMOTE SUBMITTOR AND WHETHER THE REQUESTOR TERMINAL ADCT   * B3120690
*        HAS 'ALL COMMAND' AUTHORITY.IF NOT,THE CONDITION CODE IS     * B3120700
*        SET TO RETURN CONTROL TO JES2 AT THE CHJ (HOLD JOB)          * B3120710
*        STAGE OF HASPCOMM TO MERELY HOLD THE JOB'S OUTPUT.           * B3120720
*                                                                     * B3120730
*        COMMAND SUB-PROCESSOR CONVENTIONS                            * B3120740
*                                                                     * B3120750
* CONTENTS OF REGISTERS UPON ENTRY TO EACH COMMAND SUB-PROCESSOR      * B3120760
*                                                                     * B3120770
*        R0    = UNPREDICTABLE                                        * B3120780
*        R1    = JQE ADDRESS                                          * B3120790
*        R2    = UNPREDICTABLE                                        * B3120800
*        R3    = UNPREDICTABLE                                        * B3120810
*        R4    = UNPREDICTABLE                                        * B3120820
*        R5    = FIRST OPERAND POINTER ( SET FOR BXLE R3,R4,... )     * B3120830
*        R6    = AHCTDSCT                                             * B3120840
*        R7    = APCE OF PROCESSOR                                    * B3120850
*        BASE3 = HASPCJB2 BASE - USED FOR $MWTO ADDRESSABILITY        * B3120860
*        R10   = UNPREDICTABLE                                        * B3120870
*        BASE1 = HCTDSECT                                             * B3120880
*        BASE2 = BASE REGISTER OF PROCESSOR                           * B3120890
*        SAVE  = PCE OF COMMAND PROCESSOR                             * B3120900
*        LINK  = RETURN ADDRESS TO THIS MODULE                        * B3120910
*        R15   = UNPREDICTABLE                                        * B3120920
*                                                                     * B3120930
*********************************************************************** B3120940
         EJECT                                                          B3120950
*********************************************************************** B3120960
*                                                                     * B3120970
* EXAMPLE EDIT OF COMMAND TEXT BEFORE ENTRY TO SUB-COMMAND PROCESSOR  * B3120980
*                                                                     * B3120990
* ORIGIONAL IN $WTO BUFFER  ( S OVER CHARACTER = LOWER CASE )         * B3121000
*                                                                     * B3121010
*         S  S S S    S S S    S    S    S                            * B3121020
*        $V  A B C, ' A B,C '' D ', D '' E                            * B3121030
*                                                                     * B3121040
* FORM PASSED TO COMMAND SUB-PROCESSOR                                * B3121050
*                                                                     * B3121060
*        $VABC,' A B,C ' D ',D'E                                      * B3121070
*          1   2             3   4                                    * B3121080
*                                                                     * B3121090
*        $ IS LOCATED AT COMMAND                                      * B3121100
*        V IS SINGLE CHARACTER FORM OF VERB                           * B3121110
*                                                                     * B3121120
*        R3 POINTS TO FIRST OF OPERAND POINTER TABLE WHICH, IN THIS   * B3121130
*        EXAMPLE, CONTAINS FOUR WORDS OF ADDRESSES WHICH IN TURN      * B3121140
*        LOCATES THE BEGINNING OF EACH OPERAND ( INCLUDING A NULL     * B3121150
*        END OPERAND ). THESE LOCATIONS ARE DENOTED BY REFERANCE      * B3121160
*        POINTS 1, 2, 3, AND 4 RESPECTIVELY. USING BXLE R3,R4,...     * B3121170
*        INSTRUCTION WILL FALL THROUGH WITH R3 POINTING TO COMNULOP   * B3121180
*                                                                     * B3121190
*********************************************************************** B3121200
         EJECT                                                          B3121210
*********************************************************************** B3121220
*                                                                     * B3121230
*        JOB NUMBER LISTS ARE OF THE FOLLOWING FORM                   * B3121240
*                                                                     * B3121250
*        $V JXXN1-M1,N2-M2,...,NX-MX                                  * B3121260
*        $V    = HASP COMMAND VERB                                    * B3121270
*        JXX   = JOB, JB, JOBS, ETC. ( J IS ONLY ID REQUIRED )        * B3121280
*        NI-MI ( I=1 TO X ) = PAIR OF START STOP VALUES, IE. 1-10     * B3121290
*              INDICATING THAT JOBS 1,2,3,4,5,6,7,8,9,10 ARE          * B3121300
*              DESIRED. ( -M VALUES ARE OPTIONAL )                    * B3121310
*                                                                     * B3121320
* NOTES                                                               * B3121330
*                                                                     * B3121340
*    (1) A RESPONSE IS REQUIRED FOR EACH JOB                          * B3121350
*                                                                     * B3121360
*    (2) COMNULOP IS USED FOR FLAGS DURING PROCESSING AS FOLLOWS      * B3121370
*                                                                     * B3121380
*              X'80' - A JOB WAS FOUND DURING SCAN FOR JOBS           * B3121390
*              X'40' - THE VERB IS C WITH DUMP SPECIFIED              * B3121400
*              X'02' - THE VERB IS P                                  * B3121410
*              X'01' - THE VERB IS C WITH PURGE SPECIFIED             * B3121420
*                                                                     * B3121430
*        THESE DEFINITIONS MUST NOT BE CHANGED WITHOUT CHANGING       * B3121440
*        THE $JCAN MACRO INSTRUCTION.                                 * B3121450
*                                                                     * B3121460
*********************************************************************** B3121470
         SPACE 3                                                        B3121480
         PUSH  PRINT               SAVE PRINT OPTIONS                   B3121490
         PRINT OFF                 DONT BOTHER TO PRINT STANDARD STUFF  B3121500
         COPY  $HASPGEN            HASP OPTIONS                         B3121510
         POP   PRINT               RESTORE PRINT OPTIONS                B3121520
         TITLE 'CONTROL BLOCK OPTIONS'                                  B3121530
         $SYSPARM (OFF,GEN,NODATA,NO,NO)     SET DOCUMENTATION OPTIONS  B3121540
         SPACE 3                                                        B3121550
*                                                                       B3121560
*        GENERATE HASP CONTROL BLOCKS                                   B3121570
*                                                                       B3121580
         SPACE 2                                                        B3121590
         $HASPCB DOC=&DOC,LIST=&LIST GENERATE HASP CONTROL BLOCKS       B3121600
         TITLE '$P COMMAND PROCESSOR - ENTRY POINT PROCESSING'          B3121610
JES2B312 $MODNTRY CSECT=YES,EXIT=EXITB312  MODIFICATION ENTRY           B3121620
         SPACE 2                                                        B3121630
*        COMNULOP FLAGS SET BY HASPCJB2                                 B3121640
         SPACE 1                                                        B3121650
CCJFJOBF EQU   X'80'               JOB FOUND FLAG                       B3121660
CCJFDUMP EQU   X'40'               VERB IS C WITH DUMP FLAG             B3121670
CPJQOPER EQU   X'10'               CANCEL A JOB(S) DATA SETS            B3121680
CLJHOLD  EQU   X'04'               LIST HELD DATA SETS FLAG             B3121690
CCJFSTOP EQU   X'02'               VERB IS P (STOP) FLAG                B3121700
CCJFPURG EQU   X'01'               VERB IS C WITH PURGE FLAG            B3121710
         EJECT                                                          B3121720
         L     R1,APCESR1          PICK UP JQE POINTER                  B3121722
         USING JQEDSECT,R1         ESTABLISH JQE ADDRESSABILITY         B3121730
         MVI   APCE$$X,$$W         SET DEFAULT RETURN OF NO             B3121740
         TM    JQETYPE,$XEQ         IS JOB QUEUED FOR EXECUTION?        B3121750
         BNO   EXITB312            NO,COMMAND ALLOWABLE                 B3121760
         TM    JQEFLAGS,QUEBUSY    IS IT EXECUTING NOW?                 B3121770
         BZ    EXITB312            NO,COMMAND IS ALLOWABLE              B3121780
         DROP  R1                  END JQE ADDRESSABILITY               B3121790
         SPACE 1                                                        B3121800
*        SR    R1,R1               CLEAR REGISTER                       B3121810
*        IC    R1,COMJRMT          GET REMOTE NUMBER                    B3121820
*        BAL   R4,FINDADCT         LOCATE CALLERS ADCT                  B3121830
*        USING ADCTDSCT,R1         ESTABLISH ADCT ADDRESSABILITY        B3121840
*        TM    ADCTFLG1,ADC1ALLC   IS HE ALLOWED TO DO THIS?            B3121850
*        BO    EXITB312            YES,RETURN WITH 'NO' CONDITION       B3121860
         NI    COMNULOP,255-CCJFDUMP-CPJQOPER-CCJFSTOP-CCJFPURG FLAGS   B3121862
         MVI   APCE$$X,$$X         SET 'YES' CONDITION FOR ERROR        B3121870
         SPACE 2                                                        B3121880
EXITB312 $MODEXIT ,                RETURN TO JES2 CODE FOR NEXT COMMAND B3121890
         SPACE 1                                                        B3121900
*        DROP  R1                  END ADCT ADDRESSABILITY              B3121910
         TITLE '$P COMMANDS - ADCT LOCATE SUBROUTINE'                   B3121920
*********************************************************************** B3121930
*        SUB-ROUTINE TO LOCATE THE ADCT FOR A DESIRED REMOTE          * B3121940
*        NUMBER.                                                      * B3121950
*        ON ENTRY                  R1  =  REMOTE NUMBER (LOCAL = ZERO)* B3121960
*        ON EXIT                   R1  =  ADDRESS OF ITS ADCT         * B3121970
*********************************************************************** B3121980
         SPACE 3                                                        B3121990
FINDADCT $SUBIN SAVE=(R2,R3,R4)    ENTRY                                B3122000
         LA    R3,ADCTLGTH+CBPRLGTH SET LENGTH OF EACH ADCT             B3122010
         LR    R2,R1               GET NUMBER OF ADCT REQUIRED          B3122020
         MR    R2,R2               CALCULATE OFFSET IN ADCT LIST        B3122030
         L     R2,AHCTADCT         GET START OF ADCT LIST               B3122040
         LA    R1,0(R2,R3)         GET ADDRESS OF REQUIRED ADCT         B3122050
FINDADCX $SUBOUT ,                  RETURN TO CALLER                    B3122060
         SPACE 3                                                        B3122070
         END                                                            B3122080
