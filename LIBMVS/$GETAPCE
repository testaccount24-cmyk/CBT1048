         MACRO                                                          MADB0130
&NAME   $GETAPCE  &ID,&DOC=NO                                           MADB0140
         GBLC  &SRVRTN                                                  MADB0150
         LCLC  &R,&T,&NAME1                                             MADB0160
         LCLA  &N                                                       MADB0170
         AIF   ('&DOC' NE 'YES').NODOC                                  MADB0180
         SPACE 1                                                        MADB0190
*        CORRECT FORM IS :-                                             MADB0200
*        $GETAPCE <'ID' | ID | LABEL | (REG)> NAME OF APCE REQUIRED     MADB0210
*                                  THE NAME OF THE APCE TO BE BUILT MAY MADB0220
*                                  BE A CHARACTER STRING IN QUOTES, A   MADB0230
*                                  NAME OF A FIELD CONTAINING THE NAME, MADB0240
*                                  A CHARACTER STRING WITHOUT QUOTES,   MADB0250
*                                  OR REGISTER NOTATION, IN WHICH CASE  MADB0260
*                                  THE REGISTER CONTAINS THE ADDRESS    MADB0270
*                                  OF A FIELD CONTAINING THE NAME.      MADB0280
*              <DOC=NO | YES>      REQUEST FOR DOCUMENTATION            MADB0290
         SPACE 1                                                        MADB0300
*        THE PURPOSE OF THIS MACRO IS TO ACQUIRE A SECONDARY APCE,      MADB0310
*         ASSIGN IT THE SPECIFIED NAME, AND CHAIN IT TO THE CURRENT     MADB0320
*         PRIMARY APCE.  THE MACRO INVOKES THE SERVICE ROUTINE 'GETAPCE MADB0330
*         TO ACQUIRE AN APCE FROM THE AVAILABLE QUEUE, INITIALISE IT,   MADB0340
*         AND CHAIN IT TO THE PRIMARY APCE ( USING 'APCESNXT' AS THE    MADB0350
*         ANCHOR OF A PUSH-DOWN STACK ).                                MADB0360
         SPACE 1                                                        MADB0370
.NODOC   ANOP                                                           MADB0380
&NAME1   SETC  '&NAME'                                                  MADB0390
         AIF   ('&ID' NE '').A01                                        MADB0400
         MNOTE 8,'NO APCE NAME SPECIFIED'                               MADB0410
         MEXIT                                                          MADB0420
.A01     ANOP                                                           MADB0430
&N       SETA  N'&ID                                                    MADB0440
         AIF   (&N EQ 1).NOK                                            MADB0450
         MNOTE 8,'INCORRECT PARAMETER LIST: TOO MANY OPERANDS'          MADB0460
         MEXIT                                                          MADB0470
.NOK     AIF   ('&ID'(1,1) NE '(').NOTREG                               MADB0480
&R       SETC  '&ID(1)'                                                 MADB0490
&NAME1   LA    R1,&R               POINT R1 AT THE REQUIRED APCE NAME   MADB0500
&NAME1   SETC  ' '                                                      MADB0510
         AGO   .SERV                                                    MADB0520
.NOTREG  ANOP                                                           MADB0530
&T       SETC  T'&ID                                                    MADB0540
         AIF   ('&T' EQ 'U').UNDEF                                      MADB0550
         AIF   ('&T' EQ 'N').SELFDEF                                    MADB0560
         AIF   ('&T' EQ 'C').CHAR                                       MADB0570
         MNOTE 8,'PARAMETER HAS TYPE ATTRIBUTE OF &T - INVALID'         MADB0580
         MEXIT                                                          MADB0590
.CHAR    ANOP                                                           MADB0600
&NAME1   LA    R1,&ID              POINT R1 AT PARAMETER LIST           MADB0610
&NAME1   SETC  ' '                                                      MADB0620
         AGO   .SERV                                                    MADB0630
.SELFDEF ANOP                                                           MADB0640
&NAME1   BAL   R1,*+8              POINT R1 AT PARAMETER LIST           MADB0650
         DC    CL4&ID              NAME OF APCE TO BE OBTAINED          MADB0660
&NAME1   SETC  ' '                                                      MADB0670
         AGO   .SERV                                                    MADB0680
.UNDEF   ANOP                                                           MADB0690
&NAME1   BAL   R1,*+8              POINT R1 AT NAME OF APCE TO OBTAIN   MADB0700
         DC    CL4'&ID'            NAME OF APCE TO BE OBTAINED          MADB0710
.SERV    ANOP                                                           MADB0720
         AIF   ('&SRVRTN' EQ 'YES').SRVYES                              MADB0730
      $SRVSAVE SAVE=(LINK,BASE3)   PRESERVE CORRUPTED REGISTERS         MADB0740
         AGO   .ASKALL                                                  MADB0750
.SRVYES $SRVSAVE SAVE=(LINK,BASE3) PRESERVE BASE3 AND LINK ADDRESS      MADB0760
.ASKALL  ANOP                                                           MADB0770
         L     R9,HCT$AHCT         GET ADDRESS OF AHCT                  MADB0780
         LA    R9,(GETAPCE*MRCBSIZE)+MRCBSTRT(,R9) GET ROUTINE MRCB     MADB0790
         L     BASE3,MRCBRADR-MRCBDSCT(,R9) GET ROUTINE ENTRY POINT     MADB0800
         BALR  LINK,BASE3          PERFORM SERVICE ROUTINE 'GETAPCE'    MADB0810
      $SRVREST ,                   RESTORE PRESERVED REGISTERS          MADB0820
         MEND                                                           MADB0830
