         MACRO                                                          MADD0130
&NAME   $ATTAPCE  &ID,&DOC=NO                                           MADD0140
         GBLC  &SRVRTN                                                  MADD0150
         LCLC  &R,&T,&NAME1                                             MADD0160
         LCLA  &N                                                       MADD0170
         AIF   ('&DOC' NE 'YES').NODOC                                  MADD0180
         SPACE 1                                                        MADD0190
*        CORRECT FORM IS :-                                             MADD0200
*        $ATTAPCE < (REG) >        ADDRESS OF APCE TO BE ATTACHED       MADD0210
*              <DOC=NO | YES>      REQUEST FOR DOCUMENTATION            MADD0220
         SPACE 1                                                        MADD0230
*        THE PURPOSE OF THIS MACRO IS TO ATTACH THE SECONDARY APCE      MADD0240
*         ( POINTED TO BY THE SPECIFIED REGISTER ) TO THE CURRENT       MADD0250
*         PRIMARY APCE.  THE SPECIFIED REGISTER MAY POINT AT A SINGLE   MADD0260
*         DETACHED APCE, OR AT THE START OF A CHAIN OF DETACHED APCE'S. MADD0270
*         THE MACRO INVOKES THE SERVICE ROUTINE 'ATTAPCE' TO CHAIN THE  MADD0280
*         APCE(S) USING APCESNXT AS A PUSH-DOWN STACK.  UPON RETURN     MADD0290
*         REGISTER1 CONTAINS THE NUMBER OF APCE'S ATTACHED.             MADD0300
         SPACE 1                                                        MADD0310
*        SEE THE MACRO $DETAPCE FOR MORE INFORMATION REGARDING THE      MADD0320
*         PURPOSE OF DETACHING / ATTACHING APCE'S.                      MADD0330
         SPACE 1                                                        MADD0340
.NODOC   ANOP                                                           MADD0350
&NAME1   SETC  '&NAME'                                                  MADD0360
&N       SETA  N'&ID                                                    MADD0370
         AIF   (&N EQ 1).NOK                                            MADD0380
         MNOTE 8,'INCORRECT PARAMETER LIST: TOO MANY OPERANDS'          MADD0390
         MEXIT                                                          MADD0400
.NOK     AIF   ('&ID'(1,1) EQ '(').REG                                  MADD0410
         MNOTE 8,'MACRO REQUIRES REGISTER NOTATION'                     MADD0420
         MEXIT                                                          MADD0430
.REG     ANOP                                                           MADD0440
&R       SETC  '&ID(1)'                                                 MADD0450
&NAME1   LA    R1,0(,&R)           POINT R1 AT APCE TO BE ATTACHED      MADD0460
&NAME1   SETC  ' '                                                      MADD0470
         AIF   ('&SRVRTN' EQ 'YES').SRVYES                              MADD0480
      $SRVSAVE SAVE=LINK           PRESERVE LINK REGISTER               MADD0490
         AGO   .ASKALL                                                  MADD0500
.SRVYES $SRVSAVE SAVE=(LINK,BASE3) PRESERVE BASE3 AND LINK ADDRESS      MADD0510
.ASKALL  ANOP                                                           MADD0520
.CONT    L     BASE3,(ATTAPCE*MRCBSIZE)+MRCBSTRT(,R1) GET ROUTINE ADDR  MADD0530
         BALR  LINK,BASE3          PERFORM SERVICE ROUTINE 'ATTAPCE'    MADD0540
     $SRVREST ,                    RESTORE PRESERVED REGISTERS          MADD0550
         MEND                                                           MADD0560
