         MACRO                                                          MACH0130
&NAME   $MODEXIT &CODE,&DOC=NO                                          MACH0140
         LCLB  &F                                                       MACH0150
         LCLC  &C                                                       MACH0160
&F       SETB  0                                                        MACH0170
         AIF   ('&CODE' EQ '').NOCODE                                   MACH0180
&F       SETB  1                                                        MACH0190
         AIF   ('&CODE' NE 'NO').NNO                                    MACH0200
&C       SETC  '$$W'                                                    MACH0210
         AGO   .NOCODE                                                  MACH0220
.NNO     AIF   ('&CODE' NE 'YES').NYES                                  MACH0230
&C       SETC  '$$X'                                                    MACH0240
         AGO   .NOCODE                                                  MACH0250
.NYES    AIF   ('&CODE' NE 'ERROR').NERROR                              MACH0260
&C       SETC  '$$Y'                                                    MACH0270
         AGO   .NOCODE                                                  MACH0280
.NERROR  ANOP                                                           MACH0290
&C       SETC  '&CODE'                                                  MACH0300
         MNOTE 4,'WRONG CODE SPECIFICATION - NOT YES,NO OR ERROR'       MACH0310
.NOCODE  AIF   ('&DOC' NE 'YES').NODOC                                  MACH0320
         SPACE 1                                                        MACH0330
*        THE PURPOSE OF THIS MACRO IS TO RETURN TO JES2 CODE FROM       MACH0340
*         A USER MODIFICATION ROUTINE.  THE MACRO PERFORMS THE FOLLOWIN MACH0350
*         FUNCTIONS :-                                                  MACH0360
*              . FREE ALL SECONDARY APCE'S                              MACH0370
*              . FREE THE APCE AND RETURN IT TO THE AVAILABLE QUEUE     MACH0380
*              . RESTORE ALL THE REGISTERS ( FROM THE APCE S/A )        MACH0390
*              . SET THE COND CODE IN THE PSW FOR THE CALLER            MACH0400
         SPACE 1                                                        MACH0410
*        NOTE THAT INFORMATION IS MOVED FROM THE APCE TO THE MRCB IN    MACH0420
*         CASE THE APCE IS TAKEN FROM THE AVAILABLE QUEUE AFTER WE HAVE MACH0430
*         RETURNED IT, BUT BEFORE WE HAVE LOADED OUR REGISTERS AND SET  MACH0440
*         THE COND CODE FOR THE CALLER.                                 MACH0450
         SPACE 1                                                        MACH0460
*        CORRECT FORM IS :-                                             MACH0470
*        $MODEXIT <RETURN>,<DOC=NO | YES>                               MACH0480
*              <RETURN>            WHICH EXIT FROM THE $CBAL IS         MACH0490
*                                  REQUIRED.MAY BE 'YES','NO' OR        MACH0500
*                                  'ERROR'.IF OMITTED THE CONDITION     MACH0510
*                                  CODE UPON RETURN IS SET BY THE       MACH0520
*                                  APCE$$X FIELD CONTENTS.              MACH0530
*                                  NOTE THAT IF THIS IS NOT ONE OF THE  MACH0540
*                                  STATED OPTIONS IT IS USED TO SET THE MACH0550
*                                  COND.CODE BY A STRAIGHT COMPARE      MACH0560
*                                  AGAINST X'95',SO IT MAY BE STATED    MACH0570
*                                  AS '$$W','$$X' OR '$$Y',BUT WILL     MACH0580
*                                  PRODUCE A WARNING.                   MACH0590
*              <DOC=NO | YES>      PRODUCE DOCUMENTATION ( DEFAULT NO ) MACH0600
         SPACE 1                                                        MACH0610
*        THIS MACRO ASSUMES THAT NORMAL ROUTINE REGISTER ASSIGNMENTS    MACH0620
*         ARE STILL IN EFFECT, AND SPECIFICALLY THAT R7 CONTAINS THE    MACH0630
*         ADDRESS OF THE APCE.                                          MACH0640
         SPACE 1                                                        MACH0650
.NODOC   ANOP                                                           MACH0660
&NAME    DS    0H                  $MODEXIT PROCESSING                  MACH0670
         TM    APCEFLG1,APF1SEC    HAS THIS APCE ANY SECONDARIES        MACH0672
         BZ    MODX&SYSNDX         BYPASS FREEING THEM IF NONE EXIST    MACH0674
      $FREAPCE ALL                 FREE ALL SECONDARY APCES             MACH0680
         USING MRCBDSCT,R15        ESTABLISH MRCB ADDRESSABILITY        MACH0690
MODX&SYSNDX L  R15,APCEMRCB        GET ADDRESS OF RELEVANT MRCB         MACH0700
         MVC   MRCBSA(L'APCESA),APCESA MOVE PRESERVED REGISTERS TO MRCB MACH0710
         AIF   (&F).NOMVC                                               MACH0720
         MVC   MRCB$$X,APCE$$X     MOVE COND CODE SETTING VALUE TO MRCB MACH0730
.NOMVC   ANOP                                                           MACH0740
         L     R14,AHCTFPCE        GET ADDRESS OF 1ST AVAILABLE APCE    MACH0750
         ST    R14,APCENEXT        POINT THIS APCE AT 1ST AVAILABLE     MACH0760
         CS    R14,R7,AHCTFPCE     ESTABLISH THIS AS 1ST AVAIL APCE     MACH0770
         BNE   *-8                 LOOP BACK IF ANY PROBLEM             MACH0780
         AIF   (&F).MYCODE                                              MACH0790
         CLC   AHCTX,MRCB$$X       SET COND CODE FOR CALLER             MACH0800
         AGO   .CODESET                                                 MACH0810
.MYCODE  ANOP                                                           MACH0820
         CLI   *,&C                SET COND CODE FOR CALLER             MACH0830
.CODESET ANOP                                                           MACH0840
         LM    R13,R12,MRCBSA      RESTORE ALL CALLERS REGISTERS        MACH0850
         BR    R9                  RETURN TO CALLER VIA R9              MACH0860
         DROP  R15                 DROP MRCB ADDRESSABILITY             MACH0870
         MEND                                                           MACH0880
