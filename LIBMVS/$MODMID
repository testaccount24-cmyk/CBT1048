         MACRO -- $MODMID -- ASSIGN HASP MESSAGE ID FOR WTO             MACJ0130
         $MODMID  &NUM,&DOC=NO                                          MACJ0140
         GBLC  &MODMID                                                  MACJ0150
         AIF   ('&DOC' NE 'YES').NODOC                                  MACJ0160
         SPACE 1                                                        MACJ0170
*        THE CORRECT FORM IS :-                                         MACJ0180
*      $MODMID <BNNN>              MESSAGE NUMBER                       MACJ0190
*              <,DOC=NO | YES>     PRODUCE DOCUMENTATION ( DEFAULT NO ) MACJ0200
         SPACE 1                                                        MACJ0210
*        THE PURPOSE OF THIS MACRO IS TO ADD THE SPECIFIED MESSAGE ID   MACJ0220
*         TO THE ASSEMBLY TABLE &LBL, IN ORDER TO ASSIGN A MESSAGE      MACJ0230
*         NUMBER TO A HASP WTO.                                         MACJ0240
         SPACE 1                                                        MACJ0250
*        THE MESSAGE NUMBER SHOULD BE OF THE FORM :                     MACJ0260
*              BNNN                                                     MACJ0270
*         WHERE BNNN IS THE MODIFICATION NUMBER.                        MACJ0280
         SPACE 1                                                        MACJ0290
.NODOC   ANOP                                                           MACJ0300
&MODMID     SETC  '$HSP&NUM'.' '                                        MACJ0310
.*                                                                      MACJ0320
.*             ADD MESSAGE ID TO THE &LBL TABLE                         MACJ0330
.*                                                                      MACJ0340
         GBLB  &LBLB               ON IF &LBL TABLE IS FULL             MACJ0350
         GBLC  &LBL(256)           ROOM FOR 256 $MODMID MACROES         MACJ0360
         LCLA  &N,&ALPH            INDICES                              MACJ0370
         LCLC  &LB                 TEXT TO ENTER INTO &LBL TBL          MACJ0380
         AIF   (&LBLB).OUTZ        EXIT IF TABLE FULL                   MACJ0390
         AIF   ('&NUM'(1,1) NE 'B' AND '&NUM'(1,1) NE 'J').OUTID        MACJ0400
&LB      SETC  '&NUM'              ASSUME FIRST ID OCCURRENCE           MACJ0410
.LBLAGN  ANOP                      RE-ENTER HERE IF NOT FIRST           MACJ0420
&N       SETA  0                   SET TABLE SEARCH INDEX TO 0          MACJ0430
.LBLGO   ANOP                      SEARCH LOOP STARTS HERE              MACJ0440
&N       SETA  &N+1                UP THE SEARCH INDEX                  MACJ0450
         AIF   (&N GT 256).OUTN    EXIT IF TABLE FULL                   MACJ0460
         AIF   ('&LBL(&N)' EQ '').LBLSPOT  BRANCH IF EMPTY SPOT         MACJ0470
         AIF   ('&LB' NE '&LBL(&N)').LBLGO  CONTINUE UNLESS EQUAL       MACJ0480
&ALPH    SETA  &ALPH+1             GO TO NEXT LETTER OF THE ALPH        MACJ0490
         AIF   (&ALPH GT 26).OUTA  IF OUT OF ALPHABET, MNOTE            MACJ0500
&LB      SETC  '&NUM'.'ABCDEFGHIJKLMNOPQRSTUVWXYZ'(&ALPH,1)             MACJ0510
         AGO   .LBLAGN             RESTART THE SEARCH                   MACJ0520
.LBLSPOT ANOP                      EMPTY SLOT FOUND                     MACJ0530
&LBL(&N) SETC  '&LB'               FILL IN THE SLOT                     MACJ0540
         AIF   (K'&LB GT 5).OUTK   IF TOO LONG WE CANT DO LABEL         MACJ0550
$MM&LB  DC    0AL4($MM&LB)       MESSAGE IDENTIFIER                     MACJ0560
.OUTZ    ANOP                      END OF &LBL TABLE CODE               MACJ0570
.*                                                                      MACJ0580
.*             &LBL TABLE ADDITION COMPLETE                             MACJ0590
.*                                                                      MACJ0600
         AGO   .XIT                RETURN FROM MACRO                    MACJ0610
.*                                                                      MACJ0620
.*             &LBL TABLE ERROR MESSAGES                                MACJ0630
.*                                                                      MACJ0640
.OUTN    MNOTE 0,'MORE THAN 256 OCCURRENCES OF $MID'                    MACJ0650
&LBLB    SETB  1                   ONE-TIME MESSAGE                     MACJ0660
         AGO   .OUTZ                                                    MACJ0670
.*                                                                      MACJ0680
.OUTA    MNOTE 0,'MORE THAN 27 OCCURRENCES OF MESSAGE IDENTIFIER &NUM'  MACJ0690
         AGO   .OUTZ                                                    MACJ0700
.*                                                                      MACJ0710
.OUTK    MNOTE 0,'CANNOT CREATE SYMBOL FOR $MID&LB'                     MACJ0720
         AGO   .OUTZ                                                    MACJ0730
.*                                                                      MACJ0740
.OUTID   MNOTE 0,'MESSAGE PREFIX SHOULD BE ''B'' OR ''J'''              MACJ0750
         AGO   .OUTZ                                                    MACJ0760
.*                                                                      MACJ0770
.*             END OF &LBL TABLE MESSAGES                               MACJ0780
.*                                                                      MACJ0790
.XIT     MEND                                                           MACJ0800
