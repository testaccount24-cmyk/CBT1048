         MACRO                                                          MACR0130
&NAME  $SUBOUT &SETCC=NO,&RETURN=,&DOC=                                 MACR0140
         GBLA  &SUBINC                                                  MACR0150
         GBLC  &REGTABL(16)                                             MACR0160
         GBLA  &DISPTAB(16)                                             MACR0170
         GBLA  &SUBNREG                                                 MACR0180
         GBLC  &SUBIND                                                  MACR0190
         LCLA  &LOOP,&RCNT                                              MACR0200
         LCLA  &N,&S                                                    MACR0210
         LCLC  &R,&P,&PP,&B,&LABEL                                      MACR0220
         AIF   ('&DOC' NE 'YES').NODOC                                  MACR0230
         SPACE 1                                                        MACR0240
*        THE CORRECT FORM IS :-                                         MACR0250
*     $SUBOUT  <RETURN=RN | N(RN) | ADDR> RETURN ADDRESS SPECIFICATION. MACR0260
*                                  THE RETURN ADDRESS MAY BE IN A REG,  MACR0270
*                                  A BASE AND DISPLACEMENT, OR A LABEL. MACR0280
*                                  BY DEFAULT, RETURN IS VIA R4.        MACR0290
*              <SETCC=NO | YES>    REQUESTS THAT THE CONDITION CODE     MACR0300
*                                  BE SET FOR THE CALLER BY TESTING     MACR0310
*                                  THE VALUE IN APCE$$X.  THIS IS ONLY  MACR0320
*                                  NECESSARY IF A SUBROUTINE THAT USES  MACR0330
*                                  THE PSW COND CODE TO COMMUNICATE     MACR0340
*                                  WITH ITS CALLER IN TURN CALLS A      MACR0350
*                                  SUBROUTINE ( AND CONSEQUENTLY        MACR0360
*                                  SAVES AND RESTORES REGISTERS: THE    MACR0370
*                                  PROCESS OF RESTORING CORRUPTS THE    MACR0380
*                                  PSW COND CODE). DEFAULT IS NO.       MACR0390
*              <DOC=NO | YES>      PRODUCE DOCUMENTATION ( DEFAULT NO ) MACR0400
         SPACE 1                                                        MACR0410
*        THE PURPOSE OF THIS MACRO IS TO TERMINATE AN INTERNAL SUBRTN,  MACR0420
*         RESTORING ANY REGISTERS PRESERVED BY THE PRECEDING $SUBIN,    MACR0430
*         AND RETURNING CONTROL TO THE CALLER.                          MACR0440
         SPACE 1                                                        MACR0450
.NODOC   ANOP                                                           MACR0460
&SUBIND  SETC  'OFF'                                                    MACR0470
&LABEL   SETC  '&NAME'                                                  MACR0480
&LOOP    SETA  0                                                        MACR0490
&RCNT    SETA  0                                                        MACR0500
         AIF   (&SUBNREG EQ 0).RET                                      MACR0510
&LABEL   L     R15,APCEBCTR        GET COUNT OF USED S/A SLOTS          MACR0520
         S     R15,=F'&SUBNREG'    DECREMENT BY THE NO. USED BY $SUBIN  MACR0530
         ST    R15,APCEBCTR        PRESERVE UPDATED SLOT COUNT          MACR0540
         SLL   R15,2               MULTIPLY COUNT BY 4 FOR INDEX        MACR0550
         LA    R15,APCEMSA(R15)    POINT AT 1ST SLOT FOR THIS S/A       MACR0560
&LABEL   SETC  ''                                                       MACR0570
.LOOP    ANOP                                                           MACR0580
&LOOP    SETA  &LOOP+1                                                  MACR0590
         AIF   (&LOOP GT &SUBNREG).RET                                  MACR0600
         L     &REGTABL(&LOOP),&DISPTAB(&LOOP)(,R15) RESTORE REGISTER   MACR0610
         AGO   .LOOP                                                    MACR0620
.RET     ANOP                                                           MACR0630
         AIF   ('&SETCC' NE 'YES').NOSET                                MACR0640
         CLC   AHCTX,APCE$$X       SET THE COND CODE FOR THE CALLER     MACR0650
.NOSET   ANOP                                                           MACR0660
&B       SETC  'R4'                                                     MACR0670
         AIF   ('&RETURN' EQ '').C2                                     MACR0680
         AIF   ('&RETURN'(1,1) NE '(').C11                              MACR0690
&B       SETC  '&RETURN(1)'                                             MACR0700
         AGO   .C2                                                      MACR0710
.C11     ANOP                                                           MACR0720
&B       SETC  '&RETURN'                                                MACR0730
&LABEL   B     &B 0                RETURN TO CALLER                     MACR0740
         MEXIT                                                          MACR0750
.C2      ANOP                                                           MACR0760
&LABEL   BR    &B                  RETURN TO CALLER                     MACR0770
         MEXIT                                                          MACR0780
         MEND                                                           MACR0790
