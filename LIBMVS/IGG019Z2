IGG019Z2 START 0                   CHAN END APPENDAGE
         SPACE 3
BUFDSECT DSECT
IOBFL1   DS    C
IOBFL2   DS    C
IOBSENS0 DS    C
IOBSENS1 DS    C
IOBECBCC DS    C
IOBECBPT DS    CL3
IOBFL3   DS    C
IOBCSW   DS    CL7
IOBSIOCC DS    C
IOBSTART DS    CL3
IOBNIL   DS    C
IOBDCB   DS    CL3
IOBRSTRT DS    F
IOBINCR  DS    H
IOBERRCT DS    H
IOBXTNT  DS    C
IOBSEEK  DS    CL7
         EJECT
IGG019Z2 CSECT
         USING BUFDSECT,2          IOB ADDRESSABILITY
         USING *,15                APPENDAGE ADDRESSABILITY
         L     10,IOBFL3
         LA    10,0(10)
         LTR   10,10
         BZ    NOTSION
         SH    10,EIGHT
         CLI   0(10),X'27'
         BNE   NOTSION
         ST    10,IOBFL3
         ST    10,IOBSIOCC
         BCR   15,14             -
NOTSION  EQU   *                 -
         TM    IOBFL1,X'04'        IS ERR RTN IN CNTRL
         BCR   7,14                YES. RETURN TO IOS
         LR    10,2                IOB IN WORK REG
         SH    10,FOUR             POINT TO STATUS BYTES
         TM    0(10),255           TP PGM IN O/S CONTROL
         BCR   8,14                NO. RETURN NORMAL TO O/S
         BCR   15,14               BYPASS BELOW TEMPORARILY
         SPACE 3
**       SEND NAK TO CAUSE HASP TO RESEND UNTIL TP PGM BACK IN   **
**       CONTROL OVER ITSELF.  THAT IS, NOT UNDER O/S CONTROL    **
**       FOR OPENING AND CLOSING THE DISK QUEUES.                **
         SPACE 3
         L     10,IOBSIOCC         ADDR OF FIRST CCW
         CLI   0(10),2             WAS CCW ONE A READ
         BCR   8,14                YES. GET OUT OF HERE
         L     11,0(10)            DATA ADDR FOR TP BUF
         MVC   6(3,10),NAKNOP      MAKE LEN 1ST CCW=2 + NOP 2ND CCW
         MVI   4(10),X'60'         SET COMM CHAIN IN CCWA
         MVC   0(2,11),NAKSEQ      SET UP A NAK RESPONSE
         MVC   IOBFL1(4),FLSRES    RESET FLAGS & SENSE
         XC    IOBFL3(8),IOBFL3    CLEAR FLAGS & CWS
         XC    IOBERRCT,IOBERRCT   CLEAR ERROR COUNT FIELD
         MVI   IOBECBCC,0          INSURE ECB NOT POSTED
         B     8(14)               RESTART THE CHAN PGM
FOUR     DC    H'4'
NAKSEQ   DC    X'323D'
FLSRES   DC    X'C2000000'
NAKNOP   DC    X'000203'
EIGHT    DC    H'8'
         END
