JES2B301 TITLE 'MONSANTO SEPARATOR PAGES  - CONTROL BLOCK GENERATION MA*B3010010
               CRO DEFINITIONS'                                         B3010020
*                                                                       B3010030
**** $HASPCB ****                  GENERATE HASP CONTROL BLOCKS         B3010040
*                                                                       B3010050
         MACRO                                                          B3010060
         $HASPCB &DOC=NO,&LIST=NO                                       B3010070
         GBLC  &GEN,&DATA,&PRINT                                        B3010080
         PUSH  PRINT                                                    B3010090
         PRINT &PRINT                                                   B3010100
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT           B3010110
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT              B3010120
         $AHCT DOC=&DOC            GENERATE AHCT DSECT                  B3010130
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT              B3010140
         $APCE DOC=&DOC            GENERATE MODS APCE DSECT             B3010150
         $APCE DOC=&DOC,TYPE=SPCE  GENERATE MODS SPCE DSECT             B3010160
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT              B3010170
         SPACE 1                                                        B3010180
         POP   PRINT                                                    B3010190
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS           B3010200
         MEND                                                           B3010210
         TITLE 'JES2B301 - MONSANTO SEPARATOR PAGES'                    B3010220
JES2B301 CSECT                     INTERPRET ACCOUNTING DATA            B3010230
         SPACE 2                                                        B3010240
*********************************************************************** B3010250
*                                                                     * B3010260
*        PROGRAM ID. :             JES2B301                           * B3010270
*                                                                     * B3010280
*        DATE WRITTEN :            JUNE 1977                          * B3010290
*                                                                     * B3010300
*        AUTHOR :                  D.A.MOORCROFT                      * B3010310
*                                                                     * B3010320
*        LOCATION :                MONSANTO MISD/TSG BRUSSELS         * B3010330
*                                                                     * B3010340
*********************************************************************** B3010350
         SPACE 3                                                        B3010360
*********************************************************************** B3010370
*                                                                     * B3010380
*        PURPOSE                                                      * B3010390
*       ---------                                                     * B3010400
*                                                                     * B3010410
*        THIS IS A MONSANTO JES2 MODIFICATION MODULE.                 * B3010420
*        IT IT LOADED AT JES2 INITIALISATION UPON REQUEST VIA         * B3010430
*        JES2B001,THE MONSANTO INITIALISATION MODULE,AND IS           * B3010440
*        ENTERED VIA A $CBAL IN THE SEPARATOR PAGE ROUTINE (PRINTID)  * B3010450
*        IN HASPPRPU AT STATEMENT Q4929200.  THIS IS AFTER JES2       * B3010460
*        HAS PERFORMED ITS OWN SEPARATOR PAGE FORMATTING, AND IS      * B3010470
*        IMMEDIATELY PRIOR TO THE TRANSLATION OF UNPRINTABLE CHARS    * B3010480
*        TO BLANKS.                                                   * B3010490
*                                                                     * B3010500
*        ON ENTRY:                                                    * B3010510
*        APCESR4  - CONTAINS THE ADDRESS OF THE 132 BYTE LINE         * B3010520
*                                                                     * B3010530
*        THIS ROUTINE REFORMATS THE SEPARATOR PAGE IN ACCORDANCE      * B3010540
*        WITH OUR INSTALLATION STANDARDS.  PRIMARILY, WE REMOVE       * B3010550
*        THE ASTERISKS ( IBM ALSO SELL PRINTER RIBBONS YOU KNOW ),    * B3010560
*        AND DISTINGUISH BETWEEN VARIOUS TYPES OF WORK:               * B3010570
*              TESTING             ( TRIVIAL )                        * B3010580
*              PRODUCTION          ( INTERESTING )                    * B3010590
*              OPERATIONS          ( USEFUL )                         * B3010600
*              SYSTEMS PROGRAMMING ( VITAL )                          * B3010610
*              ONLINE  PROGRAMMING ( VITAL )                          * B3010620
*              ST. LOUIS JOBS      (  U S  )                          * B3010630
*                                                                     * B3010640
*********************************************************************** B3010650
         EJECT                                                          B3010660
         PRINT OFF                 SUPPRESS PRINT OF HASP OPTIONS       B3010670
         COPY  $HASPGEN            HASP OPTIONS                         B3010680
         PRINT ON                  RE-ESTABLISH PRINT FOR PROGRAM       B3010690
         TITLE 'CONTROL BLOCK OPTIONS'                                  B3010700
         $SYSPARM (OFF,GEN,NODATA,NO,NO)     SET DOCUMENTATION OPTIONS  B3010710
         SPACE 3                                                        B3010720
*                                                                       B3010730
*        GENERATE HASP CONTROL BLOCKS                                   B3010740
*                                                                       B3010750
         SPACE 2                                                        B3010760
         $HASPCB DOC=&DOC,LIST=&LIST             GENERATE HASP C/B      B3010770
         TITLE 'INTERPRET ACCOUNTING DATA'                              B3010780
*********************************************************************** B3010790
*        FORMAT OF APCE WORK AREA  ( AS USED BY JES2B301 )            * B3010800
*********************************************************************** B3010810
         SPACE 1                                                        B3010820
APCEDSCT DSECT                                                          B3010830
         ORG   APCEUSER            REDEFINE GENERAL PURPOSE WORK AREA   B3010840
B301USER DS    0CL40               WE HAVE 40 BYTES TO PLAY WITH        B3010850
         DS    0D                  ENSURE DOUBLEWORD ALIGNMENT          B3010860
B301DWRD DS    D                   DOUBLE-WORD WORK AREA                B3010870
B301FLEN DS    C                   EXECUTABLE LENGTH OF FIELD           B3010880
         ORG                                                            B3010890
JES2B301 CSECT                                                          B3010900
         SPACE 5                                                        B3010910
*********************************************************************** B3010920
*        FORMAT OF SPCE WORK AREA ( AS USED BY JES2B301 )             * B3010930
*********************************************************************** B3010940
         SPACE 1                                                        B3010950
SPCEDSCT DSECT                                                          B3010960
         ORG   SPCEAREA            REDEFINE TOTAL DATA AREA PORTION     B3010970
B301SPCE DS    0CL236              WE HAVE 236 BYTES AVAILABLE          B3010980
B301LINE DS    CL132               SEPARATOR LINE AS SET UP BY JES2     B3010990
         ORG                                                            B3011000
JES2B301 CSECT                                                          B3011010
         EJECT                                                          B3011020
*********************************************************************** B3011030
*        FORMAT OF STANDARD JES2 SEPARATOR PAGE                       * B3011040
*********************************************************************** B3011050
         SPACE 1                                                        B3011060
JES2LINE DSECT                                                          B3011070
JES2AST1 DS    CL4     ('****')    ASTERISKS                            B3011080
JES2CL1  DS    CL1                 SYSOUT CLASS                         B3011090
         DS    CL2                 UNUSED                               B3011100
JES2TYP1 DS    CL5                 START/CONT/END INDICATION            B3011110
         DS    CL2                 UNUSED                               B3011120
JES2JBN1 DS    CL8     ('JOB NNNN') JOB NUMBER                          B3011130
         DS    CL2                 UNUSED                               B3011140
JES2NAME DS    CL8                 JOB NAME                             B3011150
         DS    CL2                 UNUSED                               B3011160
JES2PNME DS    CL20                PROGRAMMER'S NAME FIELD              B3011170
         DS    CL2                 UNUSED                               B3011180
JES2RID  DS    CL4     ('ROOM')    CHARACTER CONSTANT 'ROOM'            B3011190
         DS    CL1                 UNUSED                               B3011200
JES2ROOM DS    CL4                 ROOM NUMBER ( FROM JCTROOMN )        B3011210
         DS    CL2                 UNUSED                               B3011220
JES2T$D  DS    0CL21               TIME AND DATE STAMP                  B3011230
JES2PTME DS    CL8     (HH.MM.SS)  TIME OF PRINTING                     B3011240
         DS    CL1                 UNUSED                               B3011250
JES2AMPM DS    CL2                 AM/PM INDICATION                     B3011260
         DS    CL1                 UNUSED                               B3011270
JES2PDTE DS    CL9     (DD MMM YY) DATE OF PRINTING                     B3011280
         DS    CL2                 UNUSED                               B3011290
JES2DEV  DS    CL8                 NAME OF DEVICE ON WHICH JOB PRINTED  B3011300
         DS    CL2                 UNUSED                               B3011310
JES2SYS  DS    CL3     ('SYS')     CHARACTER CONSTANT 'SYS'             B3011320
         DS    CL1                 UNUSED                               B3011330
JES2SID  DS    CL4                 SYSTEM IDENTIFIER (JES2 INITIALIS'N) B3011340
         DS    CL2                 UNUSED                               B3011350
JES2JBN2 DS    CL8     ('JOB NNNN') JOB NUMBER AGAIN                    B3011360
         DS    CL2                 UNUSED                               B3011370
JES2TYP2 DS    CL5                 START/CONT/END INDICATION            B3011380
         DS    CL2                 UNUSED                               B3011390
JES2SCL2 DS    CL1                 SYSOUT CLASS AGAIN                   B3011400
JES2AST2 DS    CL4     ('****')    ASTERISKS AGAIN                      B3011410
JES2LEN  EQU   *-JES2LINE          LENGTH OF DSECT                      B3011420
JES2B301 CSECT                                                          B3011430
         EJECT                                                          B3011440
*********************************************************************** B3011450
*        FORMAT OF MONSANTO SEPARATOR PAGE                            * B3011460
*********************************************************************** B3011470
         SPACE 1                                                        B3011480
MONSLINE DSECT                                                          B3011490
MONS1CHR DS    0CL1                1ST CHARACTER OF LINE                B3011500
MONSHD1  DS    CL27                JOB TYPE IDENTIFIER                  B3011510
         DS    CL2                 UNUSED                               B3011520
MONSTYP1 DS    CL5                 START/CONT/END INDICATION            B3011530
         DS    CL1                 UNUSED                               B3011540
MONSJBN  DS    CL8     ('JOB NNNN') JOB NUMBER                          B3011550
         DS    CL2                 UNUSED                               B3011560
MONST$D  DS    CL21                TIME AND DATE STAMP                  B3011570
         DS    CL3                 UNUSED                               B3011580
MONSNAME DS    CL8                 JOBNAME                              B3011590
         DS    CL5                 UNUSED                               B3011600
MONSPNME DS    CL20                PROGRAMMER NAME FIELD (DISTRIBUTION) B3011610
         DS    CL2                 UNUSED                               B3011620
MONSHD2  DS    CL27                JOB TYPE INDICATION AGAIN            B3011630
         DS    CL1                 UNUSED                               B3011640
MONSLEN  EQU   *-MONSLINE          LENGTH OF DSECT                      B3011650
JES2B301 CSECT                                                          B3011660
         TITLE 'JE2B301 - MAIN PROCESSING ROUTINE'                      B3011670
*********************************************************************** B3011680
*        INITIALISATION                                               * B3011690
*********************************************************************** B3011700
         SPACE 1                                                        B3011710
JES2B301 $MODNTRY CSECT=YES,EXIT=EXITB301,DOC=&DOC MODIFICATION ENTRY   B3011720
         SPACE 2                                                        B3011730
         USING JCTDSECT,JCT        ESTABLISH JCT ADDRESSABILITY         B3011740
         USING SPCEDSCT,R8         ESTABLISH SECONDARY APCE ADDR'Y      B3011750
         USING JES2LINE,R9         ESTABLISH ADD'Y TO BASIC JES2 SEP'R  B3011760
         USING MONSLINE,R5         ESTABLISH ADDR'Y OF MONSANTO SEP'R   B3011770
         SPACE 5                                                        B3011780
*        REGISTER USAGE IN THIS MODULE :-                               B3011790
         SPACE 1                                                        B3011800
*        R0    -                                                        B3011810
*        R1    -                                                        B3011820
*        R2    -                                                        B3011830
*        R3    -                                                        B3011840
*        R4    - BAL RETURN ADDRESS                                     B3011850
*        R5    - ADDRESS OF ACTUAL SEPARATOR LINE ( IN JCT )            B3011860
*        R6    - AHCT ADDRESS                                           B3011870
*        R7    - APCE ADDRESS                                           B3011880
*        R8    - SPCE ADDRESS                                           B3011890
*        R9    - ADDRESS OF PRESERVED JES2 SEPARATOR LINE ( IN SPCE )   B3011900
*        R10   - JCT ADDRESS                                            B3011910
*        R11   - HCT ADDRESS                                            B3011920
*        R12   - MODULE BASE REGISTER                                   B3011930
*        R13   - PCE ADDRESS                                            B3011940
*        R14   -                                                        B3011950
*        R15   -                                                        B3011960
         EJECT                                                          B3011970
*********************************************************************** B3011980
*        INIALISATION ( CONT'D )                                      * B3011990
*********************************************************************** B3012000
         SPACE 1                                                        B3012010
         L     R5,APCESR4          RETRIEVE ADDRESS OF JES2'S SEPARATOR B3012020
         $GETAPCE B301             ACQUIRE A SECONDARY APCE FOR STORAGE B3012030
         LR    R8,R1               ESTABLISH ADDRESSABILITY OF SPCE     B3012040
         MVC   B301LINE,0(R5)      PRESERVE JES2'S ORIGINAL LINE        B3012050
         LA    R9,B301LINE         ESTABLISH ADDR'Y OF SAVED LINE       B3012060
         EJECT                                                          B3012070
*********************************************************************** B3012080
*        MAIN-LINE LOGIC: CREATE SEPARATOR LINE                       * B3012090
*********************************************************************** B3012100
         SPACE 1                                                        B3012110
         MVI   MONS1CHR,C' '       SET BLANK AT START OF LINE ....      B3012120
         MVC   MONS1CHR+1(MONSLEN-1),MONS1CHR ... AND PROPAGATE         B3012130
         TM    JCTROOMN+2,X'F0'    TEST NUMERIC CAT CODE (1ST DIGIT)    B3012140
         BC    12,DEFAUL           INSERT DEFAULT IF NON NUMERIC        B3012150
         BC    1,TEST2             TEST 2ND DIGIT IF 1ST NUMERIC        B3012160
TEST2    TM    JCTROOMN+3,X'F0'    TEST NUMERIC CAT CODE (2ND DIGIT)    B3012170
         BC    12,DEFAUL           INSERT DEFAULT IF NON NUMERIC        B3012180
         CLC   JCTROOMN+2(2),=C'  '  TEST FOR NO CAT CODE               B3012190
         BNE   PACKCODE            CONTINUE WITH PACK                   B3012200
DEFAUL   LA    R3,DEFAULT          ADD DEFAULT CODE                     B3012210
         B     PACKPT2             LOAD TEST OFFSET                     B3012220
PACKCODE PACK  APCEUSER(8),JCTROOMN+2(2)  PACK CAT CODE                 B3012230
         CVB   R3,APCEUSER         CONVERT TO BINARY                    B3012240
         C     R3,=A(HEADNUMB)     TEST FOR UPPER LIMIT                 B3012250
         BL    *+4+4               CONTINUE IF IN RANGE                 B3012260
         LA    R3,DEFAULT          LOAD DEFAULT OFFSET                  B3012270
PACKPT2  SLL   R3,3                MULTIPLY BY 8                        B3012280
         LA    R3,TABLE1(R3)       LOAD DEFAULT OFFSET                  B3012290
         LM    R1,R2,0(R3)         LOAD OFFSETS OF HEAD & TRAILERS      B3012300
HEADERS  MVC   MONSHD1,0(R1)       LOAD HEAD LABEL                      B3012310
         MVC   MONSHD2,0(R2)       LOAD TRAILER LABEL                   B3012320
         B     HEADDONE            GO TO PRINT                          B3012330
         SPACE 1                                                        B3012340
ERRORCOD LA    R3,DEFAULT          LOAD ADDRESS OF DEFAULT VALUE        B3012350
         MVC   JCTROOMN+2(2),0(R3)  LOAD IN DEFAULT VALUE FOR CAT       B3012360
         B     PACKCODE            RETURN TO PACK                       B3012370
         SPACE 1                                                        B3012380
TABLE1   DS    0F                                                       B3012390
         DC    A(HEADTEST,HEADTEST)  DUMMY ENTRY                        B3012400
         DC    A(HEADTEST,HEADTEST)  CC 1 = TESTING                     B3012410
         DC    A(HEADUNKN,HEADUNKN)  CC 2 = UNKNOWN                     B3012420
         DC    A(HEADUNKN,HEADUNKN)  CC 3 = UNKNOWN                     B3012430
         DC    A(HEADONL,HEADONL)    CC 4 = ONLINE PROGRAMMING          B3012440
         DC    A(HEADSP1,HEADSP2)    CC 5 = TECH SUPPORT / SYS PROG     B3012450
         DC    A(HEADSTL,HEADSTL)    CC 6 = ST. LOUIS                   B3012460
         DC    A(HEADOPS,HEADOPS)    CC 7 = OPERATIONS                  B3012470
         DC    A(HEADUNKN,HEADUNKN)  CC 8 = UNKNOWN                     B3012480
         DC    A(HEADPROD,HEADPROD)  CC 9 = PRODUCTION                  B3012490
HEADLNGT EQU   8                   LENGTH OF TABLE ENTRY                B3012500
HEADLIST EQU   *-TABLE1            LENGTH OF TABLE                      B3012510
HEADNUMB EQU   HEADLIST/HEADLNGT   NUMBER OF TABLE ENTRIES              B3012520
DEFAULT  EQU   2                   DEFAULT CATEGORY CODE                B3012530
         SPACE 1                                                        B3012540
HEADDONE EQU   *                   FORMAT THE REMAINDER OF THE LINE     B3012550
         MVC   MONSTYP1,JES2TYP1   SET START/CONT/END INDICATION        B3012560
         MVC   MONSJBN,JES2JBN1    SET 'JOB NNNN' TO IDENTIFY NUMBER    B3012570
         MVC   MONST$D,JES2T$D     SET TIME AND DATE STAMP              B3012580
         MVC   MONSNAME,JES2NAME   ADD JOB NAME TO LINE                 B3012590
         MVC   MONSPNME,JES2PNME   ADD PROGRAMMER NAME FIELD TO LINE    B3012600
MAINLNEX EQU   *                   END OF ACCTG DATA PROCESSING         B3012610
         EJECT                                                          B3012620
*********************************************************************** B3012630
*        RETURN TO JES2 MAINLINE CODE                                 * B3012640
*********************************************************************** B3012650
         SPACE 1                                                        B3012660
EXITB301 $MODEXIT YES,DOC=&DOC     RETURN TO JES2 CODE                  B3012670
         EJECT                                                          B3012680
*********************************************************************** B3012690
*        MISCELLANEOUS CONSTANTS                                      * B3012700
*********************************************************************** B3012710
         SPACE 1                                                        B3012720
HEADTEST DC    CL27'TESTING   TESTING   TESTING'                        B3012730
HEADUNKN DC    CL27'UNKNOWN CATEGORY CODE USED '                        B3012740
HEADPROD DC    CL27'PRODUCTION       PRODUCTION'                        B3012750
HEADOPS  DC    CL27'        OPERATIONS'                                 B3012760
HEADSTL  DC    CL27'    ST. LOUIS     '                                 B3012770
HEADSP1  DC    CL27'     TECHNICAL SUPPORT'                             B3012780
HEADSP2  DC    CL27'    SYSTEMS PROGRAMMING'                            B3012790
HEADONL  DC    CL27'    ONLINE  PROGRAMMING'                            B3012800
         SPACE 2                                                        B3012810
         LTORG                                                          B3012820
         SPACE 1                                                        B3012830
         END                                                            B3012840
