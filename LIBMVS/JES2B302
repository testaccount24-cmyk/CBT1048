JES2B302 TITLE '$A COMMAND PROCESSOR - CONTROL BLOCK GENERATION MACRO DCB3020130
               EFINITIONS'                                              B3020140
*                                                                       B3020150
**** $HASPCB ****                  GENERATE HASP CONTROL BLOCKS         B3020160
*                                                                       B3020170
         MACRO                                                          B3020180
         $HASPCB &DOC=NO,&LIST=NO                                       B3020190
         GBLC  &PRINT,&GEN,&DATA                                        B3020200
         PUSH  PRINT                                                    B3020210
         PRINT &PRINT                                                   B3020220
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT              B3020230
         $CBPR DOC=&DOC            GENERATE MODS CBPR DSECT             B3020240
         $AHCT DOC=&DOC            GENERATE MODS AHCT DSECT             B3020250
         $DCT  DOC=&DOC            GENERATE HASP DCT DSECT              B3020260
         $ADCT DOC=&DOC            GENERATE MODS ADCT DSECT             B3020270
         $APCE DOC=&DOC            GENERATE MODS APCE DSECT             B3020280
         $APCE DOC=NO,TYPE=SPCE    GENERATE MODS SECONDARY APCE DSECT   B3020290
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT              B3020300
         $COMWORK DOC=&DOC         GENERATE HASP COMWORK DSECT          B3020310
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT           B3020320
         $CMB  DOC=&DOC            GENERATE HASP CMB DSECT              B3020330
         $IOT  DOC=&DOC            GENERATE HASP IOT DSECT              B3020340
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT              B3020350
         $JOE  DOC=&DOC            GENERATE HASP JOE DSECT              B3020360
         $JQE  DOC=&DOC            GENERATE HASP JQE DSECT              B3020370
         $TAB  DOC=&DOC            GENERATE HASP TAB DSECT              B3020380
         $UCM  LIST=&LIST          GENERATE OS UCM DSECT                B3020390
         SPACE 1                                                        B3020400
         POP   PRINT                                                    B3020410
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS           B3020420
         MEND                                                           B3020430
         TITLE 'JES2B302 - AUXILIARY COMMAND PROCESSOR'                 B3020440
JES2B302 CSECT                     AUXILIARY COMMAND PROCESSOR          B3020450
         SPACE 2                                                        B3020460
*********************************************************************** B3020470
*                                                                     * B3020480
*        PROGRAM ID. :             JES2B302                           * B3020490
*                                                                     * B3020500
*        DATE WRITTEN :            APRIL 1977                         * B3020510
*                                                                     * B3020520
*        AUTHOR :                  D.H.CARTWRIGHT.                    * B3020530
*                                                                     * B3020540
*        LOCATION :                MONSANTO MISD/TSG BRUSSELS         * B3020550
*                                                                     * B3020560
*********************************************************************** B3020570
         EJECT                                                          B3020580
*********************************************************************** B3020590
*                                                                     * B3020600
*        PURPOSE                                                      * B3020610
*       ---------                                                     * B3020620
*                                                                     * B3020630
*        THIS IS A MONSANTO JES2 MODIFICATION MODULE.                 * B3020640
*        IT IT LOADED AT JES2 INITIALISATION UPON REQUEST VIA         * B3020650
*        JES2B001,THE MONSANTO INITIALISATION MODULE,AND IS           * B3020660
*        ENTERED VIA A $CBAL IN HASPCOMM AT K2654200,IN               * B3020670
*        HASPCJB2 CAJ (RELEASE JOB) ROUTINE. THE ROUTINE TESTS FOR    * B3020680
*        A REMOTE SUBMITTOR AND WHETHER THE REQUESTOR TERMINAL ADCT   * B3020690
*        HAS 'ALL COMMAND' AUTHORITY.IF NOT,THE CONDITION CODE IS     * B3020700
*        SET TO RETURN CONTROL TO JES2 AT THE NEXT JOB SELECTION      * B3020710
*        STAGE OF HASPCOMM AT CAJNEXT.                                * B3020720
*                                                                     * B3020730
*        COMMAND SUB-PROCESSOR CONVENTIONS                            * B3020740
*                                                                     * B3020750
* CONTENTS OF REGISTERS UPON ENTRY TO EACH COMMAND SUB-PROCESSOR      * B3020760
*                                                                     * B3020770
*        R0    = UNPREDICTABLE                                        * B3020780
*        R1    = JQE ADDRESS                                          * B3020790
*        R2    = UNPREDICTABLE                                        * B3020800
*        R3    = UNPREDICTABLE                                        * B3020810
*        R4    = UNPREDICTABLE                                        * B3020820
*        R5    = FIRST OPERAND POINTER ( SET FOR BXLE R3,R4,... )     * B3020830
*        R6    = AHCTDSCT                                             * B3020840
*        R7    = APCE OF PROCESSOR                                    * B3020850
*        BASE3 = HASPCJB2 BASE - USED FOR $MWTO ADDRESSABILITY        * B3020860
*        R10   = UNPREDICTABLE                                        * B3020870
*        BASE1 = HCTDSECT                                             * B3020880
*        BASE2 = BASE REGISTER OF PROCESSOR                           * B3020890
*        SAVE  = PCE OF COMMAND PROCESSOR                             * B3020900
*        LINK  = RETURN ADDRESS TO THIS MODULE                        * B3020910
*        R15   = UNPREDICTABLE                                        * B3020920
*                                                                     * B3020930
*********************************************************************** B3020940
         EJECT                                                          B3020950
*********************************************************************** B3020960
*                                                                     * B3020970
* EXAMPLE EDIT OF COMMAND TEXT BEFORE ENTRY TO SUB-COMMAND PROCESSOR  * B3020980
*                                                                     * B3020990
* ORIGIONAL IN $WTO BUFFER  ( S OVER CHARACTER = LOWER CASE )         * B3021000
*                                                                     * B3021010
*         S  S S S    S S S    S    S    S                            * B3021020
*        $V  A B C, ' A B,C '' D ', D '' E                            * B3021030
*                                                                     * B3021040
* FORM PASSED TO COMMAND SUB-PROCESSOR                                * B3021050
*                                                                     * B3021060
*        $VABC,' A B,C ' D ',D'E                                      * B3021070
*          1   2             3   4                                    * B3021080
*                                                                     * B3021090
*        $ IS LOCATED AT COMMAND                                      * B3021100
*        V IS SINGLE CHARACTER FORM OF VERB                           * B3021110
*                                                                     * B3021120
*        R3 POINTS TO FIRST OF OPERAND POINTER TABLE WHICH, IN THIS   * B3021130
*        EXAMPLE, CONTAINS FOUR WORDS OF ADDRESSES WHICH IN TURN      * B3021140
*        LOCATES THE BEGINNING OF EACH OPERAND ( INCLUDING A NULL     * B3021150
*        END OPERAND ). THESE LOCATIONS ARE DENOTED BY REFERANCE      * B3021160
*        POINTS 1, 2, 3, AND 4 RESPECTIVELY. USING BXLE R3,R4,...     * B3021170
*        INSTRUCTION WILL FALL THROUGH WITH R3 POINTING TO COMNULOP   * B3021180
*                                                                     * B3021190
*********************************************************************** B3021200
         EJECT                                                          B3021210
*********************************************************************** B3021220
*                                                                     * B3021230
*        JOB NUMBER LISTS ARE OF THE FOLLOWING FORM                   * B3021240
*                                                                     * B3021250
*        $V JXXN1-M1,N2-M2,...,NX-MX                                  * B3021260
*        $V    = HASP COMMAND VERB                                    * B3021270
*        JXX   = JOB, JB, JOBS, ETC. ( J IS ONLY ID REQUIRED )        * B3021280
*        NI-MI ( I=1 TO X ) = PAIR OF START STOP VALUES, IE. 1-10     * B3021290
*              INDICATING THAT JOBS 1,2,3,4,5,6,7,8,9,10 ARE          * B3021300
*              DESIRED. ( -M VALUES ARE OPTIONAL )                    * B3021310
*                                                                     * B3021320
* NOTES                                                               * B3021330
*                                                                     * B3021340
*    (1) A RESPONSE IS REQUIRED FOR EACH JOB                          * B3021350
*                                                                     * B3021360
*    (2) COMNULOP IS USED FOR FLAGS DURING PROCESSING AS FOLLOWS      * B3021370
*                                                                     * B3021380
*              X'80' - A JOB WAS FOUND DURING SCAN FOR JOBS           * B3021390
*              X'40' - THE VERB IS C WITH DUMP SPECIFIED              * B3021400
*              X'02' - THE VERB IS P                                  * B3021410
*              X'01' - THE VERB IS C WITH PURGE SPECIFIED             * B3021420
*                                                                     * B3021430
*        THESE DEFINITIONS MUST NOT BE CHANGED WITHOUT CHANGING       * B3021440
*        THE $JCAN MACRO INSTRUCTION.                                 * B3021450
*                                                                     * B3021460
*********************************************************************** B3021470
         SPACE 3                                                        B3021480
         PUSH  PRINT               SAVE PRINT OPTIONS                   B3021490
         PRINT OFF                 DONT BOTHER TO PRINT STANDARD STUFF  B3021500
         COPY  $HASPGEN            HASP OPTIONS                         B3021510
         POP   PRINT               RESTORE PRINT OPTIONS                B3021520
         TITLE 'CONTROL BLOCK OPTIONS'                                  B3021530
         $SYSPARM (OFF,GEN,NODATA,NO,NO)     SET DOCUMENTATION OPTIONS  B3021540
         SPACE 3                                                        B3021550
*                                                                       B3021560
*        GENERATE HASP CONTROL BLOCKS                                   B3021570
*                                                                       B3021580
         SPACE 2                                                        B3021590
         $HASPCB DOC=&DOC,LIST=&LIST GENERATE HASP CONTROL BLOCKS       B3021600
         TITLE '$A COMMAND PROCESSOR - ENTRY POINT PROCESSING'          B3021610
JES2B302 $MODNTRY CSECT=YES,EXIT=EXITB302  MODIFICATION ENTRY           B3021620
         SPACE 2                                                        B3021630
*        COMNULOP FLAGS SET BY HASPCJB2                                 B3021640
         SPACE 1                                                        B3021650
CCJFJOBF EQU   X'80'               JOB FOUND FLAG                       B3021660
CCJFDUMP EQU   X'40'               VERB IS C WITH DUMP FLAG             B3021670
CPJQOPER EQU   X'10'               CANCEL A JOB(S) DATA SETS            B3021680
CLJHOLD  EQU   X'04'               LIST HELD DATA SETS FLAG             B3021690
CCJFSTOP EQU   X'02'               VERB IS P (STOP) FLAG                B3021700
CCJFPURG EQU   X'01'               VERB IS C WITH PURGE FLAG            B3021710
         EJECT                                                          B3021720
         L     R1,APCESR1          PICK UP JQE POINTER                  B3021722
         USING JQEDSECT,R1         ESTABLISH JQE ADDRESSABILITY         B3021730
         MVI   APCE$$X,$$W         SET DEFAULT RETURN OF NO             B3021740
         TM    JQETYPE,$XEQ        IS IT ON EXECUTION QUEUE?            B3021750
         BNO   EXITB302            NO,COMMAND ALLOWABLE                 B3021760
         TM    COMFLAG,CMBFLAGW    IS THIS A REMOTE TERMINAL?           B3021770
         BZ    EXITB302            NO,COMMAND IS ALLOWABLE              B3021780
         DROP  R1                  END JQE ADDRESSABILITY               B3021790
         SPACE 1                                                        B3021800
         SR    R1,R1               CLEAR REGISTER                       B3021810
         IC    R1,COMJRMT          GET REMOTE NUMBER                    B3021820
         BAL   R4,FINDADCT         LOCATE CALLERS ADCT                  B3021830
         USING ADCTDSCT,R1         ESTABLISH ADCT ADDRESSABILITY        B3021840
         TM    ADCTFLG1,ADC1ALLC   IS HE ALLOWED TO DO THIS?            B3021850
         BO    EXITB302            YES,RETURN WITH 'NO' CONDITION       B3021860
         MVI   APCE$$X,$$X         SET 'YES' CONDITION FOR ERROR        B3021870
         SPACE 2                                                        B3021880
EXITB302 $MODEXIT ,                RETURN TO JES2 CODE FOR NEXT COMMAND B3021890
         SPACE 1                                                        B3021900
         DROP  R1                  END ADCT ADDRESSABILITY              B3021910
         TITLE '$A COMMANDS - ADCT LOCATE SUBROUTINE'                   B3021920
*********************************************************************** B3021930
*        SUB-ROUTINE TO LOCATE THE ADCT FOR A DESIRED REMOTE          * B3021940
*        NUMBER.                                                      * B3021950
*        ON ENTRY                  R1  =  REMOTE NUMBER (LOCAL = ZERO)* B3021960
*        ON EXIT                   R1  =  ADDRESS OF ITS ADCT         * B3021970
*********************************************************************** B3021980
         SPACE 3                                                        B3021990
FINDADCT $SUBIN SAVE=(R2,R3,R4)    ENTRY                                B3022000
         LA    R3,ADCTLGTH+CBPRLGTH SET LENGTH OF EACH ADCT             B3022010
         LR    R2,R1               GET NUMBER OF ADCT REQUIRED          B3022020
         MR    R2,R2               CALCULATE OFFSET IN ADCT LIST        B3022030
         L     R2,AHCTADCT         GET START OF ADCT LIST               B3022040
         LA    R1,0(R2,R3)         GET ADDRESS OF REQUIRED ADCT         B3022050
FINDADCX $SUBOUT ,                  RETURN TO CALLER                    B3022060
         SPACE 3                                                        B3022070
         END                                                            B3022080
