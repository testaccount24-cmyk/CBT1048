***********************************************************************
*        ADD DELETE OR CHANGE A PARM FILE ENTRY
***********************************************************************
         SPACE 2
DEFINE   ZCSECT 12,9,GETMAIN=1,WKLEN=WALENGTH
         LR    REG8,REG1           LOAD ADDRESS OF THE USER PARM FIELD
         USING USERPARM,REG8       ADDRESSABILITY FOR IT
         USING WORKAREA,REG13      ADDRESSABILITY FOR DSECT
         EJECT
***********************************************************************
*        INVITE HIM TO ENTER HIS FUNCTION
***********************************************************************
         SPACE 2
INVITE   DS    0H
         PUTSCRN STRTSCRN,(FCODE),MF=(E,CALLLIST)
INVITE2  EQU   *
         CLI   FCODE,C'X'          DOES HE WANT TO RETURN
         BE    DEFNEXIT            YES. RETURN TO CALLER
         IC    REG15,FCODE         LOAD THE RESULT
         N     REG15,=F'15'        GET NUMERIC PART
         LTR   REG15,REG15         ZERO OR BLANK OR SOMETHING ?
         BE    INVITE              YES. GO ASK AGAIN
         C     REG15,HIGHEST       IS IT WITHIN RANGE
         BH    BADNUMB             NO. GO GIVE AN ERROR
         SLL   REG15,2             MULTIPLY BY FOUR
         L     REG15,RTNTABLE-4(REG15) GET ADDRESS OF ROUTINE
         BALR  REG14,REG15         INVOKE THE ROUTINE
         B     INVITE              AND THE GO ASK AGAIN
         EJECT
***********************************************************************
*        HE WANTS TO UPDATE HIS EDIT PARAMETERS
***********************************************************************
         SPACE 2
EDITPRM  DS    0H
         L     REG1,STRTZONE       LOAD START ZONE
         LA    REG1,1(REG1)        ADD 1 FOR OFFSET
         CVD   REG1,DBLWORD        CONVERT TO DECIMAL
         OI    DBLWORD+L'DBLWORD-1,X'0F'
         UNPK  SCZONE1,DBLWORD+6(2) AND EDIT TO SCREEN
         L     REG1,ENDZONE        LOAD START ZONE
         CVD   REG1,DBLWORD        CONVERT TO DECIMAL
         OI    DBLWORD+L'DBLWORD-1,X'0F'
         UNPK  SCZONE2,DBLWORD+6(2)  AND EDIT TO SCREEN
         L     REG1,COLSTART       LOAD START ZONE
         CVD   REG1,DBLWORD        CONVERT TO DECIMAL
         OI    DBLWORD+L'DBLWORD-1,X'0F'
         UNPK  SCCOL,DBLWORD+6(2)     AND EDIT TO SCREEN
RESHOW   EQU   *
         XC    FUNCTION,FUNCTION   CLEAR THE FUNCTION AREA
  PUTSCRN  INVSCRN,(FUNCTION,UPUSRID,UPPASSWD,AUTHORTY,SCZONE1,SCZONE2,S
               SCCOL,OSDSN,DCB,VOL,UNIT,SPACE,HEX),                    X
               MF=(E,CALLLIST)     PREPARE THE PASS LIST
         CLI   FUNCTION,C'X'       EXIT REQUIRED ?
         BE    REMAP               YES. PUT DATA BACK IN TABLE
         CLI   FUNCTION,C'C'       DOES HE WANT TO FORGET IT ALL
         BE    INVITE              YES. EXIT FROM HERE
         B     RESHOW              OTHERWISE RESHOW THE SCREEN
         EJECT
***********************************************************************
*        MAP THE SCREEN DATA BACK INTO USER PARM AREA
***********************************************************************
         SPACE 2
REMAP    DS    0H
         PACK  DBLWORD,SCZONE1     PICK UP START ZONE
         CVB   REG1,DBLWORD        CONVERT TO BINARY
         BCTR  REG1,0              SUBTRACT 1 FOR OFFSET
         ST    REG1,STRTZONE       AND UPDATE THE FIELD
         PACK  DBLWORD,SCZONE2     PICK UP END ZONE
         CVB   REG1,DBLWORD        CONVERT TO BINARY
         ST    REG1,ENDZONE        STORE IN DEFAULT AREA
         PACK  DBLWORD,SCCOL       PICK UP START COLUMN
         CVB   REG1,DBLWORD        CONVERT TO BINARY
         ST    REG1,COLSTART       AND UPDATE WORK AREA
         B     INVITE              GO SHOW HIM THE RESULT
         EJECT
***********************************************************************
*        USER WISHES TO UPDATE HIS PASSWORD ETC
***********************************************************************
         SPACE 2
USERDEFN DS    0H
         XC    FUNCTION,FUNCTION   CLEAR THE FUNCTION CODE
         PUTSCRN UDSCRN,(FUNCTION,UPUSRID,UPPASSWD,AUTHORTY),          X
               MF=(E,CALLLIST)
         CLI   FUNCTION,C'X'       WISHES TO RETURN ?
         BE    INVITE              YES. EXIT FROM HERE
         B     USERDEFN            OTHERWISE LOOP TILL HE'S HAPPY
         EJECT
***********************************************************************
*        ADD OR DELETE USER PARAMETERS
***********************************************************************
         SPACE 2
CHGPARM  DS    0H
         PUTSCRN CPSCRN,(FUNCTION,UPUSRID,UPPASSWD,AUTHORTY),          X
               MF=(E,CALLLIST)
         EJECT
***********************************************************************
*        RETURN TO CALLER
***********************************************************************
         SPACE 2
DEFNEXIT DS    0H
         ZRETURN
         EJECT
***********************************************************************
*        ERROR ROUTINES
***********************************************************************
         SPACE 2
BADNUMB  DS    0H
         PUTSCRN BNSCRN,(FCODE),MF=(E,CALLLIST)
         B     INVITE2             REENTER ORIGINAL ROUTINE
         EJECT
***********************************************************************
*        CONSTANTS NOT CHANGE BY THIS PROGRAM
***********************************************************************
         SPACE 2
HIGHEST  DC    F'4'                HIGHEST VALID RETURNED DATA
RTNTABLE DS    0F
         DC    A(EDITPRM)          CHANGE EDIT PARAMETERS
         DC    A(USERDEFN)         CHANGE USER DEFINITION
         DC    A(CHGPARM)          ADD OR DELETE USER ID'S
SCREENSF LINK  EP=SCREEN,SF=L
         LTORG
         EJECT
***********************************************************************
*        DSECTS USED IN THE PROGRAM
***********************************************************************
         SPACE 2
WORKAREA DSECT
         DS    18F                 REGISTER SAVE AREA
DBLWORD  DS    D
CALLLIST DS    20F                 WORK AREA TO BUILD PASS LISTS
FCODE    DS    C                   RETURN FUNCTION CODE FROM START SCRN
FUNCTION DS    C                   RETURN FUNCTION FROM EDIT SCREEN
SCZONE1  DS    CL3                 START ZONE
SCZONE2  DS    CL3                 END ZONE
SCCOL    DS    CL3                 EDIT START COLUMN
WALENGTH EQU   *-WORKAREA
         SPACE 2
         USERPARM
         EJECT
***********************************************************************
*        SCREEN FORMAT AREAS
***********************************************************************
         SPACE 2
DEFINE   CSECT ,                   RETURN TO PRIMARY CSECT
         PRINT NOGEN
STRTSCRN SCREEN CLEAR=YES,REPLY=YES
         DATA  1,1,'*MOPE* HIT ME WITH A FUNCTION CODE ===>'
         DATA  1,*,FCODE,BRIGHT=YES,CHANGE=YES,CURSOR=YES,PASS=YES,    X
               INDEX=0
         DATA  2,1,'---------------------------------------------------X
               -----------------'
         DATA  4,5,'OPTIONS AVAILAIBLE ARE:'
         DATA  *,10,'1',BRIGHT=YES
         DATA  S,12,'CHANGE EDIT DEFAULT PARAMETERS'
         DATA  *,10,'2',BRIGHT=YES
         DATA  S,12,'CHANGE YOUR MOPE USER DEFINTITION'
         DATA  *,10,'3',BRIGHT=YES
         DATA  S,12,'ADD OR DELETE USERS (YOU MUST BE AUTHORITY ''M'')'
         DATA  *,10,'X',BRIGHT=YES
         DATA  S,12,'EXIT TO THE PREVIOUS FRAME'
         DATA  24,1,'FRAME LEVEL: 2'
         DATA  24,56,'YOU''RE GETTING IN DEEP!'
         SCEND
INVSCRN  SCREEN CLEAR=YES,REPLY=YES
         DATA  1,1,'CHANGE DEFAULT EDIT PARAMETERS'
         DATA  *,1,'----------------------------------------------------
               --------------'
         DATA  4,5,'FUNCTION ===>'
         DATA  S,*,FUNCTION,BRIGHT=YES,CHANGE=YES,CURSOR=YES,PASS=YES, X
               INDEX=0
         DATA  S,*,'  (''X'' = ACCEPT AND RETURN TO PREVIOUS FRAME)'
         DATA  *,1,' '
         DATA  *,5,'USER:'
         DATA  S,*,UPUSRID,BRIGHT=YES,PASS=YES,INDEX=1
         DATA  S,*,'PASSWORD:'
         DATA  S,*,UPPASSWD,BRIGHT=YES,PASS=YES,INDEX=2
         DATA  S,*,'AUTHORITY:'
         DATA  S,*,AUTHORTY,BRIGHT=YES,PASS=YES,INDEX=3
         DATA  13,5,'EDIT PARAMETERS:',BRIGHT=YES
         DATA  15,5,'ZONE FIELD ===>'
         DATA  S,*,SCZONE1,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=4
         DATA  S,*,SCZONE2,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=5
         DATA  *,5,'EDIT COLUMN ==>'
         DATA  S,*,SCCOL,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=6
         DATA  *,5,'HEX ? ========>'
         DATA  S,*,HEX,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=12
         DATA  *,5,'DEFAULT DSN ==>'
         DATA  S,*,OSDSN,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=7
         DATA  *,5,'DCB ==========>'
         DATA  S,*,DCB,BRIGHT=YES,CHANGE=YES,PASS=YES,INDEX=8
         DATA  *,5,'VOL ==========>'
         DATA  S,*,VOL,BRIGHT=YES,CHANGE=YES,PASS=YES,INDEX=9
         DATA  *,5,'UNIT =========>'
         DATA  S,*,UNIT,BRIGHT=YES,CHANGE=YES,PASS=YES,INDEX=10
         DATA  *,5,'SPACE ========>'
         DATA  S,*,SPACE,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=11
         SCEND
         PRINT GEN
BNSCRN   SCREEN CLEAR=NO,REPLY=YES
         DATA  22,5,'LET''S PRETEND IT WASN''T YOU WHO ENTERED THAT RUBX
               BISH',BRIGHT=YES
         DATA  *,15,'HAVE ANOTHER GO ===>'
         DATA  S,*,FCODE,BRIGHT=YES,CHANGE=YES,CURSOR=YES,PASS=YES,    X
               INDEX=0
         SCEND
UDSCRN   SCREEN CLEAR=YES,REPLY=YES
         DATA  1,1,'*MOPE*    USER DEFINITION'
         DATA  *,1,'----------------------------------------------------
               ----------------'
         DATA  5,5,'USER ID    ===>'
         DATA  S,*,UPUSRID,BRIGHT=YES,PASS=YES,INDEX=1
         DATA  *,5,'PASSWORD   ===>'
         DATA  S,*,UPPASSWD,BRIGHT=YES,CHANGE=YES,PASS=YES,INDEX=2,    X
               CURSOR=YES
         DATA  *,5,'AUTHORITY  ===>'
         DATA  S,*,AUTHORTY,BRIGHT=YES,PASS=YES,INDEX=3
         DATA  12,5,'ENTER ''X'' WHEN REDEFINTION COMPLETE ===>'
         DATA  S,*,FUNCTION,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=0
         SCEND
CPSCRN   SCREEN CLEAR=YES,REPLY=YES
         DATA  1,1,'*MOPE*    USER ADDITION/DELETION'
         DATA  *,1,'----------------------------------------------------
               ----------------'
         DATA  5,5,'USER ID    ===>'
         DATA  S,*,UPUSRID,BRIGHT=YES,PASS=YES,INDEX=1
         DATA  *,5,'PASSWORD   ===>'
         DATA  S,*,UPPASSWD,BRIGHT=YES,CHANGE=YES,PASS=YES,INDEX=2,    X
               CURSOR=YES
         DATA  *,5,'AUTHORITY  ===>'
         DATA  S,*,AUTHORTY,BRIGHT=YES,PASS=YES,INDEX=3
         DATA  12,5,'FUNCTION  ===>'
         DATA  S,*,FUNCTION,CHANGE=YES,BRIGHT=YES,PASS=YES,INDEX=0
         DATA  S,*,'  (''A'' OR ''D'')'
         SCEND
         PRINT GEN
         END
