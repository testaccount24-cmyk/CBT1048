B007     TITLE 'HASP MODIFICATIONS DISPATCHER - PROLOGUE'               MVB00130
*********************************************************************** MVB00140
*                                                                     * MVB00150
* MODULE NAME = JES2B007 ( JES2B007 CSECT )                           * MVB00160
*                                                                     * MVB00170
* DESCRIPTIVE NAME = JES2B007 CSECT OF JES2 FREEAPCE SERVICE RTN      * MVB00180
*                                                                     * MVB00190
* COPYRIGHT = NONE                                                    * MVB00200
*                                                                     * MVB00210
* STATUS = OS/VS2 RELEASE 2,3         -- JES2 VERSION 4, LEVEL 0      * MVB00220
*                                                                     * MVB00230
* FUNCTION =   THIS ROUTINE IS THE SERVICE ROUTINE THAT FREES         * MVB00240
*              SECONDARY APCE'S FROM A PRIMARY APCE, AND RETURNS THEM * MVB00250
*              TO THE AVAILABLE QUEUE.                                * MVB00260
*              SECONDARY APCE'S HAVE NAMES ASSIGNED TO THEM, AND      * MVB00270
*              THEY MAY BE FREED EITHER BY NAME OR BY ADDRESS.        * MVB00280
*                                                                     * MVB00290
* NOTES = SEE BELOW                                                   * MVB00300
*                                                                     * MVB00310
*    DEPENDENCIES = NONE                                              * MVB00320
*                                                                     * MVB00330
*    RESTRICTIONS = NONE                                              * MVB00340
*                                                                     * MVB00350
*    REGISTER CONVENTIONS = R1     - PARAMETER ADDRESS                * MVB00360
*                           R15    - WORK REGISTER                      MVB00370
*                                                                     * MVB00380
*              ALL OTHER REGISTERS ARE UNUSED BY THIS ROUTINE.        * MVB00390
*                                                                     * MVB00400
*    PATCH LABEL = NONE                                               * MVB00410
*                                                                     * MVB00420
* MODULE TYPE = SERVICE ROUTINE                                       * MVB00430
*                                                                     * MVB00440
*    PROCESSOR = ASSEMBLER F                                          * MVB00450
*                                                                     * MVB00460
*    MODULE SIZE = 26 BYTES, DECIMAL                                  * MVB00470
*                                                                     * MVB00480
*    ATTRIBUTES = REUSABLE, REENTRANT, READ ONLY                      * MVB00490
*                                                                     * MVB00500
* ENTRY POINT = JES2B007                                              * MVB00510
*                                                                     * MVB00520
*    PURPOSE = SEE FUNCTION                                           * MVB00530
*                                                                     * MVB00540
*    LINKAGE = STANDARD OS/VS PROGRAM LINKAGE                         * MVB00550
*                                                                     * MVB00560
* INPUT = R1 = +VE  : ADDRESS OF NAME OF APCE TO BE FREED            *  MVB00570
*         R1 = -VE  : COMPLEMENTED ADDRESS OF APCE TO BE FREED       *  MVB00580
*                                                                     * MVB00590
* OUTPUT = NONE                                                       * MVB00600
*                                                                     * MVB00610
* EXIT-NORMAL = PASS CONTROL TO REQUESTED ROUTINE                     * MVB00620
*                                                                     * MVB00630
* EXIT-ERROR = EXIT TO THE $UDUMP SERVICE ROUTINE IF NOT APCE         * MVB00640
*                                                                     * MVB00650
* EXTERNAL REFERENCES = NONE                                          * MVB00660
*                                                                     * MVB00670
*    ROUTINES = NONE                                                  * MVB00680
*                                                                     * MVB00690
*    DATA AREAS = SEE $HASPCB MACRO EXPANSION                         * MVB00700
*                                                                     * MVB00710
*    CONTROL BLOCKS = SEE $HASPCB MACRO EXPANSION                     * MVB00720
*                                                                     * MVB00730
* TABLES = NONE                                                       * MVB00740
*                                                                     * MVB00750
* MACROS = SEE $HASPCB MACRO FOR CONTROL BLOCK MACROS USED            * MVB00760
*                                                                     * MVB00770
* CHANGE ACTIVITY = NONE                                              * MVB00780
*                                                                     * MVB00790
*********************************************************************** MVB00800
         TITLE 'HASP MODIFICATIONS FREE SECONDARY APCE SERVICE ROUTINE' MVB00810
*                                                                       MVB00820
***** $HASPCB *****           GENERATE HASP CONTROL BLOCKS              MVB00830
*                                                                       MVB00840
         MACRO                                                          MVB00850
         $HASPCB &DOC=NO,&LIST=NO                                       MVB00860
         GBLC  &PRINT,&GEN,&DATA                                        MVB00870
         PUSH  PRINT                                                    MVB00880
         PRINT &PRINT                                                   MVB00890
         $CBPR DOC=&DOC            GENERATE CB PREFIX DSECT             MVB00900
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT              MVB00910
         $AHCT DOC=&DOC            GENERATE MODS AHCT DSECT             MVB00920
         $APCE DOC=&DOC            GENERATE MODS APCE DSECT             MVB00930
         $APCE TYPE=SPCE,DOC=NO    GENERATE MODS SPCE DSECT             MVB00940
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT              MVB00950
         POP   PRINT                                                    MVB00960
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS           MVB00970
         MEND                                                           MVB00980
         TITLE 'HASP MODIFICATION ROUTINES: FREEAPCE SERVICE ROUTINE'   MVB00990
JES2B007 CSECT                                                          MVB01000
         SPACE 5                                                        MVB01010
         COPY  $HASPGEN            COPY HESPGEN PARAMETERS              MVB01020
         TITLE 'HASP CONTROL BLOCKS'                                    MVB01030
*                                                                       MVB01040
*              DOCUMENTATION OPTIONS FOR THIS ASSEMBLY                  MVB01050
*                                                                       MVB01060
         SPACE 5                                                        MVB01070
         $SYSPARM (OFF,GEN,NODATA,NO,NO)                                MVB01080
         SPACE 5                                                        MVB01090
*                                                                       MVB01100
*              GENERATE HASP CONTROL BLOCKS                             MVB01110
*                                                                       MVB01120
         SPACE 5                                                        MVB01130
        $HASPCB DOC=&DOC,LIST=&LIST GENERATE HASP CONTROL BLOCKS        MVB01140
         TITLE 'HASP MODIFICATION ROUTINES: FREEAPCE SERVICE ROUTINE'   MVB01150
*********************************************************************** MVB01160
*        HASP MODIFICATION ROUTINES: FREEAPCE SERVICE ROUTINE         * MVB01170
*********************************************************************** MVB01180
         SPACE 1                                                        MVB01190
*        UPON ENTRY, IF R1 +VE: ADDRESS OF NAME OF APCE TO BE FREED     MVB01200
*                    IF R1 -VE: COMPLEMENTED ADDRESS OF APCE TO FREE    MVB01210
*        OPTIONALLY, R1 MAY POINT AT THE CHARACTER CONSTANT 'ALL ', IN  MVB01220
*         WHICH CASE, ALL SECONDARY APCE'S CHAINED TO THE CURRENT       MVB01230
*         PRIMARY APCE WILL BE FREED AND RETURNED TO THE AVAILABLE Q.   MVB01240
         SPACE 5                                                        MVB01250
***(LOGIC):-                                                            MVB01260
         SPACE 1                                                        MVB01270
*     1. IF R1 IS +VE, THEN THE SECONDARY APCE CHAIN IS SCANNED TO      MVB01280
*        LOCATE THE REQUIRED APCE.                                      MVB01290
*     2. THE PREFIX OF THE SECONDARY APCE IS EXEMINED TO ENSURE THAT    MVB01300
*        IT REALLY IS AN APCE, AND IF NOT THE USER DUMP SERVICE ROUTINE MVB01310
*        IS INVOKED, WITH A CODE OF 4.                                  MVB01320
*     3. THE SECONDARY APCE IS REMOVED FROM THE CHAIN OF SECONDARY      MVB01330
*        APCE'S BELONGING TO THIS PRIMARY APCE.                         MVB01340
*     4. THE RELEASED APCE IS RETURNED TO THE TOP OF THE AVAILABLE Q.   MVB01350
         EJECT                                                          MVB01360
*********************************************************************** MVB01370
*        HASP MODIFICATION ROUTINES: FREEAPCE SERVICE ROUTINE         * MVB01380
*********************************************************************** MVB01390
         SPACE 1                                                        MVB01400
         USING HCTDSECT,BASE1      ESTABLISH HCT ADDRESSABILITY         MVB01410
         USING AHCTDSCT,R6         ESTABLISH AHCT ADDRESSABILITY        MVB01420
         USING SPCEDSCT,R1         ESTABLISH SPCE ADDRESSABILITY        MVB01430
JES2B007 $SRVNTRY ,                ENTER SERVICE ROUTINE                MVB01440
      $REGSAVE SAVE=(R2,R4)        PRESERVE REGISTERS USED BY THIS RTN  MVB01450
         LTR   R1,R1               DETERMINE IF R1 +VE OR -VE           MVB01460
         BP    FRESPCE1            BR IF R1 POINTS AT CHARACTER CONST   MVB01470
         LCR   R1,R1               GET ADDRESS OF SPCE IN CORRECT FORMA MVB01480
         CLC   SPCENAME,=C'APCE'   IS THIS A PRIMARY APCE               MVB01490
         BNE   FRESPCE2            BR IF NOT TO FREE IT                 MVB01500
        $UDUMP 8,EXIT=FRESPCEX     DUMP: ATTEMPTING TO FREE PRIMARY APC MVB01510
         SPACE 1                                                        MVB01520
FRESPCE1 LR    R15,R1              POINT R15 AT NAME OF APCE            MVB01530
         CLC   0(4,R1),=C'ALL '    IS IT REQUEST TO FREE ALL            MVB01540
         BE    FRESPCEA            GO TO FREE ALL SECONDARY APCES       MVB01550
      $FNDAPCE (R15)               LOCATE ADDRESS OF SECONDARY APCE     MVB01560
         BNE   FRESPCEX            EXIT IF APCE NOT FOUND               MVB01570
FRESPCE2 BAL   R4,FREEIT           DECHAIN APCE & ATTACH TO AVAILABLE Q MVB01580
         SPACE 1                                                        MVB01590
FRESPCEX ICM   R1,7,APCESNXT+1     TEST IF ANY SECONDARIES LEFT         MVB01600
         BNZ   *+8                 BR IF NOT EMPTY                      MVB01610
         NI    APCEFLG1,255-APF1SEC INDICATE NO SECONDARIES CHAINED     MVB01620
      $REGREST ,                   RESTORE ALL SAVED REGISTERS          MVB01630
      $SRVEXIT ,                   RETURN TO CALLER                     MVB01640
         EJECT                                                          MVB01650
*********************************************************************** MVB01660
*        FREE ALL SECONDARY SPCES                                     * MVB01670
*********************************************************************** MVB01680
         SPACE 1                                                        MVB01690
FRESPCEA NULL  ,                   FREE ALL SECONDARY APCES             MVB01700
         L     R1,APCESNXT         GET ADDRESS OF FIRST SECONDARY APCE  MVB01710
FRESPCE3 LTRR  R1,R1               TEST FOR END OF CHAIN                MVB01720
         BZ    FRESPCEX            EXIT IF ALL FREED                    MVB01730
         L     R2,SPCESNXT         GET ADDRESS OF NEXT SECONDARY APCE   MVB01740
         BAL   R4,FREEIT           FREE THE SECONDARY APCE              MVB01750
         LR    R1,R2               POINT R1 AT THE NEXT SECONDARY       MVB01760
         B     FRESPCE3            LOOP BACK TO FREE THIS SPCE          MVB01770
         EJECT                                                          MVB01780
*********************************************************************** MVB01790
*        DECHAIN SPCE FORM SECONDARY CHAIN AND ADD TO AVAILABLE QUEUE * MVB01800
*********************************************************************** MVB01810
         SPACE 1                                                        MVB01820
FREEIT $SUBIN  ,                   ENETR ROUTINE                        MVB01830
         LR    R15,R1              POINT R15 AT SPCE TO FREE            MVB01840
         S     R15,=A(CBPRLGTH)    POINT R15 AT C/B PREFIX              MVB01850
         CLC   CBPRID-CBPRDSCT(L'CBPRID,R15),=C'APCE' IS IT AN APCE     MVB01860
         BE    FREEIT2             BR IF IT IS A VALID SPCE             MVB01870
       $UDUMP  4,EXIT=FRESPCEX     DUMP: ATTEMPT TO FREE OTHER THAN AP  MVB01880
FREEIT2  MVC   SPCENAME,=C'APCE'   RE-ESTABLISH NAME AS 'APCE'          MVB01890
         L     R15,SPCESNXT        GET ADDRESS OF NEXT SEC APCE ON CHAI MVB01900
         LTRR  R15,R15             VERIFY THIS IS NOT LAST ON CHAIN     MVB01910
         BZ    *+10                BR IF THIS IS LAST                   MVB01920
         MVC   SPCESPRV-SPCEDSCT(L'SPCESPRV,R15),SPCESPRV RESET BACK CH MVB01930
         L     R15,SPCESPRV        GET ADDRESS OF PREVIOUS APCE IN CHAI MVB01940
         MVC   SPCESNXT-SPCEDSCT(L'SPCESNXT,R15),SPCESNXT CH PRV TO NXT MVB01950
         L     R15,AHCTFPCE        GET ADDRESS OF 1ST AVAILABLE SPCE    MVB01960
         ST    R15,SPCENEXT        POINT THIS APCE AT 1ST AVAILABLE     MVB01970
         CS    R15,R1,AHCTFPCE     ESTABLISH THIS AS 1ST AVAILABLE      MVB01980
         BNE   *-8                 TRY AGAIN IF CHAINING FAILED         MVB01990
       $SUBOUT ,                   RETURN TO CALLER                     MVB02000
         SPACE 5                                                        MVB02010
         END                                                            MVB02020
