JES2B202 TITLE 'DATA/100 MODEL 70 COMPRESSION  - CONTROL BLOCK GENERATICB2020140
               ON MACRO DEFINITIONS'                                    B2020150
*                                                                       B2020160
**** $HASPCB ****                  GENERATE HASP CONTROL BLOCKS         B2020170
*                                                                       B2020180
         MACRO                                                          B2020190
         $HASPCB &DOC=NO,&LIST=NO                                       B2020200
         GBLC  &PRINT,&GEN,&DATA                                        B2020210
         PUSH  PRINT                                                    B2020220
         PRINT &PRINT                                                   B2020230
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT              B2020240
         $AHCT DOC=&DOC            GENERATE AHCT DSECT                  B2020250
         $ADCT DOC=&DOC            GENERATE HASP ADCT DSECT             B2020260
         $APCE DOC=&DOC            GENERATE MODS APCE DSECT             B2020270
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT              B2020280
         $DCT  DOC=&DOC            GENERATE HASP DCT DSECT              B2020290
         $CBPR DOC=&DOC            GENERATE HASP CBPR DSECT             B2020300
         SPACE 1                                                        B2020310
         POP   PRINT                                                    B2020320
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS           B2020330
         MEND                                                           B2020340
         TITLE 'JES2B202 - DATA/100 MODEL 70 COMPRESSION '              B2020350
JES2B202 CSECT             DATA/100 MODEL 70 COMPRESSION  MOD PROGRAM   B2020360
         SPACE 2                                                        B2020370
*******************************************************************     B2020380
*                                                                 *     B2020390
*        PROGRAM ID. :             JES2B202                       *     B2020400
*                                                                 *     B2020410
*        DATE WRITTEN :            SEPTEMBER 1977.                *     B2020420
*                                                                 *     B2020430
*        AUTHOR :                  J.NOLS                         *     B2020440
*                                                                 *     B2020450
*        LOCATION :                MONSANTO MISD/TSG BRUSSELS     *     B2020460
*                                                                 *     B2020470
*******************************************************************     B2020480
         SPACE 3                                                        B2020490
*******************************************************************     B2020500
*                                                                 *     B2020510
*        PURPOSE                                                  *     B2020520
*       ---------                                                 *     B2020530
*                                                                 *     B2020540
*        THIS IS A MONSANTO JES2 MODIFICATION MODULE.             *     B2020550
*        IT IS LOADED AT JES2 INITIALISATION UPON REQUEST VIA     *     B2020560
*        JES2B001,THE MONSANTO INITIALISATION MODULE,AND IS       *     B2020570
*        ENTERED VIA A $CBAL FROM THE HASP REMOTE TERMINAL        *     B2020580
*        PUT ROUTINE IN HASPRTAM AT STATEMENT S0583600.           *     B2020590
*                                                                 *     B2020600
*        ON ENTRY HASPRTAM HAS ESTABLISHED THE FOLLOWING          *     B2020610
*        REGISTER CONVENTIONS IN ADDITION TO NORMAL HASP USAGE:-  *     B2020620
*                                                                 *     B2020630
*         R0     ----------->      CONTAINS LENGTH OF THE LINE    *     B2020640
*                                   TO BE COMPRESSED.             *     B2020650
*         R1     ----------->      CONTAINS START ADDRESS OF THE  *     B2020660
*                                   LINE TO BE COMPRESSED.        *     B2020670
*         R3     ----------->      DCT                            *     B2020680
*                                                                 *     B2020690
*******************************************************************     B2020700
         EJECT                                                          B2020710
         COPY  $HASPGEN            HASP OPTIONS                         B2020720
         TITLE 'CONTROL BLOCK OPTIONS'                                  B2020730
         $SYSPARM (OFF,GEN,DATA,NO,NO)     SET DOCUMENTATION OPTIONS    B2020740
         SPACE 3                                                        B2020750
*                                                                       B2020760
*        GENERATE HASP CONTROL BLOCKS                                   B2020770
*                                                                       B2020780
         SPACE 2                                                        B2020790
         $HASPCB DOC=&DOC,LIST=&LIST GENERATE HASP CONTROL BLOCKS       B2020800
         TITLE 'JES2B202 - ENTRY'                                       B2020810
*********************************************************************** B2020820
*        MOD ENTRY                                                    * B2020830
*********************************************************************** B2020840
         SPACE 1                                                        B2020850
JES2B202 $MODNTRY CSECT=YES,EXIT=EXITB202,DOC=&DOC MODIFICATION ENTRY   B2020860
         SPACE 2                                                        B2020870
HLIMIT   EQU   APCEUSER,4          ADDRESS END +1 OF THE LINE TO COMP.  B2020880
HCHAR    EQU   APCEUSER+4,1        CHARACTER CURRENTLY TESTED           B2020890
HSIGNAL  EQU   X'11'               COMPRESSION SIGNAL                   B2020900
         SPACE 3                                                        B2020910
         USING DCTDSECT,R3         ESTABLISH DCT ADDRESSABILITY         B2020920
         TITLE 'JES2B202 - MAIN PROCESSING ROUTINE'                     B2020930
*********************************************************************** B2020940
*                                                                     * B2020950
*        DATA 100 MODEL 70 COMPRESSION                                * B2020960
*                                                                     * B2020970
*********************************************************************** B2020980
         SPACE 1                                                        B2020990
         CLI   MDCTTYPE,DCTP2780   TEST FOR 2780 TERMINAL TYPE          B2021000
         BL    B202RTNO            BRANCH IF 2770                       B2021010
         SPACE 1                                                        B2021020
         L     WC,AHCTADCT         GET ADDRESS OF FIRST ADCT            B2021030
         USING ADCTDSCT,WC         ESTABLISH ADCT ADDRESSABILITY        B2021040
         SLR   WD,WD               ZEROISE                              B2021050
         IC    WD,DCTNO+1          INSERT REMOTE NUMBER                 B2021060
         LA    WB,ADCTLGTH+CBPRLGTH LOAD LENGTH OF ADCT                 B2021070
         MR    WA,WD               GET ADDRESS OF                       B2021080
         LA    WC,0(WB,WC)          REMOTE ADCT                         B2021090
         TM    ADCTINIT,ADCID100   COMPRESSION REQUIRED FOR THIS RMT?   B2021100
         BO    B202RTNO            NO, RETURN TO JES2                   B2021110
         DROP  WC                  DROP ADCT ADDRESSABILITY             B2021120
         SPACE 1                                                        B2021130
         LR    WA,R1               POINT TO BEGINNING                   B2021140
         AR    WA,R0               ADD LENGTH TO KNOW ADDRESS END +1    B2021150
         ST    WA,HLIMIT            AND STORE THIS ADDRESS              B2021160
         LR    WC,R1               POINT WC TO 1ST RECEIVING POSITION   B2021170
         SPACE 1                                                        B2021180
HLOOPNEQ LA    WA,1                SET LENGTH TO ONE                    B2021190
         LA    WB,1(,R1)           POINT WB TO NEXT POS. FOR 1ST CLC    B2021200
         C     WB,HLIMIT           END OF LINE ?                        B2021201
         BE    HDOMOVE             BRANCH IF YES                        B2021202
         CLI   0(R1),HSIGNAL       IS THIS LINE ALREADY COMPRESSED ?    B2021203
         BNE   *+12                NO, SKIP NEXT TEST                   B2021204
         TM    1(R1),X'C0'         IS THIS CONFIRME COMPRESSION ?       B2021205
         BO    B202RTYS            BRANCH IF LINE ALREADY COMPRESSED.   B2021206
         MVC   HCHAR,0(R1)         SAVE CHARACTER FOR MOVE              B2021210
         SPACE 1                                                        B2021220
HLOOPEQ  CLC   HCHAR,0(WB)         TEST FOR                             B2021230
         BNE   HDOMOVE                COMPRESSION                       B2021240
         LA    WA,1(,WA)           ADD ONE TO LENGTH OF NEXT MOVE       B2021250
         LA    WB,1(,WB)           POINT WB TO NEXT POSITION            B2021260
         C     WB,HLIMIT           END OF LINE ?                        B2021270
         BE    HDOMOVE             BRANCH IF YES                        B2021280
         B     HLOOPEQ             GO TEST NEXT POSITION                B2021290
         SPACE 1                                                        B2021300
HDOMOVE  CH    WA,=H'3'            MORE THAN 3 CHAR. EQUAL?             B2021310
         BH    HCOMP               YES, GO COMPRESS                     B2021320
         BCTR  WA,0                DECREMENT LENGTH FOR MOVE            B2021330
         EX    WA,HMOVE            MOVE 1, 2 OR 3 POSITIONS             B2021340
         LA    WC,1(WA,WC)         POINT WC TO 1ST RECEIVING POS. FOR - B2021350
HTESLIM  C     WB,HLIMIT           END OF LINE ?        /  NEXT MOVE    B2021360
         BE    HLEND               BRANCH IF YES                        B2021370
         LR    R1,WB               POINT R1 TO NEXT POS. TO BE TESTED   B2021380
         B     HLOOPNEQ            GO TEST NEXT POSITION                B2021390
         EJECT                                                          B2021400
******** CONT'D                                                         B2021410
         SPACE 3                                                        B2021420
HCOMP    MVI   0(WC),HSIGNAL       SET COMPRESSION SIGNAL               B2021430
         STC   WA,1(WC)            STORE LENGTH OF THIS COMPRESSED SEQ. B2021440
         MVC   2(1,WC),HCHAR       STORE CHARACTER                      B2021450
         CH    WA,=H'31'           MORE THAN 31 CHAR. EQUAL?            B2021460
         BH    HCOMP31             BRANCH IF YES                        B2021470
         OI    1(WC),X'C0'         SET ON THE TWO LEFTMOST BITS         B2021480
         LA    WC,3(,WC)           POINT WC TO 1ST RECEIVING POS. FOR - B2021490
         B     HTESLIM             GO TEST IF END OF LINE   / NEXT MOVE B2021500
         SPACE 1                                                        B2021510
HCOMP31  MVI   1(WC),31            SET LENGTH TO 31                     B2021520
         OI    1(WC),X'C0'         SET ON THE TWO LEFTMOST BITS         B2021530
         LA    R1,31(,R1)          ADJUST R1 POINTER                    B2021540
         LA    WC,3(,WC)           POINT TO 1ST RECEIVING POS. FOR -    B2021550
         SH    WA,=H'31'           SUB 31 FROM REMAINING LENGTH / NMOVE B2021560
         B     HDOMOVE              AND GO CONTINUE MOVE                B2021570
         SPACE 1                                                        B2021580
HLEND    LR    WB,WC               SAVE POINTER FOR NEXT MOVE           B2021590
         L     WA,APCESR0          LOAD LENGTH OF UNCOMPRESSED LINE     B2021600
         S     WC,APCESR1          COMPUTE LENGTH OF THE                B2021610
         ST    WC,APCESR0           LINE COMPRESSED                     B2021620
         SR    WA,WC               WA = LENGTH FOR NEXT MOVE            B2021630
         BZ    B202RTYS            BRANCH IF L' = ZERO                  B2021640
         MVI   0(WB),X'40'         MOVE BLANK TO 1ST POS. OF REST       B2021650
         S     WA,=F'1'            = L' -1                              B2021660
         BZ    B202RTYS            BRANCH IF L' = ZERO                  B2021665
         BCTR  WA,0                = L' -2                              B2021670
         EX    WA,B202BLOT         BLANK OUT REST OF THE LINE           B2021680
         B     B202RTYS                                                 B2021690
         SPACE 1                                                        B2021700
B202BLOT MVC   1(*-*,WB),0(WB)     *** EXECUTE ONLY ****                B2021710
         SPACE 1                                                        B2021720
B202RTYS MVI   APCE$$X,$$X         SET CONDITION CODE TO 'YES'          B2021730
         B     EXITB202            GO TO EXIT                           B2021740
         SPACE 1                                                        B2021750
B202RTNO MVI   APCE$$X,$$Y         SET CONDITION CODE TO 'NO'           B2021760
         B     EXITB202            GO TO EXIT                           B2021770
         SPACE 2                                                        B2021780
HMOVE    MVC   0(*-*,WC),0(R1)     ***  EXECUTE ONLY  ***               B2021790
         TITLE 'JES2B202 - EXIT'                                        B2021800
         SPACE 1                                                        B2021810
*********************************************************************** B2021820
*        MOD EXIT                                                     * B2021830
*********************************************************************** B2021840
         SPACE 1                                                        B2021850
EXITB202 $MODEXIT DOC=&DOC         RETURN TO JES2 CODE                  B2021860
         SPACE 2                                                        B2021870
         LTORG                                                          B2021880
         SPACE 1                                                        B2021890
         END                                                            B2021900
