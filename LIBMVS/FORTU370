//FORTU370 JOB (1580,5),'0116 .PSG.',CLASS=N
/*JOBPARM      TIME=6
//S2 EXEC ASMFCL,LOPT='NCAL,XREF,LIST'
//ASM.SYSIN DD *
         ENTRY SETUP
         ENTRY FIXUP
SETUP    CSECT
         STM   14,12,12(13)       SAVE REGISTERS IN CALLERS SAVE AREA
         BALR  12,0
         USING *,12               ESTABLISH ADDRESSABILITY
         ST    13,SAVEA+4         SAVE ADDRESS OF USERS SAVE AREA
         LR    11,13
         LA    13,SAVEA
         ST    13,8(11)           STORE ADDRESS OF SAVE AREA
         MVC   SVADDR(4),0(1)     SAVE ADDRESS OF FORTRAN STATEMENT
*                                  CALL ERRSET, DELETING THE 215I M
*                                 MESSAGE AND TRACEBACK.  AND STORING
*                                 A(FIXUP) IN OPTIONS TABLE AS USERS
*                                 SUBROUTINE
         CALL  ERRSET,(IERNO,INOAL,INOMES,ITRACE,ADDUSE),VL
         L     13,SAVEA+4
         LM    14,12,12(13)       RESTORE REGISTERS
         BR    14                 RETURN
FIXUP    STM   14,12,12(13)       SAVE REGISTERS IN USERS SAVE AREA
         BALR  12,0
         USING *,12
         L     11,=V(IHOERRM)    LOAD R11 WITH ADDRESS OF ERROR MONITOR
         MVC   ENDERR(4,11),SVADDR MOVE FORTRAN STATEMENT NO. TO EXIT
         MVI   SPER(11),255       SET OPTIONAL EXIT FLAG ON
         LM    14,12,12(13)       RESTORE REGISTERS
         BR    14                 AND RETURN
SAVEA    DC    18F'0'
SVADDR   DC    F'0'
IERNO    DC    F'215'
INOAL    DC    F'0'
INOMES   DC    F'-1'
ITRACE   DC    F'1'
ADDUSE   DC    A(FIXUP)
ENDERR   EQU   X'448'
SPER     EQU   X'494'
         LTORG
         END
//LKED.SYSLMOD  DD  DSN=SYS1.FORTX22(SETUP),DISP=SHR
