         MACRO                                                          MADG0130
&NAME   $PASAPCE  &ID,&STACK,&DOC=NO                                    MADG0140
         GBLC  &SRVRTN                                                  MADG0150
         LCLC  &R,&T,&NAME1                                             MADG0160
         LCLA  &N                                                       MADG0170
         AIF   ('&DOC' NE 'YES').NODOC                                  MADG0180
         SPACE 1                                                        MADG0190
*        CORRECT FORM IS :-                                             MADG0200
*        $PASAPCE <'ID' | ID | LABEL | (REG)> NAME OF APCE TO PASS      MADG0210
*                                  THE NAME OF THE APCE TO PASS MAY     MADG0220
*                                  BE A CHARACTER STRING IN QUOTES, A   MADG0230
*                                  NAME OF A FIELD CONTAINING THE NAME, MADG0240
*                                  A CHARACTER STRING WITHOUT QUOTES,   MADG0250
*                                  OR REGISTER NOTATION, IN WHICH CASE  MADG0260
*                                  THE REGISTER CONTAINS THE ADDRESS    MADG0270
*                                  OF THE APCE TO BE PASED.             MADG0280
*                                  OPTIONALLY, THE NAME MAY BE 'ALL',   MADG0290
*                                  IN WHICH CASE ALL SECONDARY APCE'S   MADG0300
*                                  ARE PASSED.                          MADG0310
*              < STACK | (REG) >   NAME OR ( IN REGISTER NOTATION )     MADG0320
*                                  ADDRESS OF AHCT STACK TO WHICH APCE' MADG0330
*                                  ARE TO BE PASSED                     MADG0340
*              <,DOC=NO | YES>     REQUEST FOR DOCUMENTATION            MADG0350
         SPACE 1                                                        MADG0360
*        THE PURPOSE OF THIS MACRO IS TO PASS THE SECONDARY APCE        MADG0370
*         ( CHAINED OFF TTHIS PRIMARY APCE ) WITH THE SPECIFIED NAME.   MADG0380
*         THE MACRO INVOKES THE SRVICE ROUTINE 'PASSAPCE' TO LOCATE THE MADG0390
*         SPECIFIED APCE, DECHAIN IT FROM THE CURRENT PRIMARY, AND      MADG0400
*         CHAIN IT ON THE APPROPRIATE AHCT PASSED STACK ( IN PUSH-DOWN  MADG0410
*         MODE ).  IF 'ALL' IS SPECIFIED, THEN ALL SECONDARY PACE'S     MADG0420
*         CHAINED TO THE CURRENT PRIMARY APCE ARE DECHAINED FROM THE    MADG0430
*         PRIMARY APCE AND CHAINED TO THE APPROPRAITE AHCT STACK.       MADG0440
         SPACE 1                                                        MADG0450
*        THE MACROS $PASAPCE / $CTCHPCE PROVIDE AN ELEGANT MECHANISM    MADG0460
*         FOR PASSING DATA TO OTHER MODIFICATION ROUTINES THAT ARE NOT  MADG0470
*         SUBORDINATE TO THE CURRENT ROUTINE.  ( I.E. ASYNCHRONOUS      MADG0480
*         MODIFICATION ROUTINES, OR ROUTINES INVOKED BY OTHER $CBAL'S   MADG0490
*         ELSEWHERE IN MAINLINE JES2 CODE ).                            MADG0500
*        NOTE THAT THE MACROS $DETAPCE / $ATTAPCE MAY BE USED TO TRANSF MADG0510
*         SECONDARY APCE'S TO SUBORDINATE MODIFICATION ROUTINES.        MADG0520
         SPACE 1                                                        MADG0530
.NODOC   ANOP                                                           MADG0540
&NAME1   SETC  '&NAME'                                                  MADG0550
         AIF   ('&ID' NE '').A01                                        MADG0560
         MNOTE 8,'NO APCE NAME SPECIFIED'                               MADG0570
         MEXIT                                                          MADG0580
.A01     ANOP                                                           MADG0590
&N       SETA  N'&ID                                                    MADG0600
         AIF   (&N EQ 1).NOK                                            MADG0610
         MNOTE 8,'INCORRECT PARAMETER LIST: TOO MANY OPERANDS'          MADG0620
         MEXIT                                                          MADG0630
.NOK     AIF   ('&ID'(1,1) NE '(').NOTREG                               MADG0640
&R       SETC  '&ID(1)'                                                 MADG0650
&NAME1   LCR   R1,&R               COMPLEMENT APCE ADDRESS IN R1        MADG0660
&NAME1   SETC  ' '                                                      MADG0670
         AGO   .SERV                                                    MADG0680
.NOTREG  ANOP                                                           MADG0690
&T       SETC  T'&ID                                                    MADG0700
         AIF   ('&T' EQ 'U').UNDEF                                      MADG0710
         AIF   ('&T' EQ 'N').SELFDEF                                    MADG0720
         AIF   ('&T' EQ 'C').CHAR                                       MADG0730
         MNOTE 8,'PARAMETER HAS TYPE ATTRIBUTE OF &T - INVALID'         MADG0740
         MEXIT                                                          MADG0750
.CHAR    ANOP                                                           MADG0760
&NAME1   LA    R1,&ID              POINT R1 AT PARAMETER LIST           MADG0770
&NAME1   SETC  ' '                                                      MADG0780
         AGO   .SERV                                                    MADG0790
.SELFDEF ANOP                                                           MADG0800
&NAME1   BAL   R1,*+8              POINT R1 AT PARAMETER LIST           MADG0810
         DC    CL4&ID              NAME OF APCE TO BE PASED             MADG0820
&NAME1   SETC  ' '                                                      MADG0830
         AGO   .SERV                                                    MADG0840
.UNDEF   ANOP                                                           MADG0850
&NAME1   BAL   R1,*+8              POINT R1 AT NAME OF APCE TO BE PASED MADG0860
         DC    CL4'&ID'            NAME OF APCE TO BE PASED             MADG0870
.SERV    ANOP                                                           MADG0880
         AIF   ('&STACK' NE '').A01                                     MADG0890
         MNOTE 8,'NO STACK NAME SPECIFIED'                              MADG0900
         MEXIT                                                          MADG0910
.A01     ANOP                                                           MADG0920
&N       SETA  N'&STACK                                                 MADG0930
         AIF   (&N EQ 1).NOK                                            MADG0940
         MNOTE 8,'INCORRECT PARAMETER LIST: TOO MANY OPERANDS'          MADG0950
         MEXIT                                                          MADG0960
.NOK     AIF   ('&STACK'(1,1) NE '(').NOTREG                            MADG0970
&R       SETC  '&STACK(1)'                                              MADG0980
&NAME1   LCR   R2,&R               COMPLEMENT STACK ADDRESS IN R2       MADG0990
&NAME1   SETC  ' '                                                      MADG1000
         AGO   .SERV                                                    MADG1010
.NOTREG  ANOP                                                           MADG1020
&T       SETC  T'&STACK                                                 MADG1030
         AIF   ('&T' EQ 'C').CHAR1                                      MADG1040
         MNOTE 8,'STACK PARAMETER HAS TYPE ATTRIBUTE OF &T - INVALID'   MADG1050
         MEXIT                                                          MADG1060
.CHAR1   ANOP                                                           MADG1070
&NAME1   LA    R2,&STACK              POINT R2 AT APPROPRIATE STACK     MADG1080
&NAME1   SETC  ' '                                                      MADG1090
         AGO   .SERV1                                                   MADG1100
.SERV1   ANOP                                                           MADG1110
         AIF   ('&SRVRTN' EQ 'YES').SRVYES                              MADG1120
      $SRVSAVE SAVE=LINK           PRESERVE LINK REGISTER               MADG1130
         AGO   .ASKALL                                                  MADG1140
.SRVYES $SRVSAVE SAVE=(LINK,BASE3) PRESERVE BASE3 AND LINK ADDRESS      MADG1150
.ASKALL  ANOP                                                           MADG1160
         AIF   ('&ID' EQ 'ALL').TSTALL                                  MADG1170
         AIF   ('&ID' EQ '''ALL''').TSTALL                              MADG1180
         AGO   .CONT                                                    MADG1190
.TSTALL  TM    APCEFLG1,APF1SEC    HAS THIS APCE ANY SECONDARIES        MADG1200
         BZ    *+4+4+4+4+2         BYPASS PASSING THEM IF NOT THERE     MADG1210
.CONT    ANOP                                                           MADG1220
         L     R9,HCT$AHCT         GET AHCT START                       MADG1230
         LA    R9,(PASEAPCE*MRCBSIZE)+MRCBSTRT(,R9) GET ROUTINE MRCB    MADG1240
         L     BASE3,MRCBRADR-MRCBDSCT(,R9) GET ROUTINE ENTRY POINT     MADG1250
         BALR  LINK,BASE3          PERFORM SERVICE ROUTINE 'PASEAPCE'   MADG1260
      $SRVREST ,                   RESTORE SAVED REGISTERS              MADG1270
         MEND                                                           MADG1280
