         MACRO
&LABEL   TFRR  &EXIT=JES2B005
.**********************************************************************
.*                                                                    *
.*       THIS MACRO PROVIDES A STANDARD METHOD OF INVOKING THE        *
.*       INTERACTIVE ESTAE ROUTINE JES2B005. IT IS NORMALLY CODED     *
.*       WITHOUT OPERANDS BUT THE KEYWORD OPERAND 'EXIT' CAN BE       *
.*       ASSIGNED A VALUE WHICH IS THE NAME OF A ROUTINE OTHER THAN   *
.*       TH DEFAULT JES2B005 IF THAT ROUTINE IS WANTED INSTEAD.       *
.*         THE MACRO GENERATES CODE TO LOAD THE ESTAE EXIT DYNAMICALLY*
.*       AND ISSUES THE ESTAE MACRO TO INITIALISE A STAE CONTROL BLOCK*
.*                                                                    *
.*                                                                    *
.**********************************************************************
.*
&LABEL   ESTAE IHB&SYSNDX,PARAM=IHB&SYSNDX.P ISSUE THE ESTAE
         B     IHB&SYSNDX.Z        JUMP OVER DRIVER EXIT
IHB&SYSNDX.P DC CL8'&SYSECT'       ESTAE EXIT PARAMETER
IHB&SYSNDX DS  0H                  ESTAE DRIVER ENTRY POINT
         BALR  12,0                SET INITIAL ADDRESSABILITY
         PUSH  USING               SAVE USERS NORMAL ADDRESSABILITY
         USING *,12                DECLARE TEMPORARY BASE REGISTER
         STM   14,1,12(13)         SAVE PARAMETER REGISTERS
         LOAD  EP=&EXIT            LOAD UP THE ESTAE EXIT ROUTINE
         LR    15,0                LOAD ENTRY POINT ADDRESS
         L     14,12(13)           RESTORE RETURN ADDRESS
         LM    0,1,20(13)          RESTORE PARAMETER REGISTERS
         BR    15                  BRANCH TO ESTAE EXIT
         POP   USING               RESTORE USERS ADDRESSABILITY
IHB&SYSNDX.Z DS 0H                 END OF ESTAE EXIT
         MEND
