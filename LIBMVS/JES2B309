JES2B309 TITLE '$P COMMAND PROCESSOR - CONTROL BLOCK GENERATION MACRO DCB3090130
               EFINITIONS'                                              B3090140
*                                                                       B3090150
**** $HASPCB ****                  GENERATE HASP CONTROL BLOCKS         B3090160
*                                                                       B3090170
         MACRO                                                          B3090180
         $HASPCB &DOC=NO,&LIST=NO                                       B3090190
         GBLC  &PRINT,&GEN,&DATA                                        B3090200
         PUSH  PRINT                                                    B3090210
         PRINT &PRINT                                                   B3090220
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT              B3090230
         $CBPR DOC=&DOC            GENERATE MODS CBPR DSECT             B3090240
         $AHCT DOC=&DOC            GENERATE MODS AHCT DSECT             B3090250
         $DCT  DOC=&DOC            GENERATE HASP DCT DSECT              B3090260
         $ADCT DOC=&DOC            GENERATE MODS ADCT DSECT             B3090270
         $APCE DOC=&DOC            GENERATE MODS APCE DSECT             B3090280
         $APCE DOC=NO,TYPE=SPCE    GENERATE MODS SECONDARY APCE DSECT   B3090290
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT              B3090300
         $COMWORK DOC=&DOC         GENERATE HASP COMWORK DSECT          B3090310
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT           B3090320
         $CMB  DOC=&DOC            GENERATE HASP CMB DSECT              B3090330
         $IOT  DOC=&DOC            GENERATE HASP IOT DSECT              B3090340
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT              B3090350
         $JOE  DOC=&DOC            GENERATE HASP JOE DSECT              B3090360
         $JQE  DOC=&DOC            GENERATE HASP JQE DSECT              B3090370
         $TAB  DOC=&DOC            GENERATE HASP TAB DSECT              B3090380
         $UCM  LIST=&LIST          GENERATE OS UCM DSECT                B3090390
         SPACE 1                                                        B3090400
         POP   PRINT                                                    B3090410
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS           B3090420
         MEND                                                           B3090430
         TITLE 'JES2B309 - AUXILIARY COMMAND PROCESSOR'                 B3090440
JES2B309 CSECT                     AUXILIARY COMMAND PROCESSOR          B3090450
         SPACE 2                                                        B3090460
*********************************************************************** B3090470
*                                                                     * B3090480
*        PROGRAM ID. :             JES2B309                           * B3090490
*                                                                     * B3090500
*        DATE WRITTEN :            APRIL 1977                         * B3090510
*                                                                     * B3090520
*        AUTHOR :                  D.H.CARTWRIGHT.                    * B3090530
*                                                                     * B3090540
*        LOCATION :                MONSANTO MISD/TSG BRUSSELS         * B3090550
*                                                                     * B3090560
*********************************************************************** B3090570
         EJECT                                                          B3090580
*********************************************************************** B3090590
*                                                                     * B3090600
*        PURPOSE                                                      * B3090610
*       ---------                                                     * B3090620
*                                                                     * B3090630
*        THIS IS A MONSANTO JES2 MODIFICATION MODULE.                 * B3090640
*        IT IT LOADED AT JES2 INITIALISATION UPON REQUEST VIA         * B3090650
*        JES2B001,THE MONSANTO INITIALISATION MODULE,AND IS           * B3090660
*        ENTERED VIA A $CBAL IN HASPCOMM AT K0214150,IN COFCVB,       * B3090670
*        THE CONVERT-TO-BINARY SERVICE ROUTINE. THIS STATEMENT        * B3090680
*        IS EXECUTED BEFORE STARTING TO PROCESS THE SECOND HALF       * B3090690
*        OF A RANGE OF JOB NUMBERS.                                   * B3090700
*        THIS MODIFICATION TESTS FOR THE JES2 $P OR $C COMMANDS,      * B3090710
*        AND IF SUCH A COMMAND IS IN PROCESS WILL ONLY ALLOW          * B3090720
*        A RANGE OF JOB NUMBERS IF THE ADCT FOR THE SUBMITTOR         * B3090730
*        HAS 'ALL COMMAND' AUTHORITY.IF NOT,THE CONDITION CODE        * B3090740
*        IS SET SUCH THAT ON RETURN TO JES2 CODE THE OPERAND IS       * B3090750
*        REJECTED AS INVALID.                                         * B3090760
*                                                                     * B3090770
*        COMMAND SUB-PROCESSOR CONVENTIONS                            * B3090780
*                                                                     * B3090790
* CONTENTS OF REGISTERS UPON ENTRY TO EACH COMMAND SUB-PROCESSOR      * B3090800
*                                                                     * B3090810
*        R0    = UNPREDICTABLE                                        * B3090820
*        R1    = UNPREDICTABLE                                        * B3090830
*        R2    = UNPREDICTABLE                                        * B3090840
*        R3    = UNPREDICTABLE                                        * B3090850
*        R4    = UNPREDICTABLE                                        * B3090860
*        R5    = FIRST OPERAND POINTER ( SET FOR BXLE R3,R4,... )     * B3090870
*        R6    = AHCTDSCT                                             * B3090880
*        R7    = APCE OF PROCESSOR                                    * B3090890
*        BASE3 = HASPCJB2 BASE - USED FOR $MWTO ADDRESSABILITY        * B3090900
*        R10   = UNPREDICTABLE                                        * B3090910
*        BASE1 = HCTDSECT                                             * B3090920
*        BASE2 = BASE REGISTER OF PROCESSOR                           * B3090930
*        SAVE  = PCE OF COMMAND PROCESSOR                             * B3090940
*        LINK  = RETURN ADDRESS TO THIS MODULE                        * B3090950
*        R15   = ADDRESS OF SEPARATOR CHARACTER '-'                   * B3090960
*                                                                     * B3090970
*********************************************************************** B3090980
         EJECT                                                          B3090990
*********************************************************************** B3091000
*                                                                     * B3091010
* EXAMPLE EDIT OF COMMAND TEXT BEFORE ENTRY TO SUB-COMMAND PROCESSOR  * B3091020
*                                                                     * B3091030
* ORIGIONAL IN $WTO BUFFER  ( S OVER CHARACTER = LOWER CASE )         * B3091040
*                                                                     * B3091050
*         S  S S S    S S S    S    S    S                            * B3091060
*        $V  A B C, ' A B,C '' D ', D '' E                            * B3091070
*                                                                     * B3091080
* FORM PASSED TO COMMAND SUB-PROCESSOR                                * B3091090
*                                                                     * B3091100
*        $VABC,' A B,C ' D ',D'E                                      * B3091110
*          1   2             3   4                                    * B3091120
*                                                                     * B3091130
*        $ IS LOCATED AT COMMAND                                      * B3091140
*        V IS SINGLE CHARACTER FORM OF VERB                           * B3091150
*                                                                     * B3091160
*        R3 POINTS TO FIRST OF OPERAND POINTER TABLE WHICH, IN THIS   * B3091170
*        EXAMPLE, CONTAINS FOUR WORDS OF ADDRESSES WHICH IN TURN      * B3091180
*        LOCATES THE BEGINNING OF EACH OPERAND ( INCLUDING A NULL     * B3091190
*        END OPERAND ). THESE LOCATIONS ARE DENOTED BY REFERANCE      * B3091200
*        POINTS 1, 2, 3, AND 4 RESPECTIVELY. USING BXLE R3,R4,...     * B3091210
*        INSTRUCTION WILL FALL THROUGH WITH R3 POINTING TO COMNULOP   * B3091220
*                                                                     * B3091230
*********************************************************************** B3091240
         EJECT                                                          B3091250
*********************************************************************** B3091260
*                                                                     * B3091270
*        JOB NUMBER LISTS ARE OF THE FOLLOWING FORM                   * B3091280
*                                                                     * B3091290
*        $V JXXN1-M1,N2-M2,...,NX-MX                                  * B3091300
*        $V    = HASP COMMAND VERB                                    * B3091310
*        JXX   = JOB, JB, JOBS, ETC. ( J IS ONLY ID REQUIRED )        * B3091320
*        NI-MI ( I=1 TO X ) = PAIR OF START STOP VALUES, IE. 1-10     * B3091330
*              INDICATING THAT JOBS 1,2,3,4,5,6,7,8,9,10 ARE          * B3091340
*              DESIRED. ( -M VALUES ARE OPTIONAL )                    * B3091350
*                                                                     * B3091360
* NOTES                                                               * B3091370
*                                                                     * B3091380
*    (1) A RESPONSE IS REQUIRED FOR EACH JOB                          * B3091390
*                                                                     * B3091400
*    (2) COMNULOP IS USED FOR FLAGS DURING PROCESSING AS FOLLOWS      * B3091410
*                                                                     * B3091420
*              X'80' - A JOB WAS FOUND DURING SCAN FOR JOBS           * B3091430
*              X'40' - THE VERB IS C WITH DUMP SPECIFIED              * B3091440
*              X'02' - THE VERB IS P                                  * B3091450
*              X'01' - THE VERB IS C WITH PURGE SPECIFIED             * B3091460
*                                                                     * B3091470
*        THESE DEFINITIONS MUST NOT BE CHANGED WITHOUT CHANGING       * B3091480
*        THE $JCAN MACRO INSTRUCTION.                                 * B3091490
*                                                                     * B3091500
*********************************************************************** B3091510
         SPACE 3                                                        B3091520
         PUSH  PRINT               SAVE PRINT OPTIONS                   B3091530
         PRINT OFF                 DONT BOTHER TO PRINT STANDARD STUFF  B3091540
         COPY  $HASPGEN            HASP OPTIONS                         B3091550
         POP   PRINT               RESTORE PRINT OPTIONS                B3091560
         TITLE 'CONTROL BLOCK OPTIONS'                                  B3091570
         $SYSPARM (OFF,GEN,NODATA,NO,NO)     SET DOCUMENTATION OPTIONS  B3091580
         SPACE 3                                                        B3091590
*                                                                       B3091600
*        GENERATE HASP CONTROL BLOCKS                                   B3091610
*                                                                       B3091620
         SPACE 2                                                        B3091630
         $HASPCB DOC=&DOC,LIST=&LIST GENERATE HASP CONTROL BLOCKS       B3091640
         TITLE 'COMMAND PROCESSOR - ENTRY POINT PROCESSING'             B3091650
JES2B309 $MODNTRY CSECT=YES,EXIT=EXITB309  MODIFICATION ENTRY           B3091660
         SPACE 2                                                        B3091670
*        COMNULOP FLAGS SET BY HASPCJB2                                 B3091680
         SPACE 1                                                        B3091690
CCJFJOBF EQU   X'80'               JOB FOUND FLAG                       B3091700
CCJFDUMP EQU   X'40'               VERB IS C WITH DUMP FLAG             B3091710
CPJQOPER EQU   X'10'               CANCEL A JOB(S) DATA SETS            B3091720
CLJHOLD  EQU   X'04'               LIST HELD DATA SETS FLAG             B3091730
CCJFSTOP EQU   X'02'               VERB IS P (STOP) FLAG                B3091740
CCJFPURG EQU   X'01'               VERB IS C WITH PURGE FLAG            B3091750
         EJECT                                                          B3091760
         MVI   APCE$$X,$$W         SET DEFAULT RETURN OF NO             B3091770
         CLI   COMVERB,C'C'        IS IT A CANCEL COMMAND?              B3091780
         BE    B309COMM            YES,TEST IT                          B3091790
         CLI   COMVERB,C'P'        HOW ABOUT A PURGE?                   B3091800
         BNE   EXITB309            NO,COMMAND IS ALLOWABLE              B3091810
         L     R15,APCESR15        PICK UP HASPCOMM'S POINTER           B3091820
         CLC   JOB,1(R15)          TEST FOR EXCEPTION                   B3091830
         BNE   B309COMM            NO,COMMAND IS SUSPECT                B3091840
         LA    R15,JOBL(,R15)      UPDATE POINTER                       B3091850
         ST    R15,APCESR15        FOR JES2                             B3091860
         B     EXITB309            RETURN TO JES2                       B3091870
         SPACE 1                                                        B3091880
B309COMM DS    0H                  PROCESS DODGY COMMANDS               B3091890
         SR    R1,R1               CLEAR REGISTER                       B3091900
         IC    R1,COMJRMT          GET REMOTE NUMBER                    B3091910
         BAL   R4,FINDADCT         LOCATE CALLERS ADCT                  B3091920
         USING ADCTDSCT,R1         ESTABLISH ADCT ADDRESSABILITY        B3091930
         TM    ADCTFLG1,ADC1ALLC   IS HE ALLOWED TO DO THIS?            B3091940
         BO    EXITB309            YES,LET HIM GET ON WITH IT           B3091950
         MVI   APCE$$X,$$X         NO,SET CONDITION CODE HIGH           B3091960
         SPACE 2                                                        B3091970
EXITB309 $MODEXIT ,                RETURN TO JES2 CODE FOR NEXT COMMAND B3091980
         SPACE 1                                                        B3091990
         DROP  R1                  END ADCT ADDRESSABILITY              B3092000
         TITLE '$P AND $C COMMANDS - ADCT LOCATE SUBROUTINE'            B3092010
*********************************************************************** B3092020
*        SUB-ROUTINE TO LOCATE THE ADCT FOR A DESIRED REMOTE          * B3092030
*        NUMBER.                                                      * B3092040
*        ON ENTRY                  R1  =  REMOTE NUMBER (LOCAL = ZERO)* B3092050
*        ON EXIT                   R1  =  ADDRESS OF ITS ADCT         * B3092060
*********************************************************************** B3092070
         SPACE 3                                                        B3092080
FINDADCT $SUBIN SAVE=(R2,R3,R4)    ENTRY                                B3092090
         LA    R3,ADCTLGTH+CBPRLGTH SET LENGTH OF EACH ADCT             B3092100
         LR    R2,R1               GET NUMBER OF ADCT REQUIRED          B3092110
         MR    R2,R2               CALCULATE OFFSET IN ADCT LIST        B3092120
         L     R2,AHCTADCT         GET START OF ADCT LIST               B3092130
         LA    R1,0(R2,R3)         GET ADDRESS OF REQUIRED ADCT         B3092140
FINDADCX $SUBOUT ,                  RETURN TO CALLER                    B3092150
         SPACE 3                                                        B3092160
JOB      DC    C'3UDGIE'                                                B3092170
JOBL     EQU   L'JOB                                                    B3092180
         END                                                            B3092190
