//AUXCMM00 JOB (1586,5),'** FOR J. NOLS **',CLASS=N
// EXEC GENASMOD
         TITLE 'COMMAND OUTPUT PROCESSOR'
***********************************************************************
*                     A U X C M M 0 0                                 *
*                                                                     *
*        THIS IS THE COMMON OUTPUT ROUTINE FOR ALL MONSANTO           *
*        OPERATOR COMMANDS. A STRING OF TEXT IS INPUT TO THE          *
*        MODULE WHICH WRITES IT ALL OUT TO THE OPERATOR. EACH         *
*        LINE OF OUTPUT IS IN A SEPARATE BLOCK OF CORE WHICH IS       *
*        FREEMAINED AFTER THE LINE HAS BEEN WRITTEN.                  *
*          THE INDIVIDUAL LINES ARE WRITTEN OUT AS PART OF A          *
*        MULTIPLE LINE WTO AND WILL APPEAR IN THE DISPLAY AREA OF     *
*        A GRAPHICS CONSOLE. THIS OPERATION IS DEPENDANT ON THE       *
*        DESCRIPTOR CODES SET IN THE WTO PARAMETER LIST BY THE        *
*        LOADMSG MACRO.                                               *
*                                                                     *
*        PARAMETERS. ON ENTRY, REG1 POINTS TO A STRING OF BLOCKS      *
*              DESCRIBED BY THE DSECT WTOEL.                          *
*                                                                     *
*                                                                     *
*        ATTRIBUTES. REENTRANT, AUTHORISED, SUPERVISOR MODE.          *
*                                                                     *
*        REGISTER USAGE.                                              *
*        REG0  PARAMETER REGISTER, WORK REGISTER                      *
*        REG1  PARAMETER REGISTER, WORK REGISTER                      *
*        REG3  ADDRESS OF CURRENT OUTPUT ELEMENT                      *
*        REG12 BASE REGISTER                                          *
*        REG13 ADDRESS OF PROGRAM SAVE AREA                           *
*        REG14 RETURN ADDRESS                                         *
*        REG15 ENTRY POINT ADDRESS                                    *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        EQUATES USED BY THIS MODULE                                  *
***********************************************************************
         SPACE 2
LASTMSG  EQU   X'10'               RELATES TO DESCR CODES IN WTO.
*                                  MEANS THIS IS LAST LINE OF MSG
         EJECT
***********************************************************************
*        DSECTS USED BY THE MODULE                                    *
***********************************************************************
         SPACE 2
WTOEL    DSECT ,                   OUTPUT CHAIN FOR AUXCMM00
WTELCHN  DS    A                   WTO ELEMENT CHAINING FIELD
WTELGOT  DS    F                   LENGTH & SUBPOOL THIS ELEMENT CORE
WTELLEN  DS    H                   LENGTH THIS MESSAGE
WTELMCS  DS    XL2                 MCS FLAGS
WTELOUT  DS    0C                  MESSAGE BEGINS HERE
WTELCMD  DS    CL8                 COMMAND NAME
         DS    CL2                 SPACE FIELD
WTELTXT  DS    CL1                 COMMAND TEXT OUTPUT
WTELBLEN EQU   *-WTELLEN           BASIC LENGTH OUTPUT MESSAGE
WTOPRFX  EQU   *-WTOEL             LENGTH OF BASIC GETMAIN
         EJECT
***********************************************************************
*        PROGRAM ENTRY HOUSEKEEPING                                   *
***********************************************************************
         SPACE 2
AUXCMM00 ZCSECT GETMAIN=1
         EJECT
***********************************************************************
*        LOOP ROUND ALL INPUT ELEMENTS, OUTPUT EACH & FREEMAIN CORE   *
***********************************************************************
         SPACE 2
         USING WTOEL,REG3          ADDRESSABILITY FOR INPUT ELEMENTS
         LR    REG3,REG1           LOAD BASE FIRST ELEMENT
         LR    REG10,REG1          LOAD UCM ENTRY ID
         SRL   REG10,24            SHIFT ID TO LOW ORDER BYTE
LOOP     LA    REG1,WTELLEN        START REAL WTO PARM LIST
         OC    WTELCHN,WTELCHN     TEST IF THIS IS LAST ELEMENT
         BNZ   NOTLAST             NO. JUMP OVER BIT SETTING
         LH    REG15,WTELLEN       LOAD LENGTH OF TEXT
         LA    REG15,WTELLEN(REG15) LOAD ADDRESS OF MESSAGE TYPE FLAG
         OI    4(REG15),LASTMSG    MAKE SURE LAST MSG FLAG IS ON
NOTLAST  LR    REG0,REG10          LOAD OUTPUT CONSOLE ADDRESS
         WTO   MF=(E,(1))          ISSUE THE WRITE TO OPERATOR
         SLL   REG1,8              SHIFT MESSAGE ID TO TOP 3 BYTES
         OR    REG10,REG1          OR INTO UCM ID CONTROL REGISTER
         L     REG0,WTELGOT        LOAD UP PARM FOR FREEMAIN
         LR    REG1,REG3           LOAD ADDRESS FOR FREEMAIN
         L     REG3,WTELCHN        LOAD ADDRESS NEXT OUTPUT ELEMENT
         FREEMAIN R,LV=(0),A=(1)   FREEUP THE CORE
         LTR   REG3,REG3           DOES ANOTHER ELEMENT EXIST ?
         BNZ   LOOP                YES. GO PROCESS IT
         EJECT
***********************************************************************
*        ALL OUTPUT HAS NOW BEEN DONE. RETURN TO CALLER               *
***********************************************************************
         SPACE 2
         ZRETURN RC=0
         END
//LKED.SYSIN   DD   *
 SETCODE AC(1)
 NAME AUXCMM00(R)
 INCLUDE MLPALIB(AUXCMM00)
 INCLUDE MLPALIB(AUXCOMM)
 ENTRY AUXCOMM
 ALIAS AUXCMM10
 ALIAS AUXCMM06
 SETCODE AC(1)
 NAME AUXCOMM(R)
/*
//SYSIN2 DD   *   FOLLOWING MODULES TO BE REMLPAED LATER.
 INCLUDE MLPALIB(AUXCMM04)
 INCLUDE MLPALIB(AUXCMM00)
 SETCODE AC(1)
 NAME AUXCMM04(R)
 INCLUDE MLPALIB(AUXCMM00)
 INCLUDE MLPALIB(AUXCMM12)
 ENTRY AUXCMM12
 SETCODE AC(1)
 NAME AUXCMM12(R)
/*
