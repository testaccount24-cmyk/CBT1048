         MACRO                                                          MADA0130
&NAME   $FREAPCE  &ID,&DOC=NO                                           MADA0140
         GBLC  &SRVRTN                                                  MADA0150
         LCLC  &R,&T,&NAME1                                             MADA0160
         LCLA  &N                                                       MADA0170
         AIF   ('&DOC' NE 'YES').NODOC                                  MADA0180
         SPACE 1                                                        MADA0190
*        CORRECT FORM IS :-                                             MADA0200
*        $FREAPCE <'ID' | ID | LABEL | (REG)> NAME OF APCE REQUIRED     MADA0210
*                                  THE NAME OF THE APCE TO BE FOUND MAY MADA0220
*                                  BE A CHARACTER STRING IN QUOTES, A   MADA0230
*                                  NAME OF A FIELD CONTAINING THE NAME, MADA0240
*                                  A CHARACTER STRING WITHOUT QUOTES,   MADA0250
*                                  OR REGISTER NOTATION, IN WHICH CASE  MADA0260
*                                  THE REGISTER CONTAINS THE ADDRESS    MADA0270
*                                  OF THE APCE TO BE FREED.             MADA0280
*                                  OPTIONALLY, THE NAME MAY BE 'ALL',   MADA0290
*                                  IN WHICH CASE ALL SECONDARY APCE'S   MADA0300
*                                  ARE FREED.                           MADA0310
*              <DOC=NO | YES>      REQUEST FOR DOCUMENTATION            MADA0320
         SPACE 1                                                        MADA0330
*        THE PURPOSE OF THIS MACRO IS TO FREE THE SECONDARY APCE        MADA0340
*         ( CHAINED OFF TTHIS PRIMARY APCE ) WITH THE SPECIFIED NAME.   MADA0350
*         THE MACRO INVOKES THE SRVICE ROUTINE 'FREEAPCE' TO LOCATE THE MADA0360
*         SPECIFIED APCE, DECHAIN IT FROM THE CURRENT PRIMARY, AND      MADA0370
*         RETURN IT TO THE AVAILABLE APCE QUEUE.  IF 'ALL' IS SPECIFIED MADA0380
*         AS THE NAME OF THE APCE, THEN THE SERVICE ROUTINE FREES ALL   MADA0390
*         SECONDARY APCE'S ATTACHED TO THE CURRENT PRIMARY APCE.        MADA0400
         SPACE 1                                                        MADA0410
.NODOC   ANOP                                                           MADA0420
&NAME1   SETC  '&NAME'                                                  MADA0430
         AIF   ('&ID' NE '').A01                                        MADA0440
         MNOTE 8,'NO APCE NAME SPECIFIED'                               MADA0450
         MEXIT                                                          MADA0460
.A01     ANOP                                                           MADA0470
&N       SETA  N'&ID                                                    MADA0480
         AIF   (&N EQ 1).NOK                                            MADA0490
         MNOTE 8,'INCORRECT PARAMETER LIST: TOO MANY OPERANDS'          MADA0500
         MEXIT                                                          MADA0510
.NOK     AIF   ('&ID'(1,1) NE '(').NOTREG                               MADA0520
&R       SETC  '&ID(1)'                                                 MADA0530
&NAME1   LCR   R1,&R               COMPLEMENT APCE ADDRESS IN R1        MADA0540
&NAME1   SETC  ' '                                                      MADA0550
         AGO   .SERV                                                    MADA0560
.NOTREG  ANOP                                                           MADA0570
&T       SETC  T'&ID                                                    MADA0580
         AIF   ('&T' EQ 'U').UNDEF                                      MADA0590
         AIF   ('&T' EQ 'N').SELFDEF                                    MADA0600
         AIF   ('&T' EQ 'C').CHAR                                       MADA0610
         MNOTE 8,'PARAMETER HAS TYPE ATTRIBUTE OF &T - INVALID'         MADA0620
         MEXIT                                                          MADA0630
.CHAR    ANOP                                                           MADA0640
&NAME1   LA    R1,&ID              POINT R1 AT PARAMETER LIST           MADA0650
&NAME1   SETC  ' '                                                      MADA0660
         AGO   .SERV                                                    MADA0670
.SELFDEF ANOP                                                           MADA0680
&NAME1   BAL   R1,*+8              POINT R1 AT PARAMETER LIST           MADA0690
         DC    CL4&ID              NAME OF APCE TO BE FREED             MADA0700
         LA    R1,0(,R1)           PURIFY HIGH-ORDER BYTE TO FORCE +VE  MADA0701
&NAME1   SETC  ' '                                                      MADA0710
         AGO   .SERV                                                    MADA0720
.UNDEF   ANOP                                                           MADA0730
&NAME1   BAL   R1,*+8              POINT R1 AT NAME OF APCE TO BE FREED MADA0740
         DC    CL4'&ID'            NAME OF APCE TO BE FREED             MADA0750
         LA    R1,0(,R1)           PURIFY HIGH-ORDER BYTE TO FORCE +VE  MADA0751
.SERV    ANOP                                                           MADA0760
         AIF   ('&SRVRTN' EQ 'YES').SRVYES                              MADA0770
      $SRVSAVE SAVE=(LINK,BASE3)   PRESERVE CORRUPTED REGISTERS         MADA0780
         AGO   .ASKALL                                                  MADA0790
.SRVYES $SRVSAVE SAVE=(LINK,BASE3) PRESERVE BASE3 AND LINK ADDRESS      MADA0800
.ASKALL  ANOP                                                           MADA0810
         AIF   ('&ID' EQ 'ALL').TSTALL                                  MADA0820
         AIF   ('&ID' EQ '''ALL''').TSTALL                              MADA0830
         AGO   .CONT                                                    MADA0840
.TSTALL  TM    APCEFLG1,APF1SEC    HAS THIS APCE ANY SECONDARIES        MADA0850
         BZ    *+4+4+4+4+2         BYPASS FREEING THEM IF NOT THERE     MADA0860
.CONT    ANOP                                                           MADA0870
         L     R9,HCT$AHCT         GET ADDRESS OF AHCT                  MADA0880
         LA    R9,(FREEAPCE*MRCBSIZE)+MRCBSTRT(,R9) GET ROUTINE MRCB    MADA0890
         L     BASE3,MRCBRADR-MRCBDSCT(,R9) GET ROUTINE ENTRY POINT     MADA0900
         BALR  LINK,BASE3          PERFORM SERVICE ROUTINE 'FREEAPCE'   MADA0910
      $SRVREST ,                   RESTORE SAVED REGISTERS              MADA0920
         MEND                                                           MADA0930
