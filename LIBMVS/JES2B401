JES2B401 TITLE 'INTERPRET ACCOUNTING DATA - CONTROL BLOCK GENERATION MA*B4010160
               CRO DEFINITIONS'                                         B4010170
*                                                                       B4010180
**** $HASPCB ****                  GENERATE HASP CONTROL BLOCKS         B4010190
*                                                                       B4010200
         MACRO                                                          B4010210
         $HASPCB &DOC=NO,&LIST=NO                                       B4010220
         GBLC  &GEN,&DATA,&PRINT                                        B4010230
         PUSH  PRINT                                                    B4010240
         PRINT &PRINT                                                   B4010250
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT              B4010260
         $AHCT DOC=&DOC            GENERATE AHCT DSECT                  B4010270
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT              B4010280
         $APCE DOC=&DOC            GENERATE MODS APCE DSECT             B4010290
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT              B4010300
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT           B4010310
         SPACE 1                                                        B4010320
         POP   PRINT                                                    B4010330
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS           B4010340
         MEND                                                           B4010350
         TITLE 'JES2B401 - INTERNAL MACROS'                             B4010360
         MACRO                                                          B4010370
&NAME    FLD   &DEST=,&CVB=NO,&NONNUM=,&PAD=,&JUSTIFY=LEFT,&NEXT=       B4010380
.*                                                                      B4010390
.*       THE PURPOSE OF THIS MACRO IS TO PROCESS A FIELD FROM THE       B4010400
.*        ACCOUNTING DATA.  THE OPERANDS ARE :-                         B4010410
.*             DEST=FLD-NAME       NAME OF THE RECEIVING FLD IF ANY     B4010420
.*             CVB=<NO | YES>      CONVERT NUMERIC VALUE TO BINARY      B4010430
.*             NONNUM=IGNORE       IGNORE NONNUMERIC VALUE ( CVB=YES )  B4010440
.*                                 IF THIS IS OMITTED AND THE FIELD IS  B4010450
.*                                 NOT NUMERIC, THE DESTINATION IS SET  B4010460
.*                                 TO ZERO.                             B4010470
.*             JUSTIFY=<LEFT | RIGHT> LEFT OR RIGHT JUSTIFY THE FIELD   B4010480
.*                                 IN THE DESTINATION IF IT IS SHORT    B4010490
.*             PAD=                PAD CHARACTER ( DEFAULT BLANK )      B4010500
.*             NEXT=ADDR           BR-ADDR IF FIELD MISSING             B4010510
.*                                                                      B4010520
.*       THIS MACRO ASSUMES THE FOLLWOING REGISTER CONTENTS:-           B4010530
.*        R5  - EXECUTABLE LENGTH OF CURRENT FIELD                      B4010540
.*        R8  - ADDRESS OF CURRENT FIELD                                B4010550
.*        R14 - LENGTH OF CURRENT FIELD                                 B4010560
.*                                                                      B4010570
         LCLC  &LL,&PADC                                                B4010580
         LCLA  &LEN                                                     B4010590
&LL      SETC  'L'''                                                    B4010600
&PADC    SETC  ' '                                                      B4010610
&NAME    LA    R8,1(R14,R8)        POINT AT START OF NEXT FIELD         B4010620
         BAL   R4,GETLEN           DETERMINE LENGTH OF FIELD            B4010630
         BH    MAINLNEX            EXIT IF THE ACCTG DATA EXHAUSTED     B4010640
         BL    &NEXT               GO TO NEXT IF FIELD MISSING          B4010650
         AIF   ('&DEST' EQ '').A99                                      B4010660
         AIF   ('&CVB' EQ 'YES').A50                                    B4010670
         AIF   ('&PAD' EQ '').A01                                       B4010680
&PADC    SETC  '&PAD'                                                   B4010690
.A01     ANOP                                                           B4010700
         MVI   &DEST,C'&PADC'      MOVE PAD CHARACTER TO DESTINATION    B4010710
         AIF   (L'&DEST EQ 1).A02                                       B4010720
         MVC   &DEST+1(&LL&DEST.-1),&DEST ... AND PROPAGATE             B4010730
.A02     ANOP                                                           B4010740
         CLI   B401FLEN,&LL&DEST-1 IS FIELD LONGER THAN DESTINATION     B4010750
         BNH   *+8                 BR IF NOT LONGER                     B4010760
         MVI   B401FLEN,&LL&DEST-1 SET EXECUTABLE LENGTH TO DEST'S      B4010770
         IC    R3,B401FLEN         ESTABLISH EXECUTABLE LENGTH FOR MOVE B4010780
         LA    R2,&DEST            POINT R2 AT RECEIVING FIELD          B4010790
         AIF   ('&JUSTIFY' NE 'RIGHT').A25                              B4010800
         LA    R1,&LL&DEST.-1      GET EXECUTABLE LENGTH OF DESTINATION B4010810
         SR    R1,R3               DETERMINE RIGHT-ADJUSTMENT           B4010820
         LA    R2,0(R1,R2)         POINT AT NECESSARY OFFSET IN DEST'N  B4010830
.A25     ANOP                                                           B4010840
         EX    R3,MOVEFLD          MOVE SOURCE TO DESTINATION           B4010850
         AGO   .A99                                                     B4010860
.A50     ANOP                                                           B4010870
         BAL   R4,NUMERIC          CONVERT TO BINARY & PACKED DECIMAL   B4010880
         AIF   ('&NONNUM' NE 'IGNORE').A51                              B4010890
         BNE   *+8                 DO NOT STORE IF NOT NUMERIC          B4010900
.A51     ANOP                                                           B4010910
         AIF   (L'&DEST EQ 1).ASTC                                      B4010920
         AIF   (L'&DEST EQ 2).ASTH                                      B4010930
         AIF   (L'&DEST EQ 3).AST3                                      B4010940
         AIF   (L'&DEST EQ 4).ASTF                                      B4010950
         MNOTE 4,'DESTINATION FIELD TOO LONG'                           B4010960
         AGO   .A99                                                     B4010970
.ASTC    STC   R0,&DEST            PRESERVE BINARY VALUE IN DESTINATION B4010980
         AGO   .A99                                                     B4010990
.ASTH    STH   R0,&DEST            PRESERVE BINARY VALUE IN DESTINATION B4011000
         AGO   .A99                                                     B4011010
.AST3    STCM  R0,7,&DEST          PRESERVE BINARY VALUE IN DESTINATION B4011020
         AGO   .A99                                                     B4011030
.ASTF    ST    R0,&DEST            PRESERVE BINARY VALUE IN DESTINATION B4011040
         AGO   .A99                                                     B4011050
.A99     ANOP                                                           B4011060
         MEND                                                           B4011070
         TITLE 'JES2B401 - INTERPRET ACCOUNTING DATA'                   B4011080
JES2B401 CSECT                     INTERPRET ACCOUNTING DATA            B4011090
         SPACE 2                                                        B4011100
*********************************************************************** B4011110
*                                                                     * B4011120
*        PROGRAM ID. :             JES2B401                           * B4011130
*                                                                     * B4011140
*        DATE WRITTEN :            JUNE 1977                          * B4011150
*                                                                     * B4011160
*        AUTHOR :                  D.A.MOORCROFT                      * B4011170
*                                                                     * B4011180
*        LOCATION :                MONSANTO MISD/TSG BRUSSELS         * B4011190
*                                                                     * B4011200
*********************************************************************** B4011210
         SPACE 3                                                        B4011220
*********************************************************************** B4011230
*                                                                     * B4011240
*        PURPOSE                                                      * B4011250
*       ---------                                                     * B4011260
*                                                                     * B4011270
*        THIS IS A MONSANTO JES2 MODIFICATION MODULE.                 * B4011280
*        IT IT LOADED AT JES2 INITIALISATION UPON REQUEST VIA         * B4011290
*        JES2B001,THE MONSANTO INITIALISATION MODULE,AND IS           * B4011300
*        ENTERED VIA A $CBAL FROM THE ACCOUNTING FIELD SCAN           * B4011310
*        ROUTINE ( HASPRSCN ) IN HASPRDR AT STATEMENT R3329000.       * B4011320
*                                                                     * B4011330
*        ON ENTRY:                                                    * B4011340
*        JCTWORK CONTAINS THE ENTIRE ACCOUNTING FIELD                 * B4011350
*        RSAVE1  CONTAINS THE ADDRESS OF THE LAST CHARACTER           * B4011360
*        APCER1  CONTAINS THE ADDRESS OF THE 1ST CHARACTER            * B4011370
*        APCER15 CONTAINS THE ADDRESS OF THE DELIMITER ( X'FF' )      * B4011380
*                PLACED AT THE END OF THE ACCOUNTING FIELD            * B4011390
*                                                                     * B4011400
*        THIS ROUTINE INTERPRETS THE ACCOUNTING DATA FIELDS IN        * B4011410
*        ACCORDANCE WITH OUR CONVENTIONS ( SEE BELOW ).  IT           * B4011420
*        EXTRACTS ANY NECESSARY FIELDS AND INSERTS THEM IN THE        * B4011430
*        JCT, AND SUPPLIES ANY NECESSARY DEFAULTS IF THE JOB IS       * B4011440
*        NOT A PRODUCTION JOB ( I.E. CATEGORY CODE LESS THAN 7 ).     * B4011450
*        NOTE THAT PRODUCTION JOBS TAKE THE VALUES SUPPLIED BY        * B4011460
*        THE INITIALISATION DECK AS DEFAULTS.  NOTE ALSO THAT, IN     * B4011470
*        THIS CONTEXT ONLY, OPERATIONS JOBS ( CATEGORY CODE 7 ) ARE   * B4011471
*        TREATED AS PRODUCTION FOR THE PURPOSE OF ASSIGNING DEFAULTS. * B4011472
*                                                                     * B4011480
*        NOTE THIS ROUTINE PERFORMS NO VALIDATION OF THE FIELDS       * B4011490
*        OTHER THAN THAT NECESSARY TO AVOID 0C7 ABENDS, AND JES2      * B4011500
*        VALIDATION IS BYPASSED BY A COMBINATION OF INITIALISATION    * B4011510
*        OPTIONS AND THE RETURN FROM THIS ROUTINE.  ALL JOB CARD      * B4011520
*        ACCOUNTING DATA VALIDATION IS PERFORMED BY OUR IEFUJI        * B4011530
*        EXIT, WHICH THOROUGHLY TESTS EVERY FIELD, AND RETURNS        * B4011540
*        APPROPRIATE ERROR MESSAGES ( VIA IEFACTRT ).                 * B4011550
*                                                                     * B4011560
*********************************************************************** B4011570
         EJECT                                                          B4011580
*********************************************************************** B4011590
*                                                                     * B4011600
*        THE FORMAT OF OUR ACCOUNTING DATA IS :-                      * B4011610
*         CHARGE CODE              - NUMERIC, LENGTH 4                * B4011620
*         CATEGORY CODE            - NUMERIC, LENGTH 1-4              * B4011630
*         MAJOR COST CENTRE        - NUMERIC, LENGTH 3                * B4011640
*         MINOR COST CENTRE        - NUMERIC, LENGTH 2                * B4011650
*         ENTITY CODE              - 'E' + 2 NUMERIC                  * B4011660
*         FORMS TYPE               - NUMERIC, LENGTH 4                * B4011670
*         NO. OF COPIES            - NUMERIC, LENGTH 1-4              * B4011680
*         ESTIMATED CPU TIME       - NUMERIC, LENGTH 1-4              * B4011690
*         ESTIMATED LINES          - NUMERIC, LENGTH 1-4              * B4011700
*         ESTIMATED CARDS          - NUMERIC, LENGTH 1-7              * B4011710
*         JOBLOG REQUEST           - ALPHABETIC, LENGTH 1             * B4011720
*         LINES/PAGE               - NUMERIC, LENGTH 1-4              * B4011730
*                                                                     * B4011740
*                                                                     * B4011750
*        DEFAULT VALUES:-                                             * B4011760
*                                  PROD     TEST                      * B4011770
*         CPU TIME                 1439        5  ( MINUTES )         * B4011780
*         LINES                       0        5  ( THOUSANDS )       * B4011790
*         CARDS                       0     2000  ( CARDS )           * B4011800
*                                                                     * B4011810
*********************************************************************** B4011820
         SPACE 5                                                        B4011830
*********************************************************************** B4011840
*                                                                     * B4011850
*        NOTE THAT THE FIVE TRUE ACCOUNTING FIELDS ARE PRESERVED      * B4011860
*        IN FIELDS IN THE JCT.  TWO OF THESE RECEIVING FIELDS ARE     * B4011870
*        STANDARD JES2 FIELDS THAT WE INTERPRET IN ANOTHER WAY, WHILE * B4011880
*        THE OTHER THREE ARE PRESERVED IN FIELDS IN THE MONSANTO      * B4011890
*        EXTENSION TO THE JCT.  THE RECEIVING FIELDS ARE :-           * B4011900
*              JCTACCTN - CHARGE CODE ( LEFT JUSTIFIED )              * B4011910
*              JCTROOMN - CATEGORY CODE ( RIGHT JUSTIFIED )           * B4011920
*              AJCTMAJC - MAJOR COST CENTRE ( LEFT JUSTIFIED )        * B4011930
*              AJCTMINC - MINOR COST CENTRE ( LEFT JUSTIFIED )        * B4011940
*              AJCTENT  - ENTITY CODE ( LEFT JUSTIFIED, NO 'E' )      * B4011950
*                                                                     * B4011960
*********************************************************************** B4011970
         EJECT                                                          B4011980
         PRINT OFF                 SUPPRESS PRINT OF HASP OPTIONS       B4011990
         COPY  $HASPGEN            HASP OPTIONS                         B4012000
         PRINT ON                  RE-ESTABLISH PRINT FOR PROGRAM       B4012010
         TITLE 'CONTROL BLOCK OPTIONS'                                  B4012020
         $SYSPARM (OFF,GEN,NODATA,NO,NO)     SET DOCUMENTATION OPTIONS  B4012030
         SPACE 3                                                        B4012040
*                                                                       B4012050
*        GENERATE HASP CONTROL BLOCKS                                   B4012060
*                                                                       B4012070
         SPACE 2                                                        B4012080
         $HASPCB DOC=&DOC,LIST=&LIST             GENERATE HASP C/B      B4012090
         TITLE 'INTERPRET ACCOUNTING DATA'                              B4012100
*********************************************************************** B4012110
*        FORMAT OF APCE WORK AREA  ( AS USED BY JES2B401 )            * B4012120
*********************************************************************** B4012130
         SPACE 1                                                        B4012140
APCEDSCT DSECT                                                          B4012150
         ORG   APCEUSER            REDEFINE GENERAL PURPOSE WORK AREA   B4012160
B401USER DS    0CL40               WE HAVE 40 BYTES TO PLAY WITH        B4012170
         DS    0D                  ENSURE DOUBLEWORD ALIGNMENT          B4012180
B401DWRD DS    D                   DOUBLE-WORD WORK AREA                B4012190
B401FLEN DS    C                   EXECUTABLE LENGTH OF FIELD           B4012200
         ORG                                                            B4012210
JES2B401 CSECT                                                          B4012220
         TITLE 'JE2B401 - MAIN PROCESSING ROUTINE'                      B4012230
*********************************************************************** B4012240
*        INITIALISATION                                               * B4012250
*********************************************************************** B4012260
         SPACE 1                                                        B4012270
JES2B401 $MODNTRY CSECT=YES,EXIT=EXITB401,DOC=&DOC MODIFICATION ENTRY   B4012280
         SPACE 2                                                        B4012290
         USING JCTDSECT,JCT        ESTABLISH JCT ADDRESSABILITY         B4012300
         SPACE 5                                                        B4012310
*        REGISTER USAGE IN THIS MODULE :-                               B4012320
         SPACE 1                                                        B4012330
*        R0    - VALUE ( CHAR OR BINARY ) OF CURRENT FIELD              B4012340
*        R1    - WORK REGISTER                                          B4012350
*        R2    - ADDRESS OF DESTINATION FIELD                           B4012360
*        R3    - WORK REGISTER                                          B4012370
*        R4    - BAL RETURN ADDRESS                                     B4012380
*        R5    - EXECUTABLE LENGTH OF CURRENT FIELD                     B4012390
*        R6    - AHCT ADDRESS                                           B4012400
*        R7    - APCE ADDRESS                                           B4012410
*        R8    - ADDRESS OF CURRENT FIELD                               B4012420
*        R9    -                                                        B4012430
*        R10   - JCT ADDRESS                                            B4012440
*        R11   - HCT ADDRESS                                            B4012450
*        R12   - MODULE BASE REGISTER                                   B4012460
*        R13   - PCE ADDRESS                                            B4012470
*        R14   - TRUE LENGTH OF CURRENT FIELD                           B4012480
*        R15   - ADDRESS OF DELIMITER                                   B4012490
         EJECT                                                          B4012500
*********************************************************************** B4012510
*        MAIN-LINE LOGIC: PROCESS ACCOUNTING FIELDS                   * B4012520
*********************************************************************** B4012530
         SPACE 1                                                        B4012540
         L     R15,APCESR15        RETRIEVE ADDRESS OF DELIMITER        B4012550
         L     R8,APCESR1          RETRIEVE ADDRESS OF 1ST BYTE         B4012560
         XR    R5,R5               ZEROISE EXECUTABLE LENGTH REGISTER   B4012570
         XR    R14,R14             ZEROISE TRUE LENGTH REGISTER         B4012580
         BCTR  R8,0                DECREMENT FIELD POINTER FOR 'LA'     B4012590
         SPACE 1                                                        B4012600
PARM1    EQU   *                   CHARGE CODE                          B4012610
         FLD   DEST=JCTACCTN,NEXT=PARM2 LOCATE & STORE CHARGE CODE      B4012620
         SPACE 1                                                        B4012630
PARM2    EQU   *                   CATEGORY CODE                        B4012640
         FLD   DEST=JCTROOMN,JUSTIFY=RIGHT,PAD=0,NEXT=PARM3             B4012650
         CLC   JCTROOMN+2(2),=C'07' IS IT PRODUCTION OR OPERATIONS JOB  B4012660
         BNL   PARM3               BYPASS RESETTING DEFAULTS IF PROD'N  B4012670
         MVC   JCTETIME,=F'5'      SET DEFAULT CPU TIME TO 5 MINUTES    B4012680
         MVC   JCTESTLN,=F'10'     SET DEFAULT LINES TO 10( 000 )       B4012690
         MVC   JCTESTPU,=F'2000'   SET DEFAULT CARDS TO 2000            B4012700
         EJECT                                                          B4012710
*********************************************************************** B4012720
*        MAIN-LINE LOGIC: PROCESS ACCOUNTING FIELDS ( CONT'D )        * B4012730
*********************************************************************** B4012740
         SPACE 1                                                        B4012750
PARM3    EQU   *                   MAJOR COST CENTRE                    B4012760
         FLD   NEXT=PARM4,DEST=AJCTMAJC ISOLATE & PRESERVE MAJOR C/C    B4012770
         SPACE 1                                                        B4012780
PARM4    EQU   *                   MINOR COST CENTRE                    B4012790
         FLD   NEXT=PARM5,DEST=AJCTMINC ISOLATE & PRESERVE MINOR C/C    B4012800
         SPACE 1                                                        B4012810
PARM5    EQU   *                   ENTITY CODE                          B4012820
         FLD   NEXT=PARM6          ISOLATE ENTITY CODE                  B4012830
         MVC   AJCTENT,=C'  '      CLEAR RECEIVING FIELD LEST NOT HERE  B4012840
         CH    R3,=H'3'            IS THE ENTITY CODE 3 BYTES LONG      B4012850
         BNE   PARM6               BR IF NOT TO PROCESS NEXT            B4012860
         CLI   0(R8),C'E'          IS IT REALLY AN ENTITY CODE          B4012870
         BNE   PARM6               BR IF NOT TO PROCESS NEXT            B4012880
         MVC   AJCTENT,1(R8)       PRESERVE ENTITY CODE                 B4012890
         EJECT                                                          B4012900
*********************************************************************** B4012910
*        MAIN-LINE LOGIC: PROCESS ACCOUNTING FIELDS ( CONT'D )        * B4012920
*********************************************************************** B4012930
         SPACE 1                                                        B4012940
PARM6    EQU   *                   FORMS TYPE                           B4012950
         FLD   NEXT=PARM7,DEST=JCTFORMS LOCATE & STORE FORMS TYPE       B4012960
         SPACE 1                                                        B4012970
PARM7    EQU   *                   NO. OF COPIES                        B4012980
         FLD   NEXT=PARM8,DEST=JCTCPYCT,CVB=YES,NONNUM=IGNORE           B4012990
         SPACE 1                                                        B4013000
PARM8    EQU   *                   ESTIMATED CPU TIME                   B4013010
         FLD   NEXT=PARM9,DEST=JCTETIME,CVB=YES,NONNUM=IGNORE           B4013020
         EJECT                                                          B4013030
*********************************************************************** B4013040
*        MAIN-LINE LOGIC: PROCESS ACCOUNTING FIELDS ( CONT'D )        * B4013050
*********************************************************************** B4013060
         SPACE 1                                                        B4013070
PARM9    EQU   *                   ESTIMATED LINES IN THOUSANDS         B4013080
         FLD   NEXT=PARM10,DEST=JCTESTLN,CVB=YES,NONNUM=IGNORE          B4013090
         SPACE 1                                                        B4013100
PARM10   EQU   *                   ESTIMATED CARDS                      B4013110
         FLD   NEXT=PARM11,DEST=JCTESTPU,CVB=YES,NONNUM=IGNORE          B4013120
         SPACE 1                                                        B4013130
PARM11   EQU   *                   JOBLOG SUPPRESS REQUEST              B4013140
         FLD   NEXT=PARM12         LOCATE JOB-LOG REQUEST               B4013150
         CLI   0(R8),C'N'          IS LOG TO BE SUPPRESSED              B4013160
         BNE   *+8                 BR IF NO                             B4013170
         OI    JCTJBOPT,JCTNOLOG   SET FLAG TO SUPPRESS JOB LOG         B4013180
         SPACE 1                                                        B4013190
PARM12   EQU   *                   LINES/PAGE                           B4013200
         FLD   NEXT=MAINLNEX,DEST=JCTLINCT,CVB=YES                      B4013210
         SPACE 1                                                        B4013220
MAINLNEX EQU   *                   END OF ACCTG DATA PROCESSING         B4013230
         EJECT                                                          B4013240
*********************************************************************** B4013250
*        RETURN TO JES2 MAINLINE CODE ( WITH A 'YES' CONDITION )      * B4013260
*********************************************************************** B4013270
         SPACE 1                                                        B4013280
EXITB401 $MODEXIT YES,DOC=&DOC     RETURN TO JES2 CODE                  B4013290
         EJECT                                                          B4013300
*********************************************************************** B4013310
*        DETERMINE EXECUTABLE LENGTH OF DATA FIELD                    * B4013320
*********************************************************************** B4013330
         SPACE 1                                                        B4013340
*        ON INPUT :-                                                    B4013350
*         R8  - ADDRESS OF START OF FIELD                               B4013360
         SPACE 1                                                        B4013370
*        ON EXIT :-                                                     B4013380
*         R3  - EXECUTABLE LENGTH OF DATA FIELD                         B4013390
*         R5  - EXECUTABLE LENGTH OF DATA FIELD                         B4013400
*         R14 - TRUE LENGTH OF DATA FIELD                               B4013410
*         B401FLEN - EXECUTABLE LENGTH OF DATA FIELD                    B4013420
         SPACE 1                                                        B4013430
*        ON EXIT, THE CONC CODE IS SET SUCH THAT :-                     B4013440
*         BE  - FIELD LENGTH RETURNED                                   B4013450
*         BL  - FIELD OMITTED ( I.E. ',,' )                             B4013460
*         BH  - ACCOUNTING DATA FIELDS EXHAUSTED                        B4013470
         SPACE 1                                                        B4013480
GETLEN  $SUBIN ,                   ENTER ROUTINE                        B4013490
         LR    R5,R8               POINT R5 AT START OF FIELD           B4013500
         CR    R5,R15              HAVE WE REACHED END OF FIELD         B4013510
         BL    GETLEN1             BR IF FIELD NOT FINISHED             B4013520
         CLI   *,$$Y               SET COND CODE HIGH                   B4013530
         B     GETLENX             GO TO ROUTINE EXIT                   B4013540
GETLEN1  CLI   0(R5),255           HAVE WE REACHED DEFINITIVE DELIMITER B4013550
         BE    GETLEN2             BR IF YES                            B4013560
         CLI   0(R5),C','          HAVE WE REACHED FIELD DELIMITER      B4013570
         BE    GETLEN2             BR IF YES                            B4013580
         LA    R5,1(,R5)           INCREMENT POINTER                    B4013590
         B     GETLEN1             LOOP BACK TO TRY AGAIN               B4013600
GETLEN2  SR    R5,R8               DETERMINE TRUE LENGTH OF FIELD       B4013610
         LR    R14,R5              PRESERVE TRUE LENGTH                 B4013620
         BCTR  R5,0                DETERMINE XEECUTABLE LENGTH OF FIELD B4013630
         STC   R5,B401FLEN         PRESERVE EXECUTABLE LENGTH           B4013640
         LR    R3,R5               ALSO SET R3 TO EXECUTABLE LENGTH     B4013650
         LTR   R14,R14             TEST IF LENGTH IS ZERO               B4013660
         BZ    GETLENL             GO SET COND CODE LOW IF MISSING      B4013670
         CLI   *,$$X               SET COND CODE EQUAL TO SHOW FLD FND  B4013680
         B     GETLENX             GO TO EXIT                           B4013690
GETLENL  CLI   *,$$W               SET THE COND CODE LOW                B4013700
GETLENX $SUBOUT ,                  RETURN TO CALLER                     B4013710
         EJECT                                                          B4013720
*********************************************************************** B4013730
*        VERIFY THAT FIELD IS NUMERIC                                 * B4013740
*********************************************************************** B4013750
         SPACE 1                                                        B4013760
*        ON INPUT :-                                                    B4013770
*         R5  - EXECUTABLE LENGTH OF FIELD                              B4013780
*         R8 - ADDRESS OF CURRENT FIELD                                 B4013790
         SPACE 1                                                        B4013800
*        ON EXIT :-                                                     B4013810
*         R0  - THE BINARY VALUE OF THE FIELD ( OR ZERO IF INVALID )    B4013820
*         B401DWRD - PACKED DECIMAL VALUE OF FIELD ( OR ZERO )          B4013830
         SPACE 1                                                        B4013840
*        ON EXIT, THE COND CODE IS SET SUCH THAT THE CALLER CAN MAKE    B4013850
*         THE FOLLOWING CONDITIONAL BRANCHES :-                         B4013860
*         BE  - FIELD IS NUMERIC                                        B4013870
*         BNE - FIELD IS NOT NUMERIC                                    B4013880
         SPACE 1                                                        B4013890
NUMERIC  $SUBIN ,                  ENTER ROUTINE                        B4013900
         XR    R0,R0               ZEROISE R0 LEST NOT NUMERIC          B4013910
         ZAP   B401DWRD,=P'0'      ZEROISE WORK FIELD LEST NOT NUMERIC  B4013920
         EX    R5,NUMTEST          TEST IF FIELD IS NUMERIC             B4013930
         BNE   NUMERICX            EXIT IF FIELD IS NOT NUMERIC         B4013940
         EX    R5,PACKFLD          PACK FIELD INTO B401DWRD             B4013950
         CVB   R0,B401DWRD         CONVERT DECIMAL VALUE TO BINARY      B4013960
NUMERICX $SUBOUT ,                 RETURN TO CALLER                     B4013970
         SPACE 5                                                        B4013980
NUMTEST  TRT   0(0,R8),NUMTABLE    EXECUTED TRT                         B4013990
PACKFLD  PACK  B401DWRD,0(0,R8)    EXECUTED PACK                        B4014000
MOVEFLD  MVC   0(0,R2),0(R8)       MOVE ACCTG FIELD TO DESTINATION      B4014010
         SPACE 1                                                        B4014020
NUMTABLE DC    256X'01'            DETECT NON-NUMERIC VIA 'TRT'         B4014030
         ORG   NUMTABLE+C'0'       REDEFINE TO NUMERIC DIGITS           B4014040
         DC    10X'00'             ALLOW NUMERICS                       B4014050
         ORG                                                            B4014060
         SPACE 2                                                        B4014070
         LTORG                                                          B4014080
         SPACE 1                                                        B4014090
         END                                                            B4014100
