         MACRO                                                          MACC0130
        $APCE  &DOC=NO,&TYPE=APCE                                       MACC0140
         LCLC  &NM                                                      MACC0150
&NM      SETC  '&TYPE'                                                  MACC0160
         TITLE 'AUXILIARY PROCESSOR CONTROL ELEMENT ( APCE ) DSECT'     MACC0170
*********************************************************************** MACC0180
*        AUXILIARY PROCESSOR CONTROL ELEMENT ( APCE ) DSECT           * MACC0190
*********************************************************************** MACC0200
         AIF   ('&DOC' NE 'YES').NODOC1                                 MACC0210
         SPACE 1                                                        MACC0220
*        THE FUNCTION OF THIS MACRO IS TO PRODUCE A DSECT FOR THE APCE  MACC0230
         SPACE 1                                                        MACC0240
*        THE CORRECT FORM IS:-                                          MACC0250
*        $APCE <DOC=NO | YES>      PRODUCE DOCUMENTATION ( DEFAULT NO ) MACC0260
*                                                                       MACC0270
*              <TYPE=APCE | SPCE>  DEFINES THE PREFIX TO BE ASSIGNED    MACC0280
*                                  TO THE DATA AREAS.IF THIS APCE IS    MACC0290
*                                  ACQUIRED FOR STORAGE USING $GETAPCE  MACC0300
*                                  THEN THE SECONDARY APCE MAY BE       MACC0310
*                                  ADDRESSED BY DIFFERENT NAMES.        MACC0320
*                                  (DEFAULT IS APCE FOR PRIMARY APCE)   MACC0330
*                                                                       MACC0340
*                                                                       MACC0350
         EJECT                                                          MACC0360
*        APCE - AUXILIARY PROCESSOR CONTROL ELEMENT                     MACC0370
         SPACE 1                                                        MACC0380
*        THE PURPOSES OF THE APCE ARE:-                                 MACC0390
*         . PROVIDE A SAVE AREA FOR THE CALLER'S REGISTERS, SO THAT     MACC0400
*           THE ROUTINES MAY ISSUE $WAIT ( AND CONSEQUENTLY USE THE     MACC0410
*           SAVE AREA IN THE USER'S PCE ), AND STILL RESTORE THE USER'S MACC0420
*           REGISTERS PRIOR TO RETURNING CONTROL TO HIM.                MACC0430
*         . ALLOW $CBAL'S TO BE NESTED TO ANY DEPTH                     MACC0440
*         . ALLOW INTERNAL SUBRTNS TO BE NESTED TO ANY DEPTH BY         MACC0450
*           PROVIDING REGISTER SAVE AREAS FOR THE $SUBIN/$SUBOUT MACROS MACC0460
*           EXTERNAL TO THE ROUTINES THEMSELVES                         MACC0470
*         . ALLOW ALL MODIFICATION ROUTINES TO BE REUSABLE, HASP        MACC0480
*           REENTRANT, AND READ ONLY BY PROVIDING A WORKING STORAGE     MACC0490
*           AREA EXTERNAL TO THE ROUTINES THEMSELVES.                   MACC0500
         SPACE 1                                                        MACC0510
*        THE AUXILIARY INITIALISATION ROUTINE ( JES2B001 ) BUILDS A     MACC0520
*         POOL OF APCE'S, AND CHAINS THEM FROM THE AHCT ON THE FIELD    MACC0530
*         AHCTFPCE - THE AVAILABLE APCE QUEUE.  OTHER CHAINS OF APCE'S  MACC0540
*         ARE ALSO MAINTAINED TO ALLOW SECONDARY APCE'S TO BE PASSED    MACC0550
*         TO OTHER ROUTINES.                                            MACC0560
*        ALL OF THESE CHAINS - WHICH ARE MAINTAINED BY THE ROUTINE      MACC0570
*         DISPATCHER ( JES2B000 ), THE $MODEXIT MACRO, AND THE SERVICE  MACC0580
*         ROUTINES FREEAPCE, GETAPCE, AND PASSAPCE - ARE PUSH-DOWN      MACC0590
*         STACKS.  FREE APCES ARE TAKEN FROM THE TOP OF THE AVAILABLE   MACC0600
*         STACK, AND ADDED TO THE TOP OF THE APPROPRIATE STACK, AND     MACC0610
*         WHEN THEY ARE FREED THEY ARE RETURNED TO THE TOP OF THE       MACC0620
*         AVAILABLE STACK.  THERE ARE TWO REASONS FOR THIS:             MACC0630
*         . USING THE APCES IN THIS WAY, TYPICALLY ONLY THE FIRST FEW   MACC0640
*           APCES WILL BE USED, REDUCING THE NUMBER OF PAGES THAT MAY   MACC0650
*           POTENTIALLY BE PAGED OUT.                                   MACC0660
*         . THE QUEUES MAY BE ACCESSED BY ANY TASK IN THE JES ADDRESS   MACC0670
*           SPACE, AND THUS A SERVICE ROUTINE MAY BE INTERRUPTED WHILE  MACC0680
*           IT IS MANIPULATING THE CHAINS.  TO PREVENT CORRUPTION OF    MACC0690
*           THE CHAINS, 'COMPARE AND SWAP' IS USED TO ADD AND REMOVE    MACC0700
*           CHAIN ELEMENTS, AND ONLY BY USING A PUSH-DOWN STACK IS      MACC0710
*           CHAIN INTEGRITY GUARANTEED.                                 MACC0720
         SPACE 1                                                        MACC0730
*        NOTE THAT AN APCE IS ACQUIRED WHEN CONTROL IS PASSED TO        MACC0740
*         A MODIFICATION ROUTINE, AND IS RELEASED WHEN THE ROUTINE      MACC0750
*         RETURNS CONTROL TO THE CALLER ( VIA THE $MODEXIT MACRO ),     MACC0760
*         BUT IS RETAINED IF THE ROUTINE GOES INTO AN EXPLICIT OR       MACC0770
*         IMPLIED $WAIT.                                                MACC0780
         SPACE 1                                                        MACC0790
*        A ROUTINE MAY ALSO ACQUIRE ADDITIONAL ( SECONDARY ) APCE'S     MACC0800
*         - FOR ADDITIONAL RE-ENTRANT WORKING STORAGE SPACE - BY        MACC0810
*         THE SERVICE ROUTINES 'GETAPCE' AND 'FREEAPCE'.  THESE         MACC0820
*         ROUTINES ACQUIRE AND RELEASE APCE'S, ASSOCIATING A USER-      MACC0830
*         SPECIFIED NAME WITH EACH.  IF NOT EXPLICITLY RELEASED BY A    MACC0840
*         'FREEAPCE' REQUEST, ALL SECONDARY APCE'S ARE FREED BY         MACC0850
*         $MODEXIT PRIOR TO RETURNING CONTROL TO JES CODE.              MACC0860
*        THESE SECONDARY APCE'S MAY BE USED TO PASS DATA FROM ONE       MACC0870
*         ROUTINE TO ANOTHER.  A ROUTINE MAY ACQUIRE A SECONDARY APCE,  MACC0880
*         INITIALISE IT AS NECESSARY, AND THEN USE THE $PASAPCE MACRO   MACC0890
*         ( AND THUS THE PASSAPCE SERVICE ROUTINE ) TO DECHAIN IT FROM  MACC0900
*         HIS OWN SECONDARY CHAIN AND ADD IT TO THE TOP OF ONE OF THE   MACC0910
*         PASSED APCE CHAINS MAINTAINED IN THE AHCT.  THE RECEIVING     MACC0920
*         ROUTINE MAY USE THE MACRO $CTCHPCE ( AND THUS THE CATCHPCE    MACC0930
*         SERVICE ROUTINE ) TO REMOVE ELEMENTS FROM THE PASSED APCE     MACC0940
*         CHAIN AND ADD THEM TO ITS SECONDARY APCE CHAIN.               MACC0950
         SPACE 1                                                        MACC0960
*        NOTE THAT PRIMARY APCES ( I.E. APCE'S THAT ARE IN USE AND ARE  MACC0970
*         NOT SECONDARY APCES ) ARE NOT CHAINED TOGETHER; RATHER THEY   MACC0980
*         ARE CAST ADRIFT UNTIL THEY ARE RETRUNED TO THE AVAILABLE      MACC0990
*         QUEUE.  IF IT IS NECESSARY TO LOCATE AN ACTIVE PRIMARY APCE   MACC1000
*         OTHER THAN YOUR OWN ( WHICH IS ADDRESSABLE VIA R7 ), IT IS    MACC1010
*         POSSIBLE TO FOLLOW THE CHAIN OF CONTROL BLOCK PREFIXES, SINCE MACC1020
*         ALL CONTROL BLOCKS ARE ALWAYS CHAINED IN THIS WAY.            MACC1030
         EJECT                                                          MACC1040
*********************************************************************** MACC1050
*        DOCUMENTED APCE DSECT                                        * MACC1060
*********************************************************************** MACC1070
.NODOC1  ANOP                                                           MACC1080
         AIF   (K'&NM LE 4).GOODTYP                                     MACC1090
.TYPTEST AIF   ('&NM' EQ 'SPCE').GOODTYP                                MACC1100
         MNOTE 12,'INVALID TYPE ATTRIBUTE'                              MACC1110
         MEXIT                                                          MACC1120
.GOODTYP ANOP                                                           MACC1130
         SPACE 1                                                        MACC1140
&NM.DSCT DSECT                     AUXILIARY PROCESSOR CONTROL ELEMENT  MACC1150
&NM.FLG1 DS    0CL1                BYTE OF FLAGS (WHEN APCE IN USE)     MACC1160
&NM.NEXT DS    F                   ADDR OF THE NEXT APCE (AVAILABLE Q)  MACC1170
&NM.MRCB DS    F                   ADDRESS OF THE CREATING MRCB         MACC1180
         AIF   ('&DOC' NE 'YES').NODOC2                                 MACC1190
*                                  - MAINTAINED FOR THE USED QUEUE ONLY MACC1200
.NODOC2  ANOP                                                           MACC1210
&NM.SNXT DS    F                   ADDRESS OF NEXT SECONDARY APCE       MACC1220
&NM.SPRV DS    F                   ADDRESS OF PREVIOUS SECONDARY APCE   MACC1230
&NM.NAME DS    CL4                 NAME ASSOCIATED WITH APCE            MACC1240
         AIF   ('&DOC' NE 'YES').NODOC6                                 MACC1250
*                                  THE PURPOSE OF THIS FIELD IS TO      MACC1260
*                                  ASSOCIATE A NAME WITH A SECONDARY    MACC1270
*                                  APCE.  A PRIMARY APCE ALWAYS HAS A   MACC1280
*                                  NAME OF 'APCE', WHILE THE DEFAULT    MACC1290
*                                  FOR A SECONDARY APCE IS 'SPCE'       MACC1300
.NODOC6  ANOP                                                           MACC1310
&NM.AREA DS    CL236               DATA PORTION OF AN APCE              MACC1320
         AIF   ('&NM' EQ 'SPCE').SPCEND                                 MACC1330
         SPACE 1                                                        MACC1340
         ORG   &NM.AREA            OVERLAY DATA FIELDS                  MACC1350
         SPACE 2                                                        MACC1360
&NM.SA   DS    0CL64               SAVE AREA FOR CALLER'S REGISTERS     MACC1370
&NM.PCE  DS    0F                  ADDRESS OF CALLER'S PCE              MACC1380
&NM.SR13 DS    F                   SAVE ADDRESS FOR CALLER'S R13        MACC1390
&NM.SR14 DS    F                   SAVE ADDRESS FOR CALLER'S R14        MACC1400
&NM.SR15 DS    F                   SAVE ADDRESS FOR CALLER'S R15        MACC1410
&NM.SR0  DS    F                   SAVE ADDRESS FOR CALLER'S R0         MACC1420
&NM.SR1  DS    F                   SAVE ADDRESS FOR CALLER'S R1         MACC1430
&NM.SR2  DS    F                   SAVE ADDRESS FOR CALLER'S R2         MACC1440
&NM.SR3  DS    F                   SAVE ADDRESS FOR CALLER'S R3         MACC1450
&NM.SR4  DS    F                   SAVE ADDRESS FOR CALLER'S R4         MACC1460
&NM.SR5  DS    F                   SAVE ADDRESS FOR CALLER'S R5         MACC1470
&NM.SR6  DS    F                   SAVE ADDRESS FOR CALLER'S R6         MACC1480
&NM.SR7  DS    F                   SAVE ADDRESS FOR CALLER'S R7         MACC1490
&NM.SR8  DS    F                   SAVE ADDRESS FOR CALLER'S R8         MACC1500
&NM.SR9  DS    F                   SAVE ADDRESS FOR CALLER'S R9         MACC1510
&NM.SR10 DS    F                   SAVE ADDRESS FOR CALLER'S R10        MACC1520
&NM.SR11 DS    F                   SAVE ADDRESS FOR CALLER'S R11        MACC1530
&NM.SR12 DS    F                   SAVE ADDRESS FOR CALLER'S R12        MACC1540
&NM.$$X  DS    0CL1                FIELD USED TO SET THE PSW COND CODE  MACC1550
&NM.RETN DS    F                   RETURN ADDR FROM SEGMENT TO MAIN MOD MACC1560
         SPACE 1                                                        MACC1570
         AIF   ('&DOC' NE 'YES').NODOC5                                 MACC1580
*                                  THIS FIELD IS USED BY THE MACROS     MACC1590
*                                  $MODLINK & $MODRET TO PASS CONTROL   MACC1600
*                                  TO AND FROM SEGMENTS WITHIN A MAIN   MACC1610
*                                  MODIFICATION ROUTINE.  THIS FIELD IS MACC1620
*                                  USED INSTEAD OF PCERETN TO ALLOW A   MACC1630
*                                  $CBAL TO BE ISSUED FROM A HASP       MACC1640
*                                  SEGMENT TO A MOD RTN THAT ALSO       MACC1650
*                                  USES SEGMENTS.                       MACC1660
.NODOC5  ANOP                                                           MACC1670
&NM.USER DS    0CL40               FIELD AVAILABLE TO THE ROUTINE       MACC1680
         DS    CL40                GENERAL PURPOSE WORK AREA            MACC1690
         SPACE 1                                                        MACC1700
&NM.BCTR DS    F                   NO. OF SLOTS USED IN THE NEXT S/A    MACC1710
&NM.AMSA DS    F                   ADDRESS OF START OF NEXT S/A         MACC1720
         AIF   ('&DOC' NE 'YES').NODOC3                                 MACC1730
*                                  - THESE ARE USED BY THE MACROS       MACC1740
*                                  $SUBIN/$SUBOUT TO DETERMINE THE NEXT MACC1750
*                                  AVAILABLE SAVE AREA FOR INDIV REGS.  MACC1760
.NODOC3  ANOP                                                           MACC1770
&NM.MSA  DS    30F                 SAVE AREA AVAILABLE TO $SUBIN/$SUBOU MACC1780
         AIF   ('&DOC' NE 'YES').NODOC4                                 MACC1790
*                                  - THIS SAVE AREA ALLOWS FOR UP TO    MACC1800
*                                  6 SUBRTNS EACH SAVING 5 REGISTERS TO MACC1810
*                                  BE NESTED - SURELY ENOUGH.  NOTE     MACC1820
*                                  THAT NO CHECK IS MADE THAT THE FIELD MACC1830
*                                  IS NOT EXCEEDED.                     MACC1840
.NODOC4  ANOP                                                           MACC1850
         ORG                                                            MACC1860
         SPACE 5                                                        MACC1870
APF1SEC  EQU   1                   APCE HAS SECONDARY APCES             MACC1880
.SPCEND  ANOP                                                           MACC1890
         SPACE 1                                                        MACC1900
&NM.LGTH EQU   *-&NM.DSCT          TOTAL LENGTH OF &NM                  MACC1910
&SYSECT  CSECT                                                          MACC1920
         MEND                                                           MACC1930
