         MACRO                                                          MACP0130
&NAME   $SUBIN &SAVE=,&DOC=NO                                           MACP0140
         GBLA  &SUBINC                                                  MACP0150
         GBLA  &DISPTAB(16)                                             MACP0160
         GBLC  &REGTABL(16)                                             MACP0170
         GBLA  &SUBNREG                                                 MACP0180
         GBLC  &SUBIND                                                  MACP0190
         LCLA  &LOOP,&DISP                                              MACP0200
         LCLA  &N,&S                                                    MACP0210
         LCLC  &R,&P,&PP                                                MACP0220
         AIF   ('&DOC' NE 'YES').NODOC                                  MACP0230
         SPACE 1                                                        MACP0240
*        THE CORRECT FORM IS :-                                         MACP0250
*       $SUBIN <SAVE=(RN,..RN)>    REGISTERS TO BE SAVED - DEFAULT NONE MACP0260
*              <DOC=NO | YES>      PRODUCE DOCUMENTATION ( DEFAULT NO ) MACP0270
         SPACE 1                                                        MACP0280
*        THE HASP/JES2 IMPLEMENTATION OF INTERNAL SUBRTNS IS SOMEWHAT   MACP0290
*         PRIMITIVE, BECAUSE THERE IS NO PROVISION FOR SAVING AND       MACP0300
*         RESTORING REGISTERS UPON ENTRY AND EXIT RESPECTIVELY, UNLESS  MACP0310
*         SPECIFICALLY PROGRAMMED WITHIN THE RTN TO USE SPARE PCE       MACP0320
*         FIELDS.  WE ARE USED TO A MORE FLEXIBLE FACILITY, WHEREBY     MACP0330
*         REGISTERS MAY BE SAVED WITHOUT REGARD FOR WHOM ELSE HAS DONE  MACP0340
*         WHAT, AND AT WHAT DEPTH THE RTN WAS CALLED.                   MACP0350
         SPACE 1                                                        MACP0360
*        THE PURPOSE OF THIS MACRO IS TO ALLOW INTERNAL SUBRTNS TO BE   MACP0370
*         NESTED TO ANY DEPTH BY PRESERVING REGISTERS UPON ENTRY, AND   MACP0380
*         RETORING THEM ON EXIT ( VIA A $SUBOUT MACRO ).  TO COMPLY     MACP0390
*         WITH THE HASP PHILOSOPHY OF READ-ONLY CODE, THIS MACRO DOES   MACP0400
*         NOT PRESERVE THE REGISTERS IN SAVE AREAS WITHIN ITS OWN       MACP0410
*         EXPANSION - AS DOES THE BASIC SUBIN MACRO - BUT INSTEAD       MACP0420
*         DETERMINES THE ADDRESS OF THE NEXT AVAILABLE SAVE AREA SLOT   MACP0430
*         IN THE APCE AND SAVES THE REGISTERS IN CONTIGUOUS SLOTS.  THE MACP0440
*         ASSOCIATED $SUBOUT MACRO WILL RESTORE THE SAVED REGISTERS     MACP0450
*         AND FREE THE SLOTS.                                           MACP0460
         SPACE 1                                                        MACP0470
*        NOTE THAT THE TECHNIQUE OF USING A POOL OF SAVE AREA SLOTS IN  MACP0480
*         THE APCE ALLOWS - IN PRACTICE - ANY DEPTH OF SUBRTN NEST,     MACP0490
*         WITH ANY NUMBER OF REGISTERS SAVED BY EACH, BUT IT DOES       MACP0500
*         NECESSITATE THAT :-                                           MACP0510
*         . $SUBOUTS FOLLOW THEIR ASSOCIATED $SUBINS                    MACP0520
*         . A SUBRTN MAY ONLY BE EXITED FROM VIA A $SUBOUT MACRO        MACP0530
*         . A SUBRTN IS ALWAYS ENTERED VIA A $SUBIN MACRO               MACP0540
*         . A $SUBIN MUST BE FOLLOWED BY A $SUBOUT                      MACP0550
         SPACE 1                                                        MACP0560
*        NOTE ALSO THAT R15 IS A WORK REGISTER FOR $SUBIN/$SUBOUT       MACP0570
*         AND IS CORRUPTED BY THE PROCESS OF ENTERING AND LEAVING RTNS  MACP0580
         EJECT                                                          MACP0590
*********************************************************************** MACP0600
*        DOCUMENTED EXPANSION OF $SUBIN MACRO ( CONT'D )              * MACP0610
*********************************************************************** MACP0620
         SPACE 1                                                        MACP0630
.NODOC   ANOP                                                           MACP0640
         AIF   ('&SUBIND' NE 'ON').PREVOK                               MACP0650
         MNOTE 16,'PREVIOUS $SUBIN NOT FOLLOWED BY A $SUBOUT'           MACP0660
.PREVOK  ANOP                                                           MACP0670
&SUBIND  SETC  'ON'                                                     MACP0680
&SUBNREG SETA  0                                                        MACP0682
         AIF   ('&SAVE' NE '').START                                    MACP0690
&NAME    DS    0H                  ENTER ROUTINE                        MACP0700
         MEXIT                                                          MACP0710
.START   ANOP                                                           MACP0720
&LOOP    SETA  0                                                        MACP0730
.LOOPE1  ANOP                                                           MACP0740
&LOOP    SETA  &LOOP+1                                                  MACP0750
         AIF   (&LOOP GT 16).LOOPX1                                     MACP0760
&REGTABL(&LOOP) SETC ''                                                 MACP0770
         AGO   .LOOPE1                                                  MACP0780
.LOOPX1  ANOP                                                           MACP0790
&LOOP    SETA  0                                                        MACP0800
.LOOPE2  ANOP                                                           MACP0810
&LOOP    SETA  &LOOP+1                                                  MACP0820
         AIF   (&LOOP GT 16).LOOPX2                                     MACP0830
&DISPTAB(&LOOP) SETA 0                                                  MACP0840
         AGO   .LOOPE2                                                  MACP0850
.LOOPX2  ANOP                                                           MACP0860
&NAME    L     R15,APCEBCTR        GET COUNT OF SLOTS IN USE            MACP0870
         SLL   R15,2               MULTIPLY BY 4 FOR INDEX              MACP0880
         LA    R15,APCEMSA(R15)   POINT AT 1ST AVAILABLE SLOT           MACP0890
&LOOP    SETA  0                                                        MACP0900
&DISP    SETA  0                                                        MACP0910
&SUBNREG SETA  N'&SAVE                                                  MACP0920
.LOOP    ANOP                                                           MACP0930
&LOOP    SETA  &LOOP+1                                                  MACP0940
         AIF   (&LOOP GT &SUBNREG).LOOPX                                MACP0950
         ST    &SAVE(&LOOP),&DISP.(R15) PRESERVE REGISTER IN NEXT SLOT  MACP0960
&DISPTAB(&LOOP) SETA &DISP                                              MACP0970
&REGTABL(&LOOP) SETC '&SAVE(&LOOP)'                                     MACP0980
&DISP    SETA  &DISP+4                                                  MACP0990
         AGO   .LOOP                                                    MACP1000
.LOOPX   ANOP                                                           MACP1010
         L     R15,APCEBCTR        GET COUNT OF SLOTS USED UP TO NOW    MACP1020
         LA    R15,&SUBNREG.(,R15) INCREMENT BY SLOTS USED HERE         MACP1030
         ST    R15,APCEBCTR        PRESERVE UPDATED COUNT OF USED SLOTS MACP1040
         MEXIT                                                          MACP1050
         MEND                                                           MACP1060
