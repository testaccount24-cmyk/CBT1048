         MACRO                                                          MACW0130
&NAME   $FNDAPCE  &ID,&DOC=NO                                           MACW0140
         LCLC  &R,&T                                                    MACW0150
         AIF   ('&DOC' NE 'YES').NODOC                                  MACW0160
         SPACE 1                                                        MACW0170
*        CORRECT FORM IS :-                                             MACW0180
*        $FNDAPCE <'ID' | ID | LABEL | (REG)> NAME OF APCE REQUIRED     MACW0190
*                                  THE NAME OF THE APCE TO BE FOUND MAY MACW0200
*                                  BE A CHARACTER STRING IN QUOTES, A   MACW0210
*                                  NAME OF A FIELD CONTAINING THE NAME, MACW0220
*                                  OR REGISTER NOTATION, IN WHICH CASE  MACW0230
*                                  THE REGISTER POINTS AT THE DESIRED   MACW0240
*                                  CHARACTER STRING.                    MACW0250
         SPACE 1                                                        MACW0260
*        THE PURPOSE OF THIS MACRO IS TO LOCATE THE SECONDARY APCE      MACW0270
*         ( CHAINED OFF TTHIS PRIMARY APCE ) WITH THE SPECIFIED NAME.   MACW0280
*         IF AN APCE IS FOUND, THE ADDRESS IS PLACED IN REGISTER 1 AND  MACW0290
*         THE PSW COND CODE SET TO EQUALITY, WHILE IF NO APCE WITH THE  MACW0300
*         SPECIFIED NAME IS FOUND, THEN REGISTER 1 IS SET TO ZERO, AND  MACW0310
*         THE PSW COND CODE IS SET TO UNEQUAL.                          MACW0320
         SPACE 1                                                        MACW0330
*        NOTE THAT ONLY R1 AND THE PSW COND CODE ARE MODIFIED BY THIS   MACW0340
*         MACRO INSTRUCTION.                                            MACW0350
         SPACE 1                                                        MACW0360
.NODOC   ANOP                                                           MACW0370
&NAME    L     R1,APCENEXT         GET ADDRESS OF 1ST SECONDARY APCE    MACW0380
         LA    R1,0(,R1)           PURIFY HIGH-ORDER BYTE               MACW0390
         LTR   R1,R1               TEST FOR END OF CHAIN                MACW0400
         BZ    *+22                BR IF END OF CHAIN TO SET PSW CC     MACW0410
         AIF   (N'&ID EQ 1).NOK                                         MACW0420
         MNOTE 8,'INCORRECT PARAMETER FORMAT: TOO MANY OPERANDS'        MACW0430
         MEXIT                                                          MACW0440
.NOK     AIF   ('&ID'(1,1) NE '(').NOTREG                               MACW0450
&R       SETC  '&ID(1)'                                                 MACW0460
         AIF   ('&R' NE 'R1').NOTR1                                     MACW0470
         MNOTE 8,'REGISTER 1 MAY NOT BE USED FOR NAME POINTER'          MACW0480
         MEXIT                                                          MACW0490
.NOTR1   ANOP                                                           MACW0500
         CLC   APCENAME-APCEDSCT(L'APCENAME,R1),0(&R) COMPARE NAMES     MACW0510
         AGO   .ENDCLC                                                  MACW0520
&T       SETC  T'&ID                                                    MACW0530
         AIF   ('&T' EQ 'N').SELFDEF                                    MACW0540
         AIF   ('&T' EQ 'C').CHAR                                       MACW0550
         AIF   ('&T' EQ 'U').UNDEF                                      MACW0560
         MNOTE 8,'PARAMETER HAS TYPE ATTRIBUTE OF &T - INVALID'         MACW0570
         MEXIT                                                          MACW0580
.SELFDEF CLC   APCENAME-APCEDSCT(L'APCENAME,R1),=CL4&ID COMPARE NAMES   MACW0590
         AGO   .ENDCLC                                                  MACW0600
.UNDEF   CLC   APCENAME-APCEDSCT(L'APCENAME,R1),=CL4'&ID' COMPARE NAMES MACW0610
         AGO   .ENDCLC                                                  MACW0620
.CHAR    CLC   APCENAME-APCEDSCT(L'APCENAME,R1),&ID COMPARE NAMES       MACW0630
.ENDCLC  ANOP                                                           MACW0640
         BE    *+16                BR IF MATCH WITH PSW COND CODE EQUAL MACW0650
         L     R1,APCENEXT-APCEDSCT(,R1) GET ADDRESS OF NEXT SEC'Y APCE MACW0660
         B     *-26                LOOP BACK TO TEST NEXT SECONDRY APCE MACW0670
         CLI   *,$$Y               SET COND CODE NE BECAUSE NO APCE     MACW0680
         MEXIT                                                          MACW0690
         MEND                                                           MACW0700
