         MACRO                                                          AUXF1300
&NAME $AUXFLAG &TYPE,&FLAG,&DOC=NO                                      AUXF1400
         AIF   ('&DOC' NE 'YES').NODOC                                  AUXF1500
         SPACE 1                                                        AUXF1600
*        CORRECT FORM IS :-                                             AUXF1700
*     $AUXFLAG <ON | OFF>          INDICATES WHETHER TO SET ON OR OFF   AUXF1800
*              <FLAG-NAME>         NAME OF FLAG TO BE PROCESSED         AUXF1900
*              <,DOC=NO | YES>     PRODUCE DOCUMENTATION ( DEFAULT NO ) AUXF2000
         SPACE 1                                                        AUXF2100
*        THE FUNCTION OF THIS MACRO IS TO MAINTAIN THE FLAGS IN THE     AUXF2200
*         BYTE OF AUXILIARY HOLD FLAGS ( AJQEHOLD ) IN OUR EXTENSION TO AUXF2300
*         THE JQE.  ALTHOUGH SETTING FLAGS ON AND OFF IS NOT, NORMALLY, AUXF2400
*         A PARTICULARLY COMPLEX OPERATION, THIS FLAG BYTE HAS AN       AUXF2500
*         INTERNAL PRIORITY SEQUENCE, AND ALSO INTERACTS WITH THE FLAGS AUXF2600
*         IN 'JQEFLAGS', AND THE PURPOSE OF THIS MACRO IS TO ENSURE     AUXF2700
*         CONSISTENCY OF PROCESSING WHENEVER THE FLAGS ARE ALTERED.     AUXF2800
         SPACE 1                                                        AUXF2900
.NODOC   ANOP                                                           AUXF3000
         AIF   ('&FLAG' NE 'AJQFPHLD').A01                              AUXF3100
         MNOTE 8,'FLAG ''AJQFPHLD'' MAY NOT BE ALTERED DIRECTLY'        AUXF3200
         MEXIT                                                          AUXF3300
.A01     AIF   ('&FLAG' NE 'AJQFHLD1').A02                              AUXF3400
         AGO   .A01A        ***** J. NOLS - APRIL 1978 ******           AUXF3410
*JN-4/78 AIF   ('&SYSECT' EQ 'JES2B324').A01A                           AUXF3500
*JN-4/78 MNOTE 8,'FLAG ''AJQFHLD1'' MAY BE ALTERED DIRECTLY ONLY BY MOD*AUXF3600
               IFICATION ROUTINE ''JES2B324'''                          AUXF3700
*JN-4/78 MEXIT                                                          AUXF3800
.A01A    ANOP                                                           AUXF3900
         AIF   ('&TYPE' NE 'ON').A02                                    AUXF4000
&NAME    OI    AJQEHOLD,AJQFHLD1   SET ON 'HOLD PROPAGATION' FLAG       AUXF4100
         MEXIT                                                          AUXF4200
.A02     ANOP                                                           AUXF4300
         AIF   ('&TYPE' EQ 'ON').SETON                                  AUXF4400
         AIF   ('&TYPE' EQ 'OFF').SETOFF                                AUXF4500
         MNOTE 8,'UNKNOWN REQUEST TYPE - &TYPE '                        AUXF4600
         MNOTE 8,'THE ONLY VALID REQUESTS ARE ''ON'' AND ''OFF'''       AUXF4700
         MEXIT                                                          AUXF4800
.SETOFF  ANOP                                                           AUXF4900
&NAME    NI    AJQEHOLD,255-(&FLAG) SET OFF AUXILIARY HOLD FLAG(S)      AUXF5000
         TM    AJQEHOLD,255-(AJQFPHLD+AJQFHLD1) ARE ALL AUX FLAGS OFF   AUXF5100
         BNZ   *+24                BR IF NOT ALL AUXILIARY FLAGS OFF    AUXF5200
         NI    AJQEHOLD,255-AJQFPHLD SET OFF PRIMARY AUX HOLD FLAG      AUXF5300
         TM    AJQEHOLD,AJQFHLD1   IS 'BASIC' JES2 HOLD TO BE LEFT ON   AUXF5400
         BO    *+12                BR IF BASIC JES2 FLAG TO BE LEFT ON  AUXF5500
         NI    JQEFLAGS,255-QUEHOLD1 SET OFF BASIC JES2 HOLD FLAG       AUXF5600
         NI    AJQEHOLD,255-AJQFHLD1 SET OFF STATUS-PROPAGATING FLAG    AUXF5700
         MEXIT                                                          AUXF5800
.SETON   ANOP                                                           AUXF5900
&NAME    TM    AJQEHOLD,AJQFPHLD   IS JOB ALREADY IN AN AUX HOLD STATE  AUXF6000
         BO    *+16                BR IF JOB ALREADY IN AUXILIARY HOLD  AUXF6100
         TM    JQEFLAGS,QUEHOLD1   IS JOB ALREADY IN 'BASIC' HOLD       AUXF6200
         BZ    *+8                 BR IF JOB NOT ALREADY IN HOLD        AUXF6300
         OI    AJQEHOLD,AJQFHLD1    SET FLAG TO PRESERVE HOLD STATUS    AUXF6400
         OI    JQEFLAGS,QUEHOLD1   SET ON BASIC JES2 HOLD FLAG          AUXF6500
         OI    AJQEHOLD,AJQFPHLD+&FLAG SET ON AUXILIARY HOLD FLAGS      AUXF6600
         MEND                                                           AUXF6700
