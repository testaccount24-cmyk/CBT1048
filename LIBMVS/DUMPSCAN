//DUMPSCAN JOB (1140,5),'0000 J ABBEY',CLASS=N
// EXEC ASMFCLG,L=H
//SYSLIB DD
// DD DSN=JEA.MACLIB,DISP=SHR
DUMPSCAN ZCSECT
         OPEN  (INTRDR,OUTPUT)     OPEN THE INTERNAL READER FILE
         EJECT
LOOP     DDNAME LAST=ENDING
         CLC   0(4,REG1),=C'DUMP'  DUMP DATA SET NAME ?
         BNE   LOOP                NO. IGNORE IT
         EJECT
         LA    REG2,DUMPDCB        ADDRESS OF INPUT DUMP DCB
         USING IHADCB,REG2         ADDRESSABILITY FOR IT
         MVC   DCBDDNAM,0(REG1)    SET UP DDNAME
         MVC   EXECNUMB,0(REG1)    SET UP EXEC CARD FOR CLEAR
         OPEN  (DUMPDCB,INPUT)     OPEN THE INPUT DUMP FILE
         GET   DUMPDCB             EOF AT THIS POINT MEANS EMPTY FILE
         BAL   REG11,PUTJCL        NOT EMPTY. PREPARE CLEAR JOB
EODAD    CLOSE (DUMPDCB)           CLOSE THE FILE AGAIN
         B     LOOP                RETURN TO PROCESS THE NEXT
         EJECT
ENDING   CLOSE (INTRDR)            CLOSE THE INTERNAL READER
         ZRETURN RC=0
         EJECT
PUTJCL   DS    0H
         PUT   INTRDR,JOBCARD      OUTPUT A JOB CARD
PUTEXEC  PUT   INTRDR,EXECCARD     OUTPUT THE EXEC CARD
         BR    REG11               RETURN TO CALLER
         EJECT
JOBCARD  DC    CL80'//JEADUMPS JOB (1140,9),''FOR J ABBEY'',CLASS=O'
EXECCARD DC    C'//CLRDMP EXEC CLRDMP,DUMP='
EXECNUMB DC    CL8'DUMP00'
         DC    CL80' '
         LTORG
         PRINT NOGEN
INTRDR   DCB   DDNAME=INTRDR,DSORG=PS,MACRF=PM,LRECL=80,RECFM=F,       X
               BLKSIZE=80
DUMPDCB  DCB   DDNAME=DUMP00,DSORG=PS,EODAD=EODAD,BUFNO=1,MACRF=GL
         DCBD  DSORG=QS,DEVD=DA
         EJECT
         END
//LKED.SYSLMOD DD DSN=SYS1.LINKLIB2(DUMPSCAN),DISP=OLD
