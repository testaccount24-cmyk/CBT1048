         MACRO -- $MODXCTL -- BRANCH TO ANOTHER MOD SEGMENT             MACM0130
&NAME $MODXCTL &SEG,&DOC=NO                                             MACM0140
         AIF   ('&DOC' NE 'YES').NODOC                                  MACM0150
         SPACE 1                                                        MACM0160
*        THE CORRECT FORM IS :-                                         MACM0170
*     $MODXCTL <SEGMENT-NAME | (RN)> NAME OR ADDRESS ( IN A REGISTER )  MACM0180
*                                  OF THE SEGMENT TO WHICH TO TRANSFER  MACM0190
*              <,DOC=NO | YES>     PRODUCE DOCUMENTATION ( DEFAULT NO ) MACM0200
         SPACE 1                                                        MACM0210
*        THE PURPOSE OF THIS MACRO IS TO TRANSFER CONTROL FROM ONE      MACM0220
*         MODIFICATION SEGMENT TO ANOTHER IN THE SAME MAIN PROGRAM.     MACM0230
*         NOTE THAT THE FORMAT OF THE SEGMENT NAME MUST BE              MACM0240
*              JES2BNNN OR JES2JNNN                                     MACM0250
         SPACE 1                                                        MACM0260
.NODOC   ANOP                                                           MACM0270
         AIF   ('&SEG'(1,1) EQ '(').A                                   MACM0280
         AIF   (K'&SEG LT 6 OR K'&SEG GT 8).ERROR                       MACM0290
         AIF   ('&SEG'(1,5) EQ 'JES2B').A                               MACM0300
         AIF   ('&SEG'(1,5) EQ 'JES2J').A                               MACM0310
.ERROR   MNOTE 8,'ILLEGAL MODIFICATION SEGMENT NAME'                    MACM0320
         MEXIT                                                          MACM0330
.A       ANOP                                                           MACM0340
&NAME    L     LINK,APCERETN       LOAD RETURN ADDRESS                  MACM0350
         AIF   ('&SEG'(1,1) EQ '(').B                                   MACM0360
         L     BASE3,=V(&SEG)      LOAD MODIFICATION SEGMENT ADDRESS    MACM0370
         AGO   .C                                                       MACM0380
.B       ANOP                                                           MACM0390
         L     BASE3,0(,&SEG(1))   LOAD MODIFICATION SEGMENT ADDRESS    MACM0400
.C       ANOP                                                           MACM0410
         BR    BASE3                AND BRANCH TO IT                    MACM0420
         MEND                                                           MACM0430
