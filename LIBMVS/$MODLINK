         MACRO -- $MODLINK -- BRANCH AND LINK TO ANOTHER HASP SEGMENT   MACI0140
&NAME $MODLINK &SEG,&DOC=NO                                             MACI0150
         AIF   ('&DOC' NE 'YES').NODOC                                  MACI0160
         SPACE 1                                                        MACI0170
*        THE CORRECT FORM IS :-                                         MACI0180
*     $MODLINK <SEGMENT-NAME | (RN)> NAME OR ADDRESS ( IN A REGISTER )  MACI0190
*                                  OF THE SEGMENT TO WHICH TO LINK      MACI0200
*              <,DOC=NO | YES>     PRODUCE DOCUMENTATION ( DEFAULT NO ) MACI0210
         SPACE 1                                                        MACI0220
*        THE PURPOSE OF THIS MACRO IS TO PASS CONTROL FROM A MAIN       MACI0230
*         PROGRAM TO A SEGMENT WITHIN IT.  NOTE THAT THE SEGMENT NAME   MACI0240
*         MUST BE OF THE FORM                                           MACI0250
*              JES2BNNN OR JES2JNNN                                     MACI0260
         SPACE 1                                                        MACI0270
.NODOC   ANOP                                                           MACI0280
         AIF   ('&SEG'(1,1) EQ '(').B                                   MACI0290
         AIF   (K'&SEG LT 6 OR K'&SEG GT 8).ERROR                       MACI0300
         AIF   ('&SEG'(1,5) EQ 'JES2B').A                               MACI0310
         AIF   ('&SEG'(1,5) EQ 'JES2J').A                               MACI0320
.ERROR   MNOTE 8,'ILLEGAL MODIFICATION SEGMENT NAME'                    MACI0330
         MEXIT                                                          MACI0340
.A       ANOP                                                           MACI0350
&NAME    L     BASE3,=V(&SEG)      LOAD MODIFICATION SEGMENT ADDRESS    MACI0360
         AGO   .C                                                       MACI0370
.B       ANOP                                                           MACI0380
&NAME    L     BASE3,0(,&SEG(1))   LOAD MODIFICATION SEGMENT ADDRESS    MACI0390
.C       ANOP                                                           MACI0400
         BALR  LINK,BASE3           AND LINK TO IT                      MACI0410
         MEND                                                           MACI0420
