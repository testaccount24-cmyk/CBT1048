RDCD     TITLE 'READ ROUTINE TO SERVICE READCARD MACRO.     A036     '
*           JOHN R. EHRMAN (SLAC) 13 DECEMBER 1968 (VERSION 2)
*
MCRREAD1 CSECT                         CSECT = MCRREAD1 FOR VERSION 2
         ENTRY MCRREADC
*
*        THIS ROUTINE SERVICES THE CALLING SEQUENCE PRODUCED BY THE
*        READCARD MACRO-INSTRUCTION.  IT CAUSES A SINGLE CARD TO BE
*        READ FROM A FORTRAN LOGICAL UNIT DEFINED IN THE MACRO EXPAN-
*        SION, AND THE 80 BYTES ARE PLACED IN A BUFFER PROVIDED BY THE
*        CALLER.
*
*        CALLING SEQUENCE GENERATED BY MACRO...
*
*        CNOP  2,4
*        STM   14,15,*+22
*        L     15,*+14
*        BALR  14,15
*    *** DC    F'UNIT NUMBER'
*    OR IF THE UNIT NUMBER IS NOT SELF-DEFINING,
*    *** DC    X'01',AL3(UNIT)   WHICH POINTS TO A FULLWORD UNIT NUMBER
*        DC    A(READCARB)      THE LOCATION OF THE CARD IMAGE BUFFER
*        DC    V(MCRREADC)       IF NO ENDFILE SWITCH IS GENERATED, OR
*        DC    X'1',VL3(MCRREADC) IF THERE IS AN ENDFILE SWITCH
*        DC    2F'0'             SAVE AREA FOR R14 AND R15
*        LM    14,15,12(14)      CONTROL RETURNS HERE VIA BC 15,20(,14)
*        LA    REG,CARDADDRESS   SETS THE ADDRESS INTO SPECIFIED REG
*        NOP   ENDFILE ADDRESS   OPTIONAL BRANCH ADDRESS FOR ENDFILES
*
*
         USING *,15
MCRREADC EQU   *
         STM   13,1,SAVE               SAVE LOCALLY
         LR    1,15                    USE DIFFERENT BASE REGISTER
         USING MCRREADC,1
         DROP  15
         LA    13,TSAVE          SAVE AREA FOR I/O ROUTINES
         MVC   UNIT,0(14)              MOVE IN UNIT NUMBER OR POINTER
         MVC   LOC,4(14)               AND BUFFER LOCATION
         OI    UNIT,X'10'              INDICATE ENDFILE EXIT
         L     15,VIB
         CNOP  0,4
         BAL   14,0(0,15)              JUMP TO FRDWF
UNIT     DC    F'0'
         DC    A(FMT)
         DC    A(END)                  ENDFILE CONDITION GOES TO END
         BAL   14,12(0,15)             JUMP TO FIOAF TO READ 20 WORDS
LOC      DC    F'0'
         DC    X'04'                   ITEM LENGTH
         DC    AL3(20)                 NUMBER OF ITEMS
         BAL   14,16(0,15)             JUMP TO FENDF TO TERMINATE
         L     14,SAVE+4               GET PARAMETER LIST POINTER
         TM    8(14),X'1'              WAS ENDFILE BRANCH COMPILED
         BZ    LEAVE                   BRANCH IF NOT, NOTHING WE CAN DO
         NI    29(14),X'0F'            TURN ENDFILE BRANCH OFF
LEAVE    LM    13,1,SAVE               RESTORE REGISTERS
         SPM   14                      RESET PROGRAM MASK AND COND CODE
         BC    15,20(0,14)             RETURN
*
ENDFILE  L     14,SAVE+4               GET PARAMETER LIST POINTER
         TM    8(14),X'1'              WAS ENDFILE BRANCH COMPILED
         BZ    LEAVE                   BRANCH IF NOT, NOTHING WE CAN DO
         OI    29(14),X'F0'            TURN ENDFILE BRANCH ON
         B     LEAVE                   RETURN TO CALLER
END      DC    A(ENDFILE)
VIB      DC    V(IBCOM#)
FMT      DC    X'020614140422'
TSAVE    DC    18F'0'            SAVE AREA FOR I/O ROUTINES
SAVE     DS    5F                      REGISTER SAVE AREA
         END
