         TITLE 'WORLD CURRENCY CONVERSION SUBROUTINE - BY MISD EUROPE'
*
***********************************************************************
*                                                                     *
*                     WRITTEN BY C.G.HALL    MARCH 1968.              *
*   MANAGEMENT INFORMATION AND SYSTEMS DEPARTMENT  - LONDON - EUROPE  *
*                                                                     *
*                                                                     *
*       THIS SUBROUTINE SHOULD ONLY BE CALLED DYNAMICALLY BECAUSE     *
*        THE CONVERSION TABLES ARE CONTAINED WITHIN THE MODULE. THIS  *
*         WAS DONE INTENTIONALLY IN ORDER TO SAVE THE CORE SPACE      *
*          REQUIRED FOR DATA ACCESS METHOD.                           *
*               IF THE CONVERSION TABLE IS ALTERED IN ANY WAY, THIS   *
*                PROGRAM SHOULD BE RECOMPILED AND LINK EDITED INTO A  *
*                 LIBRARY.                                            *
*                                                                     *
***********************************************************************
*
*
*
*
*
*
*
UT410    CSECT
         WXTRN SOURCE
         WXTRN RETURN
         PGMIN EQUATES=NO,SAVEFLD=SAVE,USING=(13,3)
         B     BEGIN               GO AROUND TABLE
*
*
****** CURRENCY CONVERSION TABLE FOLLOWS ******************************
******-----------------------------------******************************
*
* TABLE  DC    C'XXCCCCCFFFFF ABRE$'
*
*              WHERE  XX         = CURRENCY CODE
*                     CCCCCFFFFF = CONVERSION FACTOR - 5 DECIMAL PLACES
*                      ABRE      = CURRENCY ABBREVIATION
*                     $          = S,1,2,3 OR 4    IE.NO OF DECIMAL
*                                            PLACES REQUIRED ON.OUTPUT.
*                                        S = STERLING
*
*
*
VERSNO   DC    C'75/04'            VERSION NUMBER
TABLE    EQU   *
         DC    C'01    020250 ADIN2'    ALGERIAN DINAR.               .
         DC    C'02    100000 USDL2'    U.S DOLLAR.                   .
         DC    C'03    100000 CDL 2'    CANADIAN DOLLAR.              .
         DC    C'04           AESC2'    ANGOLA ESCUDO.                .
         DC    C'05    010020 APES2'    ARGENTINIAN PESOS.            .
         DC    C'06    149500 AUDL2'    AUSTRALIAN DOLLARS.           .
         DC    C'07           BDL 2'    BARBADOS DOLLARS.             .
         DC    C'08           PDBES'    BERMUDAN POUNDS.              .
         DC    C'09           BPES2'    BOLIVIAN PESOS.               .
         DC    C'10    005200 ASCH2'    AUSTRIAN SCHILLINGS.          .
         DC    C'11    002500 BFR 2'    BELGIAN FRANCS.               .
         DC    C'12    002500 LFR 2'    LUXEMBURG FRANCS.             .
         DC    C'13    016100 CRUZ2'    BRAZILIAN CRUZEIROS.          .
         DC    C'14    021600 FFR 2'    FRENCH FRANCS.                .
         DC    C'15    037500 DM  2'    GERMAN MARKS.                 .
         DC    C'16    000160 LIRE2'    ITALIAN LIRE.                 .
         DC    C'17    035250 FLOR2'    NETHERLAND FLORIN.            .
         DC    C'18    031250 SFR 2'    SWISS FRANCS.                 .
POUND19  DC    C'19    230000 PDSTS'    ENGLISH POUNDS STERLING.      .
         DC    C'20           LEV 2'    BULGARIAN LEV.                .
         DC    C'21           KYAT2'    BURMA KYAT.                   .
         DC    C'22           RIEL2'    CAMBODIAN RIEL.               .
         DC    C'23    000290 CHES2'    CHILE EUSCUDO.                .
         DC    C'24    003600 GDR 2'    GREEK DRACHMAS.               .
         DC    C'25    002750 CHDL2'    CHINESE DOLLARS.              .
         DC    C'26    004050 CPES2'    COLUMBIAN PESOS.              .
         DC    C'27           CFR 2'    CONGOLESE FRANCS.             .
         DC    C'28           COL 2'    COSTA RICAN COLON.            .
         DC    C'29           CUPE2'    CUBAN PESOS.                  .
         DC    C'30    240000 PDCY2'    CYPRIOT POUNDS.               .
         DC    C'31           CR  2'    CZECHOSLOVAKIAN CROWNS.       .
         DC    C'32    004000 PESC2'    PORTUGUESE ESCUDOS.           .
         DC    C'33           DPES2'    DOMINICAN REPUBLIC PESOS.     .
         DC    C'34    001750 SPES2'    SPANISH PESETAS.              .
         DC    C'35    016000 DKR 2'    DANISH KRONER.                .
         DC    C'36    026500 FMAR2'    FINISH MARKS.                 .
         DC    C'37           SUC 2'    ECUADOR SUCRE.                .
         DC    C'38    230000 PDEG2'    EGYPTIAN POUNDS.              .
         DC    C'39    017750 NKR 2'    NORWEGIAN KRONER.             .
         DC    C'40    022250 SKR 2'    SWEDISH KRONER.               .
         DC    C'41    040000 ECOL2'    EL SALVADOR COLON.            .
         DC    C'42           EDL 2'    ETHIOPIAN DOLLARS.            .
         DC    C'43    100000 QUET2'    GUATEMALIAN QUETZAL.          .
         DC    C'44           GOUR2'    HAITI GOURDES.                .
         DC    C'45           LEMP2'    HONDURAS LEMPIRA.             .
         DC    C'46    020000 HKDL2'    HONG KONG DOLLARS.            .
         DC    C'47           FOR 2'    HUNGARIAN FORINT.             .
         DC    C'48    001136 IKR 2'    ICELANDIC KRONA.              .
         DC    C'49    012500 RUP 2'    INDIAN RUPEES.                .
         DC    C'50    000250 NEWR2'    INDONESIAN NEW RUPIAH.        .
         DC    C'51    001320 RIAL2'    IRAN RIAL.                    .
         DC    C'52    280000 IDIN2'    IRAQ DINAR.                   .
         DC    C'53    023800 PDIS2'    ISRAELI POUNDS.               .
         DC    C'54    120000 JADL2'    JAMAICAN POUNDS.              .
         DC    C'55    000333 YEN 2'    JAPANESE YEN.                 .
         DC    C'56    280000 JDIN2'    JORDANIAN DINAR.              .
         DC    C'57    014750 KSCH2'    KENYAN SCHILLINGS.            .
         DC    C'58           WON 2'    SOUTH KOREAN WON.             .
         DC    C'59    031000 PDLE2'    LEBANESE POUND.               .
         DC    C'60           LIDL2'    LIBERIAN DOLLARS.             .
         DC    C'61           PDLI2'    LIBYAN POUNDS.                .
         DC    C'62    041500 MDL 2'    MALAYSIAN DOLLARS.            .
         DC    C'63    008000 MPES2'    MEXICAN PESOS.                .
         DC    C'64           NAGU2'    NETHERLANDS ANTILLES GUILDERS..
         DC    C'65    146000 NZDL2'    NEW ZEALAND DOLLARS.          .
         DC    C'66    014290 CORD2'    NICARAGUA CORDOBA.            .
         DC    C'67    156000 PDNIS'    NIGERIAN POUNDS.              .
         DC    C'68           PRUP2'    PAKISTAN RUPEES.              .
         DC    C'69           BAL 2'    PANAMANIAN BALBOA.            .
         DC    C'70           GUAR2'    PARAGUAY GUARANI.             .
         DC    C'71           SOL 2'    PERUVIAN SOL.                 .
         DC    C'72    015000 PPES2'    PHILIPPINES PESOS.            .
         DC    C'73           ZLOT2'    POLISH ZLOTY.                 .
         DC    C'74           PEAE2'    PORTUGUESE EAST AFRICA ESCUDOS.
         DC    C'75           PDRHS'    RHODESIAN POUNDS.             .
         DC    C'76           LEU 2'    RUMANIAN LEU.                 .
         DC    C'77    022220 RIY 2'    SAUDI ARABIAN RIYAL.          .
         DC    C'78    041000 SIDL2'    SINGAPORE DOLLARS.            .
         DC    C'79           SOSH2'    SOMALIAN SHILLINGS.           .
         DC    C'80    150000 RAND2'    SOUTH AFRICAN RAND.           .
         DC    C'81    140000 SWAR2'    SOUTH WEST AFRICAN RAND.      .
         DC    C'82           PDSU2'    SUDANESE POUNDS.              .
         DC    C'83           SUGU2'    SURINAM GUILDER.              .
         DC    C'84           SLRA2'    SWAZILAND RAND.               .
         DC    C'85    026000 PDSY2'    SYRIAN POUNDS.                .
         DC    C'86           TSCH2'    TANZANIAN SCHILLINGS.         .
         DC    C'87    005000 BAHT2'    THAILAND BAHT.                .
         DC    C'88           TTDL2'    TRINIDAD + TOBAGO DOLLARS.    .
         DC    C'89    190476 TDIN1'    TUNISIAN DINARS.              .
         DC    C'90    011111 TLIR2'    TURKISH LIRA.                 .
         DC    C'91           USCH2'    UGANDA SCHILLINGS.            .
         DC    C'92           UPES2'    URAGUAYAN PESOS.              .
         DC    C'93           ROUB2'    U.S.S.R. ROUBLES.             .
         DC    C'94    023260 BOL 2'    VENEZUELA BOLIVARES.          .
         DC    C'95           VNPI2'    VIETNAM PIASTERS.             .
         DC    C'96    006667 YDIN2'    YUGOSLAVIAN DINARS.           .
         DC    C'97           PDZAS'    ZAMBIAN POUNDS.               .
POUND98  DC    C'98    230000 $DEC4'    U.K. DECIMALIZED POUNDS.      .
         DC    18X'FF'        FOR INSERTION OF CURRENCY CODE 99.
         DC    X'FFFF'
*
***********************************************************************
*
EDIT1    DC    X'40404020202020202020202020202021204B2060'
EDIT2    DC    X'404040202020202020202020202021204B202060'
EDIT3    DC    X'4020202020202020202020202021204B20202060'
EDIT4    DC    X'40202020202020202020202021204B2020202060'
EDITLSD  DC    X'404020202020202020202021204B20204B202060'
*
LASTINP  DC    18C'*'    STORE FOR INPUT CURRENCY TABLE ENTRY.
LASTULT  DC    18C'*'    STORE FOR ULTMT CURRENCY TABLE ENTRY.
MVO      DC    C'0'
CONVAREA DS    CL16      16 BYTE FIELD FOR CONVERSION CALCULATION.
VALUE    DC    PL10'0'    10 BYTE PACKED FIELD FOR INITIAL VALUE.
CONFACT  DC    PL6'0'    6  BYTE PACKED FIELD FOR CONVERSION FACTOR
TEMPAREA DS    CL32      32 BYTE TEMPORARY STORE FOR CONVAREA + INTAREA
DECPND   DC    PL6'0'    6  BYTE PACKED DECIMAL POUND CONVERSION FACTOR
STERL    DC    PL6'0'    6  BYTE PACKED STERLING CONVERSION FACTOR.
INTAREA  DS    CL16      16 BYTE WORK AREA FOR CURRENCY CALCULATIONS
LSDSW    DC    X'F0'          L.S.D SWITCH.
PDECSW   DC    X'F0'          $.DEC SWITCH.
TWODEC   DC    P'50000000'
THRDEC   DC    P'50'
FOURDEC  DC    P'5'
TWODECA  DC    P'500'
ONEDEC   DC    P'5000'
FIVETH   DC    P'50000'
PSHILL   DC    P'1000'
PPND     DC    P'100000'
FILLER   DC    X'00000C'
SMALSTRE DS    CL2
SOURCADR DC    A(SOURCE)
RETRNADR DC    A(RETURN)
*
BEGIN    L     4,SOURCADR              RPG SOURCE ADDRESS INTO REG4.
         L     5,RETRNADR              RPG RETURN ADDRESS INTO REG5.
         USING SOURCE,4                REG4 BASE FOR RPG SOURCE.
         USING RETURN,5                REG5 BASE FOR RPG RETURN.
         CLC   RETURN(7),=C'RPGCONV'   IS RPG CALLING ME.
         BE    *+12                    YES - SKIP COBOL LOADS.
         L     4,0(0,1)                LOAD ADDR OF I/P FIELD TO REG4.
         L     5,4(0,1)                LOAD ADDR OF O/P FIELD TO REG5.
         MVC   SMALSTRE,17(4)
BYPASS   NOP   NOLOOK
         OI    BYPASS+1,X'F0' SET BYPASS TABLE LOOK UP SWITCH.
         PACK  STERL,POUND19+2(10) STORE $.STERLING CONVERSION FACTOR.
         PACK  DECPND,POUND98+2(10)  STORE $.DECIMAL CONVERSION FACTOR.
NOLOOK   MVI   159(5),C'4'         ASSUME VERSION NUMBER IS REQUIRED
         MVC   128(5,5),VERSNO     MOVE VERSION NUMBER TO ABBREV DESC
         CLC   19(2,4),=C'VN'      IS THIS A VERSION NUMBER REQUEST
         BE    OUT                 RETURN TO CALLER IF YES
         CLC   0(2,4),LASTINP      COMPARE INPUT CURRENCY CODE WITH LAS
         BE    XA
         LR    9,4
         BAL   8,FINDIT       BRANCH TO TABLE LOOK UP SUBROUTINE.
         CLC   2(10,6),=CL10' '
         BE    ERROR1
         MVC   LASTINP(18),0(6)
XA       CLC   19(2,4),LASTULT  COMPARE ULTIMATE CURRNCY CD WITH LAST.
         BE    XB
         LR    9,4
         LA    9,19(9)
         BAL   8,FINDIT       BRANCH TO TABLE LOOK UP SUBROUTINE.
         CLC   2(10,6),=CL10' '
         BE    ERROR1
         MVC   LASTULT(18),0(6)
         B     XB
FINDIT   LA    6,TABLE        TABLE LOOK UP SUBROUTINE.
AGAIN    CLC   0(2,6),=X'FFFF'          .             .
         BE    ERROR1                   .             .
         CLC   0(2,9),0(6)              .             .
         BCR   8,8                      .             .
         LA    6,18(6)                  .             .
         B     AGAIN              - END OF TABLE LOOK UP RTN. -
*
XB       CLC   3(14,4),=XL14'40'   TEST INPUT VALUE FOR BLANKS.
         BNE   OK
         MVI   3(4),X'F0'          FILL INPUT VALUE WITH ZEROS.
         MVC   4(13,4),3(4)
OK       MVI   159(5),X'F1'   MOVE 1 INTO OUTPUT INDICATOR FIELD.
         CLI   LASTINP+17,C'S'
         BE    XD
         CLI   18(4),X'50'        + SIGN
         BE    PLUSIT
         CLI   18(4),X'4E'        + SIGN
         BE    PLUSIT
         CLI   18(4),X'60'        - SIGN
         BE    MINSIT
         CLI   18(4),X'40'        BLANK
         BNE   TESTD
         CLI   16(4),X'40'        BLANK
         BE    PLUSIT
         CLI   16(4),X'50'        + SIGN
         BE    PLUSIT
         CLI   16(4),X'4E'        + SIGN
         BE    PLUSIT
         CLI   16(4),X'60'        - SIGN
         BE    MINSIT
         MVZ   18(1,4),16(4)
TESTD    CLC   0(2,4),=C'02'  IS INPUT CURRENCY IN DOLLARS ?
         BE    XC
         CLC   0(2,4),=C'98'  IS INPUT CURRENCY IN POUND DECIMAL ?
         BE    XE
PACKVAL  PACK  VALUE(10),3(16,4)   PACK I/P CURRENCY VALUE INTO WORKARA
PACKFACT PACK  CONFACT(6),LASTINP+2(10) PACK CURRENCY CONVERSION FACTOR
         ZAP   CONVAREA(16),VALUE(10)
         MVO   CONVAREA,CONVAREA
         MVZ   CONVAREA+15(1),=X'00'
         MP    CONVAREA,CONFACT         CALCULATE DOLLARS.
         CLI   LSDSW,X'F1'              IS LSD SWITCH ON ?
         BNE   XF
         ZAP   INTAREA,CONVAREA
         DP    INTAREA,=P'240'          DIVIDE RESULT BY 240.
         ZAP   CONVAREA,INTAREA(14)
         CLC   0(2,4),=C'19'
         BE    XF
         MVI   LSDSW,X'F0'    TURN LSD SWITCH OFF.
XF       ZAP   INTAREA,CONVAREA      SAVE CALCULATED DOLLAR VALUE.
         MVN   TWODEC+4(1),CONVAREA+15
         AP    CONVAREA,TWODEC          ROUND TO TWO DECIMAL PLACES.
         MVN   CONVAREA+11(1),CONVAREA+15
         MVC   72(20,5),EDIT2
         ED    72(20,5),CONVAREA+4   EDIT DOLLARS TO OUTPUT FIELD.
         UNPK  92(16,5),CONVAREA+4(8)    MOVE DOLLARS TO OUTPUT FIELD.
         CLI   LSDSW,X'F1'               IS LSD SWITCH ON ?
         BNE   XG
XHH      CLI   PDECSW,X'F1'              IS $.DEC SWITCH ON ?
         BNE   XH
XHI      CLC   19(2,4),=C'98' IS ULTIMATE CURRENCY $.DECIMAL ?
         BE    XJ
         CLC   19(2,4),=C'19' IS ULTIMATE CURRENCY $.S.D. ?
         BE    XK
         CLC   19(2,4),=C'02' IS ULTIMATE CURRENCY DOLLARS ?
         BE    XL
         PACK  CONFACT(6),LASTULT+2(10) CALCULATE ULTIMATE CURRENCY.
         DP    INTAREA,CONFACT              .        .         .
         CLI   LASTULT+17,X'F1'    IS RESULT TO 1 DECIMAL PLACE ?
         BE    ONE
         CLI   LASTULT+17,X'F2'    IS RESULT TO 2 DECIMAL PLACES ?
         BE    TWO
         CLI   LASTULT+17,X'F3'    IS RESULT TO 3 DECIMAL PLACES ?
         BE    THREE
         CLI   LASTULT+17,X'F4'    IS RESULT TO 4 DECIMAL PLACES ?
         BNE   OTHSTERL
FOUR     MVN   FOURDEC(1),INTAREA+9
         AP    INTAREA(10),FOURDEC   ROUND TO FOUR DECIMAL PLACES.
         MVN   MVO,INTAREA+9
         MVO   MVO,MVO
         MVZ   INTAREA+9(1),MVO
         MVO   INTAREA,INTAREA
         MVC   108(20,5),EDIT4
         ED    108(20,5),INTAREA
         UNPK  128(16,5),INTAREA+1(8)
         B     XM
THREE    MVN   THRDEC+1(1),INTAREA+9
         AP    INTAREA(10),THRDEC  ROUND TO THREE DECIMAL PLACES.
         MVN   INTAREA+8(1),INTAREA+9
         MVC   108(20,5),EDIT3
         ED    108(20,5),INTAREA
         UNPK  128(16,5),INTAREA+1(8)
         B     XM
TWO      MVN   TWODECA+1(1),INTAREA+9
         AP    INTAREA(10),TWODECA  ROUND TO TWO DECIMAL PLACES.
         MVN   MVO,INTAREA+9
         MVO   MVO,MVO
         MVZ   INTAREA+8(1),MVO
         MVO   INTAREA,INTAREA
         MVC   108(20,5),EDIT2
         ED    108(20,5),INTAREA
         UNPK  128(16,5),INTAREA(8)
         B     XM
ONE      MVN   ONEDEC+2(1),INTAREA+9
         AP    INTAREA(10),ONEDEC    ROUND TO ONE DECIMAL PLACE.
         MVN   INTAREA+7(1),INTAREA+9
         MVC   108(20,5),EDIT1
         ED    108(20,5),INTAREA
         UNPK  128(16,5),INTAREA(8)
XM       MVC   144(5,5),LASTULT+12    MOVE ABBREVIATED ULT.DESCRIPTION.
         MVC   149(10,5),LASTINP+2    MOVE CURRENCY TO DOLLAR CONV.FACT
OUT      MVI   LSDSW,X'F0'            TURN LSD SWITCH OFF.
         MVI   PDECSW,X'F0'           TURN $.DEC SWITCH OFF.
         MVC   17(2,4),SMALSTRE
         L     13,SAVE+4
        RETURN (14,12)
*
*                                     PAD INPUT DOLLARS TO TEN
XC       PACK  CONVAREA(14),3(16,4)      PLACES OF DECIMAL TO MAKE IT
         MVN   FILLER+2(1),CONVAREA+13    COMPATIBLE WITH CALCULATED
         MVO   CONVAREA,CONVAREA             DOLLAR VALUES.
         MVO   CONVAREA,CONVAREA
         MVC   CONVAREA+13(3),FILLER
         NI    CONVAREA+12,X'F0'
         B     XF
XD       CLI   2(4),X'F3'   IS $.IND = 3    IE. PENCE INPUT ?
         BE    XN
         CLI   2(4),X'F2'   IS $.IND = 2    IE. 2 POSNS OF PENCE ?
         BE    TWOPNY
         CLI   2(4),X'F1'   IS $.IND = 1    IE. 1 POSN OF PENCE ?
         BNE   ERROR1
         CLI   16(4),X'50'  IS IT 11 D ?
         BNE   TENP
         MVC   INTAREA+12(2),=C'11'
         B     PSH
TENP     CLI   16(4),X'60'  IS IT 10 D ?
         BNE   ORDP
         MVC   INTAREA+12(2),=C'10'
         B     PSH
ORDP     MVC   INTAREA+12(1),=X'F0'
         CLI   16(4),X'4E'
         BNE   *+8
         MVI   16(4),X'F0'
         MVC   INTAREA+13(1),16(4)
PSH      MVC   INTAREA+14(2),17(4)
         CLI   18(4),X'50'        + SIGN
         BE    PLUSITA
         CLI   18(4),X'4E'        + SIGN
         BE    PLUSITA
         CLI   18(4),X'60'        - SIGN
         BE    MINSITA
         CLI   18(4),X'40'        BLANK
         BNE   LOAD9A
         CLI   13(4),X'40'        BLANK
         BE    PLUSITA
         CLI   13(4),X'50'        + SIGN
         BE    PLUSITA
         CLI   13(4),X'4E'        + SIGN
         BE    PLUSITA
         CLI   13(4),X'60'        - SIGN
         BE    MINSITA
         MVZ   INTAREA+15(1),13(4)
LOAD9A   LA    9,14(0,4)
         B     PPENCE
PLUSITA  MVI   INTAREA+15,X'F0'
         B     LOAD9A
MINSITA  MVI   INTAREA+15,X'D0'
         B     LOAD9A
PLUSITB  MVI   INTAREA+15,X'F0'
         B     LOAD9B
MINSITB  MVI   INTAREA+15,X'D0'
         B     LOAD9B
PLUSIT   MVI   18(4),X'F0'
         B     TESTD
MINSIT   MVI   18(4),X'D0'
         B     TESTD
TWOPNY   MVC   INTAREA+12(4),15(4)
         CLI   18(4),X'50'        + SIGN
         BE    PLUSITB
         CLI   18(4),X'4E'        + SIGN
         BE    PLUSITB
         CLI   18(4),X'60'        - SIGN
         BE    MINSITB
         CLI   18(4),X'40'        BLANK
         BNE   LOAD9B
         CLI   16(4),X'40'        BLANK
         BE    PLUSITB
         CLI   16(4),X'50'        + SIGN
         BE    PLUSITB
         CLI   16(4),X'4E'        + SIGN
         BE    PLUSITB
         CLI   16(4),X'60'        - SIGN
         BE    MINSITB
         MVZ   INTAREA+15(1),16(4)
LOAD9B   LA    9,13(0,4)
PPENCE   PACK  INTAREA,INTAREA+12(4)
         MVC   CONVAREA+12(2),0(9)
         MVC   CONVAREA+14(2),=C'00'
         PACK  CONVAREA,CONVAREA+12(4)
         MP    CONVAREA,=P'12'
         MVN   CONVAREA+15(1),INTAREA+15
         AP    INTAREA,CONVAREA                   PENCE + SHILLS X 12
         MVN   INTAREA+15(1),CONVAREA+15     RESTORE SIGN.
         CLI   13(4),X'4E'
         BNE   *+8
         MVI   13(4),X'F0'
         CLI   2(4),X'F2'
         BNE   MOVIT
         MVC   CONVAREA+4(10),3(4)
         B     *+10
MOVIT    MVC   CONVAREA+4(10),4(4)
         MVC   CONVAREA+14(2),=C'00'
         PACK  CONVAREA,CONVAREA+4(12)
         MP    CONVAREA,=P'240'
         MVN   CONVAREA+15(1),INTAREA+15
         AP    INTAREA,CONVAREA         TOTAL PENCE TO  2  DECIMAL PLCS
         MVN   INTAREA+15(1),CONVAREA+15     RESTORE SIGN.
         CLC   0(2,4),=C'19'       IS INPUT CURRENCY IN STERLING ?
         BNE   MOVAL
RNDIT    MVN   THRDEC+1(1),INTAREA+15
         AP    INTAREA,THRDEC           ROUND TO NEAREST PENNY.
         ZAP   CONVAREA,INTAREA
         MVN   MVO,CONVAREA+14
         MVN   CONVAREA+14(1),CONVAREA+15
         UNPK  56(16,5),CONVAREA+7(8)   MOVE PENCE VALUE TO OUTPUT FLD.
         ZAP   INTAREA,CONVAREA(15)
         DP    INTAREA,=P'12'
         DP    INTAREA(14),=P'20'
         MVO   INTAREA,INTAREA
         MVC   INTAREA+11(1),INTAREA+12
         MVC   INTAREA+12(1),INTAREA+14
         MVO   INTAREA,INTAREA
         MVN   INTAREA+12(1),CONVAREA+15     RESTORE SIGN.
         MVC   36(20,5),EDITLSD
         ED    36(20,5),INTAREA+5     EDIT LSD TO OUTPUT FIELD
         BAL   8,XEDIT
         MVN   CONVAREA+14(1),MVO
         SP    CONVAREA,THRDEC
MOVAL    MVC   VALUE(9),CONVAREA+7
         MVI   VALUE+9,X'0C'
         MVN   VALUE+9(1),VALUE+8
         NI    VALUE+8,X'F0'           PENCE ARE NOW IN -VALUE- TO
         MVI   LSDSW,X'F1'            FOUR DECIMAL PLACES.
         B     PACKFACT
*
OTHSTERL CLI   LASTULT+17,C'S'     IS RESULT TO BE L.S.D ?
         BNE   ERROR1
         BAL   8,LSDRTX
         B     DONE
LSDRTX   ZAP   CONVAREA,INTAREA(10)
         ZAP   INTAREA,CONVAREA
         SP    CONVAREA,CONVAREA                CLEAR CONVAREA.
         MVN   FIVETH+2(1),INTAREA+15
         MVC   CONVAREA+7(7),INTAREA+7
         ZAP   VALUE(10),INTAREA+7(9)
         ZAP   INTAREA(16),INTAREA+13(3)
         MP    INTAREA,=P'20'
         MVC   CONVAREA+13(1),INTAREA+12
         ZAP   INTAREA(16),INTAREA+13(3)
         MP    INTAREA,=P'12'
         AP    INTAREA,FIVETH
         MVC   CONVAREA+14(1),INTAREA+12 CONVAREA NOW CONTAINS L.S.D IN
         MVN   CONVAREA+15(1),FIVETH+2    PACKED FORMAT FOR EDITING.
         NI    CONVAREA+15,X'0F'
         CLI   CONVAREA+14,X'12'
         BNE   *+22
         MVC   CONVAREA+14(1),=X'00'
         MVN   PSHILL+2(1),CONVAREA+15
         AP    CONVAREA(16),PSHILL
         CLI   CONVAREA+13,X'20'
         BNE   *+22
         MVC   CONVAREA+13(1),=X'00'
         MVN   PPND+3(1),CONVAREA+15
         AP    CONVAREA(16),PPND
         MVO   CONVAREA,CONVAREA
         MVN   CONVAREA+14(1),CONVAREA+15
         ZAP   INTAREA(16),VALUE(10)
         MP    INTAREA,=P'240'           CALCULATE PENCE TO 4 DEC.PLACE
         MVN   FIVETH+2(1),INTAREA+15
         AP    INTAREA,FIVETH
         MVO   INTAREA,INTAREA
         MVN   INTAREA+12(1),INTAREA+15
         BR    8                          - RETURN -
*
DONE     UNPK  128(16,5),INTAREA+5(8)    PENCE TO ULTIMATE O/P FIELD.
         MVC   108(20,5),EDITLSD
         ED    108(20,5),CONVAREA+7
         CLI   122(5),X'F0'
         BNE   *+8
         MVI   122(5),X'40'
         CLI   125(5),X'F0'
         BNE   XM
         MVI   125(5),X'40'
         B     XM
*
XN       CLI   18(4),X'50'        + SIGN
         BE    PLUSITC
         CLI   18(4),X'4E'        + SIGN
         BE    PLUSITC
         CLI   18(4),X'60'        - SIGN
         BE    MINSITC
         CLI   18(4),X'40'        BLANK
         BNE   PACKXN
         CLI   16(4),X'40'        BLANK
         BE    PLUSITC
         CLI   16(4),X'50'        + SIGN
         BE    PLUSITC
         CLI   16(4),X'4E'        + SIGN
         BE    PLUSITC
         CLI   16(4),X'60'        - SIGN
         BE    MINSITC
         MVZ   18(1,4),16(4)     MOVE ZONE FROM JUNIOR PENCE ON I/P FLD
PACKXN   PACK  INTAREA(16),3(16,4)
         B     RNDIT
PLUSITC  MVI   18(4),X'F0'
         B     PACKXN
MINSITC  MVI   18(4),X'D0'
         B     PACKXN
*
XE       PACK  CONVAREA(16),3(16,4)  PACK $.DEC IN WORKAREA.
         MVC   0(20,5),EDIT4
         ED    0(20,5),CONVAREA+7    EDIT $.DEC TO OUTPUT AREA.
         UNPK  20(16,5),CONVAREA+8(8)  MOVE UNEDITED $.DEC TO OUTPUT.
         MVI   PDECSW,X'F1'
         B     PACKVAL
*
XG       MVC   TEMPAREA(16),CONVAREA       CALCULATE $.S.D
         MVC   TEMPAREA+16(16),INTAREA
         DP    INTAREA,STERL
         BAL   8,LSDRTX
         MVC   36(20,5),EDITLSD
         ED    36(20,5),CONVAREA+7         EDIT $.S.D TO OUTPUT FIELD.
         BAL   8,XEDIT
         UNPK  56(16,5),INTAREA+5(8)       MOVE PENCE VALUE TO OUTPUT.
         MVC   CONVAREA(16),TEMPAREA
         MVC   INTAREA(16),TEMPAREA+16
         B     XHH
*
XH       MVC   TEMPAREA(16),CONVAREA
         MVC   TEMPAREA+16(16),INTAREA
         DP    INTAREA,DECPND     CALCULATE  $.DEC
         MVN   FOURDEC(1),INTAREA+9    ROUND TO FOUR DECIMAL PLACES.
         AP    INTAREA(10),FOURDEC      .    .   .   .     .       .
         MVO   INTAREA,INTAREA
         MVN   INTAREA+8(1),FOURDEC
         UNPK  20(16,5),INTAREA(9)     MOVE UNEDITED $.DEC TO OUTPUT.
         MVC   0(20,5),EDIT4
         ED    0(20,5),INTAREA         EDIT $.DEC TO OUTPUT.
         MVC   CONVAREA(16),TEMPAREA
         MVC   INTAREA(16),TEMPAREA+16
         B     XHI
*
XJ       MVC   108(36,5),0(5)      MOVE $.DEC TO ULTIMATE FIELD.
         B     XM
XK       MVC   108(36,5),36(5)     MOVE $.S.D TO ULTIMATE FIELD.
         B     XM
XL       MVC   108(36,5),72(5)     MOVE DOLLARS TO ULTIMATE FIELD.
         B     XM
*
XEDIT    CLI   50(5),X'F0'
         BNE   *+8
         MVI   50(5),X'40'
         CLI   53(5),X'F0'
         BNE   *+8
         MVI   53(5),X'40'
         BR    8
*
ERROR1   MVI   0(5),X'F0'     FILL OUTPUT FIELD WITH ZEROS BECAUSE OF
         MVC   1(159,5),0(5)           ERROR CONDITION.
         B     OUT
         END   UT410
