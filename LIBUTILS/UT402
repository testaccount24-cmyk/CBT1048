*                       PROGRAM DESCRIPTION
*                      ---------------------
*                   AUTHOR         A DE MOL
*                   ABSTRACTOR     C MITCHELL
*                   LOCATION       BRUSSELS  M I S D  EUROPE
*                   DATE           SEPTEMBER  1972
*                   TITLE          TESTDATA WITHOUT ANY TEARS
*                   COMPUTER       IBM 370-145
*                   PERIPHERAL     PRINTER,TAPE OR DISK
*                   UNITS REQUIRED DISK
*                   SOURCE LANGUAG ASSEMBLER
*                   OPERATING SYST OS
*                   PROGRAM-ID     UT402
*                   DOCUM AVAIL    PROGRAM LISTING,SOURCE DECK
*                   DIRECT INQUIR  TECHNICAL SUPPORT GROUP
*                   ABSTRACT       WILL SELECT AND PRINT, BASED ON
*                                  GIVEN   PARAMETERS, RECORDS FROM
*                                  ONE DATA SET AND CREATE ANOTHER FILE
*                                  THE FEATURE IS THAT IT CAN SELECT
*                                  EVERY NTH RECORD AND PUT A LIMIT
*                                  ON THE SIZE OF THE CREATED FILE AND
*                                  DEFINE THE FIRST RECORD TO PROCESS
BEGIN1   START 0
         SAVE  (14,12)                  SAVE REGISTERS
         BALR  3,0                      DEFINE BASE REGISTER
         USING *,3
         ST    13,SAVE+4                SAVE REGISTER 13
         LR    11,13
         ST    13,8(11)
         LA    13,SAVE
*
*                   NOTE HERE ABOVE IS THE PROGRAM HOUSEKEEPING
*
*
*                            PROGRAM REGISTERS ASSIGNMENT
*                           ------------------------------
*
*
*                            R1 CONTAINS ADDRESS OF PARAMETER LIST
*                                TRANSMITTED BY JOB CONTROL
*                            R3  BASE REGISTER
*                            R5 CONTAINS ADDRESS OF PARAMETER LIST SAVE
*                            R6  CONTAINS PARAMETER LENGTH SAVED
*                            R9 CONTAINS ADDRESS OF PARAMETER LIST SAVE
*                            R7  CONTAINS PARAMETER LENGTH
*                                TRANSMITTED BY JOB CONTROL
*
*
START    L     8,0(0,1)                LOAD IN R8 ADDRESS OF PARAMETER
BEGIN    OPEN  (SYSPRINT,(OUTPUT))     OPEN PRINTER FILE
         PUT   SYSPRINT,PRTCTL          SKIP TO NEXT PAGE
         MVI   PRTCTL,X'19'             MOVE NEW SKIPCODE
         BAL   11,PRINT                 PRINT START MESSAGE
         OPEN  (SYSUT1)                OPEN INPUT FILE
         LH    7,0(0,8)                LOAD PARAMETER COUNT TO R7
         LTR   7,7                     TEST IF FIRST PARM COUNT IS ZERO
         BC    8,EXECUTE               PARM COUNT IS ZERO
         LA    5,2(0,8)                SET ADDRESS OF BEGINNING PARMS
         CLC   0(1,5),COMMA            TEST FOR FIRST DELIMITER
TESTPARA BC    7,FIRSTPAR              BRANCH TO FIRSTPAR TO PROCESS
*                                      1ST PARAMETER
*
TESTPARB S     7,FW1                   DECREMENT CHARACTER COUNT
         BC    8,PROCESS               IF ZERO, NO MORE PARAMETERS
         LA    5,1(5)                  INCREMENT PARAMETER ADDRES COUNT
         CLC   0(1,5),COMMA            TEST FOR 2ND DELIMITER CHARACTER
         BC    7,SECNDPAR              BRANCH TO SECNDPAR TO PROCESS
*                                      2ND PARAMETER
*
TESTPARC S     7,FW1                   DECREMENT CHARACTER COUNT
         BC    8,PROCESS               IF ZERO, NO MORE PARAMETERS
         LA    5,1(5)                  INCREMENT PARAMETER ADDRES COUNT
         CLC   0(1,5),COMMA            TEST FOR 3RD DELIMITER CHARACTER
         BC    7,THIRDPAR              BRANCH TO THIRDPAR TO PROCESS
*                                      3RD PARAMETER
EXECUTE  BC    15,PROCESS              NO FOURTH PARAMETER YET
FIRSTPAR L     4,FW5                   MAX. CHARS IN 1RST PARAMETER = 5
         MVI   SWITCH,C'1'
         BAL   14,SCANPAR              BRANCH AND LINK TO SCANPAR
         EX    8,PACKA                 PACK 1ST PARAMETER TO FREQREC
         A     8,FW1                   REINSTATE TRUE LENGTH OF PARAM
         SR    7,8                     DECREMENT CHARACTER COUNT
         BC    8,PROCESS               IF ZERO, NO MORE PARAMETERS
         AR    5,8                     INCREMENT PARAM ADDRESS COUNT
         CLC   0(1,5),COMMA            TEST FOR FIRST DELIMITER CHARACT
         BC    8,TESTPARB
         BC    15,ERROPARM
PACKA    PACK  FREQREC,0(0,5)          THE LENGTH OF 2ND OPERAND MODIF
*                                      BY EXECUTE INSTRUCTION HEREABOVE
*
SECNDPAR L     4,FW5                   MAX. CHARS IN 2ND  PARAMETER = 5
         MVI   SWITCH,C'2'
         BAL   14,SCANPAR              BRANCH AND LINK TO SCANPAR
         EX    8,PACKB                 PACK 2ND PARAMETER TO MAXREC
         A     8,FW1                   REINSTATE TRUE LENGTH OF PARAM
         SR    7,8                     DECREMENT CHARACTER COUNT
         BC    8,PROCESS               IF ZERO, NO MORE PARAMETERS
         AR    5,8                     INCREMENT PARAM ADDRESS COUNT
         CLC   0(1,5),COMMA            TEST FOR 2ND DELIMITER CHARACTER
         BC    8,TESTPARC
         BC    15,ERROPARM
PACKB    PACK  MAXREC,0(0,5)           THE LENGTH OF 2ND OPERAND MODIF
*                                      BY EXECUTE INSTRUCTION HEREABOVE
*
THIRDPAR L     4,FW5                   MAX. CHARS IN 3RD PARAMETER = 5
         MVI   SWITCH,C'3'
         BAL   14,SCANPAR              BRANCH AND LINK TO SCANPAR
         EX    8,PACKC                 PACK 3RD PARAMETER TO FIRSTREC
         A     8,FW1                   REINSTATE TRUE LENGTH OF PARAM
         SR    7,8                     DECREMENT CHARACTER COUNT
         BC    8,PROCESS               IF ZERO, NO MORE PARAMETERS
         AR    5,8                     INCREMENT PARAM ADDRESS COUNT
         CLC   0(1,5),COMMA            TEST FOR 3RD DELIMITER CHARACTER
         BC    8,EXECUTE               A COMMA IS FOUND
         BC    15,ERROPARM             GO TO ABEND ROUTINE
PACKC    PACK  FIRSTREC,0(0,5)         THE LENGTH OF 2ND OPERAND MODIF
*                                      BY EXECUTE INSTRUCTION HEREABOVE
*
*                   NOTE THE PURPOSE OF THIS SUBROUTINE IS TO
*                   CALCULATE THE LENGTH OF INDIVIDUAL PARAMETERS
*                   AND TO PUT THIS LENGTH IS REGISTER 8
*
SCANPAR  SR    8,8                     INITIALIZE R8
         LR    6,7                     MOVE R7 TO R6
         LR    9,5                     MOVE R5 TO R9
SCAN1    CLC   0(1,9),COMMA            COMPARE ADDRESS OF R9 WITH COMMA
         BC    8,SUBTR1                IF EQUAL GO TO SUBTR1
         LA    8,1(8)                  ADD 1 TO VALUE CONTAINED IN R8
         LA    9,1(9)                  ADD 1 TO VALUE CONTAINED IN R9
         S     6,FW1                   SUBTRACT 1 FROM R6
         BC    8,SUBTR1                BRANCH IF R6 IS ZERO
         S     4,FW1                   SUBTRACT 1 FROM R4
         BC    8,SUBTR1                BRANCH IF R4 IS ZERO
         BC    15,SCAN1                GO TO SCAN1
SUBTR1   S     8,FW1                   SUBTRACT 1 FROM R8
         BR    14                      RETURN
PROCESS  OPEN  (SYSUT2,(OUTPUT))       OPEN OUTPUT FILE
         MVC   EVRYCOUN,FREQREC        INITIALIZE EVRYCOUN
         SP    EVRYCOUN,K1             SUBTRACT 1 FROM EVRYCOUN
GETRECOR GET   SYSUT1                  GET A RECORD IN INPUT FILE
         AP    RECOUNT,K1              ADD 1 TO RECORD COUNTER
         CP    RECOUNT,FIRSTREC        COMPARE RECOUNT WITH FIRSTREC
         BC    4,GETRECOR              GO TO GETRECORD IF RECOUNT < FIR
         AP    EVRYCOUN,K1             ADD 1 TO EVRYCOUN
         CP    EVRYCOUN,FREQREC        COMPARE EVRYCOUN WITH FREQREC
         BC    7,GETRECOR              GO TO GETRECOR IF EVRYC < FREQR
         LR    0,1                     MOVE R1  TO  R0
         PUT   SYSUT2,(0)              WRITE A RECORD IN OUTPUT FILE
         AP    GENECOUN,K1             ADD 1 TO GENERATED RECORD COUNT
         MVC   EVRYCOUN,K0             INITIALIZE EVERY RECORD COUNTER
         CP    GENECOUN,MAXREC         COMPARE GENECOUN WITH MAX RECORD
         BC    4,GETRECOR              GO TO READ IF NO ENOUGH RECORDS
ENDINPUT MVC   PRTAREA(32),MESG1
         BAL   11,PRINT                PERFORM A LINE PRINTING
         ED    MASK,GENECOUN           EDIT GENERATED RECS COUNT
         MVC   PRTAREA(34),MASK
         BAL   11,PRINT                PERFORM A LINE PRINTING
         MVC   PRTAREA(15),MESG4
         BAL   11,PRINT                PERFORM A LINE PRINTING
         CLOSE (SYSUT1,,SYSUT2)        CLOSE INPUT AND OUTPUT FILES
         CLOSE (PRINT)                 CLOSE PRINTER
         L     13,SAVE+4               REINSTATE  ADDRESS OF RETURN
         RETURN (14,12),RC=0
*                   NOTE THE PURPOSE OF THIS SUBROUTINE IS TO
*                   ABEND THE PROGRAM WITHOUT CORE DUMP AND
*                   WITH CONDITION  CODE 16
*
ERROPARM EQU   *
         MVC   PRTAREA(32),MESG5       PREPARE MESSAGE
         BAL   11,PRINT                PERFORM A LINE PRINTING
         MVC   PRTAREA(21),MESG6       PREPARE MESSAGE
         BAL   11,PRINT                PERFORM A LINE PRINTING
         L     13,SAVE+4               REINSTATE  ADDRESS OF RETURN
         RETURN (14,12),RC=16
*
*
*
*                   PRINTING SUBROUTINE
*
PRINT    EQU   *
         PUT   SYSPRINT,PRTAREA-1      WRITE A LINE
         MVI   PRTAREA,C' '            INITIALIZE THE LINE
         MVC   PRTAREA+1(120),PRTAREA  INITIALIZE THE LINE
         BR    11                      RETURN
*
*                             PROGRAM  WORKING STORAGE AND CONSTANTS
*
FW1      DC    F'1'
FW5      DC    F'5'
SAVE     DS    18F
COMMA    DC    CL1','
SWITCH   DC    CL1' '
FREQREC  DC    P'00001'                ASSUMED VALUE OF 1ST PARAMETER
MAXREC   DC    P'00100'                ASSUMED VALUE OF 2ND PARAMETER
FIRSTREC DC    P'00001'                ASSUMED VALUE OF 3RD PARAMETER
K1       DC    P'1'
RECOUNT  DC    P'00000'
EVRYCOUN DC    P'00000'
GENECOUN DC    P'00000'
K0       DC    P'00000'
PRTCTL   DC    X'8B'
PRTAREA  DC    CL121'MONSANTO MISD  T W A T  UTILITY'
         DC    CL12'            '
MESG1    DC    CL32'SELECTION IS CORRECTLY COMPLETED'
MASK     DC    X'4020206B202120'
MESG3    DC    CL27' RECORDS HAS BEEN GENERATED'
MESG4    DC    C'TWAT TERMINATED'
MESG5    DC    CL32'INCORRECT PARAMETER INFORMATIONS'
MESG6    DC    CL21'TWAT BADLY TERMINATED'
         LTORG
*
*                                      DEFINE THE DCB'S
*
SYSUT1   DCB   DSORG=PS,                                               C
               MACRF=GL,                                               C
               DDNAME=SYSUT1,                                          C
               RECFM=FB,                                               C
               BFTEK=S,                                                C
               EODAD=ENDINPUT
SYSUT2   DCB   DSORG=PS,                                               C
               MACRF=PM,                                               C
               DDNAME=SYSUT2,                                          C
               RECFM=FB,                                               C
               BFTEK=S
SYSPRINT DCB   DSORG=PS,                                               C
               MACRF=PM,                                               C
               DDNAME=SYSPRINT,                                        C
               RECFM=FM,                                               C
               LRECL=133,                                              C
               BLKSIZE=133,                                            C
               BFTEK=S
         END   BEGIN1
