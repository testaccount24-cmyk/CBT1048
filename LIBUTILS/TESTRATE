       IDENTIFICATION DIVISION.                                         TES00010
                                                                        TES00020
       PROGRAM-ID. 'TESTRATE'.                                          TES00030
                                                                        TES00040
       AUTHOR.     D.A.MOORCROFT.                                       TES00050
                                                                        TES00060
       REMARKS.    LISTS THE CURRENCY CONVERSION RATE MASTER TABLE      TES00070
                    AND VERIFIES THAT EACH ROUTINE CONTAINS THE         TES00080
                    CORRECT RATES.                                      TES00090
                                                                        TES00100
       ENVIRONMENT DIVISION.                                            TES00110
                                                                        TES00120
       CONFIGURATION SECTION.                                           TES00130
                                                                        TES00140
       SOURCE-COMPUTER. IBM-360.                                        TES00150
       OBJECT-COMPUTER. IBM-360.                                        TES00160
                                                                        TES00170
       INPUT-OUTPUT SECTION.                                            TES00180
                                                                        TES00190
       FILE-CONTROL.                                                    TES00200
                                                                        TES00210
           SELECT TABLE    ASSIGN TO 'SYSIN'    UTILITY.                TES00220
           SELECT PRINT    ASSIGN TO 'SYSPRINT' UTILITY.                TES00230
                                                                        TES00240
       DATA DIVISION.                                                   TES00250
                                                                        TES00260
       FILE SECTION.                                                    TES00270
                                                                        TES00280
       FD  TABLE,                                                       TES00290
           RECORDING MODE IS F,                                         TES00300
           LABEL RECORDS ARE STANDARD,                                  TES00310
           RECORD CONTAINS 80 CHARACTERS,                               TES00320
           BLOCK CONTAINS 0 RECORDS                                        00330
           DATA RECORD IS TABLE-REC.                                    TES00340
                                                                        TES00350
       01  TABLE-REC.                                                   TES00360
           03  TABLE-ID        PICTURE X(7).                            TES00370
           03  FILLER          PICTURE XX.                              TES00380
           03  TABLE-CODE      PICTURE 99.                              TES00390
           03  FILLER          PICTURE X(4).                            TES00400
           03  TABLE-RATE      PICTURE 9V9(5).                          TES00410
           03  TABLE-RATE-D    REDEFINES TABLE-RATE                     TES00420
                               PICTURE X(6).                            TES00430
           03  FILLER          PICTURE X(4).                            TES00440
           03  TABLE-ABBREV    PICTURE X(4).                            TES00450
           03  FILLER          PICTURE X(4).                            TES00460
           03  TABLE-DESC      PICTURE X(30).                           TES00470
           03  FILLER              PICTURE X(8).                           00480
           03  VERSNO              PICTURE X(5).                           00490
           03  FILLER              PICTURE X(2).                           00500
           03  TABLE-SHUTSW    PICTURE X.                               TES00510
           03  TABLE-STER-SW   PICTURE X.                               TES00520
                                                                        TES00530
       FD  PRINT,                                                       TES00540
           RECORDING MODE IS F,                                         TES00550
           LABEL RECORDS ARE STANDARD,                                  TES00560
           RECORD CONTAINS 133 CHARACTERS,                              TES00570
           DATA RECORD IS PRINT-LINE.                                   TES00580
                                                                        TES00590
       01  PRINT-LINE.                                                  TES00600
           03  FILLER          PICTURE X.                               TES00610
           03  LINE-PRINT      PICTURE X(132).                          TES00620
                                                                        TES00630
       WORKING-STORAGE SECTION.                                         TES00640
                                                                        TES00650
       77  SHUT410             PICTURE X(8)    VALUE 'SHUT410'.         TES00660
       77  UT410               PICTURE X(8)    VALUE 'UT410'.           TES00670
       77  UT435               PICTURE X(8)    VALUE 'UT435'.           TES00680
       77  UT460               PICTURE X(8)    VALUE 'UT460'.           TES00690
       77  USERCD              PICTURE XXX     VALUE '410'.             TES00700
       77  COUNT               PICTURE S999    VALUE 0                  TES00710
                                               COMPUTATIONAL-3.         TES00720
                                                                           00721
       01  PARM.                                                           00722
           03  PARM-YEAR           PICTURE XX VALUE SPACES.                00723
           03  FILLER              PICTURE X.                              00724
           03  PARM-NO             PICTURE XX.                             00725
                                                                           00726
       01  SRNAME.                                                         00727
           03  FILLER              PICTURE X VALUE 'V'.                    00728
           03  SR-YEAR             PICTURE XX.                             00729
           03  SR-NO               PICTURE XX.                             00730
           03  FILLER              PICTURE X VALUE 'U'.                    00731
           03  SR-NAME             PICTURE XX.                             00732
       01  HEAD1.                                                       TES00733
           03  FILLER          PICTURE X(45)   VALUE SPACES.            TES00740
           03  FILLER          PICTURE X(33)                            TES00750
                               VALUE ' CURRENCY CONVERSION RATES AS OF'.TES00760
           03  DATEHD          PICTURE X(9)    VALUE 'COBOLDATE'.       TES00770
           03  FILLER          PICTURE X(45)   VALUE SPACES.            TES00780
                                                                        TES00790
       01  HEAD2.                                                       TES00800
           03  FILLER          PICTURE X(48)                            TES00810
               VALUE ' CODE   ABBREV   *- CURRENCY CODE DESCRIPTION -*'.TES00820
           03  FILLER          PICTURE X(47)                            TES00830
               VALUE '   MASTER             UT410    SHUT410    UT435'. TES00840
           03  FILLER          PICTURE X(37)                            TES00850
               VALUE '     UT460              FLAGS        '.           TES00860
                                                                        TES00870
       01  HEAD3.                                                       TES00880
           03  FILLER          PICTURE X(51)   VALUE SPACES.            TES00890
           03  FILLER          PICTURE X(19)   VALUE 'RATES'.           TES00900
           03  FILLER          PICTURE X(35)                            TES00910
               VALUE 'RATES     RATES     RATES     RATES'.             TES00920
           03  FILLER          PICTURE X(27)   VALUE SPACES.            TES00930
                                                                        TES00940
       01  LINE1.                                                       TES00950
           03  FILLER          PICTURE XX      VALUE SPACES.            TES00960
           03  LINE-CODE       PICTURE 99.                              TES00970
           03  FILLER          PICTURE X(5)    VALUE SPACES.            TES00980
           03  LINE-ABBREV     PICTURE X(4).                            TES00990
           03  FILLER          PICTURE X(4)    VALUE SPACES.            TES01000
           03  LINE-DESC       PICTURE X(31).                           TES01010
           03  FILLER          PICTURE XXX     VALUE SPACES.            TES01020
           03  LINE-RATE       PICTURE 9.99999.                         TES01030
           03  LINE-VERSNO REDEFINES LINE-RATE PICTURE X(7).               01040
           03  FILLER          PICTURE X(11)   VALUE SPACES.            TES01050
           03  LINE-UT410      PICTURE 9.99999.                         TES01060
           03    UT410-VERSNL REDEFINES LINE-UT410   PICTURE X(7).         01070
           03  FILLER          PICTURE X(3)    VALUE SPACES.            TES01080
           03  LINE-SHUT410    PICTURE 9.99999.                         TES01090
           03  SHUT410-VERSNL REDEFINES LINE-SHUT410 PICTURE X(7).         01100
           03  LINE-SHUT410-D  REDEFINES LINE-SHUT410                   TES01110
                               PICTURE X(7).                            TES01120
           03  FILLER          PICTURE X(3)    VALUE SPACES.            TES01130
           03  LINE-UT435      PICTURE 9.99999.                         TES01140
           03    UT435-VERSNL REDEFINES LINE-UT435   PICTURE X(7).         01150
           03  FILLER          PICTURE X(3)    VALUE SPACES.            TES01160
           03  LINE-UT460      PICTURE 9.99999.                         TES01170
           03    UT460-VERSNL REDEFINES LINE-UT460   PICTURE X(7).         01180
           03  FILLER          PICTURE X(13)   VALUE SPACES.            TES01190
           03  LINE-FLAG1      PICTURE X       VALUE SPACES.            TES01200
           03  LINE-FLAG2      PICTURE X       VALUE SPACES.            TES01210
           03  LINE-FLAG3      PICTURE X       VALUE SPACES.            TES01220
           03  LINE-FLAG4      PICTURE X       VALUE SPACES.            TES01230
           03  LINE-FLAG5      PICTURE X       VALUE SPACES.            TES01240
           03  LINE-FLAG6          PICTURE X      VALUE SPACES.            01250
           03  FILLER              PICTURE X(7)   VALUE SPACES.            01260
                                                                        TES01270
       01  UT410-INPUT.                                                 TES01280
           03  UT410-INCODE    PICTURE 99.                              TES01290
           03  UT410-STERIND   PICTURE X.                               TES01300
           03  UT410-INVALU    PICTURE S9(9)V9(5).                      TES01310
           03  UT410-FILLER    PICTURE XX      VALUE SPACES.            TES01320
           03  UT410-OUTCODE   PICTURE XX      VALUE '02'.              TES01330
                                                                        TES01340
       01  UT410-OUTPUT.                                                TES01350
           03  FILLER          PICTURE X(128).                          TES01360
           03  UT410-OUTVALU   PICTURE S9(11)V9(5).                     TES01370
           03  UT410-VERSNO REDEFINES UT410-OUTVALU PICTURE X(16).         01380
           03  FILLER          PICTURE X(15).                           TES01390
           03  UT410-IND       PICTURE X.                               TES01400
                                                                        TES01410
       01  UT435-AREA.                                                  TES01420
           03  UT435-INVALU    PICTURE S9(10)V9(5) COMPUTATIONAL-3.     TES01430
           03  UT435-RATE REDEFINES UT435-INVALU PICTURE X(8).             01440
           03  UT435-INCODE    PICTURE 99.                              TES01450
           03  UT435-OUTCODE   PICTURE XX.                              TES01460
           03  UT435-IND       PICTURE X.                               TES01470
           03  UT435-OUTVALU   PICTURE S9(10)V9(5) COMPUTATIONAL-3.     TES01480
           03  UT435-VERSNO REDEFINES UT435-OUTVALU PICTURE X(8).          01490
           03  FILLER          PICTURE X(8).                            TES01500
           03  UT435-ROUND         PICTURE X.                              01510
                                                                        TES01520
       PROCEDURE DIVISION.                                              TES01530
                                                                        TES01540
       GET-PARM.                                                           01541
           ENTER LINKAGE.                                                  01542
           CALL 'GETPAR' USING PARM.                                       01543
           ENTER COBOL.                                                    01544
           IF PARM-YEAR IS EQUAL TO SPACES                                 01545
           THEN GO TO A070-INITIALISE.                                     01546
           MOVE PARM-YEAR TO SR-YEAR.                                      01547
           MOVE PARM-NO TO SR-NO.                                          01548
           MOVE '10' TO SR-NAME.   MOVE SRNAME TO UT410.                   01549
           MOVE '35' TO SR-NAME.   MOVE SRNAME TO UT435.                   01550
           MOVE '60' TO SR-NAME.   MOVE SRNAME TO UT460.                   01551
           MOVE 'SH' TO SR-NAME.   MOVE SRNAME TO SHUT410.                 01552
                                                                           01553
       A070-INITIALISE.                                                 TES01554
           OPEN INPUT TABLE.                                            TES01560
           OPEN OUTPUT PRINT.                                           TES01570
           ENTER LINKAGE.                                               TES01580
           CALL 'DTERTN' USING DATEHD.                                  TES01590
           ENTER COBOL.                                                 TES01600
           ENTER LINKAGE.                                               TES01610
           CALL 'DYNAML' USING UT410.                                   TES01620
           ENTER COBOL.                                                 TES01630
           ENTER LINKAGE.                                               TES01640
           CALL 'DYNAML' USING UT435.                                   TES01650
           ENTER COBOL.                                                 TES01660
           ENTER LINKAGE.                                               TES01670
           CALL 'DYNAML' USING UT460.                                   TES01680
           ENTER COBOL.                                                 TES01690
           ENTER LINKAGE.                                               TES01700
           CALL 'DYNAML' USING SHUT410.                                 TES01710
           ENTER COBOL.                                                 TES01720
                                                                        TES01730
       A070-READ.                                                       TES01740
           READ TABLE AT END GO TO B080-END.                            TES01750
           IF TABLE-ID IS EQUAL TO 'TITLE01'                               01760
               THEN PERFORM Z300-VERSNO                                    01770
               GO TO A070-READ.                                            01780
           IF TABLE-ID IS NOT EQUAL TO 'TABLE01'                        TES01790
               THEN GO TO A070-READ.                                    TES01800
           ADD 1 TO COUNT.                                              TES01810
           IF COUNT IS NOT EQUAL TO TABLE-CODE                          TES01820
               DISPLAY 'MASTER FILE SEQUENCE ERROR'                     TES01830
               GO TO Z200-BAD-END.                                      TES01840
           MOVE TABLE-CODE     TO LINE-CODE.                            TES01850
           IF TABLE-RATE-D = SPACES NEXT SENTENCE,                      TES01860
               ELSE MOVE TABLE-RATE TO LINE-RATE.                       TES01870
           MOVE TABLE-ABBREV   TO LINE-ABBREV.                          TES01880
           MOVE TABLE-DESC     TO LINE-DESC.                            TES01890
           PERFORM Z200-READY-UT410.                                    TES01900
           PERFORM Z210-UT410.                                          TES01910
           PERFORM Z200-READY-UT410.                                    TES01920
           PERFORM Z210-SHUT410.                                        TES01930
           PERFORM Z210-READY-UT435.                                    TES01940
           PERFORM Z220-UT435.                                          TES01950
           PERFORM Z210-READY-UT435.                                    TES01960
           PERFORM Z220-UT460.                                          TES01970
           PERFORM Z200-PRINT.                                          TES01980
           GO TO A070-READ.                                             TES01990
                                                                        TES02000
       B080-END.                                                        TES02010
           IF COUNT IS NOT EQUAL TO 98                                  TES02020
               DISPLAY 'MASTER FILE WRONG SIZE'                         TES02030
               GO TO Z200-BAD-END.                                      TES02040
           CLOSE TABLE, PRINT.                                          TES02050
           STOP RUN.                                                    TES02060
                                                                        TES02070
       Z200-BAD-END.                                                    TES02080
           DISPLAY 'BAD END OF TESTRATE'.                               TES02090
           ENTER LINKAGE.                                               TES02100
           CALL 'COBDUMN' USING USERCD.                                    02110
           ENTER COBOL.                                                 TES02120
                                                                        TES02130
       Z200-PRINT.                                                      TES02140
           IF COUNT = 0 OR COUNT = 50                                      02150
               MOVE HEAD1 TO LINE-PRINT                                 TES02160
               WRITE PRINT-LINE AFTER 0 LINES                           TES02170
               MOVE HEAD2 TO LINE-PRINT                                 TES02180
               WRITE PRINT-LINE AFTER 2 LINES                           TES02190
               MOVE HEAD3 TO LINE-PRINT                                 TES02200
               WRITE PRINT-LINE AFTER 1 LINES                           TES02210
               MOVE SPACES TO LINE-PRINT                                TES02220
               WRITE PRINT-LINE AFTER 1 LINES.                          TES02230
           MOVE LINE1 TO LINE-PRINT.                                    TES02240
           WRITE PRINT-LINE AFTER 1 LINES.                              TES02250
           MOVE SPACES TO LINE1.                                        TES02260
                                                                        TES02270
       Z200-READY-UT410.                                                TES02280
           MOVE '02' TO UT410-OUTCODE.                                  TES02290
           MOVE TABLE-CODE TO UT410-INCODE.                             TES02300
           MOVE SPACES TO UT410-FILLER                                  TES02310
           IF UT410-INCODE = 19                                         TES02320
               MOVE     '2' TO UT410-STERIND                            TES02330
               ELSE MOVE SPACES TO UT410-IND.                           TES02340
           IF TABLE-STER-SW = 'S'                                       TES02350
               MOVE 100.00000 TO UT410-INVALU                           TES02360
               MOVE TABLE-STER-SW TO LINE-FLAG5                         TES02370
               ELSE MOVE 1.00000 TO UT410-INVALU.                       TES02380
                                                                        TES02390
       Z210-READY-UT435.                                                TES02400
           MOVE '02' TO UT435-OUTCODE.                                  TES02410
           MOVE TABLE-CODE TO UT435-INCODE.                             TES02420
           MOVE 1.00000 TO UT435-INVALU.                                TES02430
                                                                        TES02440
       Z210-UT410.                                                      TES02450
           ENTER LINKAGE.                                               TES02460
           CALL 'DYNAMO' USING UT410, UT410-INPUT, UT410-OUTPUT.        TES02470
           ENTER COBOL.                                                 TES02480
           IF UT410-IND = '1'                                           TES02490
               MOVE UT410-OUTVALU TO LINE-UT410.                        TES02500
           IF LINE-RATE IS NOT EQUAL TO LINE-UT410                      TES02510
               MOVE '*' TO LINE-FLAG1.                                  TES02520
                                                                        TES02530
       Z210-SHUT410.                                                    TES02540
           ENTER LINKAGE.                                               TES02550
           CALL 'DYNAMO' USING SHUT410, UT410-INPUT, UT410-OUTPUT.      TES02560
           ENTER COBOL.                                                 TES02570
           IF UT410-IND = '1'                                           TES02580
               MOVE UT410-OUTVALU TO LINE-SHUT410.                      TES02590
           IF LINE-RATE IS NOT EQUAL TO LINE-SHUT410                    TES02600
               MOVE '*' TO LINE-FLAG2.                                  TES02610
           IF LINE-SHUT410-D = SPACES AND TABLE-SHUTSW                  TES02620
               IS NOT EQUAL TO '*'                                         02630
               MOVE SPACE TO LINE-FLAG2.                                TES02640
                                                                        TES02650
       Z220-UT435.                                                      TES02660
           ENTER LINKAGE.                                               TES02670
           CALL 'DYNAMO' USING UT435, UT435-AREA.                       TES02680
           ENTER COBOL.                                                 TES02690
           IF UT435-IND IS EQUAL TO '1'                                 TES02700
               MOVE UT435-OUTVALU TO LINE-UT435.                        TES02710
           IF LINE-RATE IS NOT EQUAL TO LINE-UT435                      TES02720
               AND TABLE-STER-SW IS NOT EQUAL TO 'S'                    TES02730
               MOVE '*' TO LINE-FLAG3.                                  TES02740
                                                                        TES02750
       Z220-UT460.                                                      TES02760
           MOVE 'RATE' TO UT435-RATE.                                      02770
           ENTER LINKAGE.                                               TES02780
           CALL 'DYNAMO' USING UT460, UT435-AREA.                       TES02790
           ENTER COBOL.                                                 TES02800
           IF UT435-IND = '1'                                           TES02810
               MOVE UT435-OUTVALU TO LINE-UT460.                        TES02820
           IF LINE-RATE IS NOT EQUAL TO LINE-UT460                      TES02830
               AND TABLE-STER-SW IS NOT EQUAL TO 'S'                       02840
               MOVE '*' TO LINE-FLAG4.                                  TES02850
           MOVE SPACES TO UT435-ROUND.                                     02860
           MOVE TABLE-CODE TO UT435-OUTCODE.                               02870
           MOVE 'RATE' TO UT435-RATE.                                      02880
           ENTER LINKAGE.                                                  02890
           CALL 'DYNAMO' USING UT460, UT435-AREA.                          02900
           ENTER COBOL.                                                    02910
           MOVE UT435-ROUND TO LINE-FLAG6.                                 02920
                                                                           02930
       Z300-VERSNO.                                                        02940
           MOVE SPACES TO LINE1.                                           02950
           MOVE SPACES TO UT410-OUTPUT.                                    02960
           MOVE SPACES TO UT435-AREA.                                      02970
           MOVE 'VN' TO UT410-OUTCODE.                                     02980
           MOVE 'VN' TO UT435-OUTCODE.                                     02990
           MOVE 'TABLE VERSION NUMBER' TO LINE-DESC.                       03000
           MOVE VERSNO TO LINE-VERSNO.                                     03010
           PERFORM Z210-UT410.                                             03020
           MOVE   UT410-VERSNO TO  UT410-VERSNL.                           03030
           MOVE SPACE TO LINE-FLAG1.                                       03040
           IF VERSNO IS NOT EQUAL TO UT410-VERSNO                          03050
               MOVE '*' TO LINE-FLAG1.                                     03060
           PERFORM Z210-SHUT410.                                           03070
           MOVE   UT410-VERSNO TO  SHUT410-VERSNL.                         03080
               IF VERSNO IS NOT EQUAL TO UT410-VERSNO                      03090
               MOVE '*' TO LINE-FLAG2.                                     03100
           PERFORM Z220-UT435.                                             03110
           MOVE   UT435-VERSNO TO  UT435-VERSNL.                           03120
           MOVE SPACE TO LINE-FLAG3.                                       03130
           IF VERSNO IS NOT EQUAL TO UT435-VERSNO                          03140
               MOVE '*' TO LINE-FLAG3.                                     03150
           ENTER LINKAGE.                                                  03160
           CALL 'DYNAMO' USING UT460, UT435-AREA.                          03170
           ENTER COBOL.                                                    03180
           IF VERSNO IS NOT EQUAL TO UT435-VERSNO                          03190
               MOVE '*' TO LINE-FLAG4.                                     03200
           MOVE   UT435-VERSNO TO  UT460-VERSNL.                           03210
           PERFORM Z200-PRINT.                                             03220
