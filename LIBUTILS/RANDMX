         TITLE 'PSEUDO-RANDOM NUMBER GENERATOR'
         ISEQ  76,80
         SPACE 1
* WRITTEN BY D. R. HURTGEN
*
* MONSANTO COMPANY - CREVE COEUR
*
* MARCH 1970
*
* THIS SUBROUTINE CALCULATES A PSEUDO-RANDOM NUMBER AND RETURNS IT TO
* THE CALLER. IT IS BASED UPON AN ALGORITHM DESCRIBED BY LEWIS,
* GOODMAN, AND MILLER IN THE I.B.M. SYSTEMS JOURNAL, VOLUME EIGHT,
* NUMBER TWO.
*
* THE CALLING SEQUENCE IS -
*   CALL RANDMX (INT,REAL)
* WHERE 'INT' IS A FULL WORD INTEGER VARIABLE AND 'REAL' IS A FULL WORD
* REAL VARIABLE (SINGLE PRECISION). EACH CALL TO THE SUBROUTINE CAUSES
* NEW PSEUDO-RANDOM VALUES TO BE PLACED IN BOTH OPERANDS. IN ADDITION,
* VARIABLE 'INT' MAY BE USED FOR COMMUNICATION WITH THIS ROUTINE AS
* FOLLOWS -
*   IF IT IS ZERO, THE SUBROUTINE RESTARTS THE SEQUENCE WITH ITS OWN
*   STARTING VALUE.
*   IF IT IS A NEGATIVE INTEGER, IT RESTARTS USING THE INTEGER PORTION
*   OF THE VALUE.
* THE ROUTINE ALWAYS SAVES THE RESULTS OF THE PRIOR CALCULATION SO IT
* IS NOT NECESSARY FOR THE CALLER TO PLACE ANYTHING IN THE PARAMETERS
* EXCEPT WHEN RESTART IS DESIRED.
         SPACE 2
RANDMX   START
         USING *,15 ESTABLISH ADDRESSABILITY
         STM   INT,WB,28(13)  SAVE REGISTERS
         LM    INT,REAL,0(1)  GET ADDRESSES OF VARIABLES
         L     WB,0(,INT)     GET INTEGER VARIABLE
         LCR   WB,WB     Q.-RESTART REQUESTED
         BP    CALCULAT  B.-YES, USING CALLERS VALUE
         BM    LOADIT    B.-NO
         MVC   OLDVALUE,STARTVAL   RESTART WITH OUR VALUE
LOADIT   L     WB,OLDVALUE    LOAD PREVIOUS VALUE
CALCULAT M     WA,PPROOT COMPUTE NEW INTEGER RANDOM NUMBER
         D     WA,PRIME
         ST    WA,OLDVALUE    SAVE FOR NEXT TIME
         ST    WA,0(,INT)     GIVE CALLER INTEGER VALUE
         SRL   WA,7 CONVERT INTEGER TO REAL
         ST    WA,WORKWORD
         MVI   WORKWORD,X'40'
         SUR   FPR,FPR
         AE    FPR,WORKWORD   NORMALIZE IT
         STE   FPR,0(,REAL)   GIVE IT TO CALLER
         LM    INT,WB,28(13)  RESTORE REGISTERS
         SR    15,15     SET CONDITION CODE
         BR    14   RETURN TO CALLER
*                        CONSTANTS AND EQUATES
         SPACE 2
INT      EQU   2
REAL     EQU   3
WA       EQU   4
WB       EQU   5
FPR      EQU   6
         SPACE 2
         DS    0D   ALIGN FOR SPEED
OLDVALUE DC    F'13034519'
STARTVAL DC    F'13034519'    ARBITRARY (ALMOST) STARTING VALUE
PPROOT   DC    F'16807'  POSITIVE PRIMITIVE ROOT OF 'P'
PRIME    DC    F'2147483647'  LARGEST PRIME 'P' LESS THAN 2**31-1
WORKWORD DS    F
         END
