              ICTL  6,71,21
00000UT419    CSECT
00010         WXTRN PARAM
00020         PRINT ON,NODATA,NOGEN
00030         SAVE  (2,12)
00040         BALR  12,0
00050         USING *,12
00055SWITCH1  NOP   COBOLOAD       IF COBOL BYPASS RPG LOAD ROUTINE
00060         L     5,APARAM       RPG PARAMETER ADDRESS INTO REG 5
00075SWITCH2  NOP   PROCESS        BYPASS RPG TEST AFTER FIRST TIME
00080         CLI   60(5),C'R'     IS CALLING LANGUAGE RPG ?
00085         MVI   SWITCH2+1,X'F0' SET ON BYPASS SWITCH
00090         BE    PROCESS        YES - BYPASS COBOL LOAD
00095         MVI   SWITCH1+1,X'F0' SET ON COBOL LOAD SWITCH
00100COBOLOAD L     5,0(1)         COBOL PARAMETER ADDRESS INTO REG 5
00110         SPACE
00120PROCESS  CLI   61(5),C'P'     Q - IS OPERATION PACK
00130         BE    PACK           YES - GO TO PACK ROUTINE
00140         CLI   61(5),C'U'     IS REQUESTED OPERATION UNPACK ?
00150         BE    UNPACK         YES - GO TO UNPACK ROUTINE
00160         MVI   60(5),C'1'     INVALID OPERATION - SET ON ERR-SWTCH
00170EXIT     RETURN (2,12)        RETURN TO CALLING PROGRAM
00180         SPACE 5
00190PACK     LA    6,4(0)         SET BCT COUNT TO 4
00200         LA    7,40(5)        ADDRESS OF OUTPUT AREA TO REG 7
00210         LR    8,5            ADDRESS OF INPUT AREA TO REG 8
00220         BAL   4,PACKRTN      PERFORM PACK ROUTINE ON 10 BYTES
00230         LA    7,5(7)         INCREMENT POINTER TO OUTPUT AREA
00240         LA    8,10(8)        INCREMENT POINTER TO INPUT AREA
00250         BCT   6,PACK+10      REPEAT PROCESS 3 TIMES
00260         MVI   60(5),C'0'     INDICATE GOOD END IN ERROR SWITCH
00270         B     EXIT           RETURN TO CALLING PROGRAM
00280         SPACE 2
00290PACKRTN  MVC   AREA1,0(8)     MOVE 10 BYTES TO WORK AREA
00300         LA    9,AREA1        ADDRESS OF WORK AREA TO REG 9
00310         LA    10,10(0)       SET BCT COUNT TO 10
00320LOOP1    CLI   0(9),X'40'     Q - IS BYTE BLANK
00330         BNE   *+12           NO - GO TO NEXT TEST
00340         MVI   0(9),X'FF'     YES - SET TO HEX FF
00350         B     INCR1          GO AND INCREMENT POINTERS
00360         CLI   0(9),X'F0'     Q - IS BYTE LESS THAN ZERO
00370         BNL   *+12           NO - GO TO NEXT TEST
00380         MVI   0(9),X'FA'     YES - SET TO HEX FA
00390         B     INCR1          GO AND INCREMENT POINTER
00400         CLI   0(9),X'F9'     Q - IS BYTE GREATER THAN 9
00410         BNH   *+8            NO - GO TO INCREMENT POINTER
00420         MVI   0(9),X'FA'     YES - SET TO HEX FA
00430INCR1    LA    9,1(9)         INCREMENT POINTER
00440         BCT   10,LOOP1       REPEAT PROCESS 10 TIMES
00450         PACK  AREA2(6),AREA1(11)  PACK INPUT FIELD
00460         MVC   0(5,7),AREA2   MOVE PACKED FIELD TO OUTPUT
00470         BR    4
00480         SPACE 5
00490UNPACK   LA    6,4(0)         SET BCT COUNT TO 4
00500         LA    7,40(5)        ADDRESS OF INPUT AREA TO REG 7
00510         LR    8,5            ADDRESS OF OUTPUT AREA TO REG 8
00520         BAL   4,UNPKRTN      PERFORM UNPACK ROUTINE ON 10 BYTES
00530         LA    7,5(7)         INCREMENT POINTER TO INPUT AREA
00540         LA    8,10(8)        INCREMENT POINTER TO OUTPUT AREA
00550         BCT   6,UNPACK+10    REPEAT PROCESS 3 TIMES
00560         MVI   60(5),C'0'     INDICATE GOOD END IN ERROR SWITCH
00570         B     EXIT           RETURN TO CALLING PROGRAM
00580         SPACE 2
00590UNPKRTN  MVC   AREA2,0(7)     MOVE 5 PACKED BYTES TO WORK AREA
00600         UNPK  AREA1(11),AREA2(6)  UNPACK INTO FIRST WORK AREA
00610         LA    10,10(0)       SET BCT COUNT TO 10
00620         LA    9,AREA1        ADDRESS OF WORK AREA TO REG 9
00630LOOP2    CLI   0(9),X'FF'     Q - IS BYTE EQUAL TO HEX FF
00640         BNE   *+12           NO - GO TO NEXT TEST
00650         MVI   0(9),X'40'     YES - SET BYTE TO BLANK
00660         B     INCR2          GO AND INCREMENT POINTER
00670         CLI   0(9),X'FA'     Q - IS BYTE EQUAL TO HEX FA
00680         BNE   *+12           NO - GO TO NEXT TEST
00690ASTERISK MVI   0(9),X'5C'     YES - SET BYTE TO ASTERISK
00700         B     INCR2          GO AND INCREMENT POINTER
00710         CLI   0(9),X'F0'     Q - IS BYTE LESS THAN ZERO
00720         BL    ASTERISK       YES - GO AND MOVE IN ASTERISK
00730         CLI   0(9),X'F9'     Q - IS BYTE GREATER THAN 9
00740         BH    ASTERISK       YES - GO AND MOVE IN ASTERISK
00750INCR2    LA    9,1(9)         INCREMENT POINTER
00760         BCT   10,LOOP2       REPEAT PROCESS 10 TIMES
00770         MVC   0(10,8),AREA1  MOVE UNPACKED FIELD TO OUTPUT AREA
00780         BR    4
00790         SPACE 5
00800APARAM   DC    A(PARAM)       ADDRESS OF RPG PARAMETER
00810AREA1    DS    CL10           WORK AREA FOR UNPACKED FIELD
00820         DC    X'FF'
00830AREA2    DS    CL5            WORK AREA FOR PACKED FIELD
00840         DC    X'FF'
00850         SPACE
00860         LTORG
00870         SPACE
00880         END
