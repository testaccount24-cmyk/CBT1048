P060    TITLE 'MODIFY JOB CARDS FOR MONSANTO REQUIREMENTS'
***********************************************************************
*                                                                     *
*        ABSTRACT.                                                    *
*        ---------                                                    *
*                                                                     *
*              T158P060  READS JOBSTREAMS DETECTING JOB CARDS.        *
*        ALL INFORMATION AFTER ' JOB ' IS REPLACED BY THE CONTENTS    *
*        OF THE PARM FIELD. A NULL PARM WILL CAUSE THE JOB CARD       *
*        TO BE DELETED.                                               *
*              CARD IMAGES CONTAINED IN THE SYSIN DATASET ARE         *
*        INSERTED AFTER EACH JOB CARD DETECTED. REPLACEMENTS FOR      *
*        DELETED JOB CARDS MAY BE INSERTED HERE.                      *
*                                                                     *
*        WRITTEN BY:   P.S.GRANT - MONSANTO BXL.                      *
*                                                                     *
*        DATE:         JANUARY 1978                                   *
*                                                                     *
***********************************************************************
       SPACE 5
***********************************************************************
*                                                                     *
*        INPUT.                                                       *
*        ------                                                       *
*              SYSUT1  - EXPECTED. COMPOSED OF 80 BYTE CARD IMAGES    *
*                        CONTAINING THE JOBSTREAM TO BE EXAMINED.     *
*                        MAY BE CONCATENATED.                         *
*                                                                     *
*              PARM    - EXPECTED. COMPOSED OF ALL INFORMATION TO BE  *
*                        INSERTED AFTER ' JOB ' IN EACH JOB CARD      *
*                        DETECTED. IF NULL JOB CARDS WILL BE DELETED. *
*                                                                     *
*              SYSIN   - OPTIONAL. IF PRESENT, CARD IMAGES WILL BE    *
*                        INSERTED AFTER EACH JOB CARD DETECTED.       *
*                        MAY ALSO CONTAIN JOB CARDS WHEN PARM IS NULL.*
*                                                                     *
*        OUTPUT.                                                      *
*        -------                                                      *
*              SYSUT2  - MANDATORY. OUTPUT DATASET CONSISTING OF 80   *
*                        BYTE CARD IMAGES.                            *
*                                                                     *
*              SYSPRINT- OPTIONAL. LISTS CARD IMAGES CREATED.         *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        INITIALISATION.                                              *
*              SAVE PARM ADDRESS.                                     *
*              OPEN ALL AVAILABLE DATASETS.                           *
*              CONFIRM AVAILABILITY OF SYSUT2 DATASET.                *
***********************************************************************
         SPACE 1
T158P060 CSECT
         PGMIN EQUATES=NO
         SPACE 2
*        REGISTER EQUATES
         SPACE 1
R0       EQU   0                   COMMUNICATIONS
R1       EQU   1                   COMMUNICATIONS
R2       EQU   2                   WORK
R3       EQU   3                   WORK
R4       EQU   4                   BAL RETURN ADDRESS
R5       EQU   5                   SYSPRINT LINE ADDRESS
R6       EQU   6                   SYSUT2 RECORD ADDRESS
R7       EQU   7                   SYSUT1 RECORD ADDRESS
R8       EQU   8                   SYSIN  RECORD ADDRESS
R9       EQU   9                   UNUSED
R10      EQU   10                  UNUSED
R11      EQU   11                  UNUSED
R12      EQU   12                  BASE2
R13      EQU   13                  BASE1 / SAVEREA CHAIN
R14      EQU   14                  OS RETURN ADDRESS
R15      EQU   15                  OS ENTRY POINT / RETURN CODE.
         PRINT ON,NOGEN
INIT     EQU   *
         ST    R1,PARMADDR         SAVE PARM ADDRESS.
         BAL   R4,OPENDS           OPEN ALL AVAILABLE DATASETS
         LA    R5,PRTLINE          ADDRESSABILITY FOR PRINT-LINE
         BAL   R4,HEADINGS         PRINT FIRST PAGE HEADINGS.
         TM    SYSUT2SW,DDPRES     TEST FOR SYSUT2 DATASET
         BNO   ABEND158            ABEND IF NOT PRESENT.
         TM    SYSUT1SW,DDPRES     TEST FOR SYSUT1 DATASET
         BNO   ABEND158            ABEND IF NOT PRESENT.
         B     PROCPARM            ALL OK. JUMP OVER ABEND.
         SPACE 3
ABEND158 ABEND 158,,STEP           NO SYSUT2 DATASET FOUND.
         EJECT
***********************************************************************
*                                                                     *
*        PROCESS PARM INFORMATION.                                    *
*              RESTORE PARM ADDRESS.                                  *
*              TEST FOR NULL PARM.  SET SWITCH IF FOUND.              *
*              CHECK FOR EXCESSIVE LENGTH. (MAX 56-CHARACTERS).       *
*              CREATE SKELETON JOBCARD MINUS JOBNAME.         .       *
*                                                                     *
***********************************************************************
         SPACE 1
PROCPARM EQU   *
         L     R1,PARMADDR         RESTORE PARM ADDRESS
         L     R1,0(0,R1)          POINT TO PARM
         LH    R3,0(R1)            GET LENGTH
         LTR   R3,R3               TEST FOR NULL PARM
         BNZ   *+8                 SKIP NSI IF NOT NULL.
         OI    SWBYTE,NULLPARM     SET NULL PARM INDICATOR.
         CH    R3,=H'56'           CHECK MAXIMUM LENGTH
         BH    ABEND158            ABEND IF EXCESSIVE.
         LA    R2,2(0,R1)          R2 POINTS AT PARM DATA
         BCTR  R3,0                REDUCE LENGTH BY ONE FOR EXECUTED MV
         EX    R3,PARMMOVE         MOVE PARM TO SKELETON JOB CARD
         B     PROCUT1             JUMP EXECUTED INSTRUCTION.
         SPACE 1
PARMMOVE MVC  JOBPARM(0),0(R2)     EXECUTED MOVE PARM TO SKELETON.
         SPACE 1
         EJECT
***********************************************************************
*                                                                     *
*        PROCESS SYSUT1 RECORDS.                                      *
*              READ NEXT RECORD.                                      *
*              PASS TO SUBRTN TO CHECK FOR JOBCARD PRESENCE           *
*              AND WRITE TO OUTPUT DATASET.                           *
*              LOOP TILL END OF DATASET.                              *
*                                                                     *
***********************************************************************
         SPACE 1
PROCUT1  EQU   *
NEXTUT1  EQU   *
         BAL   R4,RUT1             READ NEXT SYSUT1 RECORD.
         BAL   R4,SCANJOB          SCAN RECORD FOR ' JOB '
         B     NEXTUT1             GO ROUND LOOP UNTIL END OF DATA
         SPACE 1
EODUT1   EQU   *
         EJECT
*******************************************************************
*        JOB TERMINATION ROUTINE.                                 *
*         CLOSE ANY OPEN DATA SETS,                               *
*         SET A COMPLETION CODE IN REGISTER 15, AND RETURN.       *
*******************************************************************
         SPACE 1
JOBEND   EQU   *
         MVI   PRTCTL,SPCE3AFT     SKIP THREE LINES
         BAL   R4,PRINT
         MVC   PRTAREA+14(17),=CL17'*** END T158P060 ***'
         BAL   R4,PRINT            PRINT EOJ MESSAGE
         LA    R3,MAXDS            ESTABLISH LOOP
         LA    R2,DCBTABLE         POINT AT START OF DCB TABLE
         BAL   R4,CLOSE            CLOSE DATA SET IF OPEN
         LA    R2,DCBNTRY(R2)      POINT AT NEXT TABLE ENTRY
         BCT   R3,*-8              CLOSE NEXT DATA SET
         PGMOUT RC=COMPCODE        SET COND CODE AND RETURN
         EJECT
***********************************************************************
*                                                                     *
*        SUBRTN - SCANJOB.                                            *
*              LOOK AT ALL JCL CARDS AS POTENTIAL JOB CARDS.          *
*              THOSE THAT ARE NOT, WRITE TO SYSUT2.                   *
*              WHEN A JOB CARD IS FOUND:-                             *
*                  1. IF THE PARM IS NULL, DELETE IT.                 *
*                  2. OTHERWISE MOVE JOBNAME TO SKELETON JOBCARD      *
*                     AND WRITE REPLACEMENT JOBCARD.                  *
*                  3. READ SYSIN RECORDS WRITING THEM TO SYSUT2 AND   *
*                     AT END CLOSE AND OPEN SYSIN AGAIN FOR NEXT JOBC.*
*                  4. DELETE REMNANTS OF INPUT JOB STATEMENT.         *
*                                                                     *
*        REGISTER USAGE                                               *
*              INPUT:-  R6 - POINTS  AT SYSUT2 RECORD                 *
*                       R7 - SYSUT1 RECORD ADDRESS.                   *
*                       R8 - SYSIN  RECORD ADDRESS.                   *
*                                                                     *
*           INTERNAL:-  R2 - WORK. POINTER                            *
*                       R3 - WORK. LENGTHS                            *
*                       R6 - OUTPUT RECORD ADDRESS.                   *
*                                                                     *
***********************************************************************
         SPACE 1
SCANJOB  SUBIN SAVE=R4
SCANAGN  EQU   *
         LR    R6,R7               DEFAULT ADDRESS FOR O/P RECORDS.
         CLC   0(2,R7),=CL2'//'    IS THIS A JCL CARD?
         BNE   SCANWUT2            NO. WRITE RECORD IMMEDIATELY.
         CLI   2(R7),C'*'          JCL COMMENT CARD?
         BE    SCANWUT2            YES. WRITE IT OUT AND EXIT.
         LR    R2,R7               START FOR SCAN.
         LH    R3,=H'80'           MAX LENGTH.
         SH    R3,=H'3'            JUMP OVER SHORTEST JOBNAME.
         LA    R2,3(0,R2)          DITTO
         SH    R3,=H'13'           SUBTRACT FOR LAST POSSIBLE COMPARE
SCANLOOP EQU   *
         CLC   0(5,R2),=CL5' JOB ' THIS A JOB CARD?
         BE    SCANJOBC            YES. GET OUT OF LOOP
         LA    R2,1(0,R2)          MOVE ALONG ONE.
         BCT   R3,SCANLOOP         LOOP UNTIL COUNT EXHAUSTED.
         B     SCANWUT2            NOT JOBCARD. WRITE OUT UNCHANGED.
         SPACE 1
SCANJOBC EQU   *
         TM    SWBYTE,NULLPARM     PARM NULL?
         BO    SCANSYSN            YES. JUMP JOBCARD REPLACEMENT.
         LR    R2,R7               GO BACK TO BEGINING
         SR    R3,R3               RESET LENGTH.
SCANSCAN EQU   *
         CLI   0(R2),C' '          END OF JOBNAME?
         BE    SCANNAME            YES. MOVE IT
         LA    R2,1(0,R2)          MOVE ALONG ONE
         LA    R3,1(0,R3)          INCREASE COUNT
         B     SCANSCAN            LOOP BACK
         SPACE 1
SCANMOVE MVC   JOBNAME(0),0(R7)    EXECUTED MOVE OF JOBNAME TO SKELETON
         SPACE 1
SCANNAME EQU   *
         BCTR  R3,0                REDUCE LENGTH BACK TO LAST CHAR.
         EX    R3,SCANMOVE         MOVE JOBNAME TO SKELETON JOBCARD.
         LA    R6,JOBCARD          ADDRESS OF REPLACEMENT
         BAL   R4,WUT2             WRITE REPLACEMENT JOBCARD.
         BAL   R4,PRINT            LIST IF REQUIRED.
SCANSYSN EQU   *      WRITE ANY RECORDS FROM SYSIN AFTER JOBCARD.
         BAL   R4,RIN              READ NEXT SYSIN RECORD
         LR    R6,R8               ADDRESS OF OUTPUT RECORD.
         BAL   R4,WUT2             WRITE SYSIN RECORD TO SYSIN
         BAL   R4,PRINT            LIST IF REQUIRED.
         B     SCANSYSN            LOOP UNTIL EOD.
         SPACE 1
EODIN    EQU   *                   END OF SYSIN DATASET
         LA    R2,SYSINSW          POINT AT SYSIN CONTROL BLOCK
         BAL   R4,CLOSE            CLOSE SYSIN
         BAL   R4,OPEN             OPEN IT AGAIN READY FOR NEXT TIME.
SCANDEL  EQU   *      DELETE ANY JOBCARD CONTINUATIONS FROM INPUT.
         BAL   R4,RUT1             READ NEXT RECORD FROM SYSUT1
         CLC   0(3,R7),=CL3'// '   CONTINUATON JOBCARD ?
         BE    SCANDEL             YES. DROP IT AND READ NEXT
         B     SCANAGN             NO. PASS THROUGH SCAN AGAIN IN
*                                  CASE IT IS A JOBCARD. UNTHINKABLE.
         SPACE 1
SCANWUT2 EQU   *
         BAL   R4,WUT2             WRITE TO SYSUT2
         BAL   R4,PRINT            LIST IF REQUIRED.
SCANEXIT EQU   *
         SUBOUT RESTORE=R4
         EJECT
*******************************************************************
*        OPENDS SUBRTN.                                           *
*        --------------                                           *
*        TEST WHICH ARE THE DD'S SUPPLIED, AND OPEN DATA SETS     *
*         PRESENT.                                                *
*******************************************************************
         SPACE 1
OPENDS   SUBIN SAVE=(R3,R4)
         LA    R3,MAXDS            ESTABLISH LOOP
         LA    R2,DCBTABLE         POINT R2 AT DCB TABLE
         OI    0(R2),DDINT         INDICATE DD INTEREST
         BAL   R4,TESTIOT          GO TEST IF DD STATEMENT PRESENT
         BAL   R4,OPEN             GO OPEN DATA SET
         LA    R2,DCBNTRY(R2)      POINT AT NEXT TABLE ENTRY
         BCT   R3,*-16             PROCESS NEXT DATA SET
         SPACE 1
OPENDSEX SUBOUT RESTORE=(R3,R4)
         SPACE 3
***********************************************************************
*        HEADINGS SUBRTN                                              *
*        ----------------                                             *
*              PRINT PAGE HEADINGS FOR OPTIONAL SYSPRINT DATASET.     *
***********************************************************************
         SPACE 1
HEADINGS SUBIN SAVE=R4
         MVI   PRTCTL,PAGESKIP     SKIP TO NEW PAGE
         BAL   R4,PRINT            GO.
         AP    PAGECOUN,ONE        ADD ONE TO PAGE COUNT
         MVC   PAGEDIT,PATRN1      GET EDIT PATTERN
         ED    PAGEDIT,PAGECOUN    MOVE PAGE COUNTER TO HEAD LINE
         MVC   PRTAREA(L'HEAD1+4),HEAD1  MOVE HEAD LINE TO PRINT.
         MVI   PRTCTL,SPCE3AFT     3-LINE SPACING
         BAL   R4,PRINT            PRINT HEAD LINE
         ZAP   LINECOUN,=PL2'4'    RESET LINE COUNTER
         SUBOUT RESTORE=R4
         TITLE 'I/O SUB-ROUTINES'
*******************************************************************
*        WRITE SUBROUTINE FOR SYSPRINT                            *
*******************************************************************
         SPACE 1
PRINT    SUBIN ,
         TM    PRINTSW,DDINT+DDPRES+DDOPEN   TEST IF NEED TO WRITE
         BNO   PRINTOFF            BRANCH IF NO
         PUT   SYSPRINT,(R5)       WRITE RECORD TO SYSPRINT D/S
         AP    LINECOUN,ONE        INCREMENT LINE CONTER
         MVI   PRTCTL,SPCE1AFT     DEFAULT TO SINGLE SPACING
PRINTOFF EQU   *
         MVI   1(R5),BLANK         MOVE BLANK TO START OF LINE
         MVC   2(131,R5),1(R5)      AND PROPAGANTE THROUGH
PRINTX   SUBOUT ,                  RETURN TO CALLER
         SPACE 2
*******************************************************************
*        READ SUBROUTINE FOR SYSIN                                *
*******************************************************************
         SPACE 1
RIN      SUBIN ,
         TM    SYSINSW,DDOPEN      SYSIN DATASET AVAILABLE?
         BNO   EODIN               NO. BRANCH IMMEDIATELY TO EOD RTN.
         GET   SYSIN               READ RECORD FROM SYSIN D/S
         LR    R8,R1               SAVE RECORD ADDRESS
         AP    CNTSYSIN,ONE        INCREMENT RECORD COUNTER
         SUBOUT ,                  RETURN TO CALLER
         SPACE 2
*******************************************************************
*        READ SUBROUTINE FOR SYSUT1                               *
*******************************************************************
         SPACE 1
RUT1     SUBIN ,
         TM    SYSUT1SW,DDOPEN     SYSUT1 DATASET AVAILABLE?
         BNO   EODUT1              NO. BRANCH IMMEDIATELY TO EOD RTN.
         GET   SYSUT1              READ RECORD FROM SYSUT1 D/S
         LR    R7,R1               SAVE RECORD ADDRESS
         AP    CNTUT1,ONE          INCREMENT RECORD COUNTER
         SUBOUT ,                  RETURN TO CALLER
         SPACE 2
*******************************************************************
*        WRITE SUBROUTINE FOR SYSUT2                              *
*******************************************************************
         SPACE 1
WUT2     SUBIN ,
         TM    SYSUT2SW,DDINT+DDPRES+DDOPEN  TEST IF NEED TO WRITE
         BNO   WUT2X               BRANCH IF NO
         PUT   SYSUT2,0(R6)        WRITE RECORD TO SYSUT2 D/S
         MVC   OUTAREA,0(R6)       MOVE RECORD TO PRINT AREA
         AP    CNTUT2,ONE          INCREMENT RECORD COUNTER
WUT2X    SUBOUT ,                  RETURN TO CALLER
         SPACE 5
*******************************************************************
*        SUPPLY DEFAULT RECORD LENGTH AND/OR BLOCK SIZE           *
*         IF NOT CODED.                                           *
*******************************************************************
         SPACE 1
BLKSZRTN SUBIN ,                   SUPPLY DEFAULT LRECL AND/OR BLKS
         USING IHADCB,R1           ESTABLISH DCB ADDRESSABILITY
         CLC   DCBLRECL,ZEROWORD   TEST IF LRECL SUPPLIED
         BNE   *+8                 BYPASS DEFAULT IF YES
         MVI   DCBLRECL+1,80       FORCE LRECL TO 80
         CLC   DCBBLKSI,ZEROWORD   TEST IF BLKSIZE SUPPLIED
         BNE   0(R14)              RETURN TO OPEN IF YES
         MVC   DCBBLKSI,DCBLRECL   FORCE SINGLE BLOCKING
         DROP  R1                  DROP DCB ADDRESSABILITY
         SUBOUT RETURN=(R14)       RETURN TO OPEN ROUTINES
         SPACE 2
*******************************************************************
*        I/O ERROR ROUTINE                                        *
*******************************************************************
         SPACE 1
IOERROR  SUBIN ,                   ANALYSE I/O ERROR
         MVC   COMPCODE,COND16     SET COMPLETION CODE
         SYNADAF ACSMETH=QSAM      CALL ANALYSIS ROUTINE
         MVC   WTOIOE+27(86),50(R1) ADD RESULT OF ANALYSIS TO LINE
         SYNADRLS ,                RELEASE OS BUFFER
         WTO   MF=(E,WTOIOE)       TELL OPERATOR OF ERROR
         SUBOUT RETURN=JOBEND      TERMINATE JOB
         EJECT
*******************************************************************
*        TEST IF A DD STATEMENT IS PRESENT BEFORE OPEN            *
*******************************************************************
         SPACE 1
TESTIOT  SUBIN ,                   TEST TASK INPUT/OUTPUT TABLE
         TESTIOT DDNAME=4(R2)      TEST IF DDNAME FOUND IN TIOT
         B     *+4(R15)            TEST CONDITION CODE
         OI    0(R2),DDPRES        TELL THAT DD STATEMENT SUPPLIED
         SUBOUT ,                  RETURN TO CALLER
         SPACE 3
*******************************************************************
*        OPEN ROUTINE                                             *
*******************************************************************
         SPACE 1
OPEN     SUBIN SAVE=(R3)           OPEN DATA SET
         TM    0(R2),DDINT+DDPRES  TEST IF DD INTERESTING AND
         BNO   OPENX               BRANCH IF NO    / PRESENT.
         L     R3,0(R2)            GET DCB ADDRESS
         CLC   12(8,R2),=C'INPUT   ' TEST FOR INPUT D/S
         BE    OPENZ               BRANCH IF INPUT D/S
         OPEN  ((R3),(OUTPUT))     OPEN OUTPUT DATA SET
         B     OPENY               GO SET SWITCH
OPENZ    OPEN  ((R3),(INPUT))      OPEN INPUT DATA SET
OPENY    OI    0(R2),DDOPEN        INDICATE DATA SET OPEN
OPENX    SUBOUT RESTORE=(R3)       RETURN TO CALLER
         SPACE 3
*******************************************************************
*        CLOSE ROUTINE                                            *
*******************************************************************
         SPACE 1
CLOSE    SUBIN SAVE=(R3)           CLOSE DATA SET
         TM    0(R2),DDOPEN        TEST IF DATA SET OPEN
         BZ    CLOSEX              EXIT IF NOT
         L     R3,0(R2)            GET DCB ADDRESS
         CLOSE ((R3))              CLOSE DATA SET
         NI    0(R2),FF-DDOPEN     SET OFF 'OPEN' SWITCH
CLOSEX   SUBOUT RESTORE=(R3)       RETURN TO CALLER
         TITLE 'DCB''S AND I/O CONTROL TABLES'
*******************************************************************
*        DCB'S TABLE                                              *
*******************************************************************
         SPACE 1
DCBTABLE DS    0F
PRINTSW  DC    X'00',AL3(SYSPRINT),CL8'SYSPRINT',CL8'OUTPUT'
SYSINSW  DC    X'00',AL3(SYSIN),CL8'SYSIN',CL8'INPUT'
SYSUT1SW DC    X'00',AL3(SYSUT1),CL8'SYSUT1',CL8'INPUT'
SYSUT2SW DC    X'00',AL3(SYSUT2),CL8'SYSUT2',CL8'OUTPUT'
UT2DDN   EQU SYSUT2SW+4
         SPACE 1
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
DCBNTRY  EQU   20                  LENGTH OF ONE TABLE ELEMENT
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUN NUMBER OF DCB'S
         SPACE 1
DDINT    EQU   16                  DD INTEREST
DDPRES   EQU   8                   DD PRESENT
DDOPEN   EQU   1                   DATA SET IS OPEN
         SPACE 2
EXITLIST DC    X'85',AL3(BLKSZRTN) ENTRY FOR DCB MODIFICATION
         SPACE 2
WTOIOE   DS    0F
         DC    AL2(WTOEND-*),H'0',CL109'******** - I/O ERROR -'
WTOEND   EQU   *
         EJECT
*******************************************************************
*        DATA CONTROL BLOCKS                                      *
*******************************************************************
         SPACE 1
SYSPRINT DCB   DDNAME=SYSPRINT,                                        C
               DSORG=PS,                                               C
               SYNAD=IOERROR,                                          C
               EXLST=EXITLIST,                                         C
               RECFM=FBM,                                              C
               MACRF=PM,                                               C
               LRECL=133
         SPACE 3
SYSIN    DCB   DDNAME=SYSIN,                                           C
               DSORG=PS,                                               C
               SYNAD=IOERROR,                                          C
               EXLST=EXITLIST,                                         C
               MACRF=GL,                                               C
               EODAD=EODIN
         SPACE 3
SYSUT1   DCB   DDNAME=SYSUT1,                                          C
               DSORG=PS,                                               C
               SYNAD=IOERROR,                                          C
               EXLST=EXITLIST,                                         C
               MACRF=GL,                                               C
               EODAD=EODUT1
         SPACE 3
SYSUT2   DCB   DDNAME=SYSUT2,                                          C
               DSORG=PS,                                               C
               SYNAD=IOERROR,                                          C
               EXLST=EXITLIST,                                         C
               RECFM=FB,                                               C
               MACRF=PM
         TITLE 'MISCELLANIOUS EQUATES, CONSTANTS AND WORK AREAS'
*******************************************************************
*        EQUATES                                                  *
*******************************************************************
         SPACE 1
SWBYTE   DC    X'00'               BYTE FOR MISC SWITCHES
NULLPARM EQU   X'80'               INDICATES NO PARM WAS SUPPLIED.
         SPACE 1
FF       EQU   255                 FOR 'NI' INSTRUCTION
BLANK    EQU   X'40'               BLANK
ON       EQU   X'FF'               ON
OFF      EQU   X'00'               OFF
HIGHVAL  EQU   X'FF'               HIGH VALUE
LOWVAL   EQU   X'00'               LOW VALUE
SIGN     EQU   X'F0'               FOR FORCE SIGN TO 'F'
         SPACE 1
SPCE1AFT EQU   X'09'               SPACE 1 AFTER PRINTING
SPCE2AFT EQU   X'11'               SPACE 2 AFTER PRINTING
SPCE3AFT EQU   X'19'               SPACE 3 AFTER PRINTING
PAGESKIP EQU   X'8B'               SKIP TO NEW PAGE
         EJECT
*******************************************************************
*        CONSTANTS                                                *
*******************************************************************
         SPACE 1
ZEROWORD DC    F'0'                BINARY ZERO FULLWORD FOR TESTS
         SPACE 1
COND4    DC    H'4'                COMPLETION CODE OF 4
COND8    DC    H'8'                COMPLETION CODE OF 8
COND12   DC    H'12'               COMPLETION CODE OF 12
COND16   DC    H'16'               COMPLETION CODE OF 16
         SPACE 1
ZERO     DC    PL1'0'              PACKED CONSTANT OF ZERO
ONE      DC    PL2'1'              PACKED CONSTANT OF ONE
PATRN1   DC    XL4'40202120'
         SPACE 3
*******************************************************************
*        WORK AREAS                                               *
*******************************************************************
         SPACE 1
DECIMAL  DS    D                   WA FOR DECIMAL OPERATIONS
COMPCODE DC    H'0'                CONDITION CODE
         SPACE 1
PARMADDR DS    F                   ADDRESS OF PARM INFO'S
         SPACE 1
PAGECOUN DC    PL2'0'              COUNT OF PAGES
LINECOUN DC    PL2'0'              COUNT OF LINES
MAXLINE  DC    PL2'60'             MAXIMUM NUMBER OF LINES PER PAGE
         SPACE 1
CNTSYSIN DC    PL4'0'
CNTUT1   DC    PL4'0'
CNTUT2   DC    PL4'0'
CNTUT3   DC    PL4'0'
         SPACE 1
PRTLINE  DS    0CL133
PRTCTL   DC    XL1'09'
PRTAREA  DS    0CL132
OUTAREA  DC    CL80' '
PRTFILLR DC    CL52' '
         SPACE 1
JOBCARD  DS    0CL80               SKELETON JOBCARD
JOBNAME  DC    CL10'// '
         DC    CL5' JOB '
JOBPARM  DC    CL56' '
JOBCONT  DC    CL1' '
JOBNO    DC    CL8' '
         SPACE 1
HEAD1    DC    C'T158P060 - OUTPUT CARD IMAGE LIST     PAGE'
PAGEDIT  DC    CL4' '
         EJECT
*******************************************************************
*        LITERAL POOL                                             *
*******************************************************************
         SPACE 1
         LTORG
         SPACE 5
*******************************************************************
*        DCB DSECT                                                *
*******************************************************************
         SPACE 1
         PRINT NOGEN
         DCBD  DSORG=PS            GENERATE DCB DSECT
         PRINT NOGEN
         END   T158P060
