         TITLE 'JOBNAME, STEPNAME EXTRACTION ROUTINE'
*
* THIS CALLED ROUTINE EXTRACTS THE JOBNAME OF THE OS JOB AND
* THE STEPNAME OF THE CURRENT STEP AND RETURNS THEM TO THE
* CALLING PROGRAM.  THERE ARE TWO ARGUMENTS OF THE CALL. EACH
* ARE EIGHT BYTES LONG.  THE FIRST IS THE USER STORAGE AREA
* FOR THE JOBNAME, THE SECOND FOR THE STEPNAME.
*
B3300    CSECT
         ENTRY UT465
UT465    EQU   *
         SAVE  (14,12),,*               SAVE REGS IN OLD SA
         BALR  12,0                     LOAD BASE REG
         USING *,12                     IDENTIFY BASE REG
         ST    13,SAVE+4                OLD SA PTR TO NEW SA
         LR    11,13                    HOLD OLD SA PTR
         LA    13,SAVE                  POINT TO NEW SA
         ST    13,8(11)                 SET SA FORWARD CHAIN
         SPACE 3
         LM    3,4,0(1)                 LOAD PARM ADDRS
         EXTRACT TIOTADDR,'S',FIELDS=(TIOT)  GET TIOT ADDR
         L     5,TIOTADDR               TIOT ADDR TO R5
         MVC   0(8,3),0(5)              MOVE JOBNAME
         MVC   0(8,4),8(5)              MOVE STEPNAME
         SPACE 3
         L     13,SAVE+4                RESTORE REG 13
         RETURN (14,12),RC=0            RESTORE REGS AND RETURN
         EJECT
* CONSTANTS AND WORKING STORAGE SECTION
*
SAVE     DC    18F'0'                   OS SAVE AREA
TIOTADDR DS    F                        TIOT ADDRESS
         END   B3300
