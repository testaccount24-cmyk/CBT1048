RPGUT378 START 0
         USING *,12                     PROGRAM BASE REGISTER
         ENTRY RPG378                   MAIN PROGRAM ENTRY POINT NAME
         ENTRY RPGSET                   SUBROUTINE ENTRY POINT NAME
         ENTRY SETNUM                   SUBROUTINE PARAMETER DATA NAME
         B     SAVE-*(15)               BRANCH TO SAVE
RPG378   OI    RPG378A-RPG378(15),X'F0' SWITCH ON FOR MAIN ENTRY-RPG378
         L     15,BASE-RPG378(15)       LOAD BASE ADDRESS TO REG 15
         B     SAVE-RPGUT378(15)        BRANCH TO SAVE
RPGSET   OI    RPGSETA-RPGSET(15),X'F0' SWITCH ON FOR SUBR ENTRY-RPGSET
         L     15,BASE-RPGSET(15)       LOAD BASE ADDRESS TO REG 15
SAVE     SAVE  (14,12),T                SAVE REGISTERS
COMMON   LR    12,15                    LOAD BASE REG 12 WITH BASE ADDR
RPGSETA  EQU   *+1                      SWITCH FOR RPGSET
         NOP   SETCODE                  BRANCH TO SETCODE WITH RPGSET
         ST    13,SAVEA+4               SAVE CALLING SAVE AREA - OS SUP
         LA    5,SAVEA
         ST    5,8(0,13)
         LR    13,5                     LOAD RPG378 SAVE AREA
RPG378A  EQU   *+1                      SWITCH FOR RPG378
         NOP   RPGCALL                  BRANCH TO RPGCALL WITH RPG378
         B     RETURN                   RETURN TO OS SUPERVISOR
RPGCALL  EQU   *
         NI    RPG378A,X'0F'            SWITCH OFF RPG378 BRANCH
         STM   0,15,REGS                SAVE REGISTERS BEFORE RPGCALL
         CALL  RPGOBJ                   CALL RPG USER PROGRAM
         LM    0,15,REGS                RESTORE REGISTERS AFTER RPGCALL
RETURN   EQU   *
         L     15,CODE                  LOAD RETURN CODE TO REG 15
         L     13,SAVEA+4               LOAD SAVEAREA FROM CALLING PROG
         RETURN    (14,12),T,RC=(15)    RETURN
SETCODE  EQU   *
         NI    RPGSETA,X'0F'            SWITCH OFF RPGSET BRANCH
         ST    13,SAVEB+4               SAVE RPG USER SAVE AREA ADDRESS
         LA    5,SAVEB
         ST    5,8(0,13)
         LR    13,5                     LOAD RPGSET SAVE AREA ADDRESS
         LA    1,WTO                    WRITE TO CONSOLE THE
         SVC   35                         COND CODE ISSUE BY RPG USER
         MVC   CODE,SETNUM              SAVE COND CODE FROM RPG USER
         OC    CODE,=X'F0F0F0F0'        MAKE CODE NUMERIC-NOT GUARANTEE
         PACK  CVD1,CODE                CONVERT TO PACK DECIMAL
         CVB   0,CVD1                   CONVERT TO BINARY
         ST    0,CODE                   STORE COND CODE IN CODE, BINARY
         L     15,CODE                  LOAD RETURN CODE TO REG 15
         L     13,SAVEB+4               LOAD SAVEAREA FROM RPG USER PGM
         RETURN    (14,12),T,RC=(15)    RETURN
*
BASE     DC    A(RPGUT378)              ADDRESS OF START
SAVEA    DS    18F
SAVEB    DS    18F
REGS     DS    18F
CVD1     DS    D
CODE     DC    F'0'
WTO      DC    AL2(18)
         DC    AL2(0)
         DC    C'COND CODE '
         DC    C'0000'
SETNUM   DC    F'0'
         END
         ENTRY SETNUM
         ENTRY RPGSET
         ENTRY RPG378
         ALIAS RPGSET
         NAME  RPGUT378(R)
