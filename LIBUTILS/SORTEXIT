         TITLE 'INTERFACE FOR COBOL SORT EXITS'
***********************************************************************
*                                                                     *
*                                                                     *
*                                                                     *
*        THIS PROGRAM IS AN INTERFACE BETWEEN SORT/MERGE AND          *
*         A COBOL LANGUAGE SORT EXIT.                                 *
*                                                                     *
*                                                                     *
*                                                                     *
*         THIS PROGRAM MAY BE USED AS AN INTERFACE FOR ALL SORT       *
*         EXITS WITH THE EXCEPTION OF THE I/O ERROR EXITS.            *
*                                                                     *
*                                                                     *
*                                                                     *
*         THE COBOL SUB-PROGRAM MUST SATISFY CERTAIN CONDITIONS -     *
*              1. THE ENTRY POINT NAME MUST BE 'MODULE'               *
*              2. BEFORE RETURNING CONTROL, THE PROGRAM MUST          *
*                 EXECUTE THE FOLLOWING CODE -                        *
*                        ENTER LINKAGE.                               *
*                        CALL 'RESOLVE' USING OUTPUT-RECORD.          *
*                        ENTER COBOL.                                 *
*         WHERE OUTPUT-RECORD IS THE NAME OF THE FIELD CONTAINING     *
*         THE OUTPUT RECORD                                           *
*                                                                     *
*                                                                     *
*                                                                     *
*         FIVE LINKAGE FIELDS ARE PASSED ACROSS TO THE COBOL          *
*         SUB-PROGRAM, REGARDLESS OF THE IDENTITY OF THE SORT         *
*         EXIT INVOLVED.  THE CONTENTS OF THE LINKAGE FIELDS          *
*         REFLECT THE CONTENTS REQUIRED BY AN E35 EXIT, WHICH         *
*         IS EFFECTIVELY THE LIMITING CASE.  WHILE THE CONTENTS       *
*         OF SOME OF THE LINKAGE AREAS WILL BE SPURIOUS FOR SOME      *
*         SORT EXITS, IT WILL NOT MATTER BECAUSE THEY SHOULD          *
*         NEVER BE REFERENCED. THE CONTENTS OF THE FIELDS ARE -       *
*              1. ADDRESS OF THE CURRENT RECORD                       *
*              2. ADDRESS OF THE PREVIOUS RECORD                      *
*              3. ADDRESS OF SEQUENCE CHECK SWITCH                    *
*              4. ADDRESS OF RETURN CODE FROM SUB-PROGRAM             *
*              5. ADDRESS OF END-OF-DATA INDICATOR                    *
*                                                                     *
*                                                                     *
*                                                                     *
*         TO USE THIS INTERFACE, THE FOLLOWING LINKAGE EDITOR         *
*         CONTROL CARDS MUST BE PLACED IN THE LKED.SYSIN DATA         *
*         STREAM DURING THE FINAL COBECL -                            *
*              INCLUDE   LINKLIB2(SORTEXIT)                           *
*              ENTRY     E99                                          *
*              NAME      SUB-PROGRAM NAME(R)                          *
*                                                                     *
*         WHERE 99 IS THE NUMBER OF THE EXIT ROUTINE.                 *
*                                                                     *
***********************************************************************
         EJECT
SORTEXIT CSECT
         PRINT ON,NODATA,GEN
         SPACE 1
         ENTRY E11
         ENTRY E21
         ENTRY E31
         ENTRY E15
         ENTRY E25
         ENTRY E35
         ENTRY E16
         ENTRY E17
         ENTRY E27
         ENTRY E37
         ENTRY E61
         ENTRY RESOLVE
         SPACE 2
E11      EQU   *
E21      EQU   *
E31      EQU   *
E15      EQU   *
E25      EQU   *
E35      EQU   *
E16      EQU   *
E17      EQU   *
E27      EQU   *
E37      EQU   *
E61      EQU   *
         PGMIN ,                   HOUSE-KEEPING
         PRINT NOGEN               SUPPRESS MACRO EXPANSION
         SPACE 1
         L     R2,0(,R1)           GET THE ADDRESS OF THE CURRENT RECOR
         ST    R2,ADDRNEW           AND MOVE IT TO THE PARAMETER AREA
         LTR   R2,R2               TEST FOR END OF DATA
         BNZ   *+8                 BRANCH IF NOT
         MVI   EODIND,C'1'         INDICATE EOD TO THE SUB-PROGRAM
         L     R2,4(,R1)           GET THE ADDR OF THE PREVIOUS RECORD
         ST    R2,ADDROLD           AND MOVE IT TO THE PARAMETER AREA
         LA    R2,10(,R1)          GET ADDRESS OF SEQUENCE CHECK SWITCH
         ST    R2,ADDRSEQ           AND MOVE IT TO THE PARAMETER AREA
         L     R15,=V(MODULE)      POINT R15 AT THE SUB-PROGRAM
         LA    R1,PARMLIST         POINT R1 AT THE PARAMETER LIST
         BALR  R14,R15             EXECUTE THE SORT EXIT
         LH    R15,RETCODE         PASS RETURN CODE TO SORT
         L     R1,OUTREC           GET OUTPUT RECORD ADCON ADDRES
         L     R1,0(0,R1)          PUT ADDRES OF OUTPUT RECORD IN R1
         L     R2,SAVEFLD+4        GET ADDRESS OF HIGHER SAVE AREA
         ST    R1,24(0,R2)         STORE REG 1 IN REG 1 SAVE FIELD
        PGMOUT RC=(R15)            RETURN TO THE SORT
         SPACE 1
RETCODE  DC    CL2'00'             RETURN CODE FROM SUB-PROGRAM
EODIND   DC    C'0'                END-OF-DATA INDICATOR
         SPACE 1
DUBLWORD DS    D                   DOUBLE-WORD WORK AREA
         SPACE 1
PARMLIST DS    0F                  PARAMETER LIST FOR SUB-PROGRAM
ADDRNEW  DS    F                   ADDRESS OF THE CURRENT RECORD
ADDROLD  DS    F                   ADDRESS OF THE PREVIOUS RECORD
ADDRSEQ  DS    F                   ADDRESS OF E35 SEQUENCE CHECK SWITCH
ADDRRET  DC    A(RETCODE)          ADDRESS OF SUB-PROGRAM RETURN CODE
ADDREOD  DC    A(EODIND)           ADDRESS OF EOD INDICATOR
         SPACE 1
         LTORG
         EJECT
RESOLVE  EQU   *                   GET ADDRESS OF OUTPUT RECORD
         USING *,R15               ESTABLISH TEMPORARY ADDRESSABILITY
         ST    R1,OUTREC           PRESERVE ADDRESS OF ADDRESS OF OUTPU
         BR    R14                 RETURN TO THE COBOL EXIT
         SPACE 1
OUTREC   DS    F                   ADDR OF ADDR OF OUTPUT RECORD
         SPACE 1
         LTORG
         SPACE 1
         DROP  R15                 DROP TEMPORARY ADDRESSABILITY
         SPACE 1
         END
         SPACE 1
         END
