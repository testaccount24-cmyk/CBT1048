              ICTL  6,71,21
00000WRITEVAR CSECT
00010         PRINT ON,NODATA,NOGEN
00020         SAVE  (14,12)
00030         BALR  12,0
00040         USING *,12
00050         LA    9,SAVEAREA
00060         ST    9,8(13)
00070         ST    13,4(9)
00080         LR    13,9
00090         SPACE
00100         L     5,0(1)         ADDRESS OF WRITE AREA TO REG 5
00110         L     6,4(1)         ADDRESS OF OTHER PARAMETER TO REG 6
00120         CLI   0(6),C'W'      Q - IS OPERATION WRITE
00130         BE    WRITE          YES - GO TO WRITE ROUTINE
00140         CLI   0(6),C'O'      Q - IS OPERATION OPEN
00150         BE    OPEN           YES - GO TO OPEN ROUTINE
00160         CLI   0(6),C'C'      Q - IS OPERATION CLOSE
00170         BE    CLOSE          YES - GO TO CLOSE ROUTINE
00180         MVI   1(6),C'1'      INDICATE INVALED OPERATION
00190EXIT     L     13,SAVEAREA+4  PREVIOUS SAVE AREA TO REG 13
00200        RETURN (14,12)        RETURN TO CALLING PROGRAM
00210        SPACE  5
00220OPEN     NOP   OPENERR        AFTER FIRST TIME  TO ERROR ROUTINE
00230         MVI   OPEN+1,X'F0'   SET ON SECOND-TIME SWITCH
00240         MVI   WRITE+1,X'00'  PERMIT WRITE OPERATION
00250         OPEN  (VARIABLE,(OUTPUT))      OPEN OUTPUT DATA SET
00260         MVI   1(6),X'F0'     INDICATE GOOD END
00270         B     EXIT           RETURN TO CALLING PROGRAM
00280OPENERR  MVI   1(6),X'F2'     INDICATE MULTIPLE OPEN
00290         B     EXIT           RETURN TO CALLING PROGRAM
00300         SPACE 5
00310CLOSE    NOP   CLOSERR        AFTER FIRST TIME GO TO ERROR ROUTINE
00320         MVI   CLOSE+1,X'F0'  SET ON SECOND-TIME SWITCH
00330         MVI   WRITE+1,X'F0'  SET ON WRITE-BYPASS SWITCH
00340         CLOSE (VARIABLE)     CLOSE DATA SET
00350         MVI   1(6),X'F0'     INDICATE GOOD END
00360         B     EXIT           RETURN TO CALLING PROGRAM
00370CLOSERR  MVI   1(6),X'F3'     INDICATE MULTIPLE CLOSE
00380         B     EXIT           RETURN TO CALLING PROGRAM
00400         EJECT
00410WRITE    BC    15,WRITERR     BYPASS WRITE IF DATA SET NOT OPEN
00420         PUT   VARIABLE,(5)   WRITE VARIABLE LENGTH RECORD
00430         MVI   1(6),X'F0'     INDICATE GOOD END
00440         B     EXIT           RETURN TO CALLING PROGRAM
00450WRITERR  MVI   1(6),X'F0'     INDICATE DATA SET NOT OPEN
00460         B     EXIT           RETURN TO CALLING PROGRAM
00470         SPACE 5
00480VARIABLE DCB   DDNAME=VARIABLE,                                   *
00490               DSORG=PS,                                          *
00500               MACRF=PM,                                          *
00510               RECFM=V
00520         SPACE
00530SAVEAREA DS    18F
00540         SPACE
00550         LTORG
00560         SPACE
00570         END
