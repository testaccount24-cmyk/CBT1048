         TITLE 'UT366 - DATA EXCEPTION (0C7) INTERRUPT INTERCEPTION'
         ISEQ  73,80
         SPACE
* WRITTEN BY D. R. HURTGEN
*
* MONSANTO COMPANY - CREVE COEUR
*
* DECEMBER 1966
*
* THIS CALLABLE SUBROUTINE INTERCEPTS INTERRUPTIONS CAUSED BY A DATA
* EXCEPTION TO DETERMINE WHETHER THEY WERE CAUSED BY A PACKED BLANK
* IN THE UNITS POSITION OF EITHER OF THE FIELDS INVOLVED IN A DECIMAL
* ARITHMETIC OPERATION. IF SUCH IS THE CASE, ERROR RECOVERY IS
* ATTEMPTED BY REPLACING THE BLANK OR BLANKS WITH AN UNSIGNED ZERO AND
* REEXECUTING THE SUBJECT INSTRUCTION. OTHERWISE, THE NORMAL JOB ABEND
* IS TAKEN.
         SPACE 2
UT366    START
         STM   14,12,12(13)   ESTABLISH SUBROUTINE LINKAGE
         BALR  12,0
         USING *,12
         ST    13,SAVEREG+4
         LA    13,SAVEREG
         SPIE  ERRORRTN,7 SPECIFY DATA EXCEPTION INTERRUPT INTERCEPTION
         L     13,SAVEREG+4   ESTABLISH RETURN LINKAGE
         LM    14,12,12(13)
         BR    14
SAVEREG  DS    18F  REGISTER SAVE AREA
         EJECT
ERRORRTN CSECT
         USING *,15 BEGIN DATA EXCEPTION RECOVERY ATTEMPT
         TM    8(1),X'C0' Q.-ILC NOT 3
         BNO   ABEND      B.-YES, ERROR NOT CAUSED BY DECIMAL INSTR.
         STM   3,13,SAVEAREA  SAVE R3 THROUGH R13
         L     3,8(0,1)  LOAD LOW ORDER FOUR BYTES OF OLD PSW TO R3
         SH    3,SIX   POINT TO START OF INSTRUCTION WHICH INTERRUPTED
         TM    0(3),X'F0' Q.-ERROR CAUSED BY EDIT INSTRUCTION
         BNO   ABEND      B.-YES
         MVC   SAVEA(12),20(1) INITIALIZE SAVE AREA TO GET BASE
         MVC   SAVEB(8),12(1)    REGISTER VALUES OF SUBJECT INSTRUCTION
         SR    4,4  INITIALIZE WORK REGISTERS
         SR    5,5
         SR    6,6
         IC    4,2(0,3)  PROCESS FIRST OPERAND
         SRL   4,4       GET BASE REGISTER
         SLL   4,2
         L     4,SAVE(4) GET SPECIFIED BASE VALUE - PLACE IN R4
         MVN   NUMERIC(1),2(3)  GET DISPLACEMENT
         MVC   CHAR(1),3(3)
         AH    4,NUMERIC COMPUTE ADDRESS OF DATA FIELD
         IC    6,1(0,3)  POINT TO ITS UNITS POSITION BY ADDING THE
         SRL   6,4         LENGTH CODE
         AR    4,6
         IC    5,4(0,3)  PROCESS SECOND OPERAND IN THE SAME MANNER
         SRL   5,4
         SLL   5,2
         L     5,SAVE(5)
         MVN   NUMERIC(1),4(3)
         MVC   CHAR(1),5(3)
         AH    5,NUMERIC
         MVN   NUMERIC(1),1(3)
         IC    6,NUMERIC
         AR    5,6
         CLI   0(4),4    Q.-FIRST OPERAND UNITS POSITION WAS A BLANK
         BNE   NOTBLANK  B.-NO
         MVI   0(4),X'0F' REPLACE THE BLANK WITH AN UNSIGNED ZERO
         CLI   0(5),4    Q.-SECOND OPERAND UNITS POSITION WAS A BLANK
         BNE   *+8       B.-NO
REPLACE  MVI   0(5),X'0F' REPLACE THE BLANK WITH AN UNSIGNED ZERO
         ST    3,8(0,1) STORE THE PSW TO REEXECUTE THE INSTRUCTION
         LM    3,13,SAVEAREA  RESTORE R3 THROUGH R13
         BR    14   RETURN TO SUPERVISOR
NOTBLANK CLI   0(5),4 Q.-BOTH DATA FIELDS ARE NON-BLANK
         BE    REPLACE B.-NO
ABEND    LM    3,13,SAVEAREA  ERROR WAS NOT CAUSED BY BLANKS IN UNITS
         L     1,COMPLCOD POSITION - RESTORE R3 THROUGH R13, SET
         SVC   13 COMPLETION CODE, AND ABEND
         EJECT
*                        CONSTANTS AND SAVE AREAS
SAVE     EQU   *
SAVEA    DS    3F
SAVEAREA DS    11F
SAVEB    DS    2F
COMPLCOD DC    X'C00C7000'
NUMERIC  DC    X'00'
CHAR     DS    C
SIX      DC    H'6'
         END
