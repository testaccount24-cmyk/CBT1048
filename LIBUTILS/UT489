UT489    CSECT
DDNREPL  EQU   *
         ENTRY DDNREPL
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,R15
         STM   R2,R4,REGSAVE       SAVE WORK REGISTERS
         L     R2,4(0,R13)         R2=ADDRESS OF S/A FOR CALLING PROG.
         L     R3,4(0,R2)          R3=ADDRESS OF NEXT HIGHER S/A
         LTR   R3,R3               HIGHEST LEVEL?
         BZ    DDNEND                YES. THEN SUPVR IS CALLING PROGRAM
*                                  SO NO DDNAME MODS. POSSIBLE
         L     R3,24(0,R2)         R3=ADDR. OF CALLERS PARAM LIST
         TM    0(R3),X'80'         IS PARM INFO LAST ADDRESS.
         BO    DDNEND              YES. NO DD MODS
         L     R3,4(0,R3)          R3=DDNAME LIST ADDRESS
         SR    R4,R4               CLEAR REG4
         LH    R4,0(0,R3)          LOAD LENGTH OF DDNAME LIST
         LA    R3,2(0,R3)          POINT AT FIRST DDNAME IN LIST
         L     R2,0(0,R1)          GET FIRST DCB ADDRESS
*                                  R1 POINTS AT CURRENT DCB ADDRESS
DDNLOOP  EQU   *
         LTR   R4,R4               END OF DDNAME LIST
         BNP   DDNEND              YES. BRANCH
         CLI   0(R3),X'00'         THIS DDNAME TO BE MODIFIED?
         BE    DDNNEXT             NO. JUMP TO NEXT PAIR
         MVC   40(8,R2),0(R3)      YES. MOVE NEW DDNAME TO DCB
DDNNEXT  EQU   *
         SH    R4,=H'8'            REDUCE DDNAME LIST LENGTH BY 8
         LA    R3,8(0,R3)          JUMP TO NEXT DDNAME
         TM    0(R1),X'80'         END OF DCB LIST?
         BO    DDNEND              YES. BRANCH
         LA    R1,4(0,R1)          POINT AT NEXT DCB LIST ADDRESS
         L     R2,0(0,R1)          GET NEXT DCB ADDRESS
         B     DDNLOOP             GO PROCESS NEXT DDNAME SUBSTITUTION
         SPACE
DDNEND   EQU   *
         LM    R2,R4,REGSAVE       RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
REGSAVE  DS    3F
         LTORG
         END   UT489
