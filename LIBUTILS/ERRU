*              ERRU SOURCE DECK , 57 CARDS
*                   ASSEMBLER SOURCE STATEMENTS IN CC 1 - 72
*                   PROGRAM ID 'ERRU' IN CC 73 - 76
*                   SEQUENCE NUMBERS 0001 - 0057 IN CC 77 - 80
ERRU     CSECT
         ENTRY RUECK
         ENTRY UT449
IHEQLSA  DXD   A                       LIBRARY SAVE AREA
IHEQSLA  DXD   A                       STORA&E LAST ALLOCATED
         USING RUECK,15
UT449    EQU   *
RUECK    BC    15,R1
         DC    AL1(5)
         DC    C'RUECK'
R1       STM   14,11,12(13)
         BALR  10,0
         USING *,10
         ST    13,SSA+4
         LA    7,SSA
         LA    4,0(12,0)               GET ADDRESS OF PR IHEQSLA
         ORG   *-2
         DC    QL2(IHEQSLA)
         ST 7,0(0,4)                   UPDATE OF PR IHEQSLA
         ST    7,8(0,13)
         LR    13,7
         SR    9,9
         L     2,0(0,1)                GET ADDRESS OF PARAMETER
         LR    3,13
*              FIRST WAY TO GET NEXT INSTRUCTION ADDRESS
NGEF     L     3,4(0,3)                WALK BACK THRU SAVE AREAS
         LA    3,0(0,3)
         CR    3,9                     CHECK IF CORRECT
         BE    WT                      NO
         CLI   0(3),X'25'              CHECK IF LWS-VDA
         BNE   NGEF                    NO
         L     3,8(0,3)
         MVC   ZWISP,957(3)            GET NEXT INSTRUCTION ADDRESS
*              SECOND WAY TO GET NEXT INSTRUCTION ADDRESS
         L     5,0(12,0)               GET ADDRESS OF PR IHEQLSA
         ORG   *-2
         DC    QL2(IHEQLSA)
         L     5,4(0,5)                WALK BACK THRU CHAIN
         CLC   ZWISP,117(5)            CHECK IF EQUAL TO ABOVE
         BNE   WT                      NO
         MVC   5(3,2),117(5)           STORE INSTR ADDR IN PARAMETER
         L     13,SSA+4
         ST    13,0(0,4)
         LM    14,11,12(13)
         SR    15,15
         ST    15,8(0,13)
         BR    14                      RETURN TO CALLING PROGRAM
WT       WTO   'ADDRESS ERROR '
         B     0                       INTERRUPT HANDLED BY PL/I
ZWISP    DS    CL3
         DS    0F
SSA      DC    X'8080'
         DC    AL2(72)
         DC    17F'0'
         END
