U493     TITLE 'UT493 - PARTIAL KEYS MATCH ROUTINE'
UT493    CSECT
         WXTRN MSTKEY              RPG PARAMETERS
         WXTRN TSTKEY
         WXTRN RESULT
         WXTRN BLNKCH
         WXTRN LENGTH
         PGMIN USING=13            REGISTER 13 ONLY BASE
         SPACE 2
***********************************************************************
*                                                                     *
* THIS ROUTINE IS INTENDED TO PROVIDE A TEST FOR AN ACCEPTABLE        *
* PARTIAL MATCH ON A KEY FIELD.THE ROUTINE SEARCHES THE TEST KEY      *
* FOR BLANKS,TURNS OFF THE CORRESPONDING BYTES IN THE MASTER KEY      *
* THEN COMPARES THE TWO KEYS.A ONE BYTE CHARACTER RETURN CODE IS      *
* RETURNED TO THE CALLER TO SHOW THE RESULT OF THE COMPARE.           *
*  D.CARTWRIGHT MONSANTO/MISD AUGUST 1975                             *
*                                                                     *
***********************************************************************
         EJECT
         USING PARAMS,R10          ADDRESS PARAMETER LIST
SWITCH1  EQU   *+1                 COBOL CALL SWITCH
         NOP   COBOLOAD            MODIFIED BY SWITCH1
         LA    R10,AMSTKEY         RPG PARAMETER ADDRESS INTO REG10
         SPACE 2
SWITCH2  EQU   *+1                 BYPASS RPG TEST AFTER FIRST TIME
         NOP   PROCESS             MODIFIED BY SWITCH2
         L     R3,RETCODE          GET ADDRESS OF RETURN CODE
         MVI   SWITCH2,X'F0'       SET ON BYPASS SWITCH
         CLI   0(R3),C'R'          Q - IS CALLING LANGUAGE RPG?
         BE    PROCESS             IT IS RPG - BYPASS COBOL LOAD
         MVI   SWITCH1,X'F0'       SET ON COBOL LOAD SWITCH
         SPACE 1
COBOLOAD DS    0H                  CALLING LANGUAGE IS COBOL
         LR    R10,R1              PICK UP COBOL PARAMETER LIS
         SPACE 2
PROCESS  DS    0H                  R10 NOW POINTS TO PARAMETER LIST
BYPASS   EQU   *+1                 SET TO BYPASS INIALISATION
         BC    0,REENTRY           NO BRANCH ON FIRST ENTRY
         L     R3,LONGTH           GET ADDRESS OF LENGTH FIELD
         MVC   DATASAVE+6(2),0(R3) PICK UP LENGTH FIELD
         CVB   R5,DATASAVE         CONVERT IT TO BINARY
         LR    R0,R5               GET INTO REGISTER ZERO
         AR    R0,R0               DOUBLE IT (QUICKLY)
         GETMAIN R,LV=(0)          OBTAIN TEMPORARY STORAGE
         LR    R6,R1               POINT TO STORAGE AREA1
         LA    R7,0(R5,R6)         POINT TO STORAGE AREA2
         BCTR  R5,0                DECREMENT LENGTH FOR EXECUTES
         STM   R5,R7,DATASAVE      SAVE ALL OUR WORK
         L     R11,BLANKCHR        GET ADDRESS OF 'MUST BE BLANK'
         SR    R3,R3               CLEAR A REGISTER
         IC    R3,0(,R11)          PICK UP 'MUST BE BLANK' CHARACTER
         L     R11,TABLESET        GET BLANK AT END OF REGISTER
         STC   R11,CHRTABLE(R3)    SET TABLE FOR 'MUST BE BLANK'
         OI    BYPASS,X'F0'        SET FOR SUBSEQUENT ENTRIES
         EJECT
REENTRY  DS    0H                  BYPASSED INITIALISATION
         LM    R5,R7,DATASAVE      PICK UP INITIAL DATA
         L     R8,MASTKEY          GET ADDRESS OF MASTER KEY
         L     R9,TESTKEY          GET ADDRESS OF KEY TO TEST
         EX    R5,SAVEMAST         MOVE MASTER KEY TO STORAGE
         EX    R5,SAVETEST         MOVE TEST KEY TO STORAGE
         EX    R5,TRBLANK          TRANSLATE TEST TO X'FF' BAR BLANKS
         EX    R5,ANDMAST          TURN OFF PARTS OF MASTER KEY
         EX    R5,ANDTEST          TURN OFF PARTS OF TEST KEY
         EX    R5,TRTESTBL         SET 'MUST BE BLANK'S  BLANK
         EX    R5,COMPARE          COMPARE TEST KEY TO MASTER KEY
         BALR  R12,0               PICK UP CONDITION CODE
         SLL   R12,2               LOSE INSTRUCTION LENGTH FIELD
         SRL   R12,30              MOVE CC TO LOW ORDER
         L     R2,RETCODE          GET ADDRESS OF RETURN CODE BYTE
         STC   R12,0(,R2)          SET RETURN CODE FROM COMPARE
         OI    0(R2),X'F0'         CONVERT RETURN CODE TO CHARACTER
         PGMOUT ,                  RETURN TO CALLER
         SPACE 3
SAVEMAST MVC   0(*-*,R6),0(R8)     STORE MASTER KEY
SAVETEST MVC   0(*-*,R7),0(R9)     STORE TEST KEY
TRBLANK  TR    0(*-*,R7),FFTABLE   TRANSLATE TESTS BLANKS TO ZERO
ANDMAST  NC    0(*-*,R6),0(R7)     TURN OFF UNTESTED BITS OF MASTER
ANDTEST  NC    0(*-*,R7),0(R9)     TURN OFF BLANKS IN TEST KEY
TRTESTBL TR    0(*-*,R7),CHRTABLE  TRANSLATE TEST TO ITSELF BAR BLANKS
COMPARE  CLC   0(*-*,R7),0(R6)     COMPARE FRIGGED KEYS
         TITLE 'UT493 -- DECLARATIONS'
         DS    0D                  ALIGN DATASAVE
DATASAVE DC    3F'0'               SAVE & WORK AREA FOR INITIALISATION
TABLESET DC    F'64'               FULLWORD ENDING 'BLANK'
FFTABLE  DS    0C                  TRANSLATE TABLE FOR 'TRBLANK'
         DC    64X'FF'             EVERYTHING HIGH VALUES
         DC    X'00'               EXCEPT 'BLANK' - LOW VALUE
         DC    191X'FF'            HIGH VALUES
         SPACE 2
CHRTABLE DS    0C                  TRANSLATE TABLE FOR 'TRTESTBL'
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'000102030405060708090A0B0C0D0E0F' 0
         DC    X'101112131415161718191A1B1C1D1E1F' 1
         DC    X'202122232425262728292A2B2C2D2E2F' 2
         DC    X'303132333435363738393A3B3C3D3E3F' 3
         DC    X'404142434445464748494A4B4C4D4E4F' 4
         DC    X'505152535455565758595A5B5C5D5E5F' 5
         DC    X'606162636465666768696A6B6C6D6E6F' 6
         DC    X'707172737475767778797A7B7C7D7E7F' 7
         DC    X'808182838485868788898A8B8C8D8E8F' 8
         DC    X'909192939495969798999A9B9C9D9E9F' 9
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF' A
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF' B
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF' C
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF' D
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF' E
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF' F
AMSTKEY  DC    A(MSTKEY)           RPG PARAMETERS SPECIFIED EXTERNALLY
         DC    A(TSTKEY)
         DC    A(RESULT)
         DC    A(BLNKCH)
         DC    A(LENGTH)
         TITLE 'UT493 - PARAMETER LIST FORMAT'
PARAMS   DSECT                     PARAMETER LIST DESCRIPTION
MASTKEY  DS    F                   ADDRESS OF MASTER KEY
TESTKEY  DS    F                   ADDRESS OF KEY UNDER TEST
RETCODE  DS    F                   ADDRESS OF ONE BYTE RETURN CHARACTER
*                                  0 - KEYS MATCH
*                                  1 - TEST KEY IS LOW
*                                  2 - TEST KEY IS HIGH
BLANKCHR DS    F                   ADDRESS OF 'MUST-BE-BLANK' CHARACTER
LONGTH   DS    F                   ADDRESS OF TWO BYTE PACKED LENGTH
         END   UT493
