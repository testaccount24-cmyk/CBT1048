         TITLE      'SIMPLIFIED CURRENCY CONVERSION ROUTINE - UT435'
UT435    CSECT
         PRINT ON,NOGEN,NODATA
         SPACE 5
*        REGISTER EQUATES AND USAGE
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3         WORK REGISTER
R4       EQU   4         BAL RETURN ADDRESS
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9         POINTER TO OUTPUT CONVERSION FACTOR
R10      EQU   10        POINTER TO INPUT CONVERSION FACTOR
R11      EQU   11        PARAMETER ADDRESS AND DSECT BASE REGISTER
R12      EQU   12        BASE REGISTER
R13      EQU   13        SAVE AREA POINTER
R14      EQU   14        BALR RETURN ADDRESS
R15      EQU   15        BALR ROUTINE ADDRESS
         SPACE 5
         SAVE  (14,12)        **
         BALR  R12,0           *
         USING *,R12           *
         LA    R9,SAVEAREA     **  HOUSEKEEPING
         ST    R9,8(R13)       *
         ST    R13,4(R9)       *
         LR    R13,R9         **
         SPACE
         USING CONVPARM,R11   ESTABLISH ADDRESSABILITY OF DSECT
         SPACE
COBOLOAD L     R11,0(R1)      POINT REG 11 AT COBOL PARAMETER ADDRESS
         EJECT
PROCESS  MVI   RETNCODE,C'4'       ASSUME VERSION NUMBER IS REQUIRED
         MVC   OUTVALUE(L'VERSNO),VERSNO SET UP VERSION NUMBER IN OUTPU
         CLC   OUTCODE,=C'VN'      IS THIS A VERSION NUMBER REQUEST
         BE    RETURN              RETURN TO CALLER IF YES
         ZAP   OUTVALUE,ZERO       ZEROISE OUTPUT VALUE IN CASE OF ERRR
         MVI   RETNCODE,X'F3' ESTABLISH OUTPUT ERROR RETURN CODE
         LA    R3,OUTCODE     POINT REG 3 AT OUTPUT CODE
         BAL   R4,GETRATE     GO OBTAIN OUTPUT CONVERSION FACTOR
         LR    R9,R10         POINT R9 AT OUTPUT CONVERSION FACTOR
         SPACE
         MVI   RETNCODE,X'F2' ESTABLISH INPUT ERROR RETURN CODE
         LA    R3,INCODE      POINT REG 3 AT INPUT CODE
         BAL   R4,GETRATE     GO POINT R10 AT INPUT CONVERSION FACTOR
         SPACE
         MVI   RETNCODE,X'F1' INDICATE VALID OPERATION COMPLETED
         ZAP   CVBAREA,INVALUE MOVE INPUT VALUE TO END OF WORK AREA
         MP    MATHSFLD(16),0(5,R10) MULTIPLY BY INPUT CONVERSION RATE
         DP    MATHSFLD(16),0(5,R9) DIVIDE BY OUTPUT CONVERSION RATE
         ZAP   OUTVALUE,MATHSFLD+3(8) MOVE RESULT TO PARAMETER AREA
         ZAP   CVBAREA,CVBAREA+3(5) PLACE REMAINDER IN DOUBLE WORD
         MP    CVBAREA,TWO    DOUBLE REMAINDER
         CP    0(5,R9),CVBAREA COMPARE DIVISOR WITH TWICE REMAINDER
         BH    *+10           GREATER, SO NO NEED TO ROUND RESULT UP
         AP    OUTVALUE,ONE   ROUND RESULT UP BY 1
         ZAP   MATHSFLD(16),ZERO ZEROISE WORK AREA
         SPACE
RETURN   EQU   *
         L     R13,4(R13)     RETRIEVE PREVIOUS SAVE AREA POINTER
        RETURN (14,12),RC=0   RETURN TO CALLING PROGRAM
         SPACE 3
GETRATE  EQU   *
         CLC   0(2,R3),LOW    Q - IS CODE LESS THAN '01'
         BL    RETURN         YES - RETURN TO SENDER
         CLC   0(2,R3),HIGH   Q - IS CODE GREATER THAN '99'
         BH    RETURN         YES - RETURN FORTHWITH TO CALLING PGM
         CLC   0(2,R3),LSD    Q - IS CODE POUNDS STERLING
         BE    RETURN         YES - CANNOT PROCESS - RETURN
         PACK  CODE,0(2,R3)   PACK INPUT CURRENCY CODE
         ZAP   CVBAREA,CODE   MOVE PACKED CODE TO DOUBLEWORD
         CVB   R3,CVBAREA     CONVERT INPUT CODE TO BINARY
         BCTR  R3,0           DECREMENT SUBSCRIPT - CURR CODE - BY 1
         MH    R3,FIVE        MULTIPLY SUBSCRIPT BY ELEMENT LENGTH
         LA    R10,TABLE      POINT R10 AT START OF TABLE
         AR    R10,R3         ADD COMPUTED OFFSET
         SPACE
*        R10 NOW POINTS AT THE CONVERSION RATE FOR THE CURRENCY CODE
         SPACE
         CP    0(5,R10),NORATE Q - IS THERE A CONVERSION FACTOR
         BE    RETURN         NO - GO BACK TO CALLER
         BR    R4             RETURN TO MAIN-LINE
         EJECT
CONVPARM DSECT
INVALUE  DS    CL8            PICTURE S9(10)V9(5) COMP-3
INCODE   DS    CL2            PICTURE XX
OUTCODE  DS    CL2            PICTURE XX
RETNCODE DS    CL1            PICTURE X
OUTVALUE DS    CL8            PICTURE S9(10)V9(5) COMP-3
UT435    CSECT
         SPACE 5
MATHSFLD DC    D'0'
CVBAREA  DC    D'0'
SAVEAREA DS    18F
ONE      DC    PL1'1'
TWO      DC    PL1'2'
FIVE     DC    H'5'
CODE     DC    PL2'0'
NORATE   DC    PL5'0'
LOW      DC    CL2'01'
HIGH     DC    CL2'99'
LSD      DC    CL2'19'
ZERO     DC    PL1'0'
         SPACE 5
VERSNO   DC    C'75/04'            VERSION NUMBER
TABLE    EQU   *                 CODE   CURRENCY ---- FORMAT S9(3)V9(6)
         DC    PL5'0202500'       01    ALGERIAN DINAR.
         DC    PL5'1000000'       02    U.S DOLLAR.
         DC    PL5'1000000'       03    CANADIAN DOLLAR.
         DC    PL5'0000000'       04    ANGOLA ESCUDO.
         DC    PL5'0100200'       05    ARGENTINIAN PESOS.
         DC    PL5'1495000'       06    AUSTRALIAN DOLLARS.
         DC    PL5'0000000'       07    BARBADOS DOLLARS.
         DC    PL5'0000000'       08    BERMUDAN POUNDS.
         DC    PL5'0000000'       09    BOLIVIAN PESOS.
         DC    PL5'0052000'       10    AUSTRIAN SCHILLINGS.
         DC    PL5'0025000'       11    BELGIAN FRANCS.
         DC    PL5'0025000'       12    LUXEMBURG FRANCS.
         DC    PL5'0161000'       13    BRAZILIAN CRUZEIROS.
         DC    PL5'0216000'       14    FRENCH FRANCS.
         DC    PL5'0375000'       15    GERMAN MARKS.
         DC    PL5'0001600'       16    ITALIAN LIRE.
         DC    PL5'0352500'       17    NETHERLAND FLORIN.
         DC    PL5'0312500'       18    SWISS FRANCS.
         DC    PL5'0000000'       19    ENGLISH POUNDS STERLING.
         DC    PL5'0000000'       20    BULGARIAN LEV.
         DC    PL5'0000000'       21    BURMA KYAT.
         DC    PL5'0000000'       22    CAMBODIAN RIEL.
         DC    PL5'0002900'       23    CHILE EUSCUDO.
         DC    PL5'0036000'       24    GREEK DRACHMAS.
         DC    PL5'0027500'       25    CHINESE DOLLARS.
         DC    PL5'0040500'       26    COLUMBIAN PESOS.
         DC    PL5'0000000'       27    CONGOLESE FRANCS.
         DC    PL5'0000000'       28    COSTA RICAN COLON.
         DC    PL5'0000000'       29    CUBAN PESOS.
         DC    PL5'2400000'       30    CYPRIOT POUNDS.
         DC    PL5'0000000'       31    CZECHOSLOVAKIAN CROWNS.
         DC    PL5'0040000'       32    PORTUGUESE ESCUDOS.
         DC    PL5'0000000'       33    DOMINICAN REPUBLIC PESOS.
         DC    PL5'0017500'       34    SPANISH PESETAS.
         DC    PL5'0160000'       35    DANISH KRONER.
         DC    PL5'0265000'       36    FINISH MARKS.
         DC    PL5'0000000'       37    ECUADOR SUCRE.
         DC    PL5'2300000'       38    EGYPTIAN POUNDS.
         DC    PL5'0177500'       39    NORWEGIAN KRONER.
         DC    PL5'0222500'       40    SWEDISH KRONER.
         DC    PL5'0400000'       41    EL SALVADOR COLON.
         DC    PL5'0000000'       42    ETHIOPIAN DOLLARS.
         DC    PL5'1000000'       43    GUATEMALIAN QUETZAL.
         DC    PL5'0000000'       44    HAITI GOURDES.
         DC    PL5'0000000'       45    HONDURAS LEMPIRA.
         DC    PL5'0200000'       46    HONG KONG DOLLARS.
         DC    PL5'0000000'       47    HUNGARIAN FORINT.
         DC    PL5'0011360'       48    ICELANDIC KRONA.
         DC    PL5'0125000'       49    INDIAN RUPEES.
         DC    PL5'0002500'       50    INDONESIAN NEW RUPIAH.
         DC    PL5'0013200'       51    IRAN RIAL.
         DC    PL5'2800000'       52    IRAQ DINAR.
         DC    PL5'0238000'       53    ISRAELI POUNDS.
         DC    PL5'1200000'       54    JAMAICAN POUNDS.
         DC    PL5'0003330'       55    JAPANESE YEN.
         DC    PL5'2800000'       56    JORDANIAN DINAR.
         DC    PL5'0147500'       57    KENYAN SCHILLINGS.
         DC    PL5'0000000'       58    SOUTH KOREAN WON.
         DC    PL5'0310000'       59    LEBANESE POUND.
         DC    PL5'0000000'       60    LIBERIAN DOLLARS.
         DC    PL5'0000000'       61    LIBYAN POUNDS.
         DC    PL5'0415000'       62    MALAYSIAN DOLLARS.
         DC    PL5'0080000'       63    MEXICAN PESOS.
         DC    PL5'0000000'       64    NETHERLANDS ANTILLES GUILDERS.
         DC    PL5'1460000'       65    NEW ZEALAND DOLLARS.
         DC    PL5'0142900'       66    NICARAGUA CORDOBA.
         DC    PL5'0000000'       67    NIGERIAN POUNDS.
         DC    PL5'0000000'       68    PAKISTAN RUPEES.
         DC    PL5'0000000'       69    PANAMANIAN BALBOA.
         DC    PL5'0000000'       70    PARAGUAY GUARANI.
         DC    PL5'0000000'       71    PERUVIAN SOL.
         DC    PL5'0150000'       72    PHILIPPINES PESOS.
         DC    PL5'0000000'       73    POLISH ZLOTY.
         DC    PL5'0000000'       74    PORTUGUESE EAST AFRICA ESCUDOS
         DC    PL5'0000000'       75    RHODESIAN POUNDS.
         DC    PL5'0000000'       76    RUMANIAN LEU.
         DC    PL5'0222200'       77    SAUDI ARABIAN RIYAL.
         DC    PL5'0410000'       78    SINGAPORE DOLLARS.
         DC    PL5'0000000'       79    SOMALIAN SHILLINGS.
         DC    PL5'1500000'       80    SOUTH AFRICAN RAND.
         DC    PL5'1400000'       81    SOUTH WEST AFRICAN RAND.
         DC    PL5'0000000'       82    SUDANESE POUNDS.
         DC    PL5'0000000'       83    SURINAM GUILDER.
         DC    PL5'0000000'       84    SWAZILAND RAND.
         DC    PL5'0260000'       85    SYRIAN POUNDS.
         DC    PL5'0000000'       86    TANZANIAN SCHILLINGS.
         DC    PL5'0050000'       87    THAILAND BAHT.
         DC    PL5'0000000'       88    TRINIDAD + TOBAGO DOLLARS.
         DC    PL5'1904760'       89    TUNISIAN DINARS.
         DC    PL5'0111110'       90    TURKISH LIRA.
         DC    PL5'0000000'       91    UGANDA SCHILLINGS.
         DC    PL5'0000000'       92    URAGUAYAN PESOS.
         DC    PL5'0000000'       93    U.S.S.R. ROUBLES.
         DC    PL5'0232600'       94    VENEZUELA BOLIVARES.
         DC    PL5'0000000'       95    VIETNAM PIASTERS.
         DC    PL5'0066670'       96    YUGOSLAVIAN DINARS.
         DC    PL5'0000000'       97    ZAMBIAN POUNDS.
         DC    PL5'2300000'       98    U.K. DECIMALIZED POUNDS.
         DC    PL5'0'             99    *** UNALLOCATED ***
         SPACE 5
         LTORG
         SPACE
         END
