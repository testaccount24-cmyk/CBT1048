UT459    START 0
         SPACE 1
***********************************************************************
*        PROGRAM ID :              UT459                              *
*        ALIAS :                   ABEND                              *
*        DATE WRITTEN :            DECEMBER 1973                      *
*        LOCATION :                EUROPEAN COMPUTER CENTER           *
*                                                                     *
*             PURPOSE                                                 *
*            =========                                                *
*              TO ACCEPT A PARM FROM THE EXEC STATEMENT , AND TO      *
*              INVOKE A DUMP USING THE NUMBER IN THE PARM FIELD AS    *
*              THE USER CODE.                                         *
*                                                                     *
***********************************************************************
         SPACE  2
         PGMIN EQUATES=YES,USING=(R13)  ESTABLISH ADDRESSABILITY
         EJECT
         L     R3,0(R1)       ADDRESS OF PARM
         LH    R4,0(R3)       LENGTH OF PARM
         LTR   R4,R4          TEST IF LENGTH OF PARM IS ZERO
         BZ    ERROR          YES - BRANCH TO ERROR
         BCTR  R4,R0          NO  - SUBTRACT ONE TO LENGTH OF PARM
         EX    R4,PACK        EXECUTE PACK
         CP    WA,VALUEMAX    TEST IF THE VALUE OF PARM IS GREATER -
         CNOP  0,4                                         THAN 4095
         BNH   OK             NO  - BRANCH TO OK
ERROR    L     R1,CC          YES - LOAD DEFAULT COMPLETION CODE
         B     ABEND          BRANCH TO ABEND
CC       DC    AL1(128)       DUMP/STEP CODE
         DC    AL3(459)       COMPLETION CODE (459 IS CC BY DEFAULT)
OK       CVB   R5,WA          CONVERT BINARY VALUE OF PARM
         ST    R5,WA          STORE BINARY VALUE OF PARM
         MVI   WA,X'80'       MOVE DUMP/STEP CODE
         L     R1,WA          LOAD INTO REG1
ABEND    SVC   13             LINK TO ABEND ROUTINE
VALUEMAX DC    PL8'4095'      VALUE MAXIMUN FOR PARM
WA       DC    D'0'           WORK AREA
PACK     PACK  WA,2(0,R3)     PACK VALUE OF PARM
         END
