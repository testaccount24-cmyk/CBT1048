     TITLE 'EXTENDED SIMPLIFIED CURRENCY CONVERSION ROUTINE - UT460'
UT460    CSECT
         PRINT ON,NOGEN,NODATA
         SPACE 5
*        REGISTER EQUATES AND USAGE
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3         WORK REGISTER
R4       EQU   4         BAL RETURN ADDRESS
R5       EQU   5
R6       EQU   6
R7       EQU   7         POINTER TO INPUT DESCRIPTION
R8       EQU   8         POINTER TO OUTPUT DESCRIPTION
R9       EQU   9         POINTER TO OUTPUT CONVERSION FACTOR
R10      EQU   10        POINTER TO INPUT CONVERSION FACTOR
R11      EQU   11        PARAMETER ADDRESS AND DSECT BASE REGISTER
R12      EQU   12        BASE REGISTER
R13      EQU   13        SAVE AREA POINTER
R14      EQU   14        BALR RETURN ADDRESS
R15      EQU   15        BALR ROUTINE ADDRESS
         SPACE 5
         SAVE  (14,12)        **
         BALR  R12,0           *
         USING *,R12           *
         LA    R9,SAVEAREA     **  HOUSEKEEPING
         ST    R9,8(R13)       *
         ST    R13,4(R9)       *
         LR    R13,R9         **
         SPACE
         USING CONVPARM,R11   ESTABLISH ADDRESSABILITY OF DSECT
         SPACE
COBOLOAD L     R11,0(R1)      POINT REG 11 AT COBOL PARAMETER ADDRESS
         EJECT
PROCESS  MVI   RETNCODE,C'4'       ASSUME VERSION NUMBER IS REQUIRED
         MVC   OUTVALUE(L'VERSNO),VERSNO SET UP VERSION NUMBER IN OUTPU
         CLC   OUTCODE,=C'VN'      IS THIS A VERSION NUMBER REQUEST
         BE    RETURN              RETURN TO CALLER IF YES
         ZAP   OUTVALUE,ZERO       ZEROISE OUTPUT VALUE IN CASE OF ERRR
         MVC   RATESW,INVALUE      PRESERVE 1ST BYTE LEST RATE REQUEST
         CLC   INVALUE(4),=C'RATE' IS THIS A RATE REQUEST
         BNE   *+10                BR IF NOT RATE REQUEST
         ZAP   INVALUE,UNITY       SET TO OBTAIN CONVERSION RATE
         MVI   RETNCODE,X'F3' ESTABLISH OUTPUT ERROR RETURN CODE
         LA    R3,OUTCODE     POINT REG 3 AT OUTPUT CODE
         BAL   R4,GETRATE     GO OBTAIN OUTPUT CONVERSION FACTOR
         LR    R8,R7          PRESERVE POINTER TO OUTPUT DESCRIPTION
         LR    R9,R10         POINT R9 AT OUTPUT CONVERSION FACTOR
         SPACE
         MVI   RETNCODE,X'F2' ESTABLISH INPUT ERROR RETURN CODE
         LA    R3,INCODE      POINT REG 3 AT INPUT CODE
         BAL   R4,GETRATE     GO POINT R10 AT INPUT CONVERSION FACTOR
         SPACE
         MVI   RETNCODE,X'F1' INDICATE VALID OPERATION COMPLETED
         ZAP   CVBAREA,INVALUE MOVE INPUT VALUE TO END OF WORK AREA
         MP    MATHSFLD(16),0(5,R10) MULTIPLY BY INPUT CONVERSION RATE
         DP    MATHSFLD(16),0(5,R9) DIVIDE BY OUTPUT CONVERSION RATE
         ZAP   OUTVALUE,MATHSFLD+3(8) MOVE RESULT TO PARAMETER AREA
         ZAP   CVBAREA,CVBAREA+3(5) PLACE REMAINDER IN DOUBLE WORD
         MP    CVBAREA,TWO    DOUBLE REMAINDER
         CP    0(5,R9),CVBAREA COMPARE DIVISOR WITH TWICE REMAINDER
         BH    *+10           GREATER, SO NO NEED TO ROUND RESULT UP
         AP    OUTVALUE,ONE   ROUND RESULT UP BY 1
         ZAP   MATHSFLD(16),ZERO ZEROISE WORK AREA
         MVC   INDESC,0(R7)   MOVE INPUT DESCRIPTION TO LINKAGE AREA
         MVC   OUTDESC,0(R8)  MOVE OUTPUT DESCRIPTION TO LINKAGE AREA
         CLI   RATESW,C'R'         WAS THIS A RATE REQUEST
         BNE   *+10                BR IF NOT
         MVC   COMMSW,9(R8)        SET 'COMMERCIAL ROUNDING' FLAG
         SPACE
RETURN   EQU   *
         L     R13,4(R13)     RETRIEVE PREVIOUS SAVE AREA POINTER
        RETURN (14,12),RC=0   RETURN TO CALLING PROGRAM
         EJECT
GETRATE  EQU   *
         CLC   0(2,R3),LOW    Q - IS CODE LESS THAN '01'
         BL    RETURN         YES - RETURN TO SENDER
         CLC   0(2,R3),HIGH   Q - IS CODE GREATER THAN '99'
         BH    RETURN         YES - RETURN FORTHWITH TO CALLING PGM
         CLC   1(1,R3),LOW         TEST IF 2ND BYTE LESS THAN ZERO
         BL    RETURN              EXIT IF LOW
         CLC   1(1,R3),HIGH        TEST IF 2ND BYTE GREATER THAN 9
         BH    RETURN              EXIT IF HIGH
         CLC   0(2,R3),LSD    Q - IS CODE POUNDS STERLING
         BE    RETURN         YES - CANNOT PROCESS - RETURN
         PACK  CODE,0(2,R3)   PACK INPUT CURRENCY CODE
         ZAP   CVBAREA,CODE   MOVE PACKED CODE TO DOUBLEWORD
         CVB   R3,CVBAREA     CONVERT INPUT CODE TO BINARY
         BCTR  R3,0           DECREMENT SUBSCRIPT - CURR CODE - BY 1
         MH    R3,TEN              MULTIPLY SUBSCRIPT BY ELEMENT LENGTH
         LA    R10,TABLE      POINT R10 AT START OF TABLE
         AR    R10,R3         ADD COMPUTED OFFSET
         LR    R7,R10         PRESERVE POINTER TO DESCRIPTION
         LA    R10,4(R10)     POINT AT FACTOR WITHIN ELEMENT
         SPACE
*        R10 NOW POINTS AT THE CONVERSION RATE FOR THE CURRENCY CODE
         SPACE
         CP    0(5,R10),NORATE Q - IS THERE A CONVERSION FACTOR
         BE    RETURN         NO - GO BACK TO CALLER
         BR    R4             RETURN TO MAIN-LINE
         EJECT
CONVPARM DSECT
INVALUE  DS    CL8            PICTURE S9(10)V9(5) COMP-3
INCODE   DS    CL2            PICTURE XX
OUTCODE  DS    CL2            PICTURE XX
RETNCODE DS    CL1            PICTURE X
OUTVALUE DS    CL8            PICTURE S9(10)V9(5) COMP-3
INDESC   DS    CL4            PICTURE X(4)
OUTDESC  DS    CL4            PICTURE X(4)
COMMSW   DS    CL1                 PICTURE X.    ( 'RATE' ONLY )
UT460    CSECT
         SPACE 5
MATHSFLD DC    D'0'
CVBAREA  DC    D'0'
SAVEAREA DS    18F
ONE      DC    PL1'1'
UNITY    DC    PL4'100000'         'INPUT VALUE' FOR RATE EXTRACTION
TWO      DC    PL1'2'
TEN      DC    H'10'               LENGTH OF TABLE ELEMENT
CODE     DC    PL2'0'
NORATE   DC    PL5'0'
LOW      DC    CL2'01'
HIGH     DC    CL2'99'
LSD      DC    CL2'19'
ZERO     DC    PL1'0'
RATESW   DC    C' '                INDICTAES 'RATE' WAS SPECIFIED
         SPACE 5
VERSNO   DC    C'75/01'            VERSION NUMBER
TABLE    EQU   *                 CODE   CURRENCY ---- FORMAT S9(3)V9(6)
 DC  CL4'ADIN',PL5'0202500',C' '  01    ALGERIAN DINAR.
 DC  CL4'USDL',PL5'1000000',C' '  02    U.S DOLLAR.
 DC  CL4'CDL ',PL5'1000000',C' '  03    CANADIAN DOLLAR.
 DC  CL4'AESC',PL5'0000000',C' '  04    ANGOLA ESCUDO.
 DC  CL4'APES',PL5'0021860',C' '  05    ARGENTINIAN PESOS.
 DC  CL4'AUDL',PL5'1300000',C' '  06    AUSTRALIAN DOLLARS.
 DC  CL4'BDL ',PL5'0000000',C' '  07    BARBADOS DOLLARS.
 DC  CL4'PDBE',PL5'0000000',C' '  08    BERMUDAN POUNDS.
 DC  CL4'BPES',PL5'0000000',C' '  09    BOLIVIAN PESOS.
 DC  CL4'ASCH',PL5'0055000',C' '  10    AUSTRIAN SCHILLINGS.
 DC  CL4'BFR ',PL5'0025000',C'C'  11    BELGIAN FRANCS.
 DC  CL4'LFR ',PL5'0025000',C'C'  12    LUXEMBURG FRANCS.
 DC  CL4'CRUZ',PL5'0119600',C' '  13    BRAZILIAN CRUZEIROS.
 DC  CL4'FFR ',PL5'0232500',C' '  14    FRENCH FRANCS.
 DC  CL4'DM  ',PL5'0375000',C' '  15    GERMAN MARKS.
 DC  CL4'LIRE',PL5'0001500',C'C'  16    ITALIAN LIRE.
 DC  CL4'FLOR',PL5'0390000',C' '  17    NETHERLAND FLORIN.
 DC  CL4'SFR ',PL5'0380000',C' '  18    SWISS FRANCS.
 DC  CL4'PDST',PL5'0000000',C' '  19    ENGLISH POUNDS STERLING.
 DC  CL4'LEV ',PL5'0000000',C' '  20    BULGARIAN LEV.
 DC  CL4'KYAT',PL5'0000000',C' '  21    BURMA KYAT.
 DC  CL4'RIEL',PL5'0000000',C' '  22    CAMBODIAN RIEL.
 DC  CL4'CHES',PL5'0000200',C' '  23    CHILE EUSCUDO.
 DC  CL4'GDR ',PL5'0036000',C' '  24    GREEK DRACHMAS.
 DC  CL4'CHDL',PL5'0026300',C' '  25    CHINESE DOLLARS.
 DC  CL4'CPES',PL5'0031900',C' '  26    COLUMBIAN PESOS.
 DC  CL4'CFR ',PL5'0000000',C' '  27    CONGOLESE FRANCS.
 DC  CL4'COL ',PL5'0000000',C' '  28    COSTA RICAN COLON.
 DC  CL4'CUPE',PL5'0000000',C' '  29    CUBAN PESOS.
 DC  CL4'PDCY',PL5'2400000',C' '  30    CYPRIOT POUNDS.
 DC  CL4'CR  ',PL5'0000000',C' '  31    CZECHOSLOVAKIAN CROWNS.
 DC  CL4'PESC',PL5'0040000',C' '  32    PORTUGUESE ESCUDOS.
 DC  CL4'DPES',PL5'0000000',C' '  33    DOMINICAN REPUBLIC PESOS.
 DC  CL4'SPES',PL5'0017500',C' '  34    SPANISH PESETAS.
 DC  CL4'DKR ',PL5'0162000',C' '  35    DANISH KRONER.
 DC  CL4'FMAR',PL5'0265000',C' '  36    FINISH MARKS.
 DC  CL4'SUC ',PL5'0000000',C' '  37    ECUADOR SUCRE.
 DC  CL4'PDEG',PL5'2300000',C' '  38    EGYPTIAN POUNDS.
 DC  CL4'NKR ',PL5'0180000',C' '  39    NORWEGIAN KRONER.
 DC  CL4'SKR ',PL5'0225000',C' '  40    SWEDISH KRONER.
 DC  CL4'ECOL',PL5'0400000',C' '  41    EL SALVADOR COLON.
 DC  CL4'EDL ',PL5'0000000',C' '  42    ETHIOPIAN DOLLARS.
 DC  CL4'QUET',PL5'1000000',C' '  43    GUATEMALIAN QUETZAL.
 DC  CL4'GOUR',PL5'0000000',C' '  44    HAITI GOURDES.
 DC  CL4'LEMP',PL5'0000000',C' '  45    HONDURAS LEMPIRA.
 DC  CL4'HKDL',PL5'0200000',C' '  46    HONG KONG DOLLARS.
 DC  CL4'FOR ',PL5'0000000',C' '  47    HUNGARIAN FORINT.
 DC  CL4'IKR ',PL5'0011360',C' '  48    ICELANDIC KRONA.
 DC  CL4'RUP ',PL5'0113000',C' '  49    INDIAN RUPEES.
 DC  CL4'NEWR',PL5'0002400',C' '  50    INDONESIAN NEW RUPIAH.
 DC  CL4'RIAL',PL5'0015000',C' '  51    IRAN RIAL.
 DC  CL4'IDIN',PL5'2800000',C' '  52    IRAQ DINAR.
 DC  CL4'PDIS',PL5'0151200',C' '  53    ISRAELI POUNDS.
 DC  CL4'JADL',PL5'1200000',C' '  54    JAMAICAN POUNDS.
 DC  CL4'YEN ',PL5'0003330',C' '  55    JAPANESE YEN.
 DC  CL4'JDIN',PL5'2800000',C' '  56    JORDANIAN DINAR.
 DC  CL4'KSCH',PL5'0147500',C' '  57    KENYAN SCHILLINGS.
 DC  CL4'WON ',PL5'0002100',C' '  58    SOUTH KOREAN WON.
 DC  CL4'PDLE',PL5'0310000',C' '  59    LEBANESE POUND.
 DC  CL4'LIDL',PL5'0000000',C' '  60    LIBERIAN DOLLARS.
 DC  CL4'PDLI',PL5'0000000',C' '  61    LIBYAN POUNDS.
 DC  CL4'MDL ',PL5'0415000',C' '  62    MALAYSIAN DOLLARS.
 DC  CL4'MPES',PL5'0080000',C' '  63    MEXICAN PESOS.
 DC  CL4'NAGU',PL5'0000000',C' '  64    NETHERLANDS ANTILLES GUILDERS.
 DC  CL4'NZDL',PL5'1075000',C' '  65    NEW ZEALAND DOLLARS.
 DC  CL4'CORD',PL5'0142900',C' '  66    NICARAGUA CORDOBA.
 DC  CL4'PDNI',PL5'0000000',C' '  67    NIGERIAN POUNDS.
 DC  CL4'PRUP',PL5'0000000',C' '  68    PAKISTAN RUPEES.
 DC  CL4'BAL ',PL5'0000000',C' '  69    PANAMANIAN BALBOA.
 DC  CL4'GUAR',PL5'0000000',C' '  70    PARAGUAY GUARANI.
 DC  CL4'SOL ',PL5'0000000',C' '  71    PERUVIAN SOL.
 DC  CL4'PPES',PL5'0130000',C' '  72    PHILIPPINES PESOS.
 DC  CL4'ZLOT',PL5'0050200',C' '  73    POLISH ZLOTY.
 DC  CL4'PEAE',PL5'0000000',C' '  74    PORTUGUESE EAST AFRICA ESCUDOS
 DC  CL4'PDRH',PL5'0000000',C' '  75    RHODESIAN POUNDS.
 DC  CL4'LEU ',PL5'0000000',C' '  76    RUMANIAN LEU.
 DC  CL4'RIY ',PL5'0222200',C' '  77    SAUDI ARABIAN RIYAL.
 DC  CL4'SIDL',PL5'0410000',C' '  78    SINGAPORE DOLLARS.
 DC  CL4'SOSH',PL5'0000000',C' '  79    SOMALIAN SHILLINGS.
 DC  CL4'RAND',PL5'1150000',C' '  80    SOUTH AFRICAN RAND.
 DC  CL4'SWAR',PL5'1400000',C' '  81    SOUTH WEST AFRICAN RAND.
 DC  CL4'PDSU',PL5'0000000',C' '  82    SUDANESE POUNDS.
 DC  CL4'SUGU',PL5'0000000',C' '  83    SURINAM GUILDER.
 DC  CL4'SLRA',PL5'0000000',C' '  84    SWAZILAND RAND.
 DC  CL4'PDSY',PL5'0260000',C' '  85    SYRIAN POUNDS.
 DC  CL4'TSCH',PL5'0000000',C' '  86    TANZANIAN SCHILLINGS.
 DC  CL4'BAHT',PL5'0050000',C' '  87    THAILAND BAHT.
 DC  CL4'TTDL',PL5'0000000',C' '  88    TRINIDAD + TOBAGO DOLLARS.
 DC  CL4'TDIN',PL5'1904760',C' '  89    TUNISIAN DINARS.
 DC  CL4'TLIR',PL5'0111110',C' '  90    TURKISH LIRA.
 DC  CL4'USCH',PL5'0000000',C' '  91    UGANDA SCHILLINGS.
 DC  CL4'UPES',PL5'0000000',C' '  92    URAGUAYAN PESOS.
 DC  CL4'ROUB',PL5'0000000',C' '  93    U.S.S.R. ROUBLES.
 DC  CL4'BOL ',PL5'0232600',C' '  94    VENEZUELA BOLIVARES.
 DC  CL4'VNPI',PL5'0000000',C' '  95    VIETNAM PIASTERS.
 DC  CL4'YDIN',PL5'0066670',C' '  96    YUGOSLAVIAN DINARS.
 DC  CL4'PDZA',PL5'0000000',C' '  97    ZAMBIAN POUNDS.
 DC  CL4'PSTG',PL5'2100000',C' '  98    U.K. DECIMALIZED POUNDS.
 DC  CL4'    ',PL5'0'             99    *** UNALLOCATED ***
         SPACE 5
         LTORG
         SPACE
         END
