U497     TITLE 'UT497  - DETERMINE IF DATASET IS CATALOGED'
UT497    CSECT
         PRINT ON,GEN
         SPACE 2
*******************************************************************
*                                                                 *
*        PROGRAM ID. :             UT497                          *
*                                                                 *
*        DATE WRITTEN :            AUGUST 1976                    *
*                                                                 *
*        AUTHOR :                  D.H.CARTWRIGHT.                *
*                                                                 *
*        LOCATION :                MONSANTO MISD/TSG BRUSSELS     *
*                                                                 *
*******************************************************************
         SPACE 3
*******************************************************************
*                                                                 *
*        PURPOSE                                                  *
*       ---------                                                 *
*                                                                 *
*        THIS PROGRAM TAKES A DATASET NAME FROM THE PARM          *
*        INFORMATION AND USES SYSTEM MACROS TO DETERMINE IF       *
*        THAT DATASET IS CATALOGED.THE RETURN CODE FROM THE       *
*        LOCATE MACRO IS USED AS THE PROGRAM COMPLETION CODE.     *
*                                                                 *
*        THERE ARE NO EXTERNAL DATASETS.                          *
*                                                                 *
*******************************************************************
         EJECT
*******************************************************************
*                                                                 *
*        LIMITATION                                               *
*       ------------                                              *
*                                                                 *
*                                                                 *
*******************************************************************
         EJECT
*******************************************************************
*        REGISTER EQUATES AND USAGES.                             *
*******************************************************************
         SPACE 1
R0       EQU   0    SVC COMMUNICATIONS AND WORK REGISTER
R1       EQU   1    SVC COMMUNICATIONS AND WORK REGISTER
R2       EQU   2    SUB-ROUTINE CONTROL INFORMATION AND WORK REG.
R3       EQU   3    WORK REGISTER
R4       EQU   4    BAL RETURN ADDRESS
R5       EQU   5    PARM LENGTH.
R6       EQU   6    PARM LOCATION
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13   SAVE AREA POINTER / BASE REGISTER 1
R14      EQU   14   BALR RETURN ADDRESS
R15      EQU   15   BALR ROUTINE ADDRESS AND WORK REGISTER
         SPACE 3
*******************************************************************
*        HOUSEKEEPING                                             *
*******************************************************************
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) ESTABLISH ADDRESSABILITY
         PRINT NOGEN               SUPPRESS MACRO EXPANSION
         SPACE 3
*******************************************************************
*        SAVE ADDRESS AND LENGTH OF PARM OF EXEC CARD             *
*******************************************************************
         SPACE 1
         L     R1,0(R1)            LOAD ADDRESS OF PARM INTO R1
         MVC   PARMLGTH+2(2),0(R1) STORE LENGTH OF PARM INFO'S
         LA    R1,2(R1)            GET ADDRESS OF PARM INFO'S
         ST    R1,PARMADDR         STORE ADDRESS OF PARM INFO'S
         L     R5,PARMLGTH         GET PARM LENGTH
         LTR   R5,R5               ARE THERE ANY?
         BZ    ERROR               NO
         SPACE 1
         CH    R5,=H'44'           TEST FOR MAXIMUM LENGTH
         BH    ERROR               WRONG - EXIT
         EJECT
*******************************************************************
*        TEST FOR CATALOGED DATASET.                              *
*******************************************************************
         SPACE 3
         BCTR  R5,0                REDUCE FOR EXECUTE
         L     R6,PARMADDR         PICK UP PARM
         EX    R5,MOVE             PUT DSN IN CAMLIST
         LOCATE POINTER            TEST FOR CATALOG ENTRY
         STH   R15,COMPCODE        SAVE CONDITION CODE
         B     JOBEND              FINISH
         SPACE 3
MOVE     MVC   DSNAME(*-*),0(R6)   MOVE PARM TO CAMLST -EXECUTED-
         EJECT
*******************************************************************
*        INVALID PARM ROUTINE.                                    *
*******************************************************************
         SPACE 3
ERROR    DS    0H                  PARM IS WRONG
         MVC   COMPCODE,COND36     SET RC=36
         B     JOBEND              AND EXIT
         EJECT
*******************************************************************
*        JOB TERMINATION ROUTINE -                                *
*         SET A COMPLETION CODE IN REGISTER 15, AND RETURN.       *
*******************************************************************
         SPACE 1
JOBEND   EQU   *
         PGMOUT RC=COMPCODE        SET CONDITION CODE AND RETURN
         EJECT
*******************************************************************
*        WORK AREAS                                               *
*******************************************************************
         SPACE 1
DECIMAL  DS    D                   WA FOR DECIMAL OPERATIONS
COMPCODE DC    H'0'                CONDITION CODE
         SPACE 1
COND4    DC    H'4'                COMPLETION CODE OF 4
COND8    DC    H'8'                COMPLETION CODE OF 8
COND12   DC    H'12'               COMPLETION CODE OF 12
COND36   DC    H'36'               COMPLETION CODE OF 36
         SPACE 1
         SPACE 1
PARMLGTH DC    F'0'                LENGTH OF PARM OF EXEC CARD
PARMADDR DS    F                   ADDRESS OF PARM INFO'S
         SPACE 3
POINTER  CAMLST NAME,DSNAME,,CATLG READ CATALOG FOR DSNAME
DSNAME   DC    CL44' '             DATASET NAME
CATLG    DS    0D                  CATALOG BLOCK AREA
         DC    256X'00'            INITIALLY ZEROES
         DC    9X'00'                                 MVS CONV.MOD        001310
         EJECT
*******************************************************************
*        LITERAL POOL                                             *
*******************************************************************
         SPACE 1
         LTORG
         END
