*       PGM=NI11JNL VER=004 VOL=NIBS05 TIME/DATE=68030 11:54:23 BAL
NI11JNL  CSECT
         TITLE  '**NIBSIO SUB-SYSTEM**  MODULE=NI11JNL. V-2'
   USING  *,15
    B   22(0,15)
   DC  FL1'18'
     DC   CL9'NI11JNL '
   DC  CL8'V002'
   STM  14,12,12(13)
   ST   13,SAVEAREA+4
    CNOP  0,4
   BAL  13,SAVEAREA+72
   DROP  15
   USING  *,13
SAVEAREA  DC  18F'0'
  L  15,SAVEAREA+4
   ST  13,8(0,15)
*
*
   LR  2,1
         L     6,8(2)
         LH    6,0(6)
         CH    6,=H'0'
         BE    CLOSE
         CLI   SWITCH,C'1'    FIRST TIME TO OPEN JOURNAL
         BE    PASTOPEN
         LR    4,1
         MVI   SWSW,X'00'
         OPEN  (JOURNAL,(OUTPUT))
         CLI   SWSW,X'FF'
         BE    ERROR79
   MVC  IOAREA+4(8),=C'JOURNALE'
         B     BLKRTN
SXSX     LR    1,4
         MVI   SWITCH,C'1'
PASTOPEN EQU *
         L     4,0(2) LOAD ADDR OF CCA
         MVC   IOAREA+4(8),0(4) MOVE CCA TO OUTPUT
         CLI   8(4),C'E'   CHECKPOINT CHAR
         BNE   PASTCHK
BLKRTN   MVC   IOAREA(4),=X'000E0000'
         B     PUTLEN
PASTCHK  L     5,4(2) LOAD ADDR OF DCA
         L     6,8(2) LOAD RECORD LENGTH ADDR
         LH    6,0(6) LOAD ACTUAL RECORD LENGTH
         STH   6,HALF
         AH    6,=H'22' INCREASE RECORD LENGTH BY OTHER DATA
         STH   6,IOAREA  STORE RECORD LENGTH
         MVC   IOAREA+12(8),0(5) STORE DCA
         L    5,12(2)  LOAD ADDR OF DATA
         CH    6,=H'278'
         BH    MAOR
         SH    6,=H'22'  RETURN RECORD LENGTH
         SH    6,=H'1' DEDUCT MACHINE BYTE
         STC   6,INST+1  OVERRIDE INST LENGTH
INST     MVC   IOAREA+20(0),0(5)  MOVE PART OR ALL OF RECORD
         AH    6,=H'1'   SET UP LAST ACTUAL MOVE LENGTH
         LA    5,IOAREA+20
         AR    5,6  MODIFY TO POINT AT END OF ENTIRE RECORD
         MVC   0(2,5),IOAREA  MOVE BART'S LENGTH
OUT      MVI   SWSW,X'00'
         PUT   JOURNAL,IOAREA
X2X2     CLI   SWSW,X'FF'
         BE    ERROR77
         CLI   SWITCH,C'1'
         BNE   SXSX
  LA  15,0
  B  BINGO
ERROR77  LA  15,8
  B  BINGO
ERROR79  LA  15,4
  B  BINGO
CLOSE    MVI   SWSW,X'00'
         CLOSE JOURNAL
         CLI   SWSW,X'FF'
         BE    ERROR99
         MVI   SWITCH,C'0'
  LA  15,0
  B  BINGO
ERROR99  LA  15,12
  B  BINGO
BINGO  EQU  *
   L  13,SAVEAREA+4
   LM  2,12,28(13)
  L  14,12(13)
   MVI  12(13),X'FF'
  BR  14
MAOR     LR    0,6
         BAL   1,*+4
         SVC   10
         SH    6,=H'22'
         MVC   0(20,1),IOAREA
         B     HIGH
TINST    MVC   20(0,1),0(5)
COMPARE  CH    6,=H'256'
         BH    HIGH
         SH    6,=H'1'
         STC   6,TINST+1
         EX    0,TINST
         AH    6,=H'1'
         AR    1,6
         MVC   20(2,1),IOAREA
         SH    1,HALF
         LR    9,1
         MVI   SWSW,X'00'
         LR    0,1
         LA    1,JOURNAL
         L     15,48(0,1)
         BALR  14,15
         LR    1,9
         LH    0,HALF
         AH    0,=H'22'
         SVC   10
         B    X2X2
HIGH     MVI   TINST+1,X'FF'
         EX    0,TINST
         AH    1,=H'256'
         SH    6,=H'256'  RECTIFY LENGTH
         AH    5,=H'256'  RECTIFY ADDR
         B     COMPARE
PUTLEN   MVC   IOAREA+12(2),IOAREA BART'SLENGTH
         B     OUT
JOURNAL  DCB   DSORG=PS,MACRF=PM,DDNAME=JOURNAL,BFALN=D,BFTEK=S,       X
               SYNAD=ERROR,BUFNO=1
HALF     DS    H
SWSW     DS    CL1
SWITCH   DC    C'0'
IOAREA   DS    0D
         DS    CL280
         LTORG
ERROR    EQU   *
         USING *,15
         L     10,=A(SWSW)
         MVI   0(10),X'FF'
         BR    14
         END
