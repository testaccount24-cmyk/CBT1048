****   B0103  TEST CASE 2
*        THIS PROGRAM BUILDS, LOADS OR DUMPS ISAM FILES FROM OR TO
*        QSAM FILES.
         SPACE 4
B0103A   START 0
         ENTRY B0102
         ENTRY B0103
         USING *,12
         USING IHADCB,2
         SPACE 2
         B     SAVE-*(15)
         SPACE 2
B0102    NI    B0102A-B0102(15),X'0F'
         L     15,BASE-B0102(15)
         B     SAVE-B0103A(15)
         SPACE 2
B0103    OI    B0103B-B0103(15),X'F0'
         L     15,BASE-B0103(15)
         SPACE 2
SAVE     SAVE  (14,12),T
         LR    12,15
         ST    13,SAVEAREA+4
         LA    5,SAVEAREA
         ST    5,8(0,13)
         LR    13,5
         MVI   ARG6,C'9'
         MVC   ARG6+1(33),ARG6
B0103B   EQU   *+1
         NOP   OPENCARD
         NI    B0103C,X'0F'
B0102A   EQU   *+1
         B     B0102C
         LA    1,B0102LST
         OI    B0102A,X'F0'
         B     B0102C
B0102B   LA    1,B0103LST
B0102C   LA    2,ISAMDCB
         LM    8,11,0(1)
         CLI   0(9),C'W'
         BE    PUT
         CLI   0(9),C'D'
         BE    PUT
         CLI   0(9),C'U'
         BE    PUT
         CLI   0(9),C'O'
         BE    OPEN
         CLI   0(9),C'C'
         BE    CLOSE
         CLI   0(9),C'A'
         BE    OPENALL
         CLI   0(9),C'I'
         BE    OPENALL
         LA    2,UNLOADCB
         CLI   0(9),C'Q'
         BE    DUMP
         CLI   0(9),C'B'
         BE    DUMP
         MVI   0(11),C'E'
         B     RETURN
         SPACE 4
RETURN0  MVI   0(11),C'0'
B0103C   EQU   *+1
         NOP   GETCARD
         SR    15,15
         L     13,SAVEAREA+4
         RETURN (14,12),T,RC=(15)
         SPACE 4
RETURN   ABEND 122,DUMP
*
         SPACE 2
         EJECT
BASE     DC    A(B0103A)
B0102LST DC    A(ISAMDATA)
         DC    A(CODE1)
         DC    A(INDATA)
         DC    A(CODE2)
B0103LST DC    A(ARG1)
         DC    A(ARG2)
         DC    A(ARG3)
         DC    A(ARG4)
SAVEAREA DS    18F
SYNADADD DC    A(ERROR)
EODADD   DC    A(CLOSER)
QEODAD   DC    A(CLOSE)
HIGSAVE  DS    F
CVD1     DS    D
F256     DC    F'256'
DUMPAD   DS    F
PADCNT   DS    H
RECNT    DS    H
PADSCNT  DS    H
CZERO    DC    H'0'
ISAMDATA DC    C'ISAMDATA'
CODE1    DC    C'A'
INDATA   DC    C'INDATA  '
CODE2    DC    C'0'
ARG1     DS    CL8
         DC    C' '
ARG2     DS    CL1
         DC    C' '
         DC    C' '
ARG3     DS    CL8
ARG4     DS    CL1
ARG5     DS    CL6
ARG6     DS    0CL39
         DC    39X'01'
ARG7     DS    0CL39
         DC    39X'01'
ARG8     DC    X'40'
COL72    DS    CL1
COL72X   DC    C' '
DDBLANK  DC    CL8'0'
MACRADD  DC    X'0200004802004882'
QMACR    DC    X'02004848'
         EJECT
WTOWTL   DS    0F
         DC    AL2(MSGL-*)
         DC    AL2(0)
MSGDD    DS    CL20
         DC    C' '
MSG      DS    CL40
MSGL     EQU   *
MSGA     DC    C'-FILE DUMP COMPLETED----SEQUENCE ERRORS-'
MSGB     DC    C'-FILE DUMP COMPLETED-      O.K.        -'
MSGC     DC    C'-I S A M     R E C O R D        K E Y S-'
MSGD     DC    C'-SEQUENCE ERRORS----FILE DUMP CONTINUES-'
MSGE     DC    C'-------B0103 TERMINATED WITH USER ABEND-'
MSGF     DC    C'----------------------GOOD END OF B0103-'
MSGG     DC    C'--------------------'
PREVNUM  DS    CL40
FOLLNUM  DS    CL40
ERTESTSW DC    X'80'
DUMPFLAG DC    X'00'                   X'00' IF GOOD DUMP
COMPARE  CLC   FOLLNUM(0),PREVNUM      COMPARE SEQ KEYS
MOVE     MVC   FOLLNUM(0),0(1)         SAVE KEY
MOVEKEY  MVC   MSG(0),FOLLNUM
WTL      LA    1,WTOWTL                HASP OUTPUT
         SVC   36                      WTL
         MVC   MSG,MSG-1               BLANK AREA
         BR    4
WTO      LA    1,WTOWTL                CONSOLE OUTPUT
         SVC   35                      WTO
         MVC   MSG,MSG-1               BLANK AREA
         BR    4
KEYERR   TM    ERTESTSW,X'80'
         BO    ERRMSG
         CLI   ARG8,C' '     Q.  WTO
         BE    PUTQSAM       A.  NO
         EX    6,MOVEKEY
         BAL   4,WTO
         B     PUTQSAM
ERRMSG   NI    ERTESTSW,X'00'
         OI    DUMPFLAG,X'FF'   ABEND ON
         MVC   MSG,MSGD
         BAL   4,WTO              WRITE TO OPERATOR
         CLI   ARG8,C' '     Q.  WTO
         BE    KEYERR        A.  NO
         MVC   MSG,MSGC
         BAL   4,WTO              WRITE TO OPERATOR
         B     KEYERR
***********************************************************************
PADSY    ST    1,HIGSAVE
         LA    3,RELOAD
         LH    5,PADSCNT
         B     CLOSING
         SPACE 2
CLOSE    LA    3,CLOSES
         LH    5,PADCNT
         MVI   COL72X,C' '
         MVI   ARG6,X'FF'
CLOSING  OI    PUTON+1,X'F0'
         SPACE 4
         LH    4,DCBRKP
         STC   4,DUMMY1+3
         SR    4,4
         IC    4,DCBKEYLE               PICKUP KEY LENGTH
         AH    4,DCBRKP                 ADD START KEY POSITION
         BCTR  4,0
         BCTR  4,0
         STC   4,DUMMY1+1               MOVE CHAR CNT
         BCTR  4,0
         STC   4,DUMMY2+3               DISPLACEMENT VALUE
         BCTR  4,0
         LA    6,ARG6(4)
         MVC   DUMPAD+1(3),0(6)
         MVI   DUMPAD,X'00'
         L     7,DUMPAD
DUMMY    LA    7,1(7)
         ST    7,DUMPAD
         BAL   4,PUTS
         MVI   0(1),X'FF'               MOVE DELETE CODE
DUMMY1   MVC   1(0,1),ARG6
DUMMY2   MVC   0(3,1),DUMPAD+1
         BCT   5,DUMMY
         BR    3
         SPACE 4
RELOAD   MVC   COL72X,COL72
         CLI   COL72X,C' '
         BNE   GETCARD
RELOADS  L     1,HIGSAVE
         B     RELOADED
***********************************************************************
CLOSES   CLOSE ISAMDCB
         CLI   0(9),C'C'
         BE    RETURN0
         SPACE 4
ACLOSE   CLOSE QSAMDCB
         XC    QSAMDCB(88),QSAMDCB
         SR    7,7
         LA    7,1(7)
         STC   7,QSAMDCB+23
         STC   7,QSAMDCB+31
         STC   7,QSAMDCB+55
         STC   7,QSAMDCB+59
         STC   7,QSAMDCB+71
         STC   7,QSAMDCB+75
         STC   7,QSAMDCB+79
         STC   7,QSAMDCB+87
         STC   7,QSAMDCB+95
         MVI   QSAMDCB+26,X'40'
         MVC   QSAMDCB+32(4),QEODAD
         MVC   QSAMDCB+40(8),DDBLANK
         MVC   QSAMDCB+48(4),QMACR
         B     RETURN0
         SPACE 4
         EJECT
OPENALL  MVC   QSAMDCB+40(8),0(10)
         OPEN  (QSAMDCB,(INPUT))
         SPACE 4
OPEN     XC    ISAMDCB(252),ISAMDCB
         BAL   4,CLEARDCB
         MVC   DCBOFLGS(4),MACRADD
         MVC   DCBDDNAM(8),0(8)         MOVE DD NAME TO DCB
         OPEN  (ISAMDCB,(OUTPUT))
***********************************************************************
         LH    6,DCBRKP
         CLI   0(9),C'A'
         BNE   KEEPRKP
         BCTR  6,0
KEEPRKP  STC   6,RELOADED+5
*THIS SECTION CALCULATES THE NUMBER OF RECORDS PER TRACK
RECNTS   LH    6,DCBLRECL
         LH    4,DCBBLKSI
         SRDA  4,32(0)
         DR    4,6
         STH   5,RECNT
         LH    6,=H'2137'
         LH    5,DCBBLKSI
         MR    4,6
         LR    4,5
         SRDA  4,32(0)
         LH    6,=H'2048'
         DR    4,6
         AH    5,=H'146'
         LR    6,5
         LH    4,=H'7249'
         SH    4,DCBBLKSI
         SRDA  4,32(0)
         DR    4,6
         LA    5,1(5)
         LH    6,RECNT
         MR    4,6
         AR    5,5
         S     5,=F'3'
         STH   5,RECNT
         STH   5,PADCNT
***********************************************************************
         CLI   0(9),C'O'
         BE    RETURN0
         SPACE 4
GET      GET   QSAMDCB
RELOADED MVC   ARG7,0(1)
         LR    10,1
PUTON    B     PUT
PUTPAD   CLC   ARG6,ARG7
         BL    PADSY
         SPACE 4
PUT      BAL   4,PUTS
         LH    4,DCBLRECL
         CLI   0(9),C'I'
         BE    I
         CLI   0(9),C'U'
         BE    I
         CLI   0(9),C'D'
         BE    D
         BCTR  4,0
         MVI   0(1),X'00'
         LA    1,1(1)
I        BAL   7,M1
         CLI   0(9),C'W'
         BE    RETURN0
         CLI   0(9),C'D'
         BE    RETURN0
         CLI   0(9),C'U'
         BE    RETURN0
         B     GET
D        MVI   0(10),X'FF'
         B     I
         SPACE 4
DUMP     XC    UNLOADCB(252),UNLOADCB
         BAL   4,CLEARDCB
         MVC   DCBOFLGS(4),MACRADD+4
         MVC   DCBDDNAM(8),0(8)         ARG1=ISAM DD
         MVC   QSAMDCB+40(8),0(10)      ARG2=QSAM DD
         OPEN  (QSAMDCB,(OUTPUT))
         OPEN  (UNLOADCB,(UPDAT))
         SETL  UNLOADCB,B
         SPACE 4
UNLOAD   GET   UNLOADCB
         LR    10,1
         AH    1,DCBRKP
         IC    6,DCBKEYLE
         BCTR  6,0
         XC    FOLLNUM,FOLLNUM
         EX    6,MOVE
         EX    6,COMPARE
         BNH   KEYERR
         MVC   PREVNUM,FOLLNUM
PUTQSAM  PUT   QSAMDCB
FIRSTNUM LH    4,DCBLRECL
         CLI   0(9),C'Q'
         BE    Q
         BCTR  4,0
         LA    10,1(10)
Q        BAL   7,M1
         B     UNLOAD
         SPACE 4
*
*
CLOSER   ESETL UNLOADCB
         CLOSE UNLOADCB
*
         MVC   MSG,MSGB                DUMP SUCCESSFUL
         TM    ERTESTSW,X'80'          Q.  DUMP GOOD
         BO    SEQERROR                A.  YES
         MVC   MSG,MSGA                NOT SUCCESSFUL
         MVI   ERTESTSW,X'80'          RESET SWITCH
SEQERROR BAL   4,WTO
         B     ACLOSE
         EJECT
OPENCARD OI    B0103C,X'F0'
         NI    B0103B,X'0F'
         OPEN  (CARDCB,(INPUT))
GETCARD  GET   CARDCB
         MVC   ARG1,0(1)
         MVC   ARG2,8(1)
         MVC   ARG3,12(1)
         MVI   ARG4,C'0'
         MVC   ARG5,24(1)         STORE NUM OF RECS TO PAD
         MVC   ARG6,34(1)         STORE LOCATION WHERE PAD DESISRED
         MVC   COL72,71(1)        STORE COL. 72
         MVC   MSGDD,ARG1              GET FILE DDNAMES
         MVC   MSGDD+8(4),=C' TO '
         MVC   MSG,MSG-1               CLEAR MSG AREA
         XC    PREVNUM,PREVNUM         SET TO BINARY ZEROS
         MVC   ARG8,10(1)     WTO OPTION
         CLI   ARG5+5,C' '        IF BLANK NO PAD DESIRED
         BE    B0102B
         PACK  CVD1,ARG5
         OI    CVD1+7,X'0C'
         CVB   5,CVD1
         LA    5,5(5)
         STH   5,PADSCNT          STORE NUM OF RECS TO PAD
         NI    PUTON+1,X'0F'
         CLI   COL72X,C' '        IS THIS A CONTINUATION CARD
         BNE   RELOADS            YES-IT IS
         B     B0102B
         SPACE 4
ENDCARD  NI    B0103C,X'0F'
         CLOSE CARDCB
*
         MVC   MSGDD,MSGG              SAVE TAPES MSG
         MVC   MSG,MSGF                GOOD END MSG
         CLI   DUMPFLAG,X'FF'     Q. GOOD END MSG
         BNE   GOODEND              A.  YES
         MVC   MSG,MSGE                BAD END MSG
         BAL   4,WTO              WRITE TO OPERATOR
         B     RETURN                   GO ABEND
GOODEND  BAL   4,WTO                   CONSOLE
*
         B     RETURN0
         SPACE 4
         EJECT
M1       S     4,F256
         BC    12,M2
         MVC   0(256,1),0(10)
         LA    1,256(1)
         LA    10,256(10)
         B     M1
M2       LA    4,255(4)
         EX    4,M3
         BR    7
M3       MVC   0(0,1),0(10)
         SPACE 4
PUTS     PUT   ISAMDCB
         LH    6,PADCNT                 RECORDS REMAINING IN BLOCK
         BCT   6,PADCNTS                SUBTRACT ONE FROM REMAINDER
         LH    6,RECNT                  RELOAD NO. OF RECORDS
PADCNTS  STH   6,PADCNT                 STORE REMAINDER IN BLOCK
         BR    4
         SPACE 4
CLEARDCB BCR   0,0
         SR    7,7
         LA    7,1(7)
         STC   7,DCBBUFCB+3
         STC   7,DCBIOBAD+3
         STC   7,DCBEODAD+3
         STC   7,DCBSYNAD+3
         STC   7,DCBMSWA+3
         STC   7,DCBMSHI+3
         STC   7,DCBSETL+3
         STC   7,DCBESETL+3
         STC   7,DCBWKPT1+3
         MVI   DCBDSORG,X'80'
         MVC   DCBEODAD,EODADD
         MVC   DCBSYNAD,SYNADADD
         MVC   DCBLRAN(24),DCBESETL
         MVC   DCBWKPT2(20),DCBWKPT1
         MVC   DCBDDNAM(8),DDBLANK
         BR    4
         SPACE 4
         EJECT
ERROR    TM    DCBEXCD1,X'20'
         BO    ERR1
         TM    DCBEXCD1,X'04'
         BO    ERR2
         TM    DCBEXCD2,X'80'
         BO    ERR3
         TM    DCBEXCD2,X'40'
         BO    ERR4
         TM    DCBEXCD2,X'20'
         BO    ERR5
*
ERR0     MVI   0(11),C'U'
         WTO   'UNEXPLAINABLE ERROR'
         B     RETURN
ERR1     MVI   0(11),C'N'
         WTO   'SPACE NOT FOUND'
         B     RETURN
ERR2     MVI   0(11),C'P'
         WTO   'UNCORRECTABLE OUTPUT ERROR'
         B     RETURN
ERR3     MVI   0(11),C'S'
         WTO   'SEQUENCE CHECK'
         B     ERR6
ERR4     MVI   0(11),C'D'
         WTO   'DUPLICATE RECORD'
         B     ERR6
ERR5     MVI   0(11),C'C'
         WTO   'DCB CLOSED WHEN ERROR ROUTINE ENTERED'
         BR    14
ERR6     MVC   DCBEXCD2(1),CZERO
         B     RETURN
         SPACE 4
         LTORG
         EJECT
         DC    C'QSAMDCB'
         DS    4F
QSAMDCB  DCB   DSORG=PS,MACRF=(GL,PL),EODAD=CLOSE
         EJECT
         DC    C'ISAMDCB'
         DS    4F
ISAMDCB  DCB   DSORG=IS,MACRF=PL,SYNAD=ERROR
         EJECT
         DC    C'UNLOADCB'
         DS    4F
UNLOADCB DCB   DSORG=IS,MACRF=(GL,SK),EODAD=CLOSER
         EJECT
         DC    C'CARDCB'
         DS    4F
CARDCB   DCB   DSORG=PS,MACRF=(GL),EODAD=ENDCARD,DDNAME=SYSIN,         X
               LRECL=80,BLKSIZE=80,RECFM=F
         EJECT
         DCBD  DSORG=IS
         END
