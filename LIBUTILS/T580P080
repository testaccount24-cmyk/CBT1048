 LIST160: PROC OPTIONS(MAIN);
      DCL
          ACTAPE    FILE SEQUENTIAL;
      DCL LIST PRINT FILE;
      DCL
          NEWTAPE FILE RECORD SEQUENTIAL,
          PREREC CHAR(160) INIT(' '),
          DUPCOUNT FIXED BIN(31) INIT(0);
      DCL INCOUNT FIXED BIN(31) INIT(0);
      DCL OUTCOUNT FIXED BIN(31) INIT(0);
      DCL ACTREC CHAR(160) BASED(RECPTR);
      DCL
        1 ACT       BASED(RECPTR),
           3 ID               CHAR(3),
           3 JDATS            CHAR(5),
           3 PSUDO            CHAR(17),
           3 PROJ             CHAR(10),
           3 JDAT2            CHAR(5),
           3 JTIMS            CHAR(6),
           3 JNUM             CHAR(3),
           3 SNUM             CHAR(2),
           3 JNAME            CHAR(8),
           3 SNAME            CHAR(8),
           3 JPRIO            CHAR(2),
           3 JCODE            CHAR(2),
           3 SPGM             CHAR(8),
           3 JUSER            CHAR(6),
           3 SDATS            FIXED DEC(5,0),
           3 STIMS            FIXED DEC(7,2),
           3 STIME            FIXED DEC(7,2),
           3 REGNR            FIXED DEC(5,0),
           3 HIWTR            FIXED DEC(5,0),
           3 CPUTM            FIXED DEC(5,2),
           3 MCHUN            FIXED DEC(7,3),
           3 COST             FIXED DEC(7,2),
           3 TAPEU            FIXED DEC(3,0),
           3 DISKU            FIXED DEC(3,0),
           3 COMPU            FIXED DEC(7,3),
           3 OCPYU            FIXED DEC(7,3),
           3 IOUNT            FIXED DEC(7,3),
           3 CORUT            FIXED DEC(7,3),
           3 IOUTL            FIXED DEC(7,3),
           3 IOTIM            FIXED DEC(5,2),
           3 JCLAS            CHAR(1),
           3 ABEND            CHAR(3),
           3 ISAM             FIXED DEC(7,3),
           3 FLAGS  UNALIGNED,
              5 IPL           BIT(1),
              5 UNUSED        BIT(7),
           3 RADDR            BIT(24),
           3 FILLR            CHAR(8);
      ON ERROR BEGIN;
          ON ERROR SYSTEM;
          PUT FILE(LIST) SKIP LIST('  DATA ERROR *******************');
          GO TO READ;
               END;
      ON ENDPAGE(LIST) BEGIN;
          PUT FILE(LIST) EDIT (
 'ID  JDATS  PSUDO    PROJ    JDAT2  JTIMS  JNUM SNUM  JNAME    SNAME
 JPRIO  JCODE  SPGM    JUSER   SDATS   STIMS     STIME' ,
     'REGNR HIWTR  CPUTM   MCHUN     COST  TAPEU DISKU  COMPU    OCPYU
   IOUNT    CORUT    IOUTL  IOTIM  JCLAS ABEND   ISAM'  ,
                                        'IPL    RADDR' )
                   ( PAGE, A, SKIP(2), COL(5), A, SKIP(2), COL(109),
                     A );
          PUT SKIP(2) FILE(LIST);
                           END;
      ON ENDFILE(ACTAPE) GO TO XIT;
          OPEN FILE(LIST) LINESIZE(132);
          SIGNAL ENDPAGE(LIST);
   READ:  READ FILE(ACTAPE) SET(RECPTR);
          INCOUNT = INCOUNT + 1;
          IF ACTREC = PREREC THEN
              DO;
                   DUPCOUNT = DUPCOUNT + 1;
                   PUT FILE(LIST) SKIP LIST('   DUPLICATE **********');
          PUT FILE(LIST) EDIT ( ACT, (ACT.RADDR / 1024), 'K' )
                   ( SKIP(2),
                     A(3), X(1), A(5), X(2), A(4), X(2),
                     A(10), X(1), A(5), X(1), A(6), X(2),
                     A(3), X(3), A(2), X(2), A(8), X(1),
                     A(8), X(2), A(2), X(5), A(2), X(3),
                     A(8), X(1), A(6), X(2), P'99999', X(2),
                     P'ZZZZZV.99', X(2),
                     P'ZZZZZV.99',
                     SKIP(1), X(4),
                     P'ZZZZ9', X(1),
                     P'ZZZZ9', X(2),
                     P'ZZZV.99', X(1),
                     P'ZZZZV.999', X(1),
                     P'ZZZZZV.99', X(1),
                     P'ZZ9', X(3),
                     P'ZZ9', X(2),
                     P'ZZZZV.999', X(1),
                     P'ZZZZV.999', X(1),
                     P'ZZZZV.999', X(1),
                     P'ZZZZV.999', X(1),
                     P'ZZZZV.999', X(1),
                     P'ZZZV.99',   X(2),
                     A(1),         X(4),
                     A(3),         X(4),
                     P'ZZZZV.999',
                    SKIP(1), COL(112),
                     B(1),  B(0), B(0),
                     A(0), X(5), P'ZZZ9', A(1)  );
                   GO TO READ;
              END;
          PREREC = ACTREC ;
          WRITE FILE(NEWTAPE) FROM(ACT);
          OUTCOUNT = OUTCOUNT + 1;
          GO TO READ;
   XIT:
          PUT FILE(LIST) SKIP(3) DATA( DUPCOUNT);
          PUT SKIP FILE(LIST) DATA ( OUTCOUNT );
          PUT SKIP FILE(LIST) DATA( INCOUNT );
 END LIST160;
