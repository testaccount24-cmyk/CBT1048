UT475    CSECT
         SPACE 1
***********************************************************************
*                                                                     *
*        PROGRAM ID :              UT475                              *
*        ALIAS :                   CALENDAR                           *
*        DATE WRITTEN :            DECEMBER 1973                      *
*        LOCATION :                EUROPEAN COMPUTER CENTER           *
*        AUTHOR :                  J.NOLS                             *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
* ******************************************************************* *
* * THIS PROGRAM IS VALID ONLY FOR A YEAR IN THE TWENTIETH CENTURY  * *
* ******************************************************************* *
*                                                                     *
***********************************************************************
         SPACE 1
CALENDAR EQU   *                   ENTRY POINT
         ENTRY CALENDAR                 FOR ALIAS NAME
         EJECT
***********************************************************************
*        REGISTERS EQUATES  /  HOUSEKEEPING                           *
***********************************************************************
         SPACE 1
         PGMIN EQUATES=YES,USING=(R13,R12,R11) ESTABLISH ADDRESSABIL.
         SPACE 1
         SAVEPARM                  ENSURE PARM PROCESSING
         SPACE 1
         PRINT NOGEN               SUPPRESS MACRO EXPANSION
         SPACE 1
         L     R2,0(,R1)           SAVE ADDRESS PARM OF EXEC CARD
         SPACE 1
         OPEN  (PRINT,(OUTPUT))    OPEN PRINT FILE
         EJECT
***********************************************************************
*        TESTS PARAMETER(S) FROM EXEC CARD                            *
***********************************************************************
         SPACE 1
         LH    R4,0(,R2)           LOAD INTO REG4 LENGTH OF PARM
         CLI   2(R2),C'J'
         BNE   NOJ
         MVI   YESJ+1,X'F0'
         BCTR  R4,R0
         LA    R2,1(R2)
NOJ      MVI   LANGUAGE+6,BLANK
         CH    R4,=H'2'
         BE    NEXT1
         CH    R4,=H'4'
         BE    NEXT2
         CH    R4,=H'8'
         BE    NEXT3
         CH    R4,=H'9'
         BE    NEXT4
         CH    R4,=H'10'
         BE    NEXT5
         CH    R4,=H'11'
         BE    NEXT6
         B     ERROR
NEXT1    MVC   LANGUAGE,=C'ENGLISH'
         MVC   LASTYY,2(R2)
         B     NEXT7
NEXT2    MVC   LANGUAGE,=C'ENGLISH'
         MVC   LASTYY,4(R2)
         B     NEXT7
NEXT3    MVC   LANGUAGE(6),4(R2)
         MVC   LASTYY,2(R2)
         B     NEXT7
NEXT4    MVC   LANGUAGE,4(R2)
         MVC   LASTYY,2(R2)
         B     NEXT7
NEXT5    MVC   LANGUAGE(6),6(R2)
         MVC   LASTYY,4(R2)
         B     NEXT7
NEXT6    MVC   LANGUAGE,6(R2)
         MVC   LASTYY,4(R2)
         B     NEXT7
NEXT7    MVC   FIRSTYY,2(R2)
         MVC   CONST1,=C'WEEK   '
         CLC   LANGUAGE,=C'FRENCH '
         BNE   NEXT8
         MVC   CONST1,=C'SEMAINE'
         B     NEXT9
NEXT8    CLC   LANGUAGE,=C'GERMAN '
         BNE   NEXT9
         MVC   CONST1,=C'WOCHE  '
         EJECT
NEXT9    PACK  FYP,FIRSTYY
         PACK  LYP,LASTYY
         LA    R4,LINE3+65
         LA    R5,LINE7+10
         LA    R6,LINE9+13
         LA    R7,LINE11+14
         LA    R9,LINE17+14
         STM   R4,R9,SAD
         MVI   OPCODE,C'6'
         B     FIRSTIME
NEXTIME  CP    FYP,LYP
         BE    ENDPGM
         BH    ERROR
         L     R5,=F'7539'
         L     R3,=F'0'
         LA    R4,LINE6+1
         LA    R2,LINE6
         MVCL  R4,R2
         AP    FYP,ONE
         UNPK  DECIMAL(3),FYP
         OI    DECIMAL+2,SIGN
         MVC   FIRSTYY,DECIMAL+1
         LM    R4,R9,SAD
FIRSTIME MVC   0(1,R4),FIRSTYY
         MVC   2(1,R4),FIRSTYY+1
         MVC   YYDDD(2),FIRSTYY
         MVC   YYDDD+2(3),=C'001'
         MVC   LINE9+2(7),CONST1
         MVC   LINE19+2(7),CONST1
         MVC   LINE29+2(7),CONST1
         MVC   LINE39+2(7),CONST1
         MVC   LINE49+2(7),CONST1
         MVC   LINE59+2(7),CONST1
         ZAP   DECIMAL,FYP
         CVB   R4,DECIMAL
         STC   R4,DECIMAL
         TM    DECIMAL,3
         BNZ   NOLEAPYY
         MVC   MAXDAYYY,=C'366'
         B     LOOP
NOLEAPYY MVC   MAXDAYYY,=C'365'
LOOP     BAL   R3,EX458
         SR    R10,R10
         CLC   DAY,=C'01'
         BNE   NEXT16
         LR    R8,R6
         MVC   SADAYN,DAYNUM
         B     NEXT17
NEXT16   CLI   DAYNUM,C'0'
         BNE   NEXT17
         LA    R8,1300(R8)
NEXT17   CLC   SADAYN,DAYNUM
         BNH   NEXT18
         LA    R10,1300
NEXT18   PACK  DECIMAL,MONTH
         CVB   R4,DECIMAL
         SH    R4,=H'1'
         MH    R4,=H'10'
         LR    R2,R4
         AR    R4,R5
         MVC   0(9,R4),MONTHNAM
         PACK  DECIMAL,DAYNUM
         CVB   R4,DECIMAL
         MH    R4,=H'130'
         AR    R4,R7
         PACK  DECIMAL,DAY
NEXT10   CP    DECIMAL,=P'7'
         BH    NEXT11
         CR    R4,R9
         BNH   NEXT12
         LA    R4,1300(R4)
         B     NEXT12
NEXT11   LA    R10,1300(R10)
         SP    DECIMAL,=P'7'
         B     NEXT10
NEXT12   AR    R4,R10
         S     R4,=F'12'
         MVC   0(9,R4),DAYNAME
         A     R4,=F'12'
         AR    R4,R2
YESJ     NOP   JLDATE
         MVC   0(2,R4),DAY
         CLI   0(R4),C'0'
         BNE   NEXT19
         MVI   0(R4),BLANK
NEXT19   CLC   MONTH,=C'01'
         BNE   NEXT13
         B     NEXT14
NEXT13   S     R4,=F'3'
         MVC   0(1,R4),DAYNAME
         B     NEXT14
JLDATE   MVC   1(3,R4),YYDDD+2
         CLI   1(R4),C'0'
         BNE   NEXT21
         MVI   1(R4),BLANK
         CLI   2(R4),C'0'
         BNE   NEXT21
         MVI   2(R4),BLANK
NEXT21   S     R4,=F'2'
         MVC   0(2,R4),DAY
         CLI   0(R4),C'0'
         BNE   NEXT14
         MVI   0(R4),BLANK
NEXT14   AR    R2,R8
         MVC   0(2,R2),WEEK
         MVI   2(R2),C'-'
         PACK  DECIMAL,YYDDD
         S     R2,=F'1'
         MVI   0(R2),C'-'
         AP    DECIMAL,ONE
         UNPK  YYDDD,DECIMAL+5(3)
         OI    YYDDD+4,SIGN
         CLC   YYDDD+2(3),MAXDAYYY
         BH    OUTPUT
         B     LOOP
         EJECT
***********************************************************************
*        ERROR ( FORCE THE END OF PROGRAM )                           *
***********************************************************************
         SPACE 1
ERROR    MVC   LINE33+40(32),=C'BAD PARAMETER FROM EXEC CARD    '
         MVC   FYP,=X'000C'
         MVC   LYP,=X'000C'
         SPACE 1
***********************************************************************
*        PRINT ROUTINE                                                *
***********************************************************************
         SPACE 1
OUTPUT   LA    R2,64
         LA    R4,LINE1
LOOPRINT MVC   ZPRINT+1(130),0(R4)
         MVI   ZPRINT+1,C'*'
         MVI   ZPRINT+130,C'*'
         MVI   ZPRINT,BLANK
         CH    R2,=H'64'
         BNE   NEXT15
         MVI   ZPRINT,C'1'
NEXT15   PUT   PRINT,ZPRINT
         LA    R4,130(R4)
         BCT   R2,LOOPRINT
         B     NEXTIME
         SPACE 1
***********************************************************************
*        EXECUTE UT458                                                *
***********************************************************************
         SPACE 1
EX458    LA    R1,ADDRESS
         MVC   REPORT(7),LANGUAGE
         CALL  UT458
         CLI   RTCODE,C'0'
         BE    NEXT20
         MVC   LINE32+40(32),=C'RETURN CODE OF UT458 IS NOT ZERO'
         B     ERROR
NEXT20   BR    R3
         SPACE 1
***********************************************************************
*        END OF PROGRAM                                               *
***********************************************************************
         SPACE 1
ENDPGM   CLOSE (PRINT)
         FREEPOOL PRINT
         PGMOUT ,
         EJECT
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FA,LRECL=133,   *
               BLKSIZE=133
         SPACE 1
***********************************************************************
*        INPUT / OUTPUT PARAMETERS FOR UT458                          *
***********************************************************************
         SPACE 1
DATE     DS    0CL86
OPCODE   DS    C
MONTH    DS    CL2
DAY      DS    CL2
YEAR     DS    CL2
         DS    CL17
YYWWD    DS    0CL5
         DS    CL2
WEEK     DS    CL2
DAYNUM   DS    CL1
YYDDD    DS    CL5
REPORT   DS    CL9
         DS    CL9
MONTHNAM DS    CL9
         DS    CL9
DAYNAME  DS    CL9
         DS    CL6
RTCODE   DS    C
         SPACE 1
ADDRESS  DC    A(DATE)
         SPACE 1
***********************************************************************
*        WORK AREAS                                                   *
***********************************************************************
         SPACE 1
DECIMAL  DS    D
SAD      DS    6F
ZPRINT   DC    CL133' '
LANGUAGE DS    CL7
CONST1   DS    CL7
FIRSTYY  DS    CL2
LASTYY   DS    CL2
MAXDAYYY DS    CL3
SADAYN   DS    C
FYP      DS    PL2
LYP      DS    PL2
         EJECT
***********************************************************************
*        DEFINITION FOR EACH LINE OF CALENDAR                         *
***********************************************************************
         SPACE 1
LINE1    DC    130C'*'
LINE2    DS    0CL130
         DC    CL58' '
         DC    C'*'
         DC    CL11' '
         DC    C'*'
         DC    CL59' '
LINE3    DS    0CL130
         DC    CL58' '
         DC    CL13'*  1 9 Y Y  *'
         DC    CL59' '
LINE4    DS    0CL130
         DC    CL58' '
         DC    C'*'
         DC    CL11' '
         DC    C'*'
         DC    CL59' '
LINE5    DC    130C'*'
LINE6    DC    CL130' '
LINE7    DC    CL130' '
LINE8    DC    CL130' '
LINE9    DC    CL130' '
LINE10   DC    CL130' '
LINE11   DC    CL130' '
LINE12   DC    CL130' '
LINE13   DC    CL130' '
LINE14   DC    CL130' '
LINE15   DC    CL130' '
LINE16   DC    CL130' '
LINE17   DC    CL130' '
LINE18   DC    CL130' '
LINE19   DC    CL130' '
LINE20   DC    CL130' '
LINE21   DC    CL130' '
LINE22   DC    CL130' '
LINE23   DC    CL130' '
LINE24   DC    CL130' '
LINE25   DC    CL130' '
LINE26   DC    CL130' '
LINE27   DC    CL130' '
LINE28   DC    CL130' '
LINE29   DC    CL130' '
LINE30   DC    CL130' '
LINE31   DC    CL130' '
LINE32   DC    CL130' '
LINE33   DC    CL130' '
LINE34   DC    CL130' '
LINE35   DC    CL130' '
LINE36   DC    CL130' '
LINE37   DC    CL130' '
LINE38   DC    CL130' '
LINE39   DC    CL130' '
LINE40   DC    CL130' '
LINE41   DC    CL130' '
LINE42   DC    CL130' '
LINE43   DC    CL130' '
LINE44   DC    CL130' '
LINE45   DC    CL130' '
LINE46   DC    CL130' '
LINE47   DC    CL130' '
LINE48   DC    CL130' '
LINE49   DC    CL130' '
LINE50   DC    CL130' '
LINE51   DC    CL130' '
LINE52   DC    CL130' '
LINE53   DC    CL130' '
LINE54   DC    CL130' '
LINE55   DC    CL130' '
LINE56   DC    CL130' '
LINE57   DC    CL130' '
LINE58   DC    CL130' '
LINE59   DC    CL130' '
LINE60   DC    CL130' '
LINE61   DC    CL130' '
LINE62   DC    CL130' '
LINE63   DC    CL130' '
LINE64   DC    130C'*'
         EJECT
***********************************************************************
*        CONSTANTS , EQUATES AND LITTERAL POOL                        *
***********************************************************************
         SPACE 1
ONE      DC    P'1'
SIGN     EQU   X'F0'
BLANK    EQU   X'40'
         END
