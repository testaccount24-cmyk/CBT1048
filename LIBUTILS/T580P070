 ACCTCPY..
        PROCEDURE OPTIONS(MAIN),.

 /* DECLARATIONS */

           DCL ACCTAP1 FILE RECORD SEQUENTIAL,.
           DCL ACCTAP2 FILE RECORD SEQUENTIAL,.
           DCL (NEWTAP1,NEWTAP2) FILE RECORD SEQUENTIAL,.
           DCL DSKFILE FILE RECORD SEQUENTIAL,.
           DCL T58RATE FILE RECORD SEQUENTIAL,.
           DCL 01 RATE_CARD,
               03 RATE_FIL1 CHAR(6),
               03 RATE_DATE CHAR(5),
               03 RATE_FIL2 CHAR(69),.
           DCL REQDATE CHAR(5),.
           DCL IHESARC ENTRY(BINARY FIXED(31,0)),.
           DCL ACCTREC CHAR (160),.

           /* LABEL VARIABLE DECLARATION */
           DCL     CLOSE LABEL,.
           DCL     WRITDSK LABEL,.

           /* INITIALIZE LABEL VARIABLE */
           CLOSE = UNLOAD,.
           WRITDSK = WRITE1,.

 /* BEGIN EXECUTION */
           ON ENDFILE (ACCTAP1) GO TO CLOSE,.

           /* READ CONTROL CARD */
           ON ENDFILE(T58RATE) GO TO CTLERR1,.
           OPEN FILE(T58RATE) INPUT,.
           READ FILE(T58RATE) INTO(RATE_CARD),.
           REQDATE = RATE_DATE,.
           CLOSE FILE(T58RATE),.
           PUT EDIT ('CONTROL CARD - ',RATE_CARD) (PAGE,A(15),A(80)),.
           /* CHECK FOR NUMERICS IN DATE FIELD. */
           DO I = 1 TO 5,.
             IF SUBSTR (REQDATE,I,1) LT '0' OR SUBSTR (REQDATE,I,1)
             GT '9' THEN GO TO CTLERR2,.
           END,.
           /* CHECK FOR A YEAR BETWEEN 69 AND 99. */
           IF SUBSTR (REQDATE,1,2) LT '69' OR  SUBSTR (REQDATE,1,2)
           GT '99' THEN GO TO CTLERR2,.

 DISPLAY ('IF MACHINE FAILS DO NOT ALLOW THIS JOB TO RESTART.'),.
           PUT EDIT ('TO RECOVER RERUN JOB USING ACCTAP2 AS ACCTAP1',
           'AND VICE VERSA.')
           (SKIP(2),A(45),X(1),A(15)),.
           /* OPEN FILES EXPLICITLY */
           OPEN FILE (ACCTAP1) INPUT,.
           OPEN FILE (NEWTAP1) OUTPUT, FILE (NEWTAP2) OUTPUT,.
 READ1..   READ FILE (ACCTAP1) INTO (ACCTREC),.
           IF SUBSTR (ACCTREC,4,2) = '99' THEN GO TO WRITNEW,.
           IF SUBSTR (ACCTREC,4,5) GT REQDATE THEN GO TO WRITDSK,.
 WRITNEW.. WRITE FILE (NEWTAP1) FROM (ACCTREC),.
           WRITE FILE (NEWTAP2) FROM (ACCTREC),.
           GO TO READ1,.

           /* THE FOLLOWING CODE IS EXECUTED IF END OF FILE IS REACHED
              ON THE INPUT TAPE BEFORE A RECORD IS FOUND WITH A DATE
              GREATER THAN THE ONE ON THE CONTROL CARD.              */
 UNLOAD..  CLOSE FILE (NEWTAP1), FILE (NEWTAP2), FILE (ACCTAP1),.
           OPEN FILE (ACCTAP1) OUTPUT, FILE (ACCTAP2) OUTPUT,.
           CLOSE FILE (ACCTAP1), FILE (ACCTAP2),.
 RET..     DISPLAY ('GOOD END OF PROGRAM'),.
           PUT EDIT ('GOOD END OF B1769')
           (SKIP(2),A(17)),.
           RETURN,.   /* END OF TASK. RETURN TO SCHEDULER. */

           /* THE FOLLOWING CODE WRITES ONTO A DISK FILE THE RECORDS
              WHICH ARE NOT WRITTEN ON THE NEW TAPES.                */
 WRITE1..  OPEN FILE (DSKFILE) OUTPUT,.
           WRITDSK = WRITE2,.
           CLOSE = STRTDSK,.
 WRITE2..  WRITE FILE (DSKFILE) FROM (ACCTREC),.
           GO TO READ1,.

           /* THE FOLLOWING CODE IS EXECUTED WHEN THE LAST RECORD ON TH
              INPUT TAPE HAS BEEN PROCESSED. IT COPIES THE DISK FILE
              BACK ONTO THE BEGINNING OF THE INPUT TAPE.*/
 STRTDSK.. CLOSE FILE (NEWTAP1), FILE (NEWTAP2),.
           CLOSE FILE (ACCTAP1), FILE (DSKFILE),.
           OPEN FILE (ACCTAP1) OUTPUT, FILE (DSKFILE) INPUT,.
           ON ENDFILE (DSKFILE) GO TO TAPE2,.
 READSK..  READ FILE (DSKFILE) INTO (ACCTREC),.
           WRITE FILE (ACCTAP1) FROM (ACCTREC),.
           GO TO READSK,.
           /* THE FOLLOWING CODE COPIES THE RECORDS WHICH HAVE BEEN
           /* COPIED ONTO THE BEGINNING OF ACCTAP1 ONTO ACCTAP2.     */
 TAPE2..   CLOSE FILE (DSKFILE), FILE (ACCTAP1),.
           OPEN FILE (ACCTAP1) INPUT, FILE (ACCTAP2) OUTPUT,.
           PUT EDIT ('ACCTAP2 HAS BEEN OPENED FOR OUTPUT')
           (SKIP(2),A(34)),.
           PUT EDIT ('NEWTAP1 AND NEWTAP2 ARE GOOD.',
           'IF GOOD END OF B1769 MESSAGE DOES NOT FOLLOW',
           'RECOVER BY GENERING ACCTAP1 TO ACCTAP2.')
           (SKIP,A(29),X(1),A(44),X(1),A(39)),.
           ON ENDFILE (ACCTAP1) GO TO FINIS,.
 READ2..   READ FILE (ACCTAP1) INTO (ACCTREC),.
           WRITE FILE (ACCTAP2) FROM (ACCTREC),.
           GO TO READ2,.
 FINIS..   CLOSE FILE (ACCTAP1), FILE (ACCTAP2),.
           GO TO RET,.       /* RETURN TO SCHEDULER. */

 CTLERR1.. PUT EDIT ('CONTROL CARD ERROR. ',
           'CONTROL CARD IS MISSING.')(SKIP(2),A(20),A(24)),.
           CALL IHESARC(16),.
           GO TO RET,.

 CTLERR2.. PUT EDIT ('CONTROL CARD ERROR. ',
           'INVALID DATA IN DATE FIELD.') (SKIP(2),A(20),A(27)),.
           CALL IHESARC(16),.
           GO TO RET,.
        END ACCTCPY,.
