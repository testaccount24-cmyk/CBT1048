T580P060 CSECT
         PRINT ON,NOGEN,NODATA
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         ST    13,SAVAREA+4
         LA    13,SAVAREA
         ENQ   (QNAME,RNAME,E,2,SYSTEM) PREVENT USE OF SYS1.ACCT
         OPEN  (SYSACTDB,,ACTAP1DB,(OUTPUT))
GETREC1  GET   SYSACTDB            READ SYS1.ACCT
         LR    0,1
         PUT   ACTAP1DB,(0)        WRITE ACTNG. REC TO TAPE
         B     GETREC1
*
* END OF DATA ROUTINE FOR SYS1.ACCT
*
NOMO     BC    0,ALLTHRU           NOP ON FIRST PASS
         OI    NOMO+1,X'F0'        SET NOP TO UNCOND.BRANCH
         CLOSE (ACTAP1DB)
         WTO   'GOOD END ON T580F070'
*
         OPEN  (ACTAP2DB,(OUTPUT))
         CLOSE (SYSACTDB)
         OPEN  (SYSACTDB)
*
GETREC2  GET   SYSACTDB            COPY ACTNG. RECS TO 2ND TAPE
         LR    0,1
         PUT   ACTAP2DB,(0)
         B     GETREC2
*
*
ALLTHRU  BC    0,EOF               NOP ON FIRST PASS
         OI    ALLTHRU+1,X'F0'     SET TO UNCONDITIONAL BRANCH
         CLOSE (ACTAP2DB)
         WTO   'GOOD END ON T580F080'
         CLOSE (SYSACTDB)
         OPEN  (SYSACTDB)
         OPEN  (T580F071,(OUTPUT))
GETREC3  GET   SYSACTDB
         LR    0,1
         PUT   T580F071,(0)
         B     GETREC3
EOF      CLOSE (T580F071)
         WTO   'GOOD END ON T580F071'
         CLOSE (SYSACTDB)
*
*
         OPEN  (SYSACTDB,(OUTPUT)) RESET EOF POINTER TO BEGINNING...
         CLOSE (SYSACTDB)          ... OF SYS1.ACCT
         OPEN  (SYSACT2,(OUTPUT))
         CLOSE SYSACT2
*
         LA    15,7                LOAD ACCOUNTING SVC ENTRANCE CODE
         SVC   254                 GO RESET BIT IEFWAD USES TO TEST...
*                                  .IF SYS1.ACCT IS OPEN
*
         WTO   'GOOD END OF PROGRAM'
         DEQ   (QNAME,RNAME,2,SYSTEM) RELEASE SYS1.ACCT FOR USE
*
         L     13,SAVAREA+4
         LM    14,12,12(13)
         RETURN ,RC=0
*
*
*
SYSACTDB DCB   DDNAME=SYS1ACCT,EODAD=NOMO,DEVD=DA,MACRF=(GL,PL),       X
               SYNAD=IOERROR,                                          *
               DSORG=PS,BUFNO=4
*
ACTAP1DB DCB   DDNAME=T580F070,DEVD=TA,MACRF=PM,DSORG=PS,BUFNO=4
*
ACTAP2DB DCB   DDNAME=T580F080,DEVD=TA,MACRF=PM,DSORG=PS,BUFNO=4
*
T580F071 DCB   DDNAME=T580F071,DEVD=DA,MACRF=PM,DSORG=PS,BUFNO=4
*
SYSACT2  DCB   DDNAME=SYS1ACT2,DEVD=DA,MACRF=(GL,PL),DSORG=PS,         *
               BUFNO=4,SYNAD=IOERROR
SAVAREA  DS    18F
QNAME    DC    CL8'SYSIEFAR'
RNAME    DC    CL2'WD'
*
*
IOERROR  SYNADAF ACSMETH=QSAM
         MVC   LINE12+12(78),50(1)
         SYNADRLS ,
         WTO   MF=(E,LINE11)
         RETURN
LINE11   DS    0F
         DC    AL2(WTOEND-*),H'0',C'T580P060 - '
LINE12   DS    0CL90
DC       DC    CL78' '
         DC    CL78' '
WTOEND   EQU   *
         END
