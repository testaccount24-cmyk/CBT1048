T581P04  CSECT
***********************************************************************
*        PROGRAM ID. : T581P04          ALIAS : X58103                *
*        DATE WRITTEN : FEBRUARY 1974   AUTHOR : J. NOLS              *
*        LOCATION : EUROPEAN COMPUTER CENTER                          *
*                                                                     *
*        CONVERTION BINARY TO PACKED FOR RPG PROGRAM NAMED T581P03    *
*                                                                     *
***********************************************************************
X58103   EQU   *                 ENTRY POINT
         ENTRY X58103                 FOR ALIAS NAME
         PRINT NOGEN             SUPPRESS MACRO EXPANSION
         PGMIN EQUATES=NO,USING=(3) ESTABLISH ADDRESSABILITY
         L     4,ADDRESS         ADDRESS OF PARAMETERS FROM RPG PROGRAM
         LH    5,0(4)            LOAD NUMBER OF FREE DSCB'S
         CVD   5,DECIMAL         CONVERT DECIMAL
         MVC   2(3,4),DECIMAL+5  SAVE RESULT
         LA    4,5(4)            ADDRESS FOR NEXT CONVERTION
         LH    5,0(4)            LOAD NUMBER OF TRACKS PER CYLINDER
         CVD   5,DECIMAL         CONVERT DECIMAL
         MVC   2(2,4),DECIMAL+6  SAVE RESULT
         LA    4,4(4)            ADDRESS FOR NEXT CONVERTION
         LH    8,=H'26'          NUMBER OF EXTENT AREA PER DSCB FORM. 5
         LA    6,130(4)          ADDRESS SAVE AREA FOR NUMBER OF CYL
         LA    7,182(4)          ADDRESS SAVE AREA FOR NUMBER OF TRK
LOOP     LH    5,2(4)            LOAD NUMBER OF FULL FREE CYLINDER
         CVD   5,DECIMAL         CONVERT DECIMAL
         MVC   0(2,6),DECIMAL+6  SAVE RESULT
         MVI   3(4),X'00'
         LH    5,3(4)            LOAD NUMBER OF FULL FREE TRACK
         CVD   5,DECIMAL         CONVERT DECIMAL
         MVC   0(2,7),DECIMAL+6  SAVE RESULT
         LA    4,5(4)
         LA    6,2(6)
         LA    7,2(7)
         BCT   8,LOOP
         PGMOUT ,                RETURN TO CALLER
         DS    0D
DECIMAL  DS    D
ADDRESS  DC    V(NOF0)
         END
