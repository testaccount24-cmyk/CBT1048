B0103B   START 0000
         ENTRY LISTISAM
LISTISAM SAVE  (14,12)
         BALR  12,0
         USING *,12
         USING *+4096,11
BEGIN    L     11,BASE2
         ST    13,SAVEBLK+4
         LA    10,SAVEBLK
         ST    10,8(,13)
         LR    13,10
         OPEN  (PRINTDCB,(OUTPUT))
         OPEN  (CARDDCB,(INPUT))
REENTER  MVI   LAST,X'00'
         LA    4,EOF
         ST    4,WORK
         MVC   ISAMDCB+33(3),WORK+1
         GET   CARDDCB    REG U POINTS TO IO AREA
         MVC   SEQ,0(1)
         CLI   SEQ,C'*'
         BE    SEQ00000
SEQ00001 EQU   *
         LR    5,1
         MVI   LASTSCAN,X'00'
         MVI   CNTRL,X'89'
         MVC   PRNTAREA(132),BLANKS
         BAL   14,PUT2
         MVI   CNTRL,X'09'
         MVC   PRNTAREA(37),HEADCNTL
         BAL   14,PUT2
         MVC   PRNTAREA(80),0(5)
         BAL   14,PUT2
         LR    1,5
         LH    2,H2
         AH    1,H1
FIRST    CLI   0(1),C' '
         BNE   TYPE
         AH    1,H1
         AH    2,H1
         CH    2,H72
         BNL   INVALID
         B     FIRST
TYPE     CLC   0(5,1),PRINT
         BNE   TRY2
        LR    8,1
         CLI   SEQ,C'*'
         BE    SEQ00002
         RDJFCB ISAMDCB
         OPEN  (ISAMDCB,(UPDAT))
SEQ00003 EQU   *
        LR    1,8
         AH    1,H5
         AH    2,H5
         CH    2,H72
         BNL   INVALID
         MVI   SCANSW,X'80'
         CLI   0(1),C' '
         BNE   INVALID
SCANA    AH    1,H1
         AH    2,H1
         CH    2,H72
         BNL   ENDSCAN
         CLI   0(1),C' '
         BE    SCANA
SCANKEY  CLC   0(4,1),KEY
         BNE   SCANSTOP
         AH    1,H3
         AH    2,H3
         CH    2,H72
         BNL   INVALID
         CLI   0(1),C'='
         BNE   INVALID
         AH    1,H1
         LR    7,1
         AH    2,H1
         CH    2,H72
         BNL     INVALID
         LR    3,1
SCANB    AH    1,H1
         AH    2,H1
         CH    2,H72
         BNL   INVALID
         CLI   0(1),C','
         BE    SCANC
         CLI   0(1),C' '
         BNE   SCANB
SCANC    LR    4,1
         SR    4,3
         SH    4,H1
         STC   4,SCAND+1
SCAND    MVC   KEYAREA,0(7)
         STC   4,KEYLEN     LENGTH OF KEY MINUS ONE
         SH    1,H1
        CLI   0(1),C'*'
         BNE   SCANE
         OI    SCANSW,X'08'
         AH    1,H1
         B     SCANH
SCANE    OI    SCANSW,X'04'
         AH    1,H1
         B     SCANH
SCANSTOP CLC   0(7,1),STOPAFT
         BNE   SCANTYPE
         OI    SCANSW,X'10'
         AH    1,H7
         AH    2,H7
         CH    2,H72
         BNL   INVALID
         CLI   0(1),C'='
         BNE   INVALID
         AH    1,H1
         AH    2,H1
         CH    2,H72
         BNL   INVALID
         LR    3,1
SCANF    AH    1,H1
         AH    2,H1
         CH    2,H72
         BNL   INVALID
         CLI   0(1),C','
         BE    SCANG
         CLI   0(1),C' '
         BNE   SCANF
SCANG    LR    4,1
         SR    4,3
         SH    4,H1
         STC   4,CHAR                  STOPAFT FLD LEN MINUS ONE
         NI    SCANGG+1,X'F0'
         OC    SCANGG+1(1),CHAR
SCANGG   PACK  RECNUM,0(1,3)
         OI    SCANSW,X'10'
         B     SCANH
SCANTYPE CLC   0(7,1),KEYTYPE
         BNE   INVALID
         AH    1,H7
         AH    2,H7
         CH    2,H72
         BNL   INVALID
         CLI   0(1),C'='
         BNE   INVALID
         AH    1,H1
         AH    2,H1
         CH    2,H72
         BNL   INVALID
         CLC   0(6,1),PACKED
         BNE   SCANGGG
         AH    1,H6
         AH    2,H6
         CH    2,H72
         BH    INVALID
         OI    SCANSW,X'02'
         B     SCANGGGG
SCANGGG  CLC   0(7,1),DISPLAY
         BNE   INVALID
         AH    1,H7
         AH    2,H7
         CH    2,H72
         BH    INVALID
         B     SCANGGGG
SCANGGGG CLI   0(1),C','
         BE    SCANJ
         CLI   0(1),C' '
         BE    SCANI
         BNE   INVALID
SCANH    OI    SCANSW,X'20'
         CLI   0(1),C' '
         BE    SCANI
SCANJ    AH    1,H1
         AH    2,H1
         CH    2,H72
         CLI   0(1),C' '
         BNE   SCANKEY
         LR    1,5
         AH    1,H71
         CLI   0(1),C' '
         BE    INVALID
         GET   CARDDCB
         MVI   CNTRL,X'09'
         MVC   PRNTAREA(80),0(1)
        LR    8,1
         BAL   14,PUT2
        LR    1,8
         LR    5,1
         AH    1,H15
         LH    2,H16
         CLI   0(1),C' '
         BE    INVALID
         B     SCANKEY
SCANI    LR    1,5
         AH    1,H71
         CLI   0(1),C' '
         BNE   INVALID
         B     ENDSCAN
INVALID  MVC   PRNTAREA(80),BLANKS
         MVC   PRNTAREA(42),ERMESS
         BAL   14,PUT2
         B     RESTORE
TRY2     CLC   BUILD(5),0(1)
         BNE   TRY3
         MVI   SCANSW,X'00'     0000 0000 FOR BUILD
         B     ENDSCAN
TRY3     CLC   UNLOAD(6),0(1)
         BNE   TRY4
         MVI   SCANSW,X'C0'     1100 0000 FOR UNLOAD
         B     UNLOADA
TRY4     CLC   INFO(4),0(1)
         BNE   INVALID
         MVI   SCANSW,X'40'    0100 0000 FOR INFO
         B     ENDSCAN
ENDSCAN  MVI   CNTRL,X'11'
         CLI   LASTSCAN,X'FF'
         BE    RESTORE
         MVI   LASTSCAN,X'FF'
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(12),EOFSYSIN
         BAL   14,PUT2
         MVI   CNTRL,X'09'
         TM    SCANSW,X'C0'
         BZ    LOADA
         BO    UNLOADB
         TM    SCANSW,X'80'
         BO    SETPARM
         BZ    INFOA
         DS    0F
BASE2    DC    AL4(BEGIN+4096)
HEADCNTL DC    CL37'CONTROL CARD LISTING FOR ISAM UTILITY'
EOFSYSIN DC    CL12'EOF ON SYSIN'
ERMESS   DC    CL42'INVALID CONTROL STATEMENT. JOB TERMINATED.'
KEYTYPE  DC    CL7'KEYTYPE'
PRINT    DC    CL5'PRINT'
DISPLAY  DC    CL7'DISPLAY'
PACKED   DC    CL6'PACKED'
STOPAFT  DC    CL7'STOPAFT'
KEY      DC    CL4'KEY='
INFO     DC    CL4'INFO'
UNLOAD   DC    CL6'UNLOAD'
BUILD    DC    CL5'BUILD'
KEYLEN   DS    CL1
KEYAREA  DS    CL40
CHAR     DS    CL1
RECNUM   DS    D
SCANSW   DC    XL1'00'
H3       DC    H'3'
H5       DC    H'5'
H6       DC    H'6'
H7       DC    H'7'
H15      DC    H'15'
H16      DC    H'16'
H71      DC    H'71'
H72      DC    H'72'
SETPARM  TM    SCANSW,X'0C'
         BZ    BEGINING
         LA    1,KEYAREA
         SR    2,2
         IC    2,KEYLEN
         AR    2,1
PARMA   CLI   0(2),C'*'
         BNE   PARMB
         MVI     0(2),X'00'
         SH    2,H1
         CR    1,2
         BNH   PARMA
PARMB    SR    2,1
         STC   2,GENLEN
         TM    SCANSW,X'02'
         BZ    PARMC    KEY IS NOT TO BE PACKED
         SR    3,3
        IC    3,KEYLEN
        SR    3,2
         SR    2,2
         LH    5,H2
        DR    2,5
        CH    2,H0
        BE    PARMBB
         MVI   XSWTCH,C'O'
PARMBB   BCR   0,0
         IC    2,KEYLEN     SENDING FIELD LEN FOR PACK
         IC    3,ISAMDCB+16
         SH    3,H1       RECEIVING FIELD LEN FOR PACK
         STC   3,PARMDD+1
         NI    PARMD+1,X'00'
         SLL   3,4
         STC   3,CHAR
         OC    PARMD+1(1),CHAR
         STC   2,CHAR
         OC    PARMD+1(1),CHAR
PARMD    PACK  PACKAREA(1),KEYAREA(2)
PARMDD   MVC   KEYAREA(1),PACKAREA
         LA    2,KEYAREA
         SR    3,3
         IC    3,ISAMDCB+16     DCB  KEYLENGTH
         AR    3,2
NEWGENLE SH    3,H1
         CLI   0(3),X'00'
         BNE   PARMCC
         B     NEWGENLE
PARMCC   SR    3,2
         STC   3,GENLEN
PARMC    TM    SCANSW,X'08'
         BZ    PARME          NOT A GENERIC SETL
         SETL  ISAMDCB,KC,KEYAREA
         B     PARMF
PARME    SETL  ISAMDCB,K,KEYAREA
PARMF    B     START
GENLEN   DS    CL1
XSWTCH  DC    CL1'E'
PACKAREA DS    CL16
BEGINING BCR   0,0
         CLI   SEQ,C'*'
         BE    SEQ00004
         SETL  ISAMDCB,B
START    SR    8,8
         LH    9,ISAMDCB+82
         CLI   SEQ,C'F'
         BE    SEQ00005
         BCTR  9,0
SEQ00005 EQU   *
         LH    10,H50
         DR    8,10
         MH    9,H2
         CH    8,H0
         BE    NOTZERO
         AH    9,H2
NOTZERO  AH    9,H1
         CH    9,H48
         BNH   LESS60
         MVI   ONE,X'0C'
         MVI   TWO,X'0C'
         B     DUMMY
LESS60   LH    7,H48
         SR    6,6
         DR    6,9
         SR    6,6
         MR    6,9
         AH    7,H4
         CVD   7,DOUBLE
         ZAP   SIXTY,DOUBLE
DUMMY    BCR   0,0
         MVI   CNTRL,X'89'
         MVC   CHARPRNT(132),BLANKS
         MVC   PRNTAREA(132),BLANKS
         BAL   14,PUT2
         MVI   CNTRL,X'09'
         MVC   PRNTAREA(32),STMESS
         MVC   PRNTAREA+52(44),JFCB
         MVC   PRNTAREA+117(8),PAGENO
         BAL   14,PUT2
         MVC   PRNTAREA(132),FORMAT1
         BAL   14,PUT2
         MVC   PRNTAREA(132),FORMAT2
         MVI   CNTRL,X'11'
         BAL   14,PUT2
         MVI   CNTRL,X'09'
         AP    LINECNT,FOUR
         CLI   SEQ,C'*'
         BE    SEQ00006
         LH  8,ISAMDCB+82
         CLI   SEQ,C'F'
         BE    SEQ00007
         BCTR  8,0
SEQ00007 EQU   *
         CVD   8,LRECL
         CP    LRECL,ENBYTE
         BNL   DUMMYA
         ZAP   ENBYTE,LRECL
         UNPK  BYTECNT+5(4),ENBYTE
         OI    BYTECNT+8,X'F0'
DUMMYA   BCR   0,0
         MVC   PRNTAREA+103(6),RECORD
         MVC   PRNTAREA+110(6),RECCNT
         PACK  RECWK(4),PRNTAREA+110(6)
         MVC   PRNTAREA+117(5),BYTES
         MVC   PRNTAREA+123(9),BYTECNT
         LA    7,TABLE
         LH    9,H50
         LH    10,H1
NEWGET   BCR   0,0
         CLI   SEQ,C'*'
         BE     SEQ00008
         GET   ISAMDCB
         CLI   SEQ,C'F'
         BE    SEQ00009
         LA    1,1(1)
SEQ00009 EQU   *
         TM    SCANSW,X'08'
         BZ    AB
         IC    3,GENLEN
         LH    4,ISAMDCB+60
         CLI   SEQ,C'F'
         BE    SEQ00014
         BCTR  4,0
SEQ00014 EQU   *
         AR    4,1
        TM    SCANSW,X'02'    IS KEY PACKED
         BO    ACC
         STC   3,AA+1
AA       CLC   KEYAREA,0(4)
         BNE   ENDREQ
        B     AB
ACC     IC    5,ISAMDCB+16
        SH    5,H1
        STC   5,AD+1
AD      MVC   PACKAREA,0(4)
        STC   3,AE+1
        CLI   XSWTCH,C'O'
        BE    AE
        LA    4,PACKAREA
        AR    4,3
        NI    0(4),X'F0'
AE      CLC   PACKAREA,KEYAREA
        BNE   ENDREQ
AB       LR    2,1
         SR    3,3
NEWLINE  SR    4,4
         LA    5,PRNTAREA
         MVC   PRNTAREA(100),BLANKS
NEWCHAR  SR    6,6
         IC    6,000(2)
         MH    6,H2
         AR    6,7
         MVC   000(2,5),000(6)
         ST    7,SAVE7
         ST    5,SAVE5
         LA    7,CHARTABL
         AH    5,H132
         SR    6,6
         IC    6,000(2)
         AR    6,7
         MVC   000(1,5),000(6)
         L     7,SAVE7
         L     5,SAVE5
         AH    5,H2
         AR    2,10
         AR    3,10
         AR    4,10
GETCHK   CR    3,8
         BL    PUTCHK
         ST    5,SAVE5
         AH    5,H132
         MVI   000(5),C'*'
         MVI   CNTRL,X'09'
         BAL   14,PUT3
         L     5,SAVE5
         MVC   CHARPRNT(132),BLANKS
         MVI   000(5),C'*'
         MVI   CNTRL,X'11'
         BAL   14,PUT2
         TM    SCANSW,X'10'
         BZ    AC
         CP    RECWK,RECNUM
         BE    ENDREQ
AC       AP    LINECNT,THREE
         MVC   PRNTAREA+103(6),RECORD
         AP    RECWK,ONEA
         UNPK  PRNTAREA+110(6),RECWK
         MVC   PRNTAREA+117(5),BYTES
         MVC   PRNTAREA+123(9),BYTECNT
         OI    PRNTAREA+115,X'F0'
         CP    LINECNT,SIXTY
         BL    AROUND
         ZAP   LINECNT,ZERO
         MVI   CNTRL,X'89'
         MVC   PRNTAREA(132),BLANKS
         BAL   14,PUT2
         MVI   CNTRL,X'09'
         AP    PAGECNT,ONEA
         UNPK  PAGENO+5(3),PAGECNT
         OI    PAGENO+7,X'F0'
         CLI   SEQ,C'*'
         BE    SEQ00010
         MVC   PRNTAREA(32),STMESS
SEQ00011 EQU   *
         MVC   PRNTAREA+52(44),JFCB
         MVC   PRNTAREA+117(8),PAGENO
         BAL   14,PUT2
         MVC   PRNTAREA(132),FORMAT1
         BAL   14,PUT2
         MVI   CNTRL,X'11'
         MVC   PRNTAREA(132),FORMAT2
         BAL   14,PUT2
         MVI   CNTRL,X'09'
         MVC   PRNTAREA+103(6),RECORD
         UNPK  PRNTAREA+110(6),RECWK
         OI    PRNTAREA+115,X'F0'
         MVC   PRNTAREA+117(5),BYTES
         MVC   PRNTAREA+123(9),BYTECNT
         AP    LINECNT,FOUR
AROUND   B     NEWGET
PUTCHK   CR    4,9
         BNE   NEWCHAR
         BAL   14,PUT3
         MVC   CHARPRNT,BLANKS
         MVI   CNTRL,X'09'
         BAL   14,PUT2
         MVC   PRNTAREA+103(13),BLANKS
         AP    LINECNT,TWO
         PACK  STBYTE(3),PRNTAREA+123(4)
         PACK  ENBYTE(3),PRNTAREA+128(4)
         AP    STBYTE,FIFTY
         AP    ENBYTE,FIFTY
         UNPK  PRNTAREA+123(4),STBYTE
         CP    LRECL,ENBYTE
         BH    OLDVAL
         ZAP   ENBYTE,LRECL
OLDVAL   UNPK  PRNTAREA+128(4),ENBYTE
         OI    PRNTAREA+126,X'F0'
         OI    PRNTAREA+131,X'F0'
         B     NEWLINE
ENDREQ   MVI   CNTRL,X'09'
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(25),REQMESS
         BAL   14,PUT2
         ESETL ISAMDCB
         CLOSE ISAMDCB
         B     RESTORE
EOF      MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(15),ENDMESS
         BAL   14,PUT2
         CLI   SEQ,C'*'
         BE    SEQ00012
         ESETL ISAMDCB
         CLOSE ISAMDCB
         B     RESTORE
PUT3     LA    0,CHARPRNT-1
         B     PUT1
PUT2     LA    0,PRNTAREA-1
PUT1     LA    1,PRINTDCB
         L     15,48(0,1)
         BR    15
H0       DC    H'0'
H1       DC    H'1'
H2       DC    H'2'
H4       DC    H'4'
H48      DC    H'48'
H50      DC    H'50'
H65      DC    H'65'
H132     DC    H'133'
LRECL    DS    D
DOUBLE   DS    D
PAGECNT  DC    PL2'1'
LINECNT  DC    PL2'0'
ZERO     DC    PL1'0'
ONE      DC    PL1'1'
ONEA     DC    PL1'1'
TWO      DC    PL1'2'
THREE    DC    PL1'3'
FOUR     DC    PL1'4'
FIFTY    DC    PL2'50'
SIXTY    DC    PL2'60'
STBYTE   DS    PL3
ENBYTE   DC    PL3'50'
RECWK    DS    PL4
RECORD   DC    CL6'RECORD'
BYTES    DC    CL5'BYTES'
RECCNT   DC    CL6'000001'
BYTECNT DC     CL9'0001-0050'
REQMESS  DC    CL25'REQUESTED RECORDS WRITTEN'
ENDMESS  DC    CL15'END OF DATA SET'
STMESS   DC    CL32'INDEXED SEQUENTIAL PRINT UTILITY'
PAGENO   DC    CL8'PAGE 001'
BLANKS   DC    132CL1' '
CNTRL    DC    XL1'89'
PRNTAREA DS    CL132
         DC    XL1'09'
CHARPRNT DS    CL132
FORMAT1  DS    0CL132
         DC    CL50'                  1         1         2         2 '
         DC    CL50'        3         3         4         4         5 '
         DC    32CL1' '
FORMAT2  DS    0CL132
         DC    CL50'1 . . . 5 . . . . 0 . . . . 5 . . . . 0 . . . . 5 '
         DC    CL50'. . . . 0 . . . . 5 . . . . 0 . . . . 5 . . . . 0 '
         DC    32CL1' '
SAVE5    DS    F
SAVE7    DS    F
CHARTABL DS    0CL256
         DC    CL32'                                '
         DC    CL32'                                '
         DC    CL32'                                '
         DC    CL32'                                '
         DC    CL32'                                '
         DC    CL32'                                '
         DC    CL32' ABCDEFGHI       JKLMNOPQR      '
         DC    CL32'  STUVWXYZ      0123456789      '
TABLE    DS    0CL512
         DC    CL32'000102030405060708090A0B0C0D0E0F'
         DC    CL32'101112131415161718191A1B1C1D1E1F'
         DC    CL32'202122232425262728292A2B2C2D2E2F'
         DC    CL32'303132333435363738393A3B3C3D3E3F'
         DC    CL32'404142434445464748494A4B4C4D4E4F'
         DC    CL32'505152535455565758595A5B5C5D5E5F'
         DC    CL32'606162636465666768696A6B6C6D6E6F'
         DC    CL32'707172737475767778797A7B7C7D7E7F'
         DC    CL32'808182838485868788898A8B8C8D8E8F'
         DC    CL32'909192939495969798999A9B9C9D9E9F'
         DC    CL32'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    CL32'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    CL32'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    CL32'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    CL32'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    CL32'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
UNLOADA  BCR   0,0
         AH    1,H6
         AH    2,H6
         CH    2,H72
         BNL   INVALID
UNC      CLI   0(1),C' '
         BNE   INVALID
UND      AH    1,H1
         AH    2,H1
         CH    2,H72
         BE    ENDSCAN
         CLI   0(1),C' '
         BE    UND
         CLC   0(7,1),PURPOSE
         BNE   INVALID
         AH    1,H7
         AH    2,H7
         CH    2,H72
         BNL   INVALID
         CLI   0(1),C'='
         BNE   INVALID
         AH    1,H1
         AH    2,H1
         CH    2,H72
         BNL   INVALID
         CLC   000(6,1),BACKUP
         BNE   UNE
         OI    SCANSW,X'20'
         B     ENDSCAN
UNE      CLC   000(5,1),REORG
         BNE   INVALID
         B     ENDSCAN
UNLOADB  BCR   0,0
         RDJFCB ISAMDCB
         MVC   UNLDA(44),JFCB
         OPEN  (ISAMDCB,(UPDAT))
         MVC   QSAMDCB+40(8),SYSUT2
         RDJFCB QSAMDCB
         LA    4,UNLOADED       EODAD
         ST    4,WORK
         MVC   ISAMDCB+33(3),WORK+1
         CLI   SEQ,C'F'
         BNE   UNBB
         CLC   JFCB+102(2),NULLS    BLKSIZE
         BNE   UNA
         MVC   QSAMDCB+62(2),ISAMDCB+62
UNA      MVC   QSAMDCB+82(2),ISAMDCB+82         LRECL
         TM    JFCB+100,X'90'
         BC    7,UNBB
         MVC   QSAMDCB+36(1),ISAMDCB+36
UNBB     OPEN  (QSAMDCB,(OUTPUT))
         SETL  ISAMDCB,B
UNB      GET   ISAMDCB
         LH    2,ISAMDCB+60
         AR    2,1
         CLI   0(2),X'FF'
         BE    UNLOADED
         AP    COUNTER,ONEA
         CLI   SEQ,C'F'
         BE    SEQ00013
         LA    1,1(1)
SEQ00013 EQU   *
         LR    0,1
         PUT   QSAMDCB,(0)
         B     UNB
UNLOADED BCR   0,0
         ESETL ISAMDCB
         CLOSE   ISAMDCB
         CLOSE QSAMDCB
         MVI   CNTRL,X'11'
         MVC   PRNTAREA(132),BLANKS
         UNPK  CNTMESS+15(6),COUNTER
         OI    CNTMESS+20,X'F0'
         MVC   PRNTAREA(37),CNTMESS
         BAL   14,PUT2
*        MVI   CNTRL,X'09'
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(78),UNLDMESS
         BAL   14,PUT2
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(30),UNLDB
         BAL   14,PUT2
*      EXEIT TO AUDIT TRAIL UPDATE ROUTINE AT THIS POINT
         B     RESTORE
***********************************************************************
***********************************************************************
SEQ00000 MVI   HEADCNTL+25,C'Q'
         B     SEQ00001
SEQ00002 RDJFCB QSAMDCB
         MVC   QSAMDCB+33(3),WORK+1
         OPEN  (QSAMDCB,(INPUT))
         B     SEQ00003
SEQ00004 SR    8,8
         LH    9,QSAMDCB+82
         B     SEQ00005
SEQ00006 LH    8,QSAMDCB+82
         B     SEQ00007
SEQ00008 GET   QSAMDCB
         B     AB
SEQ00010 MVC   STMESS(7),BLANKS
         B     SEQ00011
SEQ00012 CLOSE QSAMDCB
         B     RESTORE
SEQ      DS    CL1
***********************************************************************
***********************************************************************
WORK     DS    F
SYSUT2   DC    CL8'SYSUT2'
NULLS    DC    F'0'
BACKUP   DC    CL6'BACKUP'
REORG    DC    CL5'REORG'
PURPOSE  DC    CL7'PURPOSE'
COUNTER  DC    PL5'0'
CNTMESS  DC    CL37'RECORD COUNT = 000000 LOGICAL RECORDS'
UNLDMESS DC    CL34'INDEXED SEQUENTIAL DATA SET NAMED '
UNLDA    DS    CL44
UNLDB    DC    CL30'HAS BEEN SUCCESSFULLY UNLOADED'
LOADA    BCR   0,0
         OPEN  (QSAMDCB,(INPUT))
         RDJFCB LOADDCB
         CLC   JFCB+100(2),NULLS     BLKSIZE
         BNE   LDA
         MVC   LOADDCB+62(2),QSAMDCB+62
LDA      MVC   LOADDCB+82(2),QSAMDCB+82   LRECL
         TM    JFCB+100,X'90'
         BC    7,LDB
         MVC   LOADDCB+36(1),QSAMDCB+36
LDB      OPEN (LOADDCB,(OUTPUT))
LDF      GET   QSAMDCB
         LH    2,LOADDCB+60   RKP
         AR    2,1
         CLI   0(2),X'FF'
         BE    LOADED
         AP    COUNTER,ONEA
         LR    0,1
         PUT   LOADDCB,(0)
         B     LDF
LOADED   CLOSE LOADDCB
         CLOSE QSAMDCB
         MVI   CNTRL,X'11'
         MVC   PRNTAREA(132),BLANKS
         UNPK  CNTMESS+15(6),COUNTER
         OI    CNTMESS+20,X'F0'
         MVC   PRNTAREA(37),CNTMESS
         BAL   14,PUT2
         MVI   CNTRL,X'09'
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(48),LOADMESS
         BAL   14,PUT2
         MVC   LOADMESB(44),JFCB
         MVC   PRNTAREA(57),LOADMESA
         BAL   14,PUT2
         B     RESTORE
LOADMESS DC    CL48'INDEXED SEQUENTIAL DATA SET SUCCESSFULLY CREATED'
LOADMESA DC    CL13'AND IS NAMED '
LOADMESB DC    CL44'                                            '
INFOA    OPEN  BISAMDCB
         LH    5,BISAMDCB+206   TRKS REMAINING IN IND OFLOW AREA
         CVD   5,UNPACK
         UNPK  INFOIND(3),UNPACK
         OI    INFOIND+2,X'F0'
         LH    5,BISAMDCB+224         CYL OFLOW AREAS FULL
         CVD   5,UNPACK
         UNPK  INFOCYL(2),UNPACK
         OI    INFOCYL+1,X'F0'
         LH    5,BISAMDCB+214      NO. OF OFLOW RECORES
         CVD   5,UNPACK
         UNPK  OFLOWREC(5),UNPACK
         OI    OFLOWREC+4,X'F0'
         L     5,BISAMDCB+156    PRIME DATA RECORDS
         CVD   5,UNPACK
         UNPK  PRIMEREC(6),UNPACK
         OI    PRIMEREC+5,X'F0'
*        MVI   CNTRL,X'09'
         MVI   CNTRL,X'11'
         RDJFCB BISAMDCB
         MVC   PRNTAREA(132),BLANKS
         MVC   REORGHD2(44),JFCB
         MVC   PRNTAREA(91),REORGHD1
         BAL   14,PUT2
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(44),PRIMEREC
         BAL   14,PUT2
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(40),OFLOWREC
         BAL   14,PUT2
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(46),INFOIND
         BAL   14,PUT2
         MVC   PRNTAREA(132),BLANKS
         MVC   PRNTAREA(35),INFOCYL
         BAL   14,PUT2
         CLOSE BISAMDCB
         B     RESTORE
REORGHD1 DC    CL47'** RECORD DISTRIBUTION STATISTICS FOR DATA SET '
REORGHD2 DS    CL44
PRIMEREC DC    CL44'       RECORDS RESIDE IN THE PRIME DATA AREA'
OFLOWREC DC    CL40'      RECORDS RESIDE IN AN OVERFLOW AREA'
INFOIND  DC    CL46'    TRACKS REMAIN IN INDEPENDENT OVERFLOW AREA'
INFOCYL  DC    CL35'   CYLINDER OVERFLOW AREAS ARE FULL'
UNPACK   DS    D
ERROR    MVC   PRNTAREA(132),BLANKS
         MVI   CNTRL,X'09'
         MVC   PRNTAREA(53),ERRMESS
         BAL   14,PUT2
         RETURN (14,12),RC=12
ERRMESS DC CL53'ERROR HAS OCCURRED DURING PROCESSING. JOB TERMINATED.'
LAST     DC    X'00'
LASTSCAN DC    X'00'
LASTCARD MVI   LAST,X'FF'
         B     ENDSCAN
RESTORE  BCR   0,0
         CLI   LAST,X'FF'
         BNE   REENTER
         CLOSE PRINTDCB
         CLOSE   CARDDCB
         L     13,SAVEBLK+4
         RETURN (14,12),RC=0
EXLIST   DS    0F
         DC    XL1'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
SAVEBLK  DS    18F
CARDDCB  DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,DEVD=RD,RECFM=F,         C
               LRECL=80,BLKSIZE=80,BFTEK=S,BUFNO=2,EODAD=LASTCARD,     C
               EROPT=SKP,SYNAD=ERROR
ISAMDCB  DCB   DSORG=IS,MACRF=(GL,SK),DDNAME=SYSUT1,EODAD=EOF,         C
               SYNAD=ERROR,EXLST=EXLIST
PRINTDCB DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSOUT,DEVD=PR,RECFM=FM,     C
               LRECL=133,BLKSIZE=133,BFTEK=S,BUFNO=2,EROPT=SKP,        C
               SYNAD=ERROR
BISAMDCB DCB   DSORG=IS,MACRF=(RUS,WUA),DDNAME=SYSUT1,BUFNO=1,         C
               EXLST=EXLIST
QSAMDCB  DCB   DSORG=PS,MACRF=(GL,PM),DDNAME=SYSUT1,SYNAD=ERROR,       C
               EODAD=LOADED,EXLST=EXLIST
LOADDCB  DCB   DSORG=IS,MACRF=PM,DDNAME=SYSUT2,SYNAD=ERROR,            C
               OPTCD=YILR,EXLST=EXLIST
         END   LISTISAM
