         TITLE 'CONVERT DWW TO DDMMYY FOR IMAP SYSTEM'
UT411    START 0
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVE+4
         LA    13,SAVE
         B     BEGIN
SAVE     DS    18F            REGISTER SAVE AREA
*
BEGIN    L     2,0(0,1)       LOAD ADDRESS 1ST PARAMETER TO REGISTER 2.
         L     3,4(0,1)       LOAD ADDRESS 2ND PARAMETER TO REGISTER 3.
         L     4,8(0,1)       LOAD ADDRESS 3RD PARAMETER TO REGISTER 4.
         L     5,12(0,1)      LOAD ADDRESS 4TH PARAMETER TO REGISTER 5.
         L     6,16(0,1)        .     .    5TH     .      .     .    6.
         CLI   0(2),X'F1'
         BL    ERROR
         CLI   0(2),X'F7'
         BH    ERROR
         CLC   1(2,2),=X'F0F1'
         BL    ERROR
         CLC   1(2,2),=X'F5F3'
         BH    ERROR
         PACK  CURRW,1(2,3)
         PACK  GIVENW,1(2,2)
         PACK  CURRD,0(1,3)
         PACK  GIVEND,0(1,2)
         PACK  YEAR,6(2,4)
         CLC   1(2,3),1(2)     COMPARE CURR-DW - WW - WITH GIVEN-DW.
         BH    AA
         BL    AB
         CLC   0(1,3),0(2)     COMPARE CURR-DW - D - WITH GIVEN-DW - D.
         BH    AA
         BL    AB
         MVC   0(2,5),3(4)     MOVE CURRENT DAY TO RETURN DATE  -  DD.
         MVC   2(2,5),0(4)     MOVE CURRENT MTH TO RETURN DATE  -  MM.
         MVC   4(2,5),6(4)     MOVE CURRENT YR  TO RETURN DATE  -  YY.
SUCCESS  MVC   0(1,6),=X'0F'   RETURN GOOD INDICATOR.
EXIT     L     13,SAVE+4       RESTOAR DEM DARE REG*S BABY.
        RETURN (14,12)       PASS CONTROL BACK TO USER PROGRAM, BABY.
*
CURRW    DC    PL2'0'
GIVENW   DC    PL2'0'
CURRD    DC    P'0'
GIVEND   DC    P'0'
WEEK     DC    PL2'0'
YEAR     DC    PL2'0'
DAYS     DC    PL3'0'
LEADAYS  DC    P'68'
         DC    P'0'              1968 LEAD DAYS
         DC    P'69'
         DC    P'0'               1969 LEAD DAYS
         DC    P'70'
         DC    X'3D'              1970 LEAD DAYS
         DC    P'71'
         DC    X'4D'              1971 LEAD DAYS
         DC    P'72'
         DC    X'5D'              1972 LEAD DAYS
         DC    P'73'
         DC    P'0'               1973 LEAD DAYS
         DC    P'74'
         DC    X'1D'              1974 LEAD DAYS
         DC    P'75'
         DC    X'2D'              1975 LEAD DAYS
         DC    P'76'
         DC    X'3D'              1976 LEAD DAYS
         DC    P'77'
         DC    X'5D'              1977 LEAD DAYS
ENDTABLE DC    P'99'
DAYMONTH DC    P'31'               JAN
FEBDAYS  DC    P'28'               FEB
         DC    P'31'               MAR
         DC    P'30'               APR
         DC    P'31'               MAY
         DC    P'30'               JUN
         DC    P'31'               JUL
         DC    P'31'               AUG
         DC    P'30'               SEP
         DC    P'31'               OCT
         DC    P'30'               NOV
         DC    P'31'               DEC
SUB      DC    PL2'1'
*
*
AA       MVC   WEEK,CURRW
         SP    WEEK,GIVENW
         CP    WEEK,=P'12'
         BL    *+10
         AP    YEAR,=P'1'
CALCDAYS ZAP   DAYS,GIVENW
         MP    DAYS,=P'7'
         AP    DAYS,GIVEND
         SP    DAYS,=P'7'
         LA    7,LEADAYS
AC       CP    0(2,7),ENDTABLE
         BNE   *+14
ERROR    MVC   0(1,6),=X'9F'   RETURN BAD  INDICATOR.
         B     EXIT
         CP    0(2,7),YEAR
         BE    *+12
         LA    7,3(7)
         B     AC
         AP    DAYS,2(1,7)
         BM    AD
         CP    YEAR,=P'68'       TEST FOR LEAP YEARS UPTO 1980.
         BE    LEAPYR             .    .    .    .    .    .
         CP    YEAR,=P'72'        .    .    .    .    .    .
         BE    LEAPYR             .    .    .    .    .    .
         CP    YEAR,=P'76'        .    .    .    .    .    .
         BE    LEAPYR             .    .    .    .    .    .
         CP    YEAR,=P'80'        .    .    .    .    .    .
         BE    LEAPYR
         BH    ERROR
NOLEAP   MVC   FEBDAYS,=P'28'
         B     *+10
LEAPYR   MVC   FEBDAYS,=P'29'
         ZAP   SUB,=P'1'
         LA    7,DAYMONTH
AE       CP    DAYS,0(2,7)
         BNH   SETUP
         SP    DAYS,0(2,7)
         AP    SUB,=P'1'
         CP    SUB,=P'13'
         BE    ERROR
         LA    7,2(7)
         B     AE
SETUP    UNPK  0(2,5),DAYS
         UNPK  2(2,5),SUB
         UNPK  4(2,5),YEAR
         OI    1(5),X'F0'
         OI    3(5),X'F0'
         OI    5(5),X'F0'
         B     SUCCESS
*
AD       SP    YEAR,=P'1'
         CP    YEAR,=P'68'
         BE    LEAPYR2
         CP    YEAR,=P'72'
         BE    LEAPYR2
         CP    YEAR,=P'76'
         BE    LEAPYR2
         CP    YEAR,=P'80'
         BE    LEAPYR2
         BH    ERROR
         AP    DAYS,=P'365'
         B     NOLEAP
LEAPYR2  AP    DAYS,=P'366'
         B     LEAPYR
*
AB       MVC   WEEK,GIVENW
         SP    WEEK,CURRW
         CP    WEEK,=P'40'
         BL    CALCDAYS
         SP    YEAR,=P'1'
         B     CALCDAYS
*
         END   UT411
