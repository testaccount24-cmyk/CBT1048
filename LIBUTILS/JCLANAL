         MACRO
&NAME    WRITE &X
         LCLA  &L
&L       SETA  K'&X
         AIF   ('&NAME' NE '').M1
         MNOTE 16,'NO NAME SUPPLIED'
         MEXIT
.M1      AIF   (K'&X LE 2).M2
         MNOTE 16,'OPERAND EXCEEDS TWO CHARACTERS'
.M2      ANOP
         DS    0H
&NAME    BC    0,DETAIL&X
         MVI   &NAME.+1,X'F0'
         OPEN  (PRINT&X,(OUTPUT))
HEAD&X   MVC   LINECT&X,ZERO
         MVI   CC&X,X'8B'
         MVC   LINE&X,BLANKLNE
         PUT   PRINT&X,CC&X
         MVI   CC&X,X'11'
         AP    PAGECT&X,ONE
         MVC   LINE&X.+57(18),HEAD0
         MVC   LINE&X.+5(9),DATEFLD
         PUT   PRINT&X,CC&X
         MVC   LINE&X,BLANKLNE
         MVC   LINE&X.+5(6),REPORT
         MVC   LINE&X.+12(&L),=C'&X'
         MVC   LINE&X.+125(4),PAGEEDIT
         ED    LINE&X.+125(4),PAGECT&X
         MVC   LINE&X.+122(4),PAGEHEAD
         MVC   LINE&X.+40(53),HEAD1&X
         PUT   PRINT&X,CC&X
SW&X     BC    0,BR&X
         OI    SW&X.+1,X'F0'
         MVI   CC&X,X'09'
         B     BR2&X
BR&X     MVI   CC&X,X'11'
BR2&X    EQU   *
         MVC   LINE&X,HEAD2&X
         PUT   PRINT&X,CC&X
         MVI   CC&X,X'09'
DETAIL&X CP    LINECT&X,FIFTY5
         BNL   HEAD&X
         AP    LINECT&X,ONE
         MVC   LINE&X,LINE
         PUT   PRINT&X,CC&X
         MVC   LINE,BLANKLNE
         BR    R4
PRINT&X  DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=133,                   *
               DDNAME=WRITE&X
CC&X     DS    0CL133
         DC    X'09'
LINE&X   DC    CL132' '
LINECT&X DC    PL2'0'
PAGECT&X DC    PL2'0'
         MEND
UT446    CSECT
         SPACE 1
         PGMIN USING=(R13,R12,R7,R6)
         LA    R8,2048(R6)
         LA    R8,2048(R8)
         DROP  R13,R12,R7,R6
         USING SAVEFLD,R13,R12,R7,R6,R8
         SPACE 1
         OPEN  (SYSUT1,(INPUT))
         OPEN  (SYSUT2,(OUTPUT))
         OPEN  (SYSUT3,(OUTPUT))
         OPEN  (SNAPDUMP,(OUTPUT))
         L     R15,DATEADDR
         LA    R1,ADATEFLD
         BALR  R14,R15
         L     R1,4(R13)
         L     R1,24(R1)
         L     R1,0(R1)
         LH    R11,0(R1)
         LTR   R11,R11
         BZ    ENDPARM
         LA    R10,2(R1)
         CLC   0(7,R10),=C'REPORTS'
         BNE   ENDPARM
         MVI   REPORTS1+1,X'00'
         MVI   REPORTS2+1,X'00'
         MVI   REPORTS3+1,X'00'
ENDPARM  EQU   *
REPORTS1 BC    15,READ
         BAL   R4,A1
         BAL   R4,A2
         BAL   R4,A3
         BAL   R4,A4
         BAL   R4,A5
         BAL   R4,A6
         BAL   R4,A7
         BAL   R4,A8
         BAL   R4,A9
         BAL   R4,B1
         BAL   R4,B2
         BAL   R4,B3
         BAL   R4,B4
         EJECT
READ     EQU   *
         GET   SYSUT1,RECORD1
         LA    R4,READ
         CLC   INPUT(30),SPACES
         BE    WRITE3
         CLC   INPUT+20(12),MEMBER
         BNE   READ2
         BAL   R4,WRITE3
         MVC   PROCNME1,INPUT+33
         MVC   PROCNME2,INPUT+33
         MVC   STEPNO,ZERO
        STIMER TASK,ERRORTME,DINTVL=TYMLIMIT
         B     READ
READ2    EQU   *
         CLC   INPUT+20(3),=C'//*'
         BE    READ
         CLC   INPUT+20(2),SLASH
         BNE   WRITE3
         LA    R4,EXECKEY
         SCAN  INPUT+22,=C' EXEC ',YES=WRITE3,REG=R10,LIMIT=INPUT+92
         LA    R4,READ
         SCAN  INPUT+22,=C' PROC ',YES=WRITE3,REG=R10,LIMIT=INPUT+92
         LA    R4,READ3
         SCAN  INPUT+22,=C' DD ',YES=WRITE3,REG=R10,LIMIT=INPUT+92
         CLI   INPUT+22,C' '
         BNE   ERROR
READSW   BC    15,READ
         LA    R10,INPUT+23
         B     DDKEY3
READ3    MVI   WRITE3SW+1,X'00'
         MVI   READSW+1,X'00'
         B     DDCARD
         EJECT
EXECKEY  EQU   *
         AP    STEPNO,ONE
         MVC   UNPACKED,EDITMASK
         ED    UNPACKED,STEPNO
         OI    UNPACKED+3,X'F0'
         MVC   STEPNO1,UNPACKED+2
         MVC   STEPNO2,UNPACKED+2
         MVI   STEPNME2,C' '
         MVC   STEPNME2+1(15),STEPNME2
         MVC   KEYWORD,PGM
         BAL   4,KEYFIND
         B     *+8
         B     ERROR
         LA    10,4(10)
         LA    11,9(10)
         CLI   0(10),C'*'
         BNE   EXECKEY2
         LA    9,7(0)
         B     EXECKEY3
EXECKEY2 BAL   4,GETDELIM
         B     EXECKEY4
         CR    R10,R11
         BE    ERROR
EXECKEY3 BAL   R4,CHECKEX8
         EX    9,MOVEPGM1
         EX    9,MOVEPGM2
EXECKEY7 LA    10,INPUT+22
         LA    11,9(10)
         CLI   INPUT+22,C' '
         BE    WRITE2
         BAL   4,GETDELIM
         B     ERROR
         CLI   DELIMITR,C' '
         BNE   ERROR
         BAL   R4,CHECKEX8
         EX    9,MOVESTP1
         EX    9,MOVESTP2
WRITE2   PUT   SYSUT2,RECORD2
         CLC   PROGRAM1,=C'IERRCO00'
         BNE   EXECKEY8
         SCAN  15(R10),=C'CORE=',REG=R10,LIMIT=INPUT+92
         B     EXECKEY8
         LA    R10,5(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BCTR  R9,0
         BAL   R4,CHECKEX5
         EX    R9,MOVECORE
         LA    R4,EXECKEY8
         B     IERRCO00
EXECKEY8 DS    0H
         MVI   STEPNME1,C' '
         MVC   STEPNME1+1(17),STEPNME1
         B     READ
         SPACE 1
MOVEPGM1 MVC   PROGRAM1(0),0(R10)
MOVEPGM2 MVC   PROGRAM2(0),0(R10)
MOVESTP1 MVC   STEPNME1(0),0(R10)
MOVESTP2 MVC   STEPNME2(0),0(R10)
MOVECORE MVC   SORTCORE(0),0(R10)
         SPACE 1
EXECKEY4 EQU   *
         LA    R9,8
         LR    R11,R10
         LA    R10,8(R10)
EXECKEY5 BCTR  R10,0
         CLI   0(R10),X'50'
         BE    EXECKEY6
         BCT   R9,EXECKEY5
         B     ERROR
EXECKEY6 MVC   PROGRAM1,0(R11)
         MVC   PROGRAM2,0(R11)
         B     EXECKEY7
         EJECT
DDCARD   EQU   *
         MVI   DDNAME,C' '         CLEAR DDNAME
         MVC   DDNAME+1(51),DDNAME   AND DSNAME
         MVI   MEMBR,C' '          CLEAR REST
         MVC   MEMBR+1(129),MEMBR    OF RECORD3
         CLI   INPUT+22,C' '
         BE    DDKEY1
         LA    10,INPUT+22
         LA    11,INPUT+31
         BAL   4,GETDELIM
         B     ERROR
         CLI   DELIMITR,C' '
         BNE   ERROR
         MVC   DDNAME,SPACES
         BAL   R4,CHECKEX8
         EX    9,MOVEDDN
         AR    R10,R9
         LA    R10,1(R10)
         CLC   0(4,R10),=C' DD '
         BNE   *-10
DDKEY1   EQU   *
         LA    R10,3(R10)
DDKEY2   EQU   *
         LA    R10,1(R10)
DDKEY3   EQU   *
         CLI   0(R10),C' '
         BE    DDKEY2
IDENTIFY EQU   *
         CLC   0(2,R10),=C'* '
         BE    DASTRSK
         CLI   0(R10),C','
         BNE   IDENT1
         LA    R10,1(R10)
         B     IDENTIFY
         LA    R11,INPUT+92
         CR    R10,R11
         BNL   READ
IDENT1   EQU   *
         CLI   0(R10),C' '
         BE    READ
         CLC   0(4,R10),=C'DSN='
         BE    DSNKEY
         CLC   0(7,R10),=C'DSNAME='
         BE    DSNMEKEY
         CLC   0(7,R10),=C'SYSOUT='
         BE    SYSOUT
         CLC   0(5,R10),=C'DISP='
         BE    DISP
         CLC   0(6,R10),=C'SPACE='
         BE    SPACE
         CLC   0(5,R10),=C'UNIT='
         BE    UNIT
         CLC   0(4,R10),=C'VOL='
         BE    VOL
         CLC   0(7,R10),=C'VOLUME='
         BE    VOLUME
         CLC   0(5,R10),=C'DUMMY'
         BE    DUMMY
         CLC   0(4,R10),=C'DATA'
         BE    DDATA
         CLC   0(5,R10),=C'DYNAM'
         BE    DDYNAM
         CLC   0(7,R10),=C'DDNAME='
         BE    DDDNAME
         CLC   0(4,R10),=C'AFF='
         BE    AFF
         CLC   0(4,R10),=C'SEP='
         BE    SEP
         CLC   0(6,R10),=C'SPLIT='
         BE    SSPLIT
         CLC   0(9,R10),=C'SUBALLOC='
         BE    SSUBALOC
         MVI   DCBSW+1,X'F0'
         MVI   DCBSW2+1,X'00'
         MVI   DCBSW3+1,X'F0'
         CLC   0(5,R10),=C'DCB=('
         BE    DCB
         CLC   0(4,R10),=C'DCB=('
         BE    DCB1
         CLC   0(6,R10),=C'LABEL='
         BE    LABEL
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         MVI   DCBSW+1,X'00'
         MVI   DCBSW2+1,X'F0'
         MVI   DCBSW3+1,X'00'
         CLC   0(6,R10),=C'RECFM='
         BE    DRECFM
         CLC   0(6,R10),=C'LRECL='
         BE    DLRECL
         CLC   0(8,R10),=C'BLKSIZE='
         BE    DBLKSIZE
         CLC   0(6,R10),=C'OPTCD='
         BE    DOPTCD
         CLC   0(6,R10),=C'BUFNO='
         BE    DBUFNO
         CLC   0(6,R10),=C'DSORG='
         BE    DDSORG
         CLC   0(7,R10),=C'CYLOFL='
         BE    DCYLOFL
         CLC   0(7,R10),=C'KEYLEN='
         BE    DKEYLEN
         CLC   0(4,R10),=C'RKP='
         BE    DRKP
         LA    R4,SPACE5
         TM    LASTPARM,BIT0
         BO    IDENT4
         TM    LASTPARM,BIT1
         BZ    IDENT11
         BCTR  R10,0
         LA    R4,SPACE10
         B     IDENT4
IDENT11  TM    LASTPARM,BIT2
         BZ    IDENT12
         BCTR  R10,0
         LA    R4,DISP4
         B     IDENT4
IDENT12  EQU   *
         BAL   R4,SNAP
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         B     IDENTIFY
*
*        TESTS IN HERE
*
IDENT2   LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         MVC   ENDOCARD,DELIMITR
         CLI   DELIMITR,C' '
         BE    READ
         CLI   DELIMITR,C','
         BE    IDENTIFY
         B     *+8
IDENT3   LA    R10,1(R10)
         CLI   0(R10),C' '
         BE    READ
         CLI   0(R10),C'A'
         BNL   IDENTIFY
         CLI   0(R10),C','
         BNE   IDENT3
         LA    R10,1(R10)
         B     IDENTIFY
         SPACE 1
IDENT4   EQU   *
         MVI   LASTPARM,X'00'
         BR    R4
         EJECT
DSNMEKEY LA    10,3(10)
DSNKEY   LA    10,4(10)
         LA    11,45(10)
         BAL   4,GETDELIM
         B     ERROR
         BAL   R4,CHECK44
         EX    9,MOVEDSN
         MVC   ENDOCARD,DELIMITR
         CLI   DELIMITR,C' '
         BE    READ
         CLI   DELIMITR,C','
         LA    10,1(11)
         BE    IDENTIFY
         CLI   DELIMITR,C'('
         BNE   ERROR
         LA    11,9(10)
         BAL   4,GETDELIM
         B     DSNKEY2
         CLI   DELIMITR,C')'
         BNE   ERROR
         CLC   0(5,10),INDEX
         BE    ISAM
         CLC   0(5,10),PRIME
         BE    ISAM
         CLC   0(6,10),OVFLOW
         BNE   GDGTST
ISAM     BAL   R4,CHECKEX6
         EX    9,MOVEISAM
         B     DSNEND
GDGTST   CLI   0(10),C'+'
         BE    GDGYES
         CLI   0(10),C'-'
         BE    GDGYES
         CLI   0(10),C'0'
         BL    GDGYES
         BCTR  10,0
         MVI   0(10),X'55'
         LA    9,1(9)
GDGYES   BAL   R4,CHECK10
         EX    9,MOVEGDG
DSNEND   LA    R10,1(R11)
         MVI   ENDOCARD,C' '
         CLI   0(R10),C' '
         BE    READ
         MVI   ENDOCARD,C','
         CLI   0(R10),C','
         LA    R10,1(R10)
         BE    IDENTIFY
         B     ERROR
MOVEDDN  MVC   DDNAME(0),0(10)
MOVEDSN  MVC   DSNAME(0),0(10)
MOVEISAM MVC   ISAMUSE(0),0(10)
MOVEGDG  MVC   RELGENNO(0),0(10)
         SPACE 1
DSNKEY2  EQU   *
         LA    R9,8
         LA    R11,8(R10)
DSNKEY3  BCTR  R11,0
         CLI   0(R11),X'50'
         BE    DSNKEY4
         BCT   R9,DSNKEY3
         B     ERROR
DSNKEY4  LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R9,7
         EX    R9,MOVEGDG
         LA    R10,1(R11)
         B     IDENTIFY
         TITLE 'ANALYSE DCB PARAMETER'
DCB      EQU   *
         LA    R10,1(R10)
         MVI   DCBSW+1,X'00'
         MVI   DCBSW3+1,X'00'
DCB1     EQU   *
         LA    R10,4(R10)
         B     *+8
DCBSW    BC    0,IDENTIFY
         CLI   0(R10),C')'
         BNE   DCB2
         LA    R10,1(R10)
         B     IDENTIFY
DCB2     CLI   0(R10),C','
         BNE   DCB3
         LA    R10,1(R10)
         B     DCBSW
DCB3     CLI   0(R10),C' '
         BE    READ
DCBSW2   BC    0,DCB4
         MVI   DCBSW2+1,X'F0'
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LR    R9,R11
         SCAN  0(R10),=C'=',LIMIT=INPUT+92,REG=R11
         BC    0,*+4
         CR    R9,R11
         BNL   DCB4
         LR    R11,R9
         SR    R9,R10
         BCTR  R9,0
         BAL   R4,CHECK19
         EX    R9,MVEREFBK
         LA    R10,2(R10)
         AR    R10,R9
DCBSW3   BC    0,IDENTIFY
DCB4     EQU   *
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         CLC   0(6,R10),=C'RECFM='
         BE    DRECFM
         CLC   0(6,R10),=C'LRECL='
         BE    DLRECL
         CLC   0(8,R10),=C'BLKSIZE='
         BE    DBLKSIZE
         CLC   0(6,R10),=C'OPTCD='
         BE    DOPTCD
         CLC   0(6,R10),=C'BUFNO='
         BE    DBUFNO
         CLC   0(6,R10),=C'DSORG='
         BE    DDSORG
         CLC   0(7,R10),=C'CYLOFL='
         BE    DCYLOFL
         CLC   0(7,R10),=C'KEYLEN='
         BE    DKEYLEN
         CLC   0(4,R10),=C'RKP='
         BE    DRKP
         CLC   0(5,R10),=C'BUFL='
         BE    DCBBYPAS
         CLC   0(6,R10),=C'EROPT='
         BE    DCBBYPAS
         LA    R10,0(R11)
         CLI   0(R10),C'('
         BE    ERROR
         BAL   R4,SNAPDCB
         B     DCBSW
         SPACE 1
DCBBYPAS EQU   *
         LA    R10,0(R11)
         B     DCBSW
         SPACE 5
DRECFM   LA    R10,6(R10)
         S     R9,=F'6'
         CLI   1(R10),X'50'
         BNE   DRECFM1
         LA    R9,1
DRECFM1  EQU   *
         BAL   R4,CHECKEX3
         EX    R9,MVERECFM
         LA    R10,0(R11)
         B     DCBSW
DLRECL   LA    R10,6(R10)
         S     R9,=F'6'
         BAL   R4,CHECKEX4
         EX    R9,MVELRECL
         LA    R10,0(R11)
         B     DCBSW
DBLKSIZE LA    R10,8(R10)
         S     R9,=F'8'
         CLI   0(R10),X'50'
         BNE   DBLKSIZ1
         LA    R9,2
DBLKSIZ1 EQU   *
         BAL   R4,CHECKEX4
         EX    R9,MVEBLKSZ
         LA    R10,0(R11)
         B     DCBSW
DOPTCD   LA    R10,6(R10)
         S     R9,=F'6'
         BAL   R4,CHECKEX6
         EX    R9,MVEOPTCD
         LA    R10,0(R11)
         B     DCBSW
DBUFNO   LA    R10,6(R10)
         S     R9,=F'6'
         BAL   R4,CHECKEX2
         EX    R9,MVEBUFNO
         LA    R10,0(R11)
         B     DCBSW
DDSORG   LA    R10,6(R10)
         S     R9,=F'6'
         BAL   R4,CHECKEX3
         EX    R9,MVEDSORG
         LA    R10,0(R11)
         B     DCBSW
DCYLOFL  LA    R10,7(R10)
         S     R9,=F'7'
         BAL   R4,CHECKEX2
         EX    R9,MVECYLOF
         LA    R10,0(R11)
         B     DCBSW
DKEYLEN  LA    R10,7(R10)
         S     R9,=F'7'
         BAL   R4,CHECKEX3
         EX    R9,MVEKEYLN
         LA    R10,0(R11)
         B     DCBSW
DRKP     LA    R10,4(R10)
         S     R9,=F'4'
         BAL   R4,CHECKEX3
         EX    R9,MVERKP
         LA    R10,0(R11)
         B     DCBSW
         SPACE 1
MVERECFM MVC   RECFM(0),0(R10)
MVELRECL MVC   LRECL(0),0(R10)
MVEBLKSZ MVC   BLKSIZE(0),0(R10)
MVEOPTCD MVC   OPTCD(0),0(R10)
MVEBUFNO MVC   BUFNO(0),0(R10)
MVEDSORG MVC   DSORG(0),0(R10)
MVECYLOF MVC   CYLOFL(0),0(R10)
MVEKEYLN MVC   KEYLEN(0),0(R10)
MVERKP   MVC   RKP(0),0(R10)
MVEREFBK MVC   DCBREFBK(0),0(R10)
         TITLE 'ANALYSE LABEL PARAMETER'
LABEL    EQU   *
         LA    R10,6(R10)
         CLI   0(R10),C'('
         BE    LABEL2
         CLC   0(5,R10),=C'EXPDT'
         BE    LABEL1
         CLC   0(5,R10),=C'RETPD'
         BE    LABEL1
         LA    R11,3(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX2
         EX    R9,MVELBLNO
         LA    R10,1(R11)
         B     IDENTIFY
LABEL1   LA    R10,6(R10)
         LA    R11,6(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX5
         EX    R9,MVELBDTE
         LA    R10,0(R11)
         B     IDENTIFY
LABEL2   LA    R10,1(R10)
         CLI   0(R10),C','
         BE    LABEL3
         CLC   0(5,R10),=C'EXPDT'
         BE    LABEL7
         CLC   0(5,R10),=C'RETPD'
         BE    LABEL7
         LA    R11,3(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX2
         EX    R9,MVELBLNO
         LA    R10,0(R11)
LABEL3   LA    R10,1(R10)
         CLI   0(R10),C','
         BE    LABEL4
         CLC   0(5,R10),=C'EXPDT'
         BE    LABEL7
         CLC   0(5,R10),=C'RETPD'
         BE    LABEL7
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX3
         EX    R9,MVELBTYP
         LA    R10,0(R11)
         CLI   0(R10),C')'
         BNE   LABEL4
         LA    R10,1(R10)
         B     IDENTIFY
LABEL4   LA    R10,1(R10)
         CLI   0(R10),C','
         BE    LABEL5
         CLC   0(5,R10),=C'RETPD'
         BE    LABEL7
         CLC   0(5,R10),=C'EXPDT'
         BE    LABEL7
         MVC   LABLPSWD,0(R10)
         LA    R10,8(R10)
         CLI   0(R10),C')'
         BNE   LABEL5
         LA    R10,1(R10)
LABEL5   LA    R10,1(R10)
         CLI   0(R10),C','
         BE    LABEL6
         CLC   0(5,R10),=C'RETPD='
         BE    LABEL7
         CLC   0(5,R10),=C'EXPDT'
         BE    LABEL7
         LA    R11,4(R10)
         BAL   R4,GETDELIM
         B     ERROR
         MVC   LABLIO,0(R10)
         LA    R10,0(R11)
         CLI   0(R10),C')'
         BNE   LABEL6
         LA    R10,1(R10)
         B     IDENTIFY
LABEL6   LA    R10,1(R10)
         CLC   0(5,R10),=C'EXPDT'
         BE    LABEL7
         CLC   0(5,R10),=C'RETPD'
         BNE   ERROR
LABEL7   LA    R10,6(R10)
         LA    R11,6(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX5
         EX    R9,MVELBDTE
         LA    R10,1(R11)
         B     IDENTIFY
         SPACE 1
MVELBLNO MVC   LABLNO(0),0(R10)
MVELBTYP MVC   LABLTYPE(0),0(R10)
MVELBDTE MVC   LABLDATE(0),0(R10)
         TITLE ' '
SSUBALOC SUBIN
         MVC   SUBALLOC(8),0(R10)
         LA    R10,9(R10)
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         CLI   DELIMITR,C','
         BE    IDENTIFY
         CLI   DELIMITR,C'('
         BNE   ERROR
SUBALOC2 LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         CLI   DELIMITR,C','
         BE    SUBALOC2
         CLI   DELIMITR,C')'
         BE    IDENTIFY
SUBALOC3 LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         CLI   DELIMITR,C')'
         BE    SUBALOC2
         B     SUBALOC3
         EJECT
SSPLIT   SUBIN
         MVC   SPLIT(5),0(R10)
         LA    R10,6(R10)
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         CLI   DELIMITR,C' '
         BE    READ
         CLI   DELIMITR,C','
         BE    IDENTIFY
         CLI   DELIMITR,C'('
         BNE   ERROR
SSPLIT2  LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         CLI   DELIMITR,C')'
         BNE   SSPLIT2
         LA    R10,1(R10)
         B     IDENTIFY
         EJECT
DUMMY    SUBIN
         MVC   DDDUMMY,0(R10)
         LA    R10,5(R10)
         B     IDENTIFY
         SPACE 5
DDATA    SUBIN
         MVC   DDDATA,0(R10)
         LA    R10,4(R10)
         B     IDENTIFY
         SPACE 5
DASTRSK  SUBIN
         MVC   DDASTRSK,0(R10)
         LA    R10,1(R10)
         B     IDENTIFY
         SPACE 5
DDYNAM   SUBIN
         MVC   DDDYNAM,0(R10)
         LA    R10,5(R10)
         B     IDENTIFY
         SPACE 5
DDDNAME  SUBIN
         LA    R10,7(R10)
         LA    R11,9(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX8
         EX    R9,MOVEDDNM
         LA    R10,1(R11)
         B     IDENTIFY
         SPACE 1
MOVEDDNM MVC   DDDDNAME(0),0(R10)
         EJECT
AFF      SUBIN
         MVI   CHANAFF,C'A'
         LA    R10,4(R10)
         LA    R11,9(R10)
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         B     IDENTIFY
         SPACE 5
SEP      SUBIN
         MVI   CHANSEP,C'S'
         LA    R10,4(R10)
         CLI   0(R10),C'('
         BE    SEP2
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         B     IDENTIFY
SEP2     LA    R10,1(R10)
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,0(R11)
         CLI   DELIMITR,C')'
         BNE   SEP2
         LA    R10,1(R10)
         B     IDENTIFY
         EJECT
VOLUME   LA    R10,3(R10)
VOL      LA    R10,4(R10)
         CLI   0(R10),C'S'
         BNE   VOL3
         MVI   VOLSER,C'S'
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         CLI   DELIMITR,C' '
         BE    READ
         CLI   DELIMITR,C','
         BNE   VOL2
         LA    R10,1(R11)
         B     IDENTIFY
VOL2     LA    R10,1(R11)
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         CLI   DELIMITR,C')'
         BNE   VOL2
         LA    R10,1(R11)
         B     IDENTIFY
VOL3     CLI   0(R10),C'R'
         BNE   VOL4
         MVI   VOLREF,C'R'
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         CLI   DELIMITR,C','
         BE    IDENTIFY
         CLI   DELIMITR,C' '
         BE    IDENTIFY
         B     ERROR
VOL4     LA    R10,1(R10)
         CLI   0(R10),C','
         BE    VOL5
         CLC   0(3,R10),=C'SER'
         BE    VOL10
         CLC   0(3,R10),=C'REF'
         BE    VOL9
         MVI   VOLPRIV,C'P'
         LA    R10,7(R10)
         CLI   0(R10),C')'
         BNE   VOL5
         LA    R10,1(R10)
         B     IDENTIFY
VOL5     LA    R10,1(R10)
         CLI   0(R10),C','
         BE    VOL6
         CLC   0(3,R10),=C'SER'
         BE    VOL10
         CLC   0(3,R10),=C'REF'
         BE    VOL9
         MVI   VOLRET,C'R'
         LA    R10,6(R10)
         CLI   0(R10),C')'
         BNE   VOL6
         LA    R10,1(R10)
         B     IDENTIFY
VOL6     LA    R10,1(R10)
         CLC   0(3,R10),=C'SER'
         BE    VOL10
         CLC   0(3,R10),=C'REF'
         BE    VOL9
         CLI   0(R10),C','
         BE    VOL7
         MVC   VOLSEQ,0(R10)
         CLI   1(R10),C'A'
         BNL   ERROR
         LA    R10,1(R10)
         CLI   0(R10),C')'
         BNE   VOL7
         LA    R10,1(R10)
         B     IDENTIFY
VOL7     LA    R10,1(R10)
         CLC   0(3,R10),=C'SER'
         BE    VOL10
         CLC   0(3,R10),=C'REF'
         BE    VOL9
         CLI   0(R10),C','
         BE    VOL8
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
VOL74    CLI   0(R10),C'0'
         BNE   VOL75
         LA    R10,1(R10)
         B     VOL74
VOL75    CLC   0(2,R10),=C'35'
         BNE   VOL76
         MVI   VOLCNT,C'+'
         LA    R10,0(R11)
         B     VOL77
VOL76    CLI   0(R10),X'50'
         BNE   VOL765
         MVI   VOLCNT,X'50'
         LA    R10,0(R11)
         B     VOL77
VOL765   EQU   *
         MVC   VOLCNT,0(R10)
         CLI   1(R10),C'A'
         BNL   ERROR
         LA    R10,1(R10)
VOL77    EQU   *
         CLI   0(R10),C')'
         BNE   VOL8
         LA    R10,1(R10)
         B     IDENTIFY
VOL8     CLI   0(R10),C','
         BNE   ERROR
         LA    R10,1(R10)
         CLC   0(3,R10),=C'SER'
         BE    VOL10
         CLC   0(3,R10),=C'REF'
         BNE   ERROR
VOL9     MVI   VOLREF,C'R'
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         B     IDENTIFY
VOL10    MVI   VOLSER,C'S'
         LA    R10,6(R10)
VOL11    LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         CLI   DELIMITR,C')'
         BE    VOL12
         LA    R10,1(R11)
         B     VOL11
VOL12    LA    R10,1(R11)
         CLI   0(R10),C')'
         BNE   IDENTIFY
         LA    R10,1(R10)
         B     IDENTIFY
         EJECT
UNIT     SUBIN
         LA    R10,5(R10)
         CLC   0(3,R10),=C'AFF'
         BNE   UNIT2
         MVI   UNITAFF,C'A'
         LA    R11,13(R10)
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         B     IDENTIFY
UNIT2    CLI   0(R10),C'('
         BE    UNIT3
         CLI   0(R10),X'50'
         BE    UNIT7
         LA    R11,9(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX8
         EX    R9,MOVEUNIT
         LA    R10,1(R11)
         B     IDENTIFY
UNIT3    LA    R10,1(R10)
         CLI   0(R10),C','
         BE    UNIT35
         CLI   0(R10),X'50'
         BE    UNIT7
         LA    R11,9(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX8
         EX    R9,MOVEUNIT
         LA    R10,0(R11)
         CLI   DELIMITR,C')'
         BNE   UNIT34
         LA    R10,1(R10)
         B     IDENTIFY
UNIT34   EQU   *
UNIT35   LA    R10,1(R10)
         CLI   0(R10),C','
         BE    UNIT4
         CLC   0(3,R10),=C'SEP'
         BE    UNIT6
         MVC   UNITCNT,0(R10)
         LA    R10,1(R10)
         CLI   0(R10),C','
         BE    UNIT4
         CLI   0(R10),C')'
         BNE   ERROR
         LA    R10,1(R10)
         B     IDENTIFY
UNIT4    LA    R10,1(R10)
         CLI   0(R10),C','
         BE    UNIT5
         CLC   0(3,R10),=C'SEP'
         BE    UNIT6
         MVI   UNITDEF,C'D'
         LA    R10,5(R10)
         CLI   0(R10),C','
         BE    UNIT5
         CLI   0(R10),C')'
         BNE   ERROR
         LA    R10,1(R10)
         B     IDENTIFY
UNIT5    LA    R10,1(R10)
         CLI   0(R10),C'S'
         BNE   ERROR
         MVI   UNITSEP,C'S'
UNIT6    LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         CLI   DELIMITR,C')'
         BNE   UNIT6
         CLI   0(R10),C','
         BE    IDENTIFY
         LA    R10,1(R10)
         B     IDENTIFY
UNIT7    MVC   UNITTYPE,=CL8'SYMBOLIC'
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         B     IDENTIFY
         SPACE 1
MOVEUNIT MVC   UNITTYPE(0),0(R10)
         EJECT
SYSOUT   SUBIN
         LA    R10,7(R10)
         CLI   0(R10),C'('
         BE    SYSOUT2
         MVC   SYSOUTC,0(R10)
         LA    R10,1(R10)
         B     IDENTIFY
SYSOUT2  LA    R10,1(R10)
         MVC   SYSOUTC,0(R10)
         LA    R10,1(R10)
         CLI   0(R10),C','
         BE    SYSOUT25
         BCTR  R10,0
         CLI   0(R10),X'50'
         BNE   ERROR
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,0(R11)
         CLI   DELIMITR,C','
         BNE   ERROR
SYSOUT25 EQU   *
         LA    R10,1(R10)
         CLI   0(R10),C','
         BNE   *-8
         LA    R10,1(R10)
         LA    R11,5(R10)
         BAL   R4,GETDELIM
         B     SYSOUT3
         BAL   R4,CHECKEX4
         EX    R9,MOVEFORM
         LA    R10,1(R11)
         B     IDENTIFY
         SPACE 1
MOVEFORM MVC   SYSOUTF(0),0(R10)
         SPACE 1
SYSOUT3  EQU   *
         LA    R9,4
         LA    R11,4(R10)
SYSOUT4  BCTR  R11,0
         CLI   0(R11),X'50'
         BE    SYSOUT5
         BCT   R9,SYSOUT4
         B     ERROR
SYSOUT5  MVC   SYSOUTF,0(R10)
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         B     IDENTIFY
         EJECT
DISP     SUBIN
         LA    R10,5(R10)
         CLI   0(R10),C'('
         BE    DISP2
         MVC   DISPSTAT,0(R10)
         LA    R10,4(R10)
         B     IDENTIFY
DISP2    LA    R10,1(R10)
         CLI   0(R10),C','
         BE    DISP3
         MVC   DISPSTAT,0(R10)
         LA    R10,3(R10)
         CLI   0(R10),C','
         BE    DISP3
         CLI   0(R10),C')'
         BNE   ERROR
         LA    R10,1(R10)
         B     IDENTIFY
DISP3    LA    R10,1(R10)
         CLI   0(R10),C','
         BE    DISP4
         LA    R11,8(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX7
         EX    R9,MOVEDISP
         LA    R10,0(R11)
         CLI   0(R10),C','
         BE    DISP4
         CLI   0(R10),C')'
         BNE   ERROR
         LA    R10,1(R10)
         B     IDENTIFY
DISP4    LA    R10,1(R10)
         CLI   0(R10),C' '
         BNE   DISP5
         OI    LASTPARM,BIT2
         B     READ
DISP5    EQU   *
         LA    R11,8(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX7
         EX    R9,MOVECOND
         LA    R10,1(R11)
         CLI   DELIMITR,C')'
         BNE   ERROR
         B     IDENTIFY
         SPACE 1
MOVEDISP MVC   DISPDISP(0),0(R10)
MOVECOND MVC   DISPCOND(0),0(R10)
         EJECT
SPACE    SUBIN
         LA    R10,7(R10)
         MVI   SPACETYP,C'C'
         CLC   0(3,R10),=C'CYL'
         BNE   SPACE2
         LA    R10,4(R10)
         B     SPACE5
SPACE2   MVI   SPACETYP,C'T'
         CLC   0(3,R10),=C'TRK'
         BNE   SPACE3
         LA    R10,4(R10)
         B     SPACE5
SPACE3   MVI   SPACETYP,C'A'
         CLC   0(5,R10),=C'ABSTR'
         BNE   SPACE4
         LA    R10,6(R10)
         B     SPACE5
SPACE4   LA    R11,5(R10)
         BAL   R4,GETDELIM
         B     ERROR
         LA    R10,1(R11)
         MVI   SPACETYP,C'B'
SPACE5   CLI   0(R10),C'('
         BE    SPACE6
         CLI   0(R10),C' '
         BNE   SPACE55
         OI    LASTPARM,BIT0
         B     READ
SPACE55  EQU   *
         LA    R11,4(R10)
         BAL   R4,GETDELIM
         B     ERROR
         CLI   0(R10),X'50'
         BNE   SPACE56
         LA    R9,2
SPACE56  EQU   *
         BAL   R4,CHECKEX3
         EX    R9,MOVEPRI
         LA    R10,1(R11)
         CLI   DELIMITR,C')'
         BE    IDENTIFY
         BCTR  R10,0
         B     SPACE10
SPACE6   LA    R10,1(R10)
         LA    R11,4(R10)
         BAL   R4,GETDELIM
         B     SPACE12
         BAL   R4,CHECKEX3
SPACE61  EX    R9,MOVEPRI
         LA    R10,1(R11)
         CLI   DELIMITR,C')'
         BE    SPACE8
         CLI   0(R10),C','
         BE    SPACE7
         LA    R11,4(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX3
         EX    R9,MOVESEC
         LA    R10,1(R11)
         CLI   DELIMITR,C')'
         BE    SPACE8
         BCTR  R10,0
SPACE7   LA    R10,1(R10)
         CLI   0(R10),C')'
         BNE   SPACE75
         LA    R10,1(R10)
         B     SPACE8
SPACE75  EQU   *
         LA    R11,4(R10)
         BAL   R4,GETDELIM
         B     ERROR
         BAL   R4,CHECKEX2
         EX    R9,MOVEDIR
         LA    R10,1(R11)
SPACE8   CLI   0(R10),C')'
         BNE   SPACE85
         LA    R10,1(R10)
         B     IDENTIFY
SPACE85  CLI   0(R10),C','
         BE    SPACE10
         B     ERROR
SPACE10  LA    R10,1(R10)
         CLI   0(R10),C','
         BE    SPACE105
         CLI   0(R10),C' '
         BNE   SPACE104
         OI    LASTPARM,BIT1
         B     READ
SPACE104 CLC   0(4,R10),=C'RLSE'
         BNE   ERROR
         MVI   SPACERLS,C'R'
         LA    R10,4(R10)
         CLI   0(R10),C')'
         BNE   SPACE105
         LA    R10,1(R10)
         B     IDENTIFY
SPACE105 LA    R10,1(R10)
         CLI   0(R10),C','
         BE    SPACE11
         LA    R11,7(R10)
         BAL   R4,GETDELIM
         B     ERROR
         MVC   SPACEALX,0(R10)
         LA    R10,0(R11)
         CLI   0(R10),C')'
         BNE   SPACE11
         LA    R10,1(R10)
         B     IDENTIFY
SPACE11  MVI   SPACERND,C'R'
         LA    R10,7(R10)
         B     IDENTIFY
         SPACE 1
SPACE12  EQU   *
         CLI   0(R10),X'50'
         BNE   ERROR
         LA    R11,INPUT+92
         BAL   R4,GETDELIM
         B     ERROR
         LA    R9,2
         B     SPACE61
         SPACE 1
MOVEPRI  MVC   SPACEPRI(0),0(R10)
MOVESEC  MVC   SPACESEC(0),0(R10)
MOVEDIR  MVC   SPACEDIR(0),0(R10)
         EJECT
WRITE3   SUBIN SAVE=R4,PREFIX=W
WRITE3SW BC    15,0(,R4)
         MVI   READSW+1,X'F0'
         MVI   LASTPARM,X'00'
         MVI   WRITE3SW+1,X'F0'
         CLI   DSNAME,C' '
         BE    WRITE31
         PUT   SYSUT3,RECORD3
WRITE31  EQU   *
REPORTS2 B     0(0,R4)
         CLI   SYSOUTC,C'L'
         BNE   WRITE315
         CLC   DDNAME,=C'SYSUDUMP'
         BE    WRITE315
         BAL   R4,SYSOUTL
WRITE315 LA    R4,WRITE32
         CLC   UNITTYPE,=C'SYSDA   '
         BNE   WRITE32
         CLI   DSNAME,X'50'
         BE    WRITE32
         CLI   DSNAME,C' '
         BE    WRITE32
         BAL   R4,NONTEMP
WRITE32  CLC   DDNAME(6),=C'SORTWK04'
         BNE   WRITE33
         CLI   SPACEALX,C' '
         BNE   WRITE33
         BAL   R4,NONCONTG
WRITE33  CLC   DDNAME,=C'SORTWK04'
         BNE   WRITE34
         BAL   R4,SORTWK04
WRITE34  CLC   DDNAME,=C'IAMRIGAM'
         BE    WRITE35
         CLC   DDNAME,=C'RIGAM   '
         BNE   WRITE36
WRITE35  CLI   UNITCNT,C' '
         BNE   WRITE36
         BAL   R4,RIGAM
WRITE36  CLI   SPACEALX,C' '
         BNE   WRITE37
         LA    R4,WRITE37
         CLC   DISPSTAT,=C'OLD'
         BE    WRITE37
         CLC   DISPSTAT,=C'SHR'
         BE    WRITE37
         CLC   DISPSTAT,=C'MOD'
         BE    WRITE37
         CLI   DSORG,C'I'
         BE    ISAMSPCE
         CLC   ISAMUSE(5),=C'PRIME'
         BE    ISAMSPCE
         CLC   ISAMUSE(5),=C'INDEX'
         BE    ISAMSPCE
         CLC   ISAMUSE(6),=C'OVFLOW'
         BE    ISAMSPCE
WRITE37  CLC   RECFM(11),=C'           '
         BNE   WRITE38
         CLI   DDDDNAME,C' '
         BNE   WRITE38
         LA    R4,WRITE38
         CLC   DDNAME,=C'SORTIN  '
         BE    SORTDCB
         CLC   DDNAME,=C'SORTOUT '
         BE    SORTDCB
WRITE38  LA    R4,WRITE39
         CLC   DISPSTAT,=C'OLD'
         BNE   WRITE39
         CLC   DDNAME,=C'SORTLIB '
         BE    DISPOLD
         CLC   DDNAME,=C'STEPLIB '
         BE    DISPOLD
         CLC   ISAMUSE(5),=C'PRIME'
         BE    WRITE39
         CLC   ISAMUSE(5),=C'INDEX'
         BE    WRITE39
         CLC   ISAMUSE(6),=C'OVFLOW'
         BE    WRITE39
         CLI   ISAMUSE,C' '
         BNE   DISPOLD
WRITE39  LA    R4,WRITE40
         CLC   UNITTYPE,=C'SYSDA   '
         BE    WRITE40
         CLI   UNITTYPE,C' '
         BE    WRITE40
         CLI   SPACETYP,C' '
         BE    WRITE40
         CLI   VOLSER,C' '
         BNE   WRITE40
         CLC   DISPSTAT,=C'   '
         BE    NONSYSDA
         CLC   DISPSTAT,=C'NEW'
         BE    NONSYSDA
WRITE40  LA    R4,WRITE41
         CLC   UNITTYPE,=CL8'SYSDA'
         BE    WRITE41
         CLI   SPACETYP,C' '
         BE    WRITE41
         CLC   DISPSTAT(17),=C'                 '
         BE    WRITE41
         CLI   SPACERLS,C'R'
         BE    WRITE41
         CLC   ISAMUSE,=C'OVFLOW'
         BE    WRITE41
         CLC   ISAMUSE,=C'PRIME '
         BE    WRITE41
         CLC   ISAMUSE,=C'INDEX '
         BE    WRITE41
         CLC   SPACETYP(5),=C'SPLIT'
         BE    WRITE41
         CLC   SPACETYP(8),=C'SUBALLOC'
         BNE   NORLSE
WRITE41  CLC   DDNAME,=C'SYSABEND'
         BNE   WRITE42
         BAL   R4,SYSABEND
WRITE42  LA    R4,WRITE43
         CLI   SYSOUTF,C' '
         BNE   SPECFORM
         CLI   SYSOUTC,C' '
         BE    WRITE43
         CLI   SYSOUTC,C'A'
         BE    WRITE43
         CLI   SYSOUTC,C'L'
         BNE   SPECFORM
WRITE43  EQU   *
         MVI   DSNAME,C' '
         MVC   DSNAME+1(175),DSNAME
         MVC   STEPNO2,UNPACKED+2
        SUBOUT RESTORE=R4,PREFIX=W
         TITLE 'REPORT ON SYSOUT=L'
SYSOUTL  SUBIN
         MVC   LINE+42(8),DDNAME
         MVC   LINE+56(8),STEPNME2
         MVC   LINE+70(8),PROCNME2
         MVC   LINE+84(8),PROGRAM2
A1       WRITE A1
HEAD1A1  DS    0CL53
         DC    CL18' '
         DC    CL35'USE OF ''SYSOUT=L'''
HEAD2A1  DS    0CL132
         DC    CL43' '
         DC    C'DDNAME       STEPNAME      PROCNAME      PROGRAM'
         DC    CL41' '
         TITLE 'NON-TEMPORARY DATA SETS ON SYSDA'
INDATA   SUBIN
NONTEMP  SUBIN
         MVC   LINE+19(8),PROCNME2
         MVC   LINE+35(8),STEPNME2
         MVC   LINE+51(44),DSNAME
         MVC   LINE+85(10),MEMBR
         MVC   LINE+103(3),=C'CYL'
         CLI   SPACETYP,C'C'
         BE    NONTEMP1
         MVC   LINE+103(3),=C'TRK'
         CLI   SPACETYP,C'T'
         BE    NONTEMP1
         MVC   LINE+103(5),=C'ABSTR'
         CLI   SPACETYP,C'A'
         BE    NONTEMP1
         MVC   LINE+103(6),=C'BLOCKS'
NONTEMP1 MVC   LINE+111(3),SPACEPRI
A2       WRITE A2
HEAD1A2  DS    0CL53
         DC    CL8' '
         DC    CL45'NON-TEMPORARY DATA SETS ASSIGNED TO SYSDA'
HEAD2A2  DS    0CL132
         DC    CL19' '
         DC    C'PROCNAME        STEPNAME        '
         DC    C'*----------------- DSNAME -----------------*'
         DC    C'        *- SPACE -*'
         DC    CL18' '
         TITLE 'REPORT ON NON-CONTIGUOUS SORT WORK AREAS'
NONCONTG SUBIN
         MVC   LINE+43(8),DDNAME
         MVC   LINE+59(8),STEPNME2
         MVC   LINE+75(8),PROCNME2
A3       WRITE A3
HEAD1A3  DS    0CL53
         DC    CL12' '
         DC    CL41'NON-CONTIGUOUS SORT WORK AREAS'
HEAD2A3  DS    0CL132
         DC    CL44' '
         DC    C'DDNAME         STEPNAME        PROCNAME'
         DC    CL49' '
         TITLE 'REPORT ON USE OF SORTWK04'
SORTWK04 SUBIN
         MVC   LINE+42(8),STEPNME2
         MVC   LINE+60(8),PROCNME2
         MVC   LINE+86(3),SPACEPRI
         MVC   LINE+78(3),=C'CYL'
         CLI   SPACETYP,C'C'
         BE    SORTWK4
         MVC   LINE+78(3),=C'TRK'
         CLI   SPACETYP,C'T'
         BE    SORTWK4
         MVC   LINE+78(5),=C'ABSTR'
         CLI   SPACETYP,C'A'
         BE    SORTWK4
         MVC   LINE+78(6),=C'BLOCKS'
SORTWK4  EQU   *
A4       WRITE A4
HEAD1A4  DS    0CL53
         DC    CL19' '
         DC    CL34'USE OF SORTWK04'
HEAD2A4  DS    0CL132
         DC    CL42' '
         DC    C'STEPNAME          PROCNAME          *- SPACE -*'
         DC    CL43' '
         TITLE 'REPORT ON US E OF RIGAM WITHOUT '',P'' OR '',N'''
RIGAM    SUBIN
         MVC   LINE+13(8),PROCNME2
         MVC   LINE+29(8),STEPNME2
         MVC   LINE+45(8),PROGRAM2
         MVC   LINE+61(8),DDNAME
         MVC   LINE+77(44),DSNAME
A5       WRITE A5
HEAD1A5  DS    0CL53
         DC    CL11' '
         DC    CL42'USE OF RIGAM WITHOUT '',P'' OR '',N'''
HEAD2A5  DS    0CL132
         DC    CL13' '
         DC    C'PROCNAME        STEPNAME        PROGRAM         '
         DC    C' DDNAME         *----------------- DSNAME -----------'
         DC    C'------*'
         DC    CL11' '
         TITLE 'NON-CONTIGUOUS SPACE FOR NEW ISAM DATA SETS'
ISAMSPCE SUBIN
         MVC   LINE+14(8),PROCNME2
         MVC   LINE+27(8),STEPNME2
         MVC   LINE+40(8),PROGRAM2
         MVC   LINE+53(8),DDNAME
         MVC   LINE+69(44),DSNAME
         MVC   LINE+122(6),ISAMUSE
A6       WRITE A6
HEAD1A6  DS    0CL53
         DC    C'USE OF NON-CONTIGUOUS SPACE WITH NEW ISAM DATA SETS  '
HEAD2A6  DS    0CL132
         DC    CL10' '
         DC    C'    PROCNAME     STEPNAME     PROGRAM      '
         DC    C' DDNAME         *----------------- DSNAME -----------'
         DC    C'------*        ISAM USE   '
         TITLE 'REPORT ON SORTIN / SORTOUT WITHOUT DCB'
SORTDCB  SUBIN
         MVC   LINE+21(8),PROCNME1
         MVC   LINE+37(8),STEPNME2
         MVC   LINE+53(8),DDNAME
         MVC   LINE+69(44),DSNAME
         MVC   LINE+111(4),RELGENNO
         MVC   LINE+123(8),UNITTYPE
A7       WRITE A7
HEAD1A7  DS    0CL53
         DC    CL9' '
         DC    CL44'USE OF SORTIN / SORTOUT WITHOUT DCB'
HEAD2A7  DS    0CL132
         DC    CL21' '
         DC    C'PROCNAME        STEPNAME         DDNAME         '
         DC    C'*----------------- DSNAME -----------------*'
         DC    CL19'        UNIT TYPE  '
         TITLE 'USE OF DISP=OLD WITH LIBRARIES'
DISPOLD  SUBIN
         MVC   LINE+14(8),PROCNME2
         MVC   LINE+27(8),STEPNME2
         MVC   LINE+40(8),PROGRAM2
         MVC   LINE+53(8),DDNAME
         MVC   LINE+69(44),DSNAME
         MVC   LINE+121(8),MEMBR
A8       WRITE A8
HEAD1A8  DS    0CL53
         DC    CL10' '
         DC    CL43'USE OF ''DISP=OLD'' WITH LIBRARIES'
HEAD2A8  DS    0CL132
         DC    CL10' '
         DC    C'    PROCNAME     STEPNAME     PROGRAM      '
         DC    C' DDNAME         *----------------- DSNAME -----------'
         DC    C'------*         MEMBER    '
         TITLE 'NON-SPECIFIC NON-SYSDA SPACE REQUESTS'
NONSYSDA SUBIN
         MVC   LINE+15(8),UNITTYPE
         MVC   LINE+25(8),PROCNME2
         MVC   LINE+38(8),STEPNME2
         MVC   LINE+51(8),PROGRAM2
         MVC   LINE+64(8),DDNAME
         MVC   LINE+77(44),DSNAME
         MVC   LINE+111(10),MEMBR
A9       WRITE A9
HEAD1A9  DS    0CL53
         DC    CL8' '
         DC    CL45'NON-SPECIFIC NON-SYSDA SPACE REQUESTS'
HEAD2A9  DS    0CL132
         DC    CL10' '
         DC    C'   UNIT TYPE   '
         DC    C'PROCNAME     STEPNAME     PROGRAM      '
         DC    C' DDNAME      *----------------- DSNAME -----------'
         DC    C'------*'
         DC    CL11' '
         TITLE 'ABSENCE OF RLSE IN SPACE REQUESTS'
NORLSE   SUBIN
         MVC   LINE+13(8),PROCNME2
         MVC   LINE+29(8),STEPNME2
         MVC   LINE+45(8),PROGRAM2
         MVC   LINE+60(8),DDNAME
         MVC   LINE+76(44),DSNAME
         MVC   LINE+110(10),MEMBR
         MVC   LINE+123(1),SPACETYP
         MVC   LINE+125(3),SPACEPRI
B1       WRITE B1
HEAD1B1  DS    0CL53
         DC    CL6' '
         DC    CL47'NON-WORK AREA SPACE REQUESTS WITHOUT ''RLSE'''
HEAD2B1  DS    0CL132
         DC    CL13' '
         DC    C'PROCNAME        STEPNAME        PROGRAM        '
         DC    C' DDNAME         *----------------- DSNAME -----------'
         DC    C'------*'
         DC    CL12'   SPACE    '
         TITLE 'REPORT ON USE OF ''SYSABEND'''
SYSABEND SUBIN
         MVC   LINE+46(8),PROCNME2
         MVC   LINE+62(8),STEPNME2
         MVC   LINE+78(8),PROGRAM2
B2       WRITE B2
HEAD1B2  DS    0CL53
         DC    CL18' '
         DC    CL35'USE OF ''SYSABEND'''
HEAD2B2  DS    0CL132
         DC    CL46' '
         DC    C'PROCNAME        STEPNAME        PROGRAM'
         DC    CL47' '
         TITLE 'REPORT ON USE OF SPECIAL FORMS'
SPECFORM SUBIN
         MVC   LINE+26(8),PROCNME2
         MVC   LINE+42(8),STEPNME2
         MVC   LINE+58(8),PROGRAM2
         MVC   LINE+74(8),DDNAME
         MVC   LINE+92(1),SYSOUTC
         MVC   LINE+103(4),SYSOUTF
B3       WRITE B3
HEAD1B3  DS    0CL53
         DC    CL4' '
         DC    CL49'USE OF SPECIAL FORMS AND SPECIAL QUEUES'
HEAD2B3  DS    0CL132
         DC    CL26' '
         DC    C'PROCNAME        STEPNAME        PROGRAM         '
         DC    C' DDNAME         CLASS        FORM'
         DC    CL25' '
         TITLE 'REPORT ON USE OF ''PARM=''CORE'' FOR IERCO00'
IERRCO00 SUBIN
         MVC   LINE+50(8),PROCNME1
         MVC   LINE+62(8),STEPNME1
         MVC   LINE+82(5),SORTCORE
B4       WRITE B4
HEAD1B4  DS    0CL53
         DC    CL15' '
         DC    CL38'USE OF ''PARM=''CORE'' FOR IERRCO00'
HEAD2B4  DS    0CL132
         DC    CL50' '
         DC    C'PROCNAME        STEPNAME        VALUE'
         DC    CL45' '
         TITLE ' '
ERRORTME SUBIN
         MVC   REASON(12),TIMEMESS
ERROR    EQU   *
         CLC   PROCNME2,=C'BRDR    '
         BE    READ
         CLC   PROCNME2,=C'STHASP30'
         BE    READ
         CLC   PROCNME2,=C'STRTHASP'
         BE    READ
         TIME  DEC
         STM   R0,R15,REGISTRS
         PUT   SNAPDUMP,CCA
         MVC   REASON,SPACES
         MVC   REASON(5),=C'ERROR'
         B     READ
         SPACE 1
SNAPDCB  EQU   *
         MVC   REASON(8),=C'DCB SNAP'
         B     SNAPRTN
SNAP     EQU   *
         MVC   REASON(8),=C'SNAP    '
SNAPRTN  EQU   *
         CLC   PROCNME2,=C'BRDR    '
         BE    0(R4)
         CLC   PROCNME2,=C'STHASP30'
         BE    0(R4)
         CLC   PROCNME2,=C'STRTHASP'
         BE    0(R4)
         TIME  DEC
         STM   R0,R15,REGISTRS
         PUT   SNAPDUMP,CCA
         MVC   REASON,SPACES
         MVC   REASON(5),=C'ERROR'
         BR    R4
         SPACE 1
SNAPDUMP DCB   DDNAME=SNAPDUMP,                                        *
               DSORG=PS,                                               *
               LRECL=488,                                              *
               MACRF=PM,                                               *
               RECFM=FB
         EJECT
ENDODATA EQU   *
        TTIMER CANCEL
REPORTS3 BC    15,END2
         CLOSE (PRINTA1)
         CLOSE (PRINTA2)
         CLOSE (PRINTA3)
         CLOSE (PRINTA4)
         CLOSE (PRINTA5)
         CLOSE (PRINTA6)
         CLOSE (PRINTA7)
         CLOSE (PRINTA8)
         CLOSE (PRINTA9)
         CLOSE (PRINTB1)
         CLOSE (PRINTB2)
         CLOSE (PRINTB3)
         CLOSE (PRINTB4)
END2     EQU   *
         CLOSE (SYSUT1)
         CLOSE (SYSUT2)
         CLOSE (SYSUT3)
         CLOSE (SNAPDUMP)
        PGMOUT
         EJECT
KEYFIND  EQU   *
         LA    10,INPUT+22
         LA    11,INPUT+92
KEYFIND2 CLC   0(4,10),KEYWORD
         BE    0(4)
         LA    10,1(10)
         CR    10,11
         BNL   4(4)
         B     KEYFIND2
         SPACE 5
GETDELIM EQU   *
         BCTR  11,0
         SR    11,10
         LTR   11,11
         BZ    ERROR
         EX    11,TRTDELIM
         BZ    0(4)
         STC   2,DELIMITR
         LR    11,1
         LR    9,1
         SR    9,10
         BCTR  9,0
         LA    R9,0(R9)
         B     4(4)
         SPACE 1
TRTDELIM TRT   0(0,10),DELIMTAB
         SPACE 1
DELIMTAB DC    64X'00'
         DC    X'40'
         DC    12X'00'
         DC    X'4D'
         DC    15X'00'
         DC    X'5D'
         DC    13X'00'
         DC    X'6B'
         DC    148X'00'
         SPACE 5
CHECKEX  EQU   *
         LTR   R9,R9
         BM    ERROR
         BR    R4
         EJECT
CHECKEX2 EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD2
         BH    ERROR
         B     CHECKEX
CHECKEX3 EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD3
         BH    ERROR
         B     CHECKEX
CHECKEX4 EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD4
         BH    ERROR
         B     CHECKEX
CHECKEX5 EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD5
         BH    ERROR
         B     CHECKEX
CHECKEX6 EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD6
         BH    ERROR
         B     CHECKEX
CHECKEX7 EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD7
         BH    ERROR
         B     CHECKEX
CHECKEX8 EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD8
         BH    ERROR
         B     CHECKEX
CHECK10  EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD10
         BH    ERROR
         B     CHECKEX
CHECK19  EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD19
         BH    ERROR
         B     CHECKEX
CHECK44  EQU   *
         LA    R9,0(R9)
         C     R9,FULLWD44
         BH    ERROR
         B     CHECKEX
         EJECT
SYSUT1   DCB   DDNAME=SYSUT1,                                          *
               DSORG=PS,                                               *
               LRECL=72,                                               *
               MACRF=GM,                                               *
               RECFM=FB,                                               *
               EODAD=ENDODATA
         SPACE 3
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               LRECL=26,                                               *
               MACRF=PM,                                               *
               RECFM=FB
         SPACE 3
SYSUT3   DCB   DDNAME=SYSUT3,                                          *
               DSORG=PS,                                               *
               LRECL=208,                                              *
               MACRF=PM,                                               *
               RECFM=FB
         EJECT
         DS    0F
CCA      EQU   *
REASON   DC    CL16'ERROR'
         DC    A(CCA)
         DC    A(RECORD1)
         DC    A(RECORD2)
         DC    A(RECORD3)
REGISTRS DS    16F
         DC    CL8'INPUT'
INPUT    DC    CL133' '
RECORD1  EQU   INPUT+19
         SPACE 1
         DC    CL8'RECORD2'
RECORD2  DS    0CL26
PROCNME1 DC    CL8' '
STEPNME1 DC    CL8' '
PROGRAM1 DC    CL8' '
STEPNO1  DC    CL2' '
         SPACE 1
         DC    CL8'RECORD3'
RECORD3  DS    0CL208
PROCNME2 DC    CL8' '
STEPNME2 DC    CL8' '
PROGRAM2 DC    CL8' '
DDNAME   DC    CL8' '
DSNAME   DC    CL44' '
STEPNO2  DC    CL2' '
MEMBR    DS    0CL10               'MEMBER'
RELGENNO DC    CL4' '
ISAMUSE  DC    CL6' '              PRIME/INDEX/OVFLOW
DISPSTAT DC    CL3' '              (STATUS,
DISPDISP DC    CL7' '                     ,DISPOSITION,
DISPCOND DC    CL7' '                                 ,COND DISP)
DDDATA   DS    0CL4                DATA
DDASTRSK DS    0CL1                *
DDDUMMY  DS    0CL5                DUMMY
DDDYNAM  DS    0CL5                DYNAM
DDDDNAME DC    CL8' '              'DDNAME'
CHANAFF  DS    0CL1                A
CHANSEP  DC    CL1' '              S
DCBREFBK DC    CL19' '             'REFERENCE'
RECFM    DC    CL3' '
LRECL    DC    CL4' '
BLKSIZE  DC    CL4' '
BUFNO    DC    CL2' '
OPTCD    DC    CL6' '
DSORG    DC    CL3' '
CYLOFL   DC    CL2' '
KEYLEN   DC    CL3' '
RKP      DC    CL3' '
LABLNO   DC    CL2' '
LABLTYPE DC    CL3' '
LABLPSWD DC    CL1' '              P - PASSWORD / N - NOPWREAD
LABLIO   DC    CL1' '              I - IN / O - OUT
LABLDATE DC    CL5' '              'YYDDD'/'NNNN '
SPACETYP DC    CL1' '              A - ABSTR, B - BLOCK-LENGTH
*                                  C - CYL,   T - TRK
SPACEPRI DC    CL3' '
SPACESEC DC    CL3' '
SPACEDIR DC    CL2' '
SPACERLS DC    CL1' '              R - RLSE
SPACEALX DC    CL1' '              C - CONTIG, M - MXIG,  A - ALX
SPACERND DC    CL1' '              R - ROUND
SPLIT    EQU   SPACETYP            SPLIT
SUBALLOC EQU   SPACETYP            SUBALLOC
SYSOUTC  DC    CL1' '              'CLASSNAME'
SYSOUTF  DC    CL4' '              'FORMS TYPE'
UNITTYPE DC    CL8' '
UNITCNT  DC    CL1' '              'N' / P
UNITDEF  DC    CL1' '              D - DEFER
UNITSEP  DS    0CL1                S- SEP
UNITAFF  DC    CL1' '              A - AFF
VOLPRIV  DC    CL1' '              P - PRIVATR
VOLRET   DC    CL1' '              R - RETAIN
VOLSEQ   DC    CL1' '              'N'
VOLCNT   DC    CL1' '              'N'
VOLSER   DS    0CL1                S
VOLREF   DC    CL1' '              R
         SPACE 1
LASTPARM DC    X'00'
*        BIT 0 SPACE=(CYL,
*        BIT 1 SPACE=(CYL,(N,N),
*        BIT 2 DISP=(OLD,KEEP,
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
         SPACE 5
         DC    C'PRECAUTIONARY BUFFER'
INDEX    DC    C'INDEX)'
PRIME    DC    C'PRIME)'
OVFLOW   DC    C'OVFLOW)'
SPACES   DC    CL30' '
MEMBER   DC    C'MEMBER NAME '
SLASH    DC    C'//'
EXEC     DC    C'EXEC'
DD       DC    C' DD '
DSN      DC    C'DSN='
DSNA     DC    C'DSNA'
KEYWORD  DC    CL4' '
PGM      DC    C'PGM='
STEPNO   DC    PL2'0'
ONE      DC    PL2'1'
ZERO     DC    PL2'0'
FIFTY5   DC    PL2'55'
LINE     DC    CL132' '
BLANKLNE DC    CL132' '
STDSNAME DC    CL44' '
SORTSTEP DC    CL8' '
EDITMASK DC    X'40202120'
UNPACKED DC    CL4' '
DELIMITR DC    C' '
DATEFLD  DC    CL9'COBOLDATE'
DATEADDR DC    V(DTERTN)
ACTUALDT DC    V(JPDATE)
ADATEFLD DC    A(DATEFLD)
HEAD0    DC    C'PROCEDURE ANALYSIS'
REPORT   DC    C'REPORT'
PAGEHEAD DC    C'PAGE'
PAGEEDIT DC    X'40202021'
ENDOCARD DC    CL1' '
TYMLIMIT DS    0D
         DC    C'00002000'   20 SECOND LIMIT / PROC
FULLWD2  DC    F'1'
FULLWD3  DC    F'2'
FULLWD4  DC    F'3'
FULLWD5  DC    F'4'
FULLWD6  DC    F'5'
FULLWD7  DC    F'6'
FULLWD8  DC    F'7'
FULLWD10 DC    F'9'
FULLWD19 DC    F'18'
FULLWD44 DC    F'43'
TIMEMESS DC    C'TIME EXPIRED'
SORTCORE DC    CL5' '              SORT CORE REQUEST FOR IERRCO00
         SPACE 1
         LTORG
         SPACE 1
FINISH   EQU   *
         END
