SIZEUP   CSECT
UT427    EQU   *
*
*        REGISTERS EQUATES
*
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         STM   R14,R12,12(R13)     SAVE REGISTERS
         BALR  R12,0               INIT BASE REGISTER
         USING *,R12
         L     R4,0(R1)            GET ARG. ADDR
         SR    R3,R3               INIT PROGRAM SIZE TO ZERO
         GETMAIN VC,LA=SIZLIM,A=ADDRESS  GET BIGGEST FREE AREA
         LTR   R15,R15             TEST RETURN CODE
         BNZ   OUT2                BRANCH IF GETMAIN FAILED
         L     R6,SIZE             SAVE AREA SIZE
         FREEMAIN E,LV=(R6),A=ADDRESS  FREE GETMAINED AREA
         L     R1,16               GET CVT
         TM    116(R1),X'10'       TEST IF UNDER MVS
         BNZ   MVS                 BRANCH IF UNDER MVS
         L     R1,0(R1)            GET TCB PTR
         L     R1,4(R1)            GET TCB
         L     R1,24(R1)           GET BDRY BOX
         LM    R2,R3,4(R1)         GET PARTITION LIMITS
         SR    R3,R2               GET PARTITION SIZE
         B     OUT
MVS      EQU   *
         L     R1,548              GET ASCB
         L     R1,48(R1)           GET LDA
         L     R3,1468(R1)         GET 'EXPLICIT' REGION SIZE
OUT      EQU   *
         SR    R3,R6               R3 = REG.SIZE - BIGGEST FREE AREA
OUT2     EQU   *
         CVD   R3,NUMWRK           CONVERT PROGRAM SIZE
*
         OI    NUMWRK+7,X'0F'
         UNPK  0(8,R4),NUMWRK+4(4) UNPACK RESULT TO CALLER'S AREA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
*
SIZLIM   DC    F'8'                MIN SIZE
         DC    F'16000000'         MAX SIZE
NUMWRK   DS    D                   WORKAREA
         ORG NUMWRK
ADDRESS  DS    F                   ADDR OF GETMAINED AREA
SIZE     DS    F                   SIZE OF GETMAINED AREA
*
LENGTH   EQU   *-SIZEUP            LENGTH OF ROUTINE
         END
