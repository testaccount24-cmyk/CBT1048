000100 ID DIVISION.
000200 PROGRAM-ID. T586P17.
000300 AUTHOR. J.P.RAINSON FROM AN ADR USER  PROGRAM BY EARL RYAN.
000400 DATE-WRITTEN. SEPTEMBER 76.
000500 DATE-COMPILED.
000600*REMARKS.    CREATION OF OUTPUT FILE CONTAINING A RECORD
000700*            FOR EVERY PROGRAM ON LIBRARIAN LIBRARY.
000800*                1. LIBRARY READ BASE ON CTL CARDS.
000900*                2. SOURCE LANGUAGE FROM HISTORY CARDS.
001000*                3. ALIAS NAMES FROM HISTORY CARDS.
001100*                4. SYSTEM NAME FROM HISTORY CARDS. NOT PRINTED.
001200
001300*               SUBROUTINES USED - ADR FAIR ROUTINES
001400*                   MASTOPN - MASTMOD - MASTHST - MASTCLS.
001500*                   MONSANTO UT458 UNIVERSAL DATE ROUTINE.
001600
001700 ENVIRONMENT DIVISION.
001800 CONFIGURATION SECTION.
001900 SOURCE-COMPUTER. IBM-370.
002000 OBJECT-COMPUTER. IBM-370.
002100 INPUT-OUTPUT SECTION.
002200 FILE-CONTROL.
002300     SELECT INDEX-FILE ASSIGN TO UT-S-INDEXREC.
002400     SELECT SYSTEM-FILE ASSIGN TO UT-S-SYSIN.
002500 DATA DIVISION.
002600 FILE SECTION.
002700 FD  INDEX-FILE
002800     RECORDING MODE IS F
002900     LABEL RECORDS ARE STANDARD
003000     RECORD CONTAINS 170 CHARACTERS
003100     BLOCK CONTAINS 0 RECORDS
003200     DATA RECORD IS INDEXREC.
003300 01  INDEXREC.
003400     05 MODULE-NAME     PIC X(8).
003500     05 MODULE-INPUT-AREA.
003600       10 PASS-WORD      PIC X(4).
003700       10 DATE-ADDED.
003800         15 MONTH-ADDED PIC XX.
003900         15 DAY-ADDED   PIC XX.
004000         15 YEAR-ADDED  PIC XX.
004100       10 LAST-UPDATED.
004200         15 MONTH-UPDATED PIC XX.
004300         15 DAY-UPDATED PIC XX.
004400         15 YEAR-UPDATED PIC XX.
004500         15 TIME-UPDATED PIC X(4).
004600       10 NUMBER-OF-CARDS PIC S9(5) USAGE COMP-3.
004700       10 PROGRAMMER.
004800         15 PGMR         PIC X(10).
004900       10  FILLER        PIC XX.
005000       10  TEAM          PIC XXX.
005100       10 PROC-PARM     PIC X(8).
005200       10 DESCRIPTION   PIC X(30).
005300       10 SEQ-COL-X     PIC X.
005400       10 SEQ-LENGTH-X  PIC X.
005500       10 SEQ-INCREMENT PIC 9(4) USAGE COMPUTATIONAL.
005600       10 SEQ-START-VAL PIC 9(4) USAGE COMPUTATIONAL.
005700       10  ACCESSES      PIC 9(4) COMP.
005800       10  UPDATES       PIC 9(4) COMP.
005900       10  EXTENTS       PIC X.
006000       10  BLOCKS        PIC X.
006100       10  LAST-ACCESS   PIC X(6).
006200       10  FILLER        PIC X(6).
006300     05  MODULE-EXTEND.
006400       10  ALIAS1        PIC X(8).
006500       10  ALIAS2        PIC X(8).
006600       10  SLANG         PIC X(5).
006700       10  SYSTEM        PIC X(8).
006800       10  LIBRARY       PIC X(8).
006900       10  SETSSI.
007000         15  SETSSI-YEAR PIC XX.
007100         15  SETSSI-NBR  PIC X(5).
007200       10  SETSSI-NUM  REDEFINES  SETSSI  PIC 9(7).
007300       10  FILLER        PIC X(8).
007400     05  REP-FIELDS.
007500       10  REP-KEY       PIC X(8).
007600       10  FILLER        PIC X(2).
007700 FD  SYSTEM-FILE
007800         RECORDING MODE IS F
007900         LABEL RECORDS ARE STANDARD
008000         RECORD CONTAINS 80 CHARACTERS
008100         BLOCK CONTAINS 0 RECORDS
008200         DATA RECORD IS SYSTEM-REC.
008300 01  SYSTEM-REC.
008400     03  SR-ID           PIC X(4).
008500     03  SR-SYSTEM       PIC X(8).
008600     03  FILLER          PIC X(68).
008700 WORKING-STORAGE SECTION.
008800 77  RETCD               PIC 9.
008900 77  TPHDR               PIC X.
009000 01  HST-AREA.
009100     03  FILLER          PIC X(5) VALUE '0'.
009200     03  HST-ID          PIC X(6).
009300     03  FILLER          PIC X.
009400     03  HST-SYS.
009500         05  FILLER      PIC X.
009600         05 HST-SYSTEM   PIC X(8).
009700     03  HST-AL REDEFINES HST-SYS.
009800         05  HST-ALIAS   PIC X(8).
009900         05  FILLER      PIC X.
010000     03  HST-SL REDEFINES HST-AL.
010100         05  HST-SLANG   PIC X(5).
010200         05  FILLER      PIC X(4).
010300     03  FILLER          PIC X(54).
010400     03  HST-NBR         PIC X(5).
010500     03  HST-DATE-PLACED.
010600         05  HST-MONTH   PIC XX.
010700         05  FILLER      PIC X.
010800         05  HST-DAY     PIC XX.
010900         05  FILLER      PIC X.
011000         05  HST-YEAR    PIC XX.
011100 PROCEDURE DIVISION.
011200     OPEN INPUT SYSTEM-FILE.
011300     OPEN OUTPUT INDEX-FILE.
011400 0002-READ-CTL-RECORD.
011500     READ SYSTEM-FILE AT END GO TO 0910-SET-GOOD-RETURN.
011600     IF SR-ID NOT = 'LIB '
011700         GO TO 0002-READ-CTL-RECORD.
011800 0005-OPEN-MODULE-LIBRARY.
011900     CALL 'MASTOPN' USING TPHDR, RETCD, SR-SYSTEM.
012000     IF RETCD EQUAL TO ZERO GO TO 0007-GET-A-MODULE.
012100     DISPLAY '** T586P17 - INVALID LIBRARY -  ' SR-SYSTEM.
012200     GO TO 0002-READ-CTL-RECORD.
012300 0007-GET-A-MODULE.
012400     MOVE SPACES TO INDEXREC.
012500     CALL 'MASTMOD' USING MODULE-NAME, RETCD, MODULE-INPUT-AREA.
012600     IF RETCD = ZERO GO TO 0072-GET-HSTS.
012700     IF RETCD EQUAL TO 1 GO TO 0090-CLOSE-MASTER.
012800     GO TO 0900-SET-BAD-RETURN.
012900 0072-GET-HSTS.
013000     MOVE  SR-SYSTEM  TO  LIBRARY.
013100 0072-GET-HSTS-LOOP.
013200     CALL 'MASTHST' USING HST-AREA, RETCD.
013300     IF RETCD NOT = ZERO GO TO 0080-WR-INDEXREC.
013400     IF  HST-NBR  NOT = SPACES
013500         MOVE  HST-YEAR  TO  SETSSI-YEAR
013600         MOVE  HST-NBR   TO  SETSSI-NBR.
013700     IF HST-ID = 'SYSTEM'
013800          MOVE HST-SYSTEM TO SYSTEM.
013900     IF HST-ID = 'SLANG'
014000         MOVE HST-SLANG TO SLANG.
014100     IF HST-ID = 'ALIAS '
014200         AND ALIAS1 = SPACES
014300         MOVE HST-ALIAS TO ALIAS1
014400         GO TO 0072-GET-HSTS-LOOP.
014500     IF HST-ID = 'ALIAS '
014600         AND ALIAS1 NOT = SPACES
014700         MOVE HST-ALIAS TO ALIAS2.
014800     GO TO 0072-GET-HSTS-LOOP.
014900 0080-WR-INDEXREC.
015000     EXAMINE  SETSSI  REPLACING  ALL  ' '  BY  '0'.
015100     IF  SETSSI-NUM  IS NOT NUMERIC
015200         MOVE  ZERO  TO  SETSSI-NUM.
015300     MOVE  MODULE-NAME  TO  REP-KEY.
015400     WRITE INDEXREC.
015500     GO TO 0007-GET-A-MODULE.
015600 0090-CLOSE-MASTER.
015700     CALL 'MASTCLS'.
015800     GO TO 0002-READ-CTL-RECORD.
015900 0900-SET-BAD-RETURN.
016000     MOVE 16 TO RETURN-CODE.
016100     GO TO  0920-EOJ.
016200 0910-SET-GOOD-RETURN.
016300     MOVE 0 TO RETURN-CODE.
016400 0920-EOJ.
016500     CLOSE SYSTEM-FILE.
016600     CLOSE INDEX-FILE.
016700     STOP RUN.
016800 9999-EXIT-EOJ.
016900     EXIT.
