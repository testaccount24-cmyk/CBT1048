         TITLE 'GENERATE TEST CARDS'
         START 0
BEGIN    SAVE  (14,12),T
         BALR  12,0
         USING *,12                     AS BASE REGISTER
         USING IHADCB,11                FOR DCB DSECT
         USING CRDSEC,7                 FOR OUTPUT DSECT
         L     2,0(0,1)                 LOAD X2 WITH ADDRESS OF PARM
         CLC   0(2,2),ZERO             COMPARE LENGTH TO ZERO
         BE    NOOVRIDE                IF EQUAL 500 LIMIT APPLIES
         CLC   0(2,2),BIN4             COMPARE LENGTH TO FOUR
         BH    ABORT                   IF GREATER ABEND
         L     3,=F'111'                LOAD 3 TO BINARY 111
         AH    3,0(0,2)                ADD PARM LENGTH TO 3
         STC   3,PACKER+1              STORE LENGTH BYTE
         A     2,BIN2                  SET ADDRESS TO DATA AREA
PACKER   PACK  ALIGN,0(0,2)            PACK PARM DATA
         MVC   MAX,ALIGN+4             MOVE TO LIMIT
NOOVRIDE ST    13,SV+4                  STORE SUPVIS SAVE ADDRESS
         LA    13,SV                    LOAD PROGRAM SAVE ADDRESS
         LA    11,INDCB                 LOAD INPUT DCB IN X11 FOR DCBD
         OPEN  (INDCB,(INPUT,REREAD),OUTDCB,(OUTPUT))
INCOME   GET   INDCB
         LR    3,1                      LOAD BUFFER ADDRESS INTO X3
         LR    4,3                           AND X4
         LH    5,DCBLRECL               LOAD RECORD LENGTH INTO X5
         AR    5,3                      ADD LENGTH TO BUFFER ADDRESS
LOOP     A     4,SEVENTY                ADD 70 TO X4
         LR    6,5                      LOAD X6 WITH ADDRESS OF END OF
         PUT   OUTDCB                     DATA IN INPUT BUFFER
         LR    7,1                      LOAD X7 WITH ADDRESS OF OUT BUF
         SR    6,4                      SUB INC+70 FROM END ADDRESS
         UNPK  UNPCK,SEQ                UNPACK SERIAL NUMBER
         MVC   CRDSEQ,UNPCK+4           PUT SERIAL NUMBER IN OUT CARD
         OI    CRDSEQ+3,X'F0'           MAKE SEQUENCE NO UNSIGNED
         MVC   WASTED,BLANK             PUT SPACES IN UNUSED FIELD
         AP    CRDCT,ONE                ADD 1 TO CARD COUNT
         AP    SEQ,TEN                  ADD 10 TO SERIAL NUMBER
         C     6,ZERO                   COMPARE X6 TO ZEROES
         BNH   LASTCD                   BRANCH IF LESS OR EQUAL
         MVC   OPCOD,BLANK              PUT SPACES IN OPERATION
         MVC   CHARCT,COLUMNS           PUT 70 IN COUNT FIELD
         MVC   CRDDATA(70),0(3)         MOVE DATA FROM IN BUF TO OUTPUT
         A     3,SEVENTY                ADD 70 TO INC
         B     LOOP                     RETURN TO PROCESS LOOP
LASTCD   A     6,SEVENTY                ADD 70 TO X6
         MVC   OPCOD,WRTCOD             PUT WRITE CODE IN OPERATION
         MVC   CRDDATA(1),BLANK         PUT SPACES IN 70 BYTE DATA AREA
         LR    9,6                      LOAD X9 FROM X6
         S     9,BIN1                   REDUCE X9 BY ONE
         MVC   CRDDATA+1(69),CRDDATA          OF OUTPUT BUFFER
         STC   9,*+5                    MOVE LENGTH FROM X9 TO NEXT INS
         MVC   CRDDATA,0(3)             MOVE DATA FROM IN BUF TO OUTPUT
         CVD   6,ALIGN                  CONVERT LENGTH TO DECIMAL
         UNPK  DECIMAL,ALIGN            UNPACK LENGTH
         MVC   CHARCT,DECIMAL+2         PUT RESULT IN COUNT FIELD
         OI    CHARCT+1,X'F0'           MAKE COUNT FIELD UNSIGNED
         CP    CRDCT,MAX                IS CARD COUNT OVER LIMIT
         BNL   EOFIN                    YES - GO TO END ROUTINE
         B     INCOME                   NO - GET NEXT RECORD
EOFIN    CLOSE (INDCB,REREAD,OUTDCB,LEAVE)
         FREEPOOL INDCB                 RETURN POOLS TO SUPERVISOR
         FREEPOOL OUTDCB
         L     13,SV+4                  RETURN TO SUPERVISOR
         RETURN (14,12)
ABORT    ABEND 4,STEP                  PARM DATA IN ERROR
SV       DS    18F
         DS    0D
ALIGN    DS    PL8
DECIMAL  DS    ZL4
UNPCK    DS    ZL8
SEVENTY  DC    F'70'
BIN1     DC    F'1'
ZERO     DC    F'0'
BIN2     DC    F'2'
TEN      DC    PL4'10'
ONE      DC    PL4'1'
MAX      DC    PL4'500'
CRDCT    DC    PL4'0'
SEQ      DC    PL4'10'
COLUMNS  DC    CL2'70'
BLANK    DC    CL2'  '
WRTCOD   DC    CL2'WT'
BIN4     DC    H'4'
INDCB    DCB   DSORG=PS,MACRF=(GL),RECFM=U,BLKSIZE=10000,BFTEK=S,      A
               BUFNO=2,BFALN=F,EODAD=EOFIN,EROPT=ABE,DDNAME=TESTDATA
OUTDCB   DCB   DSORG=PS,MACRF=(PL),RECFM=F,LRECL=80,BLKSIZE=80,        B
               BFTEK=S,BUFNO=3,BFALN=F,EROPT=ABE,DDNAME=DSGCARDS
         DCBD  DSORG=(LR)
CRDSEC   DSECT
CRDSEQ   DS    CL4
OPCOD    DS    CL2
CHARCT   DS    CL2
WASTED   DS    CL2
CRDDATA  DS    XL70
         END   BEGIN
