      *                                                                   000010
      *                                                                   000020
       IDENTIFICATION DIVISION.                                           000030
      *------------------------                                           000040
      *------------------------                                           000050
       PROGRAM-ID. HIERARCH.                                              000060
       AUTHOR. MSALMON.                                                   000070
       DATE-WRITTEN. 5AUG76.                                              000080
      *                                                                   000090
       REMARKS. TO PRINT A HIERARCHY CHART FROM CONTROL CARDS.            000100
      *         ----------------------------------------------            000110
      *                                                                   000120
       ENVIRONMENT DIVISION.                                              000130
      *---------------------                                              000140
      *---------------------                                              000150
      *                                                                   000160
       CONFIGURATION SECTION.                                             000170
       SOURCE-COMPUTER. IBM-370.                                          000180
       OBJECT-COMPUTER. IBM-370.                                          000190
      *                                                                   000200
       INPUT-OUTPUT SECTION.                                              000210
       FILE-CONTROL.                                                      000220
      *                                                                   000230
           SELECT FILE-CARD-IN ASSIGN UT-S-SYSUT1.                        000240
           SELECT FILE-REPORT  ASSIGN UT-S-PRINT.                         000250
       DATA DIVISION.                                                     000260
      *--------------                                                     000270
      *--------------                                                     000280
      *                                                                   000290
       FILE SECTION.                                                      000300
      *                                                                   000310
       FD  FILE-CARD-IN                                                   000320
           RECORDING F                                                    000330
           LABEL RECORDS STANDARD                                         000340
           BLOCK 0 RECORDS                                                000350
           DATA RECORD CARDS.                                             000360
       01  CARDS                       PIC X(80).                         000370
       FD  FILE-REPORT                                                    000380
           RECORDING F                                                    000390
           LABEL RECORDS STANDARD                                         000400
           BLOCK 0 RECORDS                                                000410
           DATA RECORD PRINTLINE.                                         000420
       01  PRINTLINE                   PIC X(133) JUSTIFIED RIGHT.        000430
       WORKING-STORAGE SECTION.                                           000440
       77  ROW-SUB PIC 99 VALUE 01.                                       000450
       77  COL-SUB PIC 99 VALUE 01.                                       000460
       77  ROW-NO  PIC 99 VALUE ZEROS.                                    000470
       77  COL-NO  PIC 99 VALUE ZEROS.                                    000480
      *                                                    CARD INPUT.    000490
      *                                                    -----------    000500
      *                                                                   000510
      *                                                      "HR02"       000520
      *                                                      ------       000530
       01  CARD-IN.                                                       000540
           03  CD-TYPE PIC X(4).                                          000550
           03  BOX-CONTAINS.                                              000560
               05  BOX-NO   PIC X(6).                                     000570
               05  BOX-DES1 PIC X(8).                                     000580
               05  BOX-DES2 PIC X(8).                                     000590
               05  BOX-DES3 PIC X(8).                                     000600
           03  BOX-COL PIC 99.                                            000610
           03  BOX-ROW PIC 99.                                            000620
           03  PARENT.                                                    000630
               05  PAR-COL PIC 99.                                        000640
               05  PAR-ROW PIC 99.                                        000650
               05 FILLER PIC X(38) VALUE SPACES.                          000660
      *                                                      "HR01"       000670
      *                                                      ------       000680
       01  HR01-CARD REDEFINES CARD-IN.                                   000690
           05  CARD-TYPE           PICTURE X(4).                          000700
           05  REP-HEAD            PICTURE X(76).                         000710
      *                                                                   000720
      *                                                    PRINT OUTPUT   000730
      *                                                    ------------   000740
      *                                                -HEADER LINE       000750
      *                                                ------------       000760
       01  HEAD-1.                                                        000770
           05  FILLER              PICTURE X(28)   VALUE SPACE.           000780
           05  REP-HEAD-LINE       PICTURE X(76).                         000790
           05  FILLER              PIC X(6)        VALUE 'DATE :'.        000800
           05  HEAD-1-DATE         PIC X(9)        VALUE 'COBOLDATE'.     000810
           05  FILLER                  PIC X(11)   VALUE SPACES.          000820
      *                                                -CONNECTOR LINES   000830
      *                                                ----------------   000840
       01  LINE7.                                                         000850
           03  BOX7 OCCURS 11.                                            000860
               05  GAP7A       PIC X(5).                                  000870
               05  CON7        PIC X.                                     000880
               05  GAP7B PIC X(6).                                        000890
      *                                                                   000900
       01  LINE8.                                                         000910
           03 GAP8A PIC X(5).                                             000920
           03  BOX8 OCCURS 10.                                            000930
               05  BOXA8.                                                 000940
                   07  CON8    PIC X.                                     000950
                   07  CONA8   PIC X(11).                                 000960
           03 GAP8B PIC X(7).                                             000970
      *                                                                   000980
       01  LINE9.                                                         000990
           03  BOX9 OCCURS 11.                                            001000
               05  GAP9A       PIC X(5).                                  001010
               05  CON9        PIC X.                                     001020
               05  GAP9B       PIC X(6).                                  001030
      *                                                -BOX LINES         001040
      *                                                ----------         001050
       01  LINE1.                                                         001060
           03  BOX1 OCCURS 11.                                            001070
               05  HYPS PIC X(10).                                        001080
               05  GAPS PIC XX.                                           001090
       01  LINE2.                                                         001100
           03  BOX2 OCCURS 11.                                            001110
               05  LHS2 PIC X.                                            001120
               05  NAR2 PIC X(8).                                         001130
               05  RHS2 PIC X.                                            001140
               05  GAP2 PIC XX.                                           001150
       01  LINE3.                                                         001160
           03  BOX3 OCCURS 11.                                            001170
               05  LHS3 PIC X.                                            001180
               05  NAR3 PIC X(8).                                         001190
               05  RHS3 PIC X.                                            001200
               05  GAP3 PIC XX.                                           001210
       01  LINE4.                                                         001220
           03 BOX4 OCCURS 11.                                             001230
               05  LHS4 PIC X.                                            001240
               05  NAR4 PIC X(8).                                         001250
               05  RHS4 PIC X.                                            001260
               05  GAP4 PIC XX.                                           001270
      *                                                                   001280
       01  LINE5.                                                         001290
           03 BOX5 OCCURS 11.                                             001300
               05  LHS5 PIC X.                                            001310
               05 GAP5A PIC XX.                                           001320
               05 NAR5 PIC X(6).                                          001330
               05 RHS5 PIC X.                                             001340
               05 GAP5 PIC XX.                                            001350
      *                                                                   001360
      *                                                        TABLES     001370
      *                                                        ------     001380
      *                                                    -BOXES         001390
      *                                                    ------         001400
       01  TABLE-BOX.                                                     001410
           03 COLS OCCURS 11.                                             001420
               05 ROW OCCURS 7.                                           001430
                 06 BX-CNTNS.                                             001440
                   07  BX-NO       PICTURE  X(6).                         001450
                   07  BX-DES1     PICTURE  X(8).                         001460
                   07  BX-DES2     PICTURE  X(8).                         001470
                   07  BX-DES3     PICTURE  X(8).                         001480
                 06 THRSW PIC X.                                          001490
      *                                                    -CONNECTORS    001500
      *                                                    -----------    001510
      *                                                                   001520
       01  TABLE-CONNECT.                                                 001530
           03 COLS OCCURS 11.                                             001540
               05 ROW OCCURS 7.                                           001550
                   07  BOXLINE     PICTURE X.                             001560
                   07  LN7LINE     PICTURE X.                             001570
                   07 LN8LINE     PICTURE X.                              001580
                   07  LN8ALINE        PICTURE X.                         001590
                   07  LN9LINE     PICTURE X.                             001600
      *                                                                   001610
      *                                                                   001620
      *      *------------------------------------*                       001630
      *      *PROGRAMME TO PRINT HIERQRCHY CHARTS                         001640
      *      *------------------------------------*                       001650
      *      *------------------------------------*                       001660
      *                        ----------                                 001670
      *                        |HIERARCH|                                 001680
      *                        |        |                                 001690
      *                        |        |                                 001700
      *                        |        |                                 001710
      *                        ----------                                 001720
      *                            |                                      001730
      *    --------------------------------------------------------       001740
      *    |                       |                              |       001750
      *----------              ----------                     ------------001760
      *|START   |              |MAINLINE|                     |   END  |  001770
      *|START   |              |MAINLINE|                     |   END  |  001780
      *|START   |              |MAINLINE|                     |   END  |  001790
      *|       1|              |       2|                     |       3|  001800
      *----------              ----------                     ----------  001810
      *                            |                                      001820
      *     ------------------------------------------------------        001830
      *     |                                                    |        001840
      * ----------                                           ----------   001850
      * |READ-   |                                           |PTINT-  |   001860
      * | CARDS  |                                           | CHARTS |   001870
      * |        |                                           |        |   001880
      * |      21|                                           |      22|   001890
      * ----------                                           ----------   001900
      *     |                                                    |        001910
      *     --------------------------                           |        001920
      *     |           |           |-                           |        001930
      * ----------  ----------  ----------                       |        001940
      * |PRINT   |  |BUILD-  |  |BUILD-  |                       |        001950
      * |-HEADER |  | BOX-   |  |CONNECTS|                       |        001960
      * |        |  | TABLE  |  |-TABLE  |                       |        001970
      * |     211 |  |     212|  |     213|                      |        001980
      * ----------  ----------  ----------                       |        001990
      *                                  -------------------------        002000
      *                                  |                       |        002010
      *                              ----------  ----------  ----------   002020
      *                              |PREPARE-|  |PREPARE-|  |PRINT   |   002030
      *                              |CONNECTS|  |BOXES   |  |CONNECTS|   002040
      *                              |FROM TAB|  |FROM TAB|  |+ BOXES |   002050
      *                              |     221|  |     222|  |     223|   002060
      *                              ----------  ----------  ----------   002070
       PROCEDURE DIVISION.                                                002080
      *-------------------                                                002090
      *-------------------                                                002100
      *                                                        START      002110
      *                                                        -----      002120
           PERFORM INITIATION.                                            002130
      *                                                        MAINLINE   002140
      *                                                        --------   002150
           PERFORM READ-CARDS UNTIL CARDS = HIGH-VALUES.                  002160
           PERFORM PRINT-CHART UNTIL ROW-SUB > 7.                         002170
      *                                                        END        002180
      *                                                        ---        002190
           PERFORM TERMINATION.                                           002200
           STOP RUN.                                                      002210
       INITIATION SECTION.                                                002220
           OPEN INPUT FILE-CARD-IN.                                       002230
           OPEN OUTPUT FILE-REPORT.                                       002240
           MOVE SPACES TO TABLE-BOX, TABLE-CONNECT.                       002250
       INITIATION-END.      EXIT.                                         002260
       READ-CARDS SECTION.                                                002270
      *-------------------                                                002280
      *                                CASE1CASE2CASE3CASE4               002290
      *TESTS***************************---------------------              002300
      *1                               |    |    |    |    |              002310
      *CARD-TYPE = HR01                | Y  | N  |  N | N  |              002320
      *2                               |    |    |    |    |              002330
      *TYPE = HR02 AND PARENT DOES NOT | N  | Y  |  N | N  |              002340
      *CONTAINS BLANKS.                |    |    |    |    |              002350
      *3                               |    |    |    |    |              002360
      *TYPE = HR02 AND PARENT DOES     | N  | N  |  Y | N  |              002370
      *CONTAINS BLANKS.                |    |    |    |    |              002380
      *                                ---------------------              002390
      *PROCESS*************************|    |    |    |    |              002400
      *1                               |    |    |    |    |              002410
      *CALL DATE ROUTINE,              | X  |    |    |    |              002420
      *2                               |    |    |    |    |              002430
      *PREPARE HEADER LINE.            | X  |    |    |    |              002440
      *3                               |    |    |    |    |              002450
      *PRINT HEADER LINE..             | X  |    |    |    |              002460
      *4                               |    |    |    |    |              002470
      *PREPARE BOX TABLE.              |    | X  |  X |    |              002480
      *5                               |    |    |    |    |              002490
      *PREPARE CONNECT TABLE.          |    | X  |    |    |              002500
      *                                ---------------------              002510
      *SET-UP**************************|    |    |    |    |              002520
      *1                               |    |    |    |    |              002530
      *READ ANOTHER CARD               |  X | X  |  X | X  |              002540
      *                                ---------------------              002550
      *                                                                   002560
      *SET-UP                                                             002570
      *-------                                                            002580
           READ FILE-CARD-IN INTO CARD-IN AT END                          002590
           MOVE HIGH-VALUES TO CARDS.                                     002600
      *CASE1                                               PRINT HEADER   002610
      *------                                              ------------   002620
           IF CARD-TYPE = 'HR01'                                          002630
           THEN CALL 'DTERTN' USING HEAD-1-DATE                           002640
                MOVE REP-HEAD TO REP-HEAD-LINE                            002650
                WRITE PRINTLINE FROM HEAD-1 BEFORE ADVANCING 2 LINES.     002660
      *                                                                   002670
      *CASE2                               BUILD BOX AND CONNECT TABLES   002680
      *-----                               ----------------------------   002690
           IF CD-TYPE = 'HR02'                                            002700
      *    AND                                                            002710
           IF PARENT IS NOT EQUAL TO SPACES                               002720
           THEN MOVE '1' TO THRSW (BOX-COL, BOX-ROW)                      002730
                MOVE BOX-CONTAINS TO BX-CNTNS (BOX-COL, BOX-ROW)          002740
                PERFORM CONNECTOR                                         002750
      *                                                                   002760
      *CASE3                               BUILD BOX TABLE ONLY           002770
      *-----                               --------------------           002780
           ELSE MOVE '1' TO THRSW (BOX-COL, BOX-ROW)                      002790
                MOVE BOX-CONTAINS TO BX-CNTNS (BOX-COL, BOX-ROW).         002800
      *                                                                   002810
      *CASE4                               ERROR CONDITION,IGNORE CARD    002820
      *-----                               ---------------------------    002830
      *                                                                   002840
       READ-CARDS-END.     EXIT.                                          002850
      *---------------                                                    002860
      *                                                                   002870
       CONNECTOR SECTION.                                                 002880
      *                                CASE1CASE2CASE3CASE4               002890
      *TESTS***************************---------------------              002900
      *1                               |    |    |    |    |              002910
      *BOX COLUMN : PARENT COLUMN      | E  | G  | L  | -  |              002920
      *2                               |    |    |    |    |              002930
      *PARENT ROW IS LESS THAN BOX ROW | Y  | Y  | Y  | N  |              002940
      *PROCESS*************************---------------------              002950
      *1                               |    |    |    |    |              002960
      *PREPARE CONNECTOR TABLE FROM    |    |    |    |    |              002970
      *PARENT ROW TO BOX ROW           | X  | X  | X  |    |              002980
      *2                               |    |    |    |    |              002990
      *PREPARE CONNECTOR TABLE FROM    |    |    |    |    |              003000
      *PARENT COLUMN TO BOX COLUMN     |    | X  |    |    |              003010
      *3                               |    |    |    |    |              003020
      *PREPARE CONNECTOR TABLE FROM    |    |    |    |    |              003030
      *BOX COLUMN TO PARENT COLUMN     |    |    | X  |    |              003040
      *SET-UP**************************---------------------              003050
      *1                               |    |    |    |    |              003060
      *READ ANOTHER CARD               | X  | X  | X  | X  |              003070
      *                                ---------------------              003080
      *                                                                   003090
           IF PAR-ROW NOT LESS THAN BOX-ROW GO TO CONNECTOR-END.          003100
      *                                                    PARRR          003110
      *CASE1                                                 |            003120
      *-----                                                 |            003130
      *                                                      |            003140
      *                                                    BOXXX          003150
           IF BOX-COL = PAR-COL                                           003160
           THEN COMPUTE ROW-NO = PAR-ROW + 1                              003170
                PERFORM DRAW-VERT UNTIL  ROW-NO = BOX-ROW                 003180
                MOVE '1' TO LN7LINE (BOX-COL, BOX-ROW),                   003190
                           LN9LINE (BOX-COL, BOX-ROW).                    003200
      *                                                                   003210
      *CASE2                                               PARRR          003220
      *-----                                                 |            003230
      *                                                      |            003240
      *                                                      -------      003250
      *                                                            |      003260
      *                                                          BOXXX    003270
      *                                                                   003280
           IF BOX-COL > PAR-COL                                           003290
           THEN COMPUTE ROW-NO = PAR-ROW + 1                              003300
                PERFORM DRAW-VERT UNTIL  ROW-NO = BOX-ROW                 003310
                MOVE '1' TO LN7LINE (PAR-COL, BOX-ROW),                   003320
                            LN9LINE (BOX-COL, BOX-ROW)                    003330
                MOVE PAR-COL TO COL-NO                                    003340
                PERFORM DRAW-HORIZ UNTIL COL-NO = BOX-COL                 003350
                MOVE '1' TO LN8ALINE (COL-NO, BOX-ROW).                   003360
      *                                                                   003370
      *CASE3                                                     PARRR    003380
      *-----                                                       |      003390
      *                                                      -------      003400
      *                                                      |            003410
      *                                                    BOXXX          003420
           IF BOX-COL < PAR-COL                                           003430
           THEN COMPUTE ROW-NO = PAR-ROW + 1                              003440
                PERFORM DRAW-VERT UNTIL ROW-NO = BOX-ROW                  003450
                MOVE '1' TO LN7LINE (PAR-COL, BOX-ROW),                   003460
                            LN9LINE (BOX-COL, BOX-ROW)                    003470
                MOVE BOX-COL TO COL-NO                                    003480
                PERFORM DRAW-HORIZ UNTIL COL-NO = PAR-COL.                003490
      *                                                                   003500
      *CASE4                               ERROR CONDITION,IGNORE CARD    003510
      *-----                               ---------------------------    003520
       CONNECTOR-END.      EXIT.                                          003530
      *                                                                   003540
       DRAW-VERT SECTION.                                                 003550
           MOVE '1' TO BOXLINE (PAR-COL, ROW-NO),                         003560
                       LN7LINE (PAR-COL, ROW-NO),                         003570
                       LN9LINE (PAR-COL, ROW-NO).                         003580
           ADD 1 TO ROW-NO.                                               003590
       DRAW-VERT-END.      EXIT.                                          003600
      *                                                                   003610
       DRAW-HORIZ SECTION.                                                003620
           MOVE '1' TO LN8LINE (COL-NO, BOX-ROW).                         003630
           ADD 1 TO COL-NO.                                               003640
       DRAW-HORIZ-END.      EXIT.                                         003650
      *                                                                   003660
       PRINT-CHART SECTION.                                               003670
      *--------------------                                               003680
      *                                    PREPARE CONNECTORS FROM TABLE  003690
      *                                    -----------------------------  003700
      *                                    PREPARE BOXES FROM TABLE       003710
      *                                    ------------------------       003720
           MOVE SPACES TO LINE1,                                          003730
                          LINE2,                                          003740
                          LINE3,                                          003750
                          LINE4,                                          003760
                          LINE5,                                          003770
                          LINE7,                                          003780
                          LINE8,                                          003790
                          LINE9.                                          003800
           MOVE 1 TO COL-SUB.                                             003810
           PERFORM SETUP-LINES UNTIL COL-SUB > 11.                        003820
      *                                    PRINT CONNECTORS AND BOXES     003830
      *                                    --------------------------     003840
           IF ROW-SUB > 1                                                 003850
           THEN WRITE PRINTLINE FROM LINE7 BEFORE ADVANCING 1 LINES       003860
                WRITE PRINTLINE FROM LINE8 BEFORE ADVANCING 1 LINES       003870
                WRITE PRINTLINE FROM LINE9 BEFORE ADVANCING 1 LINES       003880
                WRITE PRINTLINE FROM LINE1 BEFORE ADVANCING 1 LINES       003890
                WRITE PRINTLINE FROM LINE2 BEFORE ADVANCING 1 LINES       003900
                WRITE PRINTLINE FROM LINE3 BEFORE ADVANCING 1 LINES       003910
                WRITE PRINTLINE FROM LINE4 BEFORE ADVANCING 1 LINES       003920
                WRITE PRINTLINE FROM LINE5 BEFORE ADVANCING 1 LINES       003930
                WRITE PRINTLINE FROM LINE1 BEFORE ADVANCING 1 LINES       003940
           ELSE WRITE PRINTLINE FROM LINE1 BEFORE ADVANCING 1 LINES       003950
                WRITE PRINTLINE FROM LINE2 BEFORE ADVANCING 1 LINES       003960
                WRITE PRINTLINE FROM LINE3 BEFORE ADVANCING 1 LINES       003970
                WRITE PRINTLINE FROM LINE4 BEFORE ADVANCING 1 LINES       003980
                WRITE PRINTLINE FROM LINE5 BEFORE ADVANCING 1 LINES       003990
                WRITE PRINTLINE FROM LINE1 BEFORE ADVANCING 1 LINES.      004000
           ADD 1 TO ROW-SUB.                                              004010
       PRINT-CHART-END.    EXIT.                                          004020
      *----------------                                                   004030
       SETUP-LINES SECTION.                                               004040
      ******************************************************************  004050
      *  THE CONTENTS OF THE BRACKETS REPRESENTS THE SECTION OF THE    *  004060
      *  CHART PREPARED IN THE FOLLOWING RONTINES IF THE CONDITION(S)  *  004070
      *  ARE MET.                                                      *  004080
      ******************************************************************  004090
      *                               *                                *  004100
      * ROUTINE1 :      PARRR         * ROUTINE2 :                     *  004110
      *                  (|)          *            PARRR               *  004120
      *                  (|)          *                                *  004130
      *                               *             (-------)          *  004140
      *                               *                                *  004150
      *                               *                   BOXXX        *  004160
      ******************************************************************  004170
      *                               *                                *  004180
      * ROUTINE3 :                    * ROUTINE4 :                     *  004190
      *                  (|)          *         -FRAME + CONTENTS.     *  004200
      *                  (|)          *                                *  004210
      *                 BOXXX         *           (BOXXX)              *  004220
      *                               *                                *  004230
      ******************************************************************  004240
      *                                                                *  004250
      * ROUTINE5 :                                                     *  004260
      *         - CONNECTING LINE THRU AN EMPTY BOX POSITION,          *  004270
      *                (  |  )                                         *  004280
      *                                                                *  004290
      ******************************************************************  004300
      *  THIS IS ONLY A DIAGRAMATIC REPRESENTATION , SEE SPEC FOR      *  004310
      *  ACTUAL POSITIONING ,ETC.                                      *  004320
      ******************************************************************  004330
      *                                                                   004340
      *ROUTINE1                                                           004350
           IF ROW-SUB > 1                                                 004360
      *    AND                                                            004370
           IF LN7LINE (COL-SUB, ROW-SUB) = '1'                            004380
           THEN MOVE '|' TO CON7 (COL-SUB), CON8 (COL-SUB).               004390
      *                                                                   004400
      *ROUTINE3                                                           004410
           IF ROW-SUB > 1                                                 004420
      *    AND                                                            004430
           IF LN9LINE (COL-SUB, ROW-SUB) = '1'                            004440
               THEN MOVE '|' TO CON8 (COL-SUB), CON9 (COL-SUB).           004450
      *                                                                   004460
      *ROUTINE2                                                           004470
           IF ROW-SUB > 1                                                 004480
      *    AND                                                            004490
           IF LN8LINE (COL-SUB, ROW-SUB) = '1'                            004500
               THEN MOVE '------------' TO BOXA8 (COL-SUB).               004510
           IF ROW-SUB > 1                                                 004520
      *    AND                                                            004530
           IF LN8ALINE (COL-SUB, ROW-SUB) = '1'                           004540
               THEN MOVE '-' TO CON8 (COL-SUB).                           004550
      *                                                                   004560
      *ROUTINE4                                                           004570
           IF THRSW (COL-SUB, ROW-SUB) = '1'                              004580
           THEN MOVE '----------' TO HYPS (COL-SUB)                       004590
                MOVE '|' TO LHS2 (COL-SUB),                               004600
                            RHS2 (COL-SUB),                               004610
                            LHS3 (COL-SUB),                               004620
                            RHS3 (COL-SUB),                               004630
                            LHS4 (COL-SUB),                               004640
                            RHS4 (COL-SUB),                               004650
                            LHS5 (COL-SUB),                               004660
                            RHS5 (COL-SUB)                                004670
                MOVE BX-DES1 (COL-SUB, ROW-SUB) TO NAR2 (COL-SUB)         004680
                MOVE BX-DES2 (COL-SUB, ROW-SUB) TO NAR3 (COL-SUB)         004690
                MOVE BX-DES3 (COL-SUB, ROW-SUB) TO NAR4 (COL-SUB)         004700
               MOVE BX-NO (COL-SUB, ROW-SUB) TO NAR5 (COL-SUB).           004710
      *                                                                   004720
      *ROUTINE5                                                           004730
           IF BOXLINE (COL-SUB, ROW-SUB) = '1'                            004740
           THEN MOVE '     |      ' TO BOX1 (COL-SUB),                    004750
                                       BOX2 (COL-SUB),                    004760
                                       BOX3 (COL-SUB),                    004770
                                       BOX4 (COL-SUB),                    004780
                                       BOX5 (COL-SUB).                    004790
           ADD 1 TO COL-SUB.                                              004800
       SETUP-LINES-END.    EXIT.                                          004810
       TERMINATION SECTION.                                               004820
           CLOSE FILE-CARD-IN.                                            004830
           CLOSE FILE-REPORT.                                             004840
       TERMINATION-END.    EXIT.                                          004850
