GETPAR   START
         ENTRY SETCOD
*        SAVE REGISTERS AND POST SAVE AREAS
*
         STM   14,5,12(13)             SAVE REGISTERS
         BALR  5,0                     ESTABLISH BASE REGISTERS
         USING *,5
         LA    14,SAVEAREA             LOAD NEW SAVE AREA ADDRESS
         ST    14,8(13)           NEW SAVE AREA ADDR TO OLD SAVE AREA
         ST    13,4(14)           OLD SAVE AREA ADDR TO NEW SAVE AREA
         L     2,4(0,13)               ADDR OF SAVE AREA USED BY COBOL
         LR   13,14              NEW SAVE AREA ADDRESS TO REGISTER 13
*
*        THIS ROUTINE PICKS UP THE PARAMETER OPERAND VALUE,
*        ITS LENGTH AND STORES THE CHARACTER STRING IN THE MAIN PROGRAM
*
         L     4,24(0,2)          ADDRESS OF INITIATOR PARAMETER LIST
         L     2,0(0,4)       ADDRESS OF LENGTH OF PARAMETER OP DATA
         LH    4,0(0,2)      LOAD LENGTH VALUE TO REG.4.
         LTR   4,4           TEST IF ZERO.
         BZ    *+22
         S     4,=F'1'       REDUCE LENGTH BY 1 FOR MOVE.
         STC   4,*+9         STORE LENGTH IN MVC COMMAND.
         L     4,0(0,1)           ADDRESS OF CALLING PROG PARM FIELD
         MVC    0(1,4),2(2)       MOVE THE PARAMETER DATA TO MAIN
*
*         RESTORE REGISTERS, RETURN TO COBOL PROGRAM
*
         L     13,4(13)                RESTORE REGISTER 13
         LM    14,5,12(13)             RESTORE REGISTERS
         BR    14                      RETURN TO COBOL PROGRAM
*
*        SET CONDITION CODE AT TERMINATION OF COBOL PROGRAM
SETCOD   L     2,0(0,1)                COND-CODE VALUE ADDR
         L     15,0(0,2)               SET COND-CODE
         L     13,4(0,13)              RESTORE REGISTER 13
         L     14,12(13)               RETURN ADDR IN TERMINATOR
         LM    0,12,20(13)             RESTORE REGISTERS
         BR    14                      RETURN TO TERMINATOR
*
*        CONSTANTS USED BY ASSEMBLER ROUTINE
*
DATEAREA DS    CL4                     WORK-AREA FOR DATE
SAVEAREA DS    18F
*
      END
