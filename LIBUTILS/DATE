DATE     START 0
UT430    SAVE  (2,12),T
         BALR  12,0
         USING *,12
         ST    13,SV+4
         LA    13,SV
*
* THIS SUBROUTINE RETURNS THE DATE TO THE PROBLEM IN 3 FULL WORDS.
* WORD 1  MONTH
* WORD 2  DAY
* WORD 3  YEAR
* THE ROUTINE TAKES INTO ACCOUNT ALL LEAP YEARS FROM NOW TILL ETERNITY.
*
         LR    11,1                     SAVE ARGUMENT LIST.
         TIME
*
*    DATE RETURNED IN REG1   00YYDDDC
*
         LR    2,1                      SAVE DATE IN R2 - 00YYDDDC.
         SRL   2,8                                   R2 - 0000YYDD.
         IC    2,=X'CC'                              R2 - 0000YYCC.
         SRL   2,4                                   R2 - 00000YYC.
         ST    2,WORD                   SAVE PACKED YEAR IN WORD.
         XC    DOUBLE,DOUBLE            DOUBLE = HEX ZEROS.
         ST    2,DOUBLE+4               DOUBLE = 00000000-00000YYC.
         CVB   2,DOUBLE                 R2 = YEAR IN BINARY.
         L     10,8(11)                 OBTAIN PARAMETER-SUPPLIED YEAR
         ST    2,0(10)                  IN RIO AND LOAD IN YEAR VALUE.
*
*    CHECK FOR LEAP YEARS.
*
         DP    WORD(4),=PL1'4'          DIV YEAR BY 4 GIVING 000QQCRC.
         CP    WORD+3(1),=PL1'0'        IS REMAINDER (RC) ZERO-IE LEAP.
         BE    *+12                     YES-GO TO LOAD-LEAPYEAR-TABLE.
         LA    3,PDAYS                  NO- LOAD NON-LEAPYEAR TABLE.
         B     *+8
         LA    3,LPDAYS                 LOAD LEAPYEAR-TABLE.
*
*    INITIALISE THE SEARCH LOOP.
*
         SLL   1,16                     DROP THE YEAR LEAVING
         SRL   1,16                     REG1 WITH  0000DDDC.
         XC    DOUBLE,DOUBLE            DOUBLE = HEX ZEROS.
         ST    1,DOUBLE+4               DOUBLE = 00000000-0000DDDC.
         CVB   1,DOUBLE                 R1 = DAY OF YEAR IN BINARY.
         LA    4,4                      R4 = 4.
         LA    5,11                     R5 = 11  SEARCH LIMIT, NOT JAN.
*
*    THIS IS THE LOOP - ROUND AND ROUND 11 TIMES.
*
SEARCHEM C     1,4(0,3)                 COMP. DAYS TO TABLE-ENTRY + 1.
         BNH   GOTIT
         AR    3,4                      INCREMENT TABLE POINTER.
         BCT   5,SEARCHEM               LOOP JAN - NOV, FALL THRU DEC.
GOTIT    L     6,0(0,3)                 LOAD THE TABLE ENTRY.
         SR    1,6                      R1= DAY OF YEAR - TABLE ENTRY.
         L     10,4(11)                 LOAD PARM.-SUPPLIED DAYS TO R10
         ST    1,0(10)                  AND STORE DAYS IN BINARY.
         LA    7,12                     R7 = 12.
         SR    7,5                      R7 = 12 - (11 - NO. OF LOOPS).
         L     10,0(11)                 LOAD PARM.-SUPPLIED MTH TO R10
         ST    7,0(10)                  AND STORE MONTH IN BINARY.
*
*    THAT'S ALL CHARLIE. MONTH - DAY - YEAR IN THE 3-WORD LIST.
*
         L     13,SV+4                  LET'S GO HOME.
         RETURN (14,12),T
*
*    CONSTANTS AND WORK AREAS.
*
DOUBLE   DS    D                        8-POSITION WORK AREA.
WORD     DS    F
PDAYS    DC    F'0'
         DC    F'31'                    TABLE  OF
         DC    F'59'
         DC    F'90'                    DAYS   OF
         DC    F'120'
         DC    F'151'                   NORMAL
         DC    F'181'
         DC    F'212'                   NON
         DC    F'243'
         DC    F'273'                   LEAP-YEAR.
         DC    F'304'
         DC    F'334'
         DC    F'365'
LPDAYS   DC    F'0'
         DC    F'31'                    TABLE  OF
         DC    F'60'
         DC    F'91'
         DC    F'121'                   DAYS  OF
         DC    F'152'
         DC    F'182'                   ANY
         DC    F'213'
         DC    F'244'                   LEAP-YEAR.
         DC    F'274'
         DC    F'305'
         DC    F'335'
         DC    F'366'
SV       DS    18F                      REGISTER SAVE AREA.
         END   DATE
