         TITLE 'RPG CROSS REFERENCE LISTING PROGRAM'
         SPACE 5
*        RPG CROSS REFERENCE LISTING PROGRAM
         SPACE 5
*        WRITTEN BY DEAN A MOORCROFT             APRIL 1970
         SPACE 3
*        DIRECT INQUIRIES TO - D A MOORCROFT,
*                              SYSTEMS PROGRAMMER,
*                              MISD,
*                              MONSANTO HOUSE,
*                              10-18 VICTORIA STREET,
*                              LONDON SW1.
         SPACE 5
*        DDNAMES AND ASSOCIATED DATA SETS .........
*              LISTING - EDITED PROGRAM LISTING,
*                        CROSS-REFERENCE LISTING OF NAMES,
*                        CROSS-REFERENCE LISTING OF INDICATORS
*              XREFIN  - RPG SOURCE DECK
*              XREFOUT - CARD IMAGE FILE OF INPUT SOURCE DECK
         EJECT
RPGXLIST CSECT
         SPACE 5
*    REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4         BRANCH AND LINK RETURN ADDRESS
R5       EQU   5         ADDRESS OF INPUT BUFFER FOR CARD
R6       EQU   6         POINTER TO CURRENT ENTRY IN INDICATOR TABLE
R7       EQU   7         POINTER TO CURRENT ENTRY IN NAME TABLE
R8       EQU   8         ADDRESS OF OUTPUT BUFFER FOR CARD IMAGES
R9       EQU   9         ADDRESS OF OUTPUT BUFFER FOR LISTING
R10      EQU   10        BASE REGISTER 1
R11      EQU   11        BASE REGISTER 2
R12      EQU   12        BASE REGISTER 3
R13      EQU   13        POINTER TO REGISTER SAVE AREA
R14      EQU   14        BALR RETURN ADDRESS
R15      EQU   15        BALR ROUTINE ADDRESS
         SPACE 5
*    HOUSEKEEPING
         SPACE 1
         SAVE  (14,12)
         BALR  10,0
         USING HERE,10,11,12
HERE     LM    11,12,BASE
         B     BEGIN
SAVEAREA DS    18F
BASE     DC    A(HERE+4096,HERE+8192)
BEGIN    LA    9,SAVEAREA
         ST    9,8(13)
         ST    13,4(9)
         LR    13,9
         LA    7,NAMETBLE
         LA    6,INDTABLE
         EJECT
         PRINT ON,NODATA,NOGEN
         OPEN  (PRINTDCB,(OUTPUT),CARDDCB,(INPUT),IMAGEDCB,(OUTPUT))
         PUT   PRINTDCB
         LR    9,1
         MVC   LINE,BLANK
         MVC   0(133,9),LINE
         BAL   4,HEADINGS
         SPACE 2
READ     BAL   4,CARDIO
         CLC   ID,TYPE
         BE    *+14
         BAL   4,PRINTIO
         MVC   TYPE,ID
         CLC   LINECNT,=P'55'
         BL    *+14
         MVC   LINECNT,ZERO
         BAL   4,HEADINGS
         MVC   22(2,9),SEQNO
         MVC   25(3,9),SEQNO+2
         MVC   33(69,9),ID
         MVC   105(6,9),PGMID
         CLI   COMMENT,C'*'
         BNE   *+12
         LA    4,READ
         B     PRINTIO
         CLI   ID,C'H'
         BE    *-12
         AP    CNTR,ONE
         MVC   16(4,9),EDU
         ED    16(4,9),CNTR
         CLI   ID,C'F'
         BE    FRTN
         CLI   ID,C'E'
         BE    ERTN
         CLI   ID,C'I'
         BE    IRTN
         CLI   ID,C'C'
         BE    CRTN
         CLI   ID,C'O'
         BE    ORTN
         CLI   ID,C'L'
         BE    LRTN
         MVC   9(3,9),ASTERISK
         SP    CNTR,ONE
         MVC   16(4,9),=C'    '
         LA    4,READ
         B     PRINTIO
         EJECT
FRTN     BAL   4,PRINTIO
         MVC   X(8),CARD+6    MOVE FILENAME TO TABLE
         MVI   Y,C'D'
         BAL   4,NAME
         CLI   CARD+32,C' '   OVERFLOW INDICATOR ?
         BE    READ           NO - GO TO READ
         MVC   X(2),CARD+32
         BAL   4,IND
         B     READ
*    FILE DESCRIPTION SPECIFICATIONS ROUTINE
         SPACE 5
         EJECT
*    FILE EXTENSION SPECIFICATIONS ROUTINE
         SPACE 5
ERTN     BAL   4,PRINTIO
         MVC   X(8),CARD+10   MOVE FILENAME TO TABLE
         MVI   Y,C'U'
         BAL   4,NAME
         CLC   CARD+18(8),=C'        '  TO FILENAME ?
         BE    *+18                     NO - GO TO NEXT TEST
         MVC   X(8),CARD+18
         MVI   Y,C'U'
         BAL   4,NAME
         CLI   CARD+26,C' '             TABLE NAME ?
         BE    *+24                     NO - GO TO NEXT TEST
         MVC   X,X-1
         MVC   X(6),CARD+26
         MVI   Y,C'D'
         BAL   4,NAME
         CLI   CARD+45,C' '   ALTERNATE NAME ?
         BE    *+24           NO - GO TO NEXT TEST
         MVC   X,X-1
         MVC   X(6),CARD+45
         MVI   Y,C'D'
         BAL   4,NAME
         CLI   CARD+8,C' '    CHAINING FIELD ?
         BE    READ           NO
         MVC   X,X-1
         MVC   X(2),CARD+8
         LA    4,READ
         B     IND
         EJECT
*    INPUT SPECIFICATIONS ROUTINE
         SPACE 3
IRTN     BAL   4,PRINTIO
         CLI   CARD+6,C' '    FILENAME ?
         BE    *+18           NO - GO TO NEXT TEST
         MVC   X(8),CARD+6
         MVI   Y,C'U'
         BAL   4,NAME
         CLI   CARD+18,C' '   RESULTING INDICATOR
         BE    *+20           NO - GO TO NEXT TEST
         MVC   X,X-1
         MVC   X(2),CARD+18
         BAL   4,IND
         CLI   CARD+52,C' '   FIELD NAME ?
         BE    *+24           NO - GO TO NEXT TEST
         MVC   X,X-1
         MVC   X(6),CARD+52
         MVI   Y,C'D'
         BAL   4,NAME
         CLI   CARD+58,C' '   CONTROL LEVEL ?
         BE    *+20           NO - GO TO NEXT TEST
         MVC   X,X-1
         MVC   X(2),CARD+58
         BAL   4,IND
         CLI   CARD+60,C' '   MATCHING FIELD IND ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+60
         BAL   4,IND
         CLI   CARD+62,C' '   FIELD-RECORD RELATION IND ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+62
         BAL   4,IND
         CLI   CARD+64,C' '   FIELD INDICATOR PLUS ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+64
         BAL   4,IND
         CLI   CARD+66,C' '   FIELD INDICATOR MINUS ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+66
         BAL   4,IND
         CLI   CARD+68,C' '   ZERO OR BLANK IND ?
         BE    READ
         MVC   X,X-1
         MVC   X(2),CARD+68
         LA    4,READ
         B     IND
         EJECT
*    LINE COUNTER SPECIFICATIONS ROUTINE
         SPACE 5
LRTN     BAL   4,PRINTIO
         CLI   CARD+6,C' '    FILENAME ?
         BE    READ
         MVC   X(8),CARD+6
         MVI   Y,C'U'
         LA    4,READ
         B     NAME
         EJECT
*    CALCULATION SPECIFICATIONS ROUTINE
         SPACE 5
CRTN     BAL   4,PRINTIO
         CLI   CARD+6,C' '    CONTROL LEVEL ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+6
         BAL   4,IND
         CLI   CARD+9,C' '    IND ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+9
         BAL   4,IND
         CLI   CARD+12,C' '   IND ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+12
         BAL   4,IND
         CLI   CARD+15,C' '   IND ?
         BE    *+20
         MVC   X,X-1
         MVC   X(2),CARD+15
         BAL   4,IND
         CLI   CARD+17,C' '   FACTOR 1 ?
         BE    *+48           NO - GO TO NEXT TEST
         CLI   CARD+17,X'7D'  STARTS WITH QUOTE ?
         BE    *+40           YES - GO TO NEXT TEST
         TM    CARD+17,X'F0'  DIGIT ?
         BO    *+32           YES - GO TO NEXT TEST
         MVC   X(8),CARD+17
         MVI   Y,C'U'
         CLC   CARD+27(3),=C'TAG'
         BNE   *+8
         MVI   Y,C'D'
         BAL   4,NAME
         CLI   CARD+32,C' '   FACTOR 2 ?
         BE    *+48           NO - GO TO NEXT TEST
         CLI   CARD+32,X'7D'  STARTS WITH QUOTE ?
         BE    *+40           YES - GO TO NEXT TEST
         TM    CARD+32,X'F0'  DIGIT ?
         BO    *+32           YES - GO TO NEXT TEST
         MVC   X(8),CARD+32
         MVI   Y,C'U'
         CLC   CARD+27(4),HEAD16
         BNE   *+8
         MVI   Y,C'E'
         BAL   4,NAME
         CLI   CARD+42,C' '   RESULT FIELD
         BE    *+50
         MVC   X,X-1
         MVC   X(6),CARD+42
         MVI   Y,C'U'
         CLI   CARD+50,C' '   FILED DEFINED ?
         BE    *+22
          CLC   CARD+27(5),=C'RLABL'
         MVI   Y,C'R'
         BE   *+8
         MVI   Y,C'D'
         BAL   4,NAME
         CLI   CARD+53,C' '   HIGH IND ?
         BE    *+14
         MVC   X(2),CARD+53
         BAL   4,IND
         CLI   CARD+55,C' '   LOW IND ?
         BE    *+14
         MVC   X(2),CARD+55
         BAL   4,IND
         CLI   CARD+57,C' '   EQUAL IND ?
         BE    READ
         MVC   X(2),CARD+57
         LA    4,READ
         B     IND
         EJECT
*    OUTPUT-FORMAT SPECIFICATION ROUTINE
         SPACE 5
ORTN     BAL   4,PRINTIO
         CLI   CARD+6,C' '    FILENAME ?
         BE    *+18
         MVC   X,CARD+6
         MVI   Y,C'U'
         BAL   4,NAME
         CLI   CARD+23,C' '   IND ?
         BE    *+14
         MVC   X(2),CARD+23
         BAL   4,IND
         CLI   CARD+26,C' '   IND ?
         BE    *+14
         MVC   X(2),CARD+26
         BAL   4,IND
         CLI   CARD+29,C' '   IND ?
         BE    *+14
         MVC   X(2),CARD+29
         BAL   4,IND
         CLI   CARD+31,C' '   FIELD NAME ?
         BE    READ
         MVC   X,X-1
         MVC   X(6),CARD+31
         MVI   Y,C'U'
         LA    4,READ
         B     NAME
         EJECT
NAME     BCR   0,4
         MVC   0(8,7),X
         MVC   8(1,7),Y
         MVC   9(2,7),CNTR
         LA    7,11(7)
         C     7,ENDNTBLE
         BNL   ERROR1
         BR    4
         SPACE 5
ERROR1   OI    NAME+1,X'F0'   SET BYPASS SWITCH IN NAME ROUTINE
         OI    ERSWTCH1+1,X'F0'
         MVC   0(11,7),ASTERISK    ESTABLISH END OF TABLE
         BR    4
         SPACE 5
ERROR2   OI    IND+1,X'F0'    SET BYPASS SWITCH IN IND ROUTINE
         OI    ERSWTCH2+1,X'F0'
         MVC   0(4,6),ASTERISK
         BR    4
         SPACE 5
IND      BCR   0,4
         MVC   0(2,6),X
         MVC   2(2,6),CNTR
         LA    6,4(6)
         C     6,ENDITBLE
         BNL   ERROR2
         BR    4
         EJECT
*    ROUTINE TO READ A CARD AND WRITE A CARD IMAGE
         SPACE 2
CARDIO   STM   2,7,REGSAVE2
         GET   CARDDCB,CARD
         LR    5,1
         PUT   IMAGEDCB,CARD
         LR    8,1
         MVC   0(80,8),0(5)
         LM   2,7,REGSAVE2
         BR    4
         EJECT
*    ROUTINE TO WRITE REPORT HEADINGS FOR PROGRAM LISTING
         SPACE 2
HEADINGS STM   2,8,REGSAVE1
         AP    PAGENO,ONE
         MVC   0(1,9),PAGESKIP
         BAL   4,PRINTIO
         MVC   14(8,9),DATE
         MVC   101(4,9),HEAD15
         MVC   105(4,9),EDU
         ED    105(4,9),PAGENO
         BAL   4,PRINTIO
         MVC   0(1,9),ADV3AFTR
         MVC   47(38,9),HEAD1
         BAL   4,PRINTIO
         MVC   0(1,9),ADV2AFTR
         MVC   14(44,9),HEAD2
         MVC   105(6,9),HEAD14
         BAL   4,PRINTIO
         MVC   LINECNT,FIVE
         LM    2,8,REGSAVE1
         BR    4
         SPACE 5
PRINTIO  PUT   PRINTDCB
         LR    9,1
         MVC   0(133,9),LINE
         MVC   0(1,9),ADV1AFTR
         AP    LINECNT,=P'1'
         CP    LINECNT,=P'55'
         BCR   12,4
         ST    4,REGSAVE3
         MVC   LINECNT,ZERO
SW1      BC    0,BAL1         IND HEADINGS
SW2      BC    0,BAL2         NAME HEADINGS
         BAL   4,HEADINGS
         L     4,REGSAVE3
         BR    4
BAL1     BAL   4,HEADING3
         B     SW2+8
BAL2     BAL   4,HEADING2
         B     SW2+8
         EJECT
*    ROUTINE TO WRITE NAME REPORT HEADINGS
         SPACE 2
HEADING2 STM   2,8,REGSAVE1
         AP    PAGENO,ONE
         MVC   0(1,9),PAGESKIP
         BAL   4,PRINTIO
         MVC   14(8,9),DATE
         MVC   101(4,9),HEAD15
         MVC   105(4,9),EDU
         ED    105(4,9),PAGENO
         BAL   4,PRINTIO
         MVC   0(1,9),ADV3AFTR
         MVC   50(32,9),HEAD3
         BAL   4,PRINTIO
         MVC   0(1,9),ADV2AFTR
         MVC   10(24,9),HEAD4
         BAL   4,PRINTIO
         MVC   LINECNT,FIVE
         LM    2,8,REGSAVE1
         BR    4
         SPACE 5
*    ROUTINE TO WRITE INDICATOR REPORT HEADINGS
         SPACE 2
HEADING3 STM   2,8,REGSAVE1
         AP    PAGENO,ONE
         MVC   0(1,9),PAGESKIP
         BAL   4,PRINTIO
         MVC   14(8,9),DATE
         MVC   101(4,9),HEAD15
         MVC   105(4,9),EDU
         ED    105(4,9),PAGENO
         BAL   4,PRINTIO
         MVC   0(1,9),ADV3AFTR
         MVC   47(26,9),HEAD3
         MVC   74(10,9),HEAD6
         BAL   4,PRINTIO
         MVC   0(1,9),ADV2AFTR
         MVC   23(3,9),HEAD4+21
         MVC   10(3,9),HEAD5
         BAL   4,PRINTIO
         MVC   LINECNT,FIVE
         LM    2,8,REGSAVE1
         BR    4
         EJECT
*    ERROR MESSAGE ROUTINES IF TABLE CAPACITIES EXCEEDED
         SPACE 5
ERORMSG1 MVC   0(1,9),ADV3AFTR     NAME ROUTINE
         MVC   LINECNT,ZERO
         BAL   4,PRINTIO
         MVC   10(23,9),HEAD3
         MVC   34(10,9),HEAD7
         BAL   4,PRINTIO
         MVC   10(4,9),HEAD4
         MVC   15(23,9),HEAD8
         BAL   4,PRINTIO
         CLC   COMPCODE,=F'8'
         BNL   *+10
         MVC   COMPCODE,=F'8'
         B     ERSWTCH1+4
         SPACE 5
ERORMSG2 MVC   0(1,9),ADV3AFTR     IND ROUTINE
         MVC   LINECNT,ZERO
         BAL   4,PRINTIO
         MVC   10(23,9),HEAD3
         MVC   34(10,9),HEAD7
         BAL   4,PRINTIO
         MVC   10(9,9),HEAD6
         MVC   20(23,9),HEAD8
         BAL   4,PRINTIO
         CLC   COMPCODE,=F'8'
         BNL   *+10
         MVC   COMPCODE,=F'8'
         B     ERSWTCH2+4
         EJECT
ENDDATA  MVC   0(11,7),ASTERISK
         MVC   0(4,6),ASTERISK
         SPACE 5
*    ROUTINE TO SORT NAME TABLE
         SPACE 2
         LA    7,NAMETBLE
         LA    5,NAMETBLE+11
         SPACE 1
LOOP1    CLC   0(11,7),0(5)
         BNH   LOOP2
         MVC   HOLD1,0(5)
         MVC   0(11,5),0(7)
         MVC   0(11,7),HOLD1
LOOP2    LA    5,11(5)
         CLC   0(11,5),ASTERISK
         BNE   LOOP1
         LA    7,11(7)
         LR    5,7
         LA    5,11(5)
         CLC   0(11,5),ASTERISK
         BNE   LOOP1
         SPACE 5
*    ROUTINE TO SORT IND TABLE
         SPACE 2
         LA    6,INDTABLE
         LA    5,INDTABLE+4
         SPACE 1
LOOP3    CLC   0(4,6),0(5)
         BNH   LOOP4
         MVC   HOLD1(4),0(5)
         MVC   0(4,5),0(6)
         MVC   0(4,6),HOLD1
LOOP4    LA    5,4(5)
         CLC   0(4,5),ASTERISK
         BNE   LOOP3
         LA    6,4(6)
         LR    5,6
         LA    5,4(5)
         CLC   0(4,5),ASTERISK
         BNE   LOOP3
         EJECT
*    ROUTINE TO PRINT NAME REPORT
         SPACE 2
         MVI   SW2+1,X'F0'    ESTABLISH CORRECT PAGE OVERFLOW ROUTINE
         MVC   LINECNT,ZERO
         MVC   PAGENO,ZERO
         BAL   4,HEADING2
         MVC   HOLD1,BLANK
         MVC   HOLD1+1(10),HOLD1
         LA    7,NAMETBLE
         LA    5,30(9)
         LA    3,96(9)
CYCLE1   CLC   0(11,7),ASTERISK
         BE    OUT1
         CLI   8(7),C'D'
         BNE   NOTD1
         CLC   0(8,7),HOLD1
         BNE   *+10
         MVC   0(9,5),HEAD9
         BAL   4,PRINTIO
         LA    5,30(9)
SETUP1   LA    3,96(9)
         MVC   10(8,9),0(7)
         MVC   22(4,9),EDU
         ED    22(4,9),9(7)
         MVC   HOLD1,0(7)
INCR1    LA    7,11(7)
         B     CYCLE1
NOTD1    CLC   0(8,7),HOLD1
         BE    U1
         CLI   8(7),C'R'
         BE    SETUP1-8
         BAL   4,PRINTIO
         MVC   10(8,9),0(7)
         CLI   8(7),C'E'
         BNE   *+18
         MVC   30(4,9),HEAD16
         LA    5,36(9)
         B     *+14
         MVC   30(9,9),HEAD10
         LA    5,42(9)
         MVC   HOLD1,0(7)
         LA    3,96(9)
U1       CR    5,3
         BL    *+16
         BAL   4,PRINTIO
         LA    5,30(9)
         LA    3,96(9)
         MVC   0(4,5),EDU
         ED    0(4,5),9(7)
         LA    5,6(5)
         B     INCR1
OUT1     BAL   4,PRINTIO
ERSWTCH1 BC    0,ERORMSG1
         EJECT
*    ROUTINE TO PRINT INDICATOR REPORT
         SPACE 2
         MVI   SW1+1,X'F0'    ESTABLISH CORRECT PAGE OVERFLOW ROUTINE
         MVC   LINECNT,ZERO
         MVC   PAGENO,ZERO
         BAL   4,HEADING3
         MVC   HOLD1,BLANK
         MVC   HOLD1+1(10),HOLD1
         LA    6,INDTABLE
         LA    5,30(9)
         LA    3,96(9)
         SPACE 2
CYCLE2   CLC   0(4,6),ASTERISK
         BE    OUT2
         CLC   0(2,6),HOLD1
         BE    EOL
         BAL   4,PRINTIO
         MVC   10(2,9),0(6)
RESET    LA    3,96(9)
         LA    5,18(9)
MTL      MVC   0(4,5),EDU
         ED    0(4,5),2(6)
         MVC   HOLD1(4),0(6)
         LA    6,4(6)
         LA    5,6(5)
         B     CYCLE2
         SPACE 2
EOL      CR    5,3
         BL    MTL
         BAL   4,PRINTIO
         B     RESET
         SPACE 2
OUT2     BAL   4,PRINTIO
ERSWTCH2 BC    0,ERORMSG2
         EJECT
FINISH   MVC   LINECNT,ZERO
         MVC   0(1,9),PAGESKIP
         BAL   4,PRINTIO
         MVC   10(6,9),HEAD11
         MVC   17(19,9),HEAD1+11
         MVC   37(7,9),HEAD12
         BAL   4,PRINTIO
         MVC   10(35,9),HEAD13
         L     5,COMPCODE
         CVD   5,DECMCODE
         UNPK  UNPKCODE,DECMCODE+6(2)
         MVC   46(2,9),UNPKCODE+1
         OI    47(9),X'F0'
         BAL   4,PRINTIO
     CLOSE (PRINTDCB,,CARDDCB,,IMAGEDCB)
         L     13,SAVEAREA+4
         L     15,COMPCODE
        RETURN (14,12),RC=(15)
         EJECT
*
*    DCB'S
*
         SPACE 5
PRINTDCB DCB   DSORG=PS,                                               *
               MACRF=PL,                                               *
               DDNAME=LISTING,                                         *
               RECFM=FM,                                               *
               LRECL=133,                                              *
               BLKSIZE=133
         SPACE 5
CARDDCB  DCB   DSORG=PS,                                               *
               MACRF=GM,                                               *
               DDNAME=XREFIN,                                          *
               RECFM=FB,                                               *
               LRECL=80,                                               *
               EODAD=ENDDATA
         SPACE 5
IMAGEDCB DCB   DSORG=PS,                                               *
               MACRF=PM,                                               *
               DDNAME=XREFOUT,                                         *
               RECFM=F,                                                *
               LRECL=80,                                               *
               BLKSIZE=80
         EJECT
HEAD1    DC    C'SYSTEM/360 RPG CROSS REFERENCE LISTING'
HEAD2    DC    C'SEQ.NO  PG LIN     SPECIFICATIONS COL.6 - 74'
HEAD3    DC    C'CROSS REFERENCE LISTING OF NAMES'
HEAD4    DC    C'NAME         DEF     USE'
HEAD5    DC    C'IND'
HEAD6    DC    C'INDICATORS'
HEAD7    DC    C'INCOMPLETE'
HEAD8    DC    C'TABLE CAPACITY EXCEEDED'
HEAD9    DC    C'DUPLICATE'
HEAD10   DC    C'UNDEFINED'
HEAD11   DC    C'END OF'
HEAD12   DC    C'UTILITY'
HEAD13   DC    C'HIGHEST RETURN CODE ENCOUNTERED WAS'
HEAD14   DC    C'PGM ID'
HEAD15   DC    C'PAGE'
HEAD16   DC    C'EXIT'
PAGESKIP DC    X'8B'
ADV1AFTR DC    X'09'
ADV2AFTR DC    X'11'
ADV3AFTR DC    X'19'
BLANK    DC    C' '
LINE     DS    CL133
LINECNT  DC    PL2'0'
PAGENO   DC    PL2'0'
CNTR     DC    PL2'0'
ENDNTBLE DC    A(NAMETBLE+16489)
ENDITBLE DC    A(INDTABLE+5996)
TYPE     DC    C'H'
COMPCODE DC    F'0'
DECMCODE DC    D'0'
UNPKCODE DC    CL3'0'
ZERO     DC    PL2'0'
ONE      DC    PL2'1'
FIVE     DC    PL2'5'
ASTERISK DC    C'***********'
EDU      DC    X'40202021'
DATE     DC    CL8' '
EDDATE   DC    X'402021612020612020'
REGSAVE1 DS    7F
REGSAVE2 DS    6F
REGSAVE3 DS    F
CARD     DS    0CL80
SEQNO    DS    CL5
ID       DS    CL1
COMMENT  DS    CL1
         DS    CL67
PGMID    DS    CL6
         DC    C' '
X        DS    CL8
Y        DS    CL1
HOLD1    DS    CL11
         EJECT
         LTORG
         SPACE 5
INDTABLE DS    1500CL4
NAMETBLE DS    1500CL11
         SPACE 5
         END   RPGXLIST
