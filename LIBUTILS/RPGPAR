RPGPAR   CSECT
         PRINT ON,NOGEN,NODATA
         SPACE 1
         EXTRN PARM
         SPACE 1
UT417    SAVE  (14,12)        SAVE REGISTERS
         BALR  12,0           SET BASE REGISTER
         USING *,12           ESTABLISH BASE REGISTER
         LA    9,SAVEAREA     LOAD NEW SAVE AREA ADDRESS
         ST    9,8(13)        NEW SAVE AREA ADDRESS TO OLD SAVE AREA
         ST    13,4(9)        OLD SAVE AREA ADDRESS TO NEW SAVE AREA
         LR    13,9           NEW SAVE AREA ADDRESS TO REGISTER 13
         SPACE 1
         L     4,APARM        ADDRESS OF USER'S FIELD TO REGISTER 4
         SPACE 1
         L     11,16(0)       ADDRESS OF CVT TO REGISTER 11
         L     10,0(11)       LOAD CVTTCBP INTO REGISTER 10
         L     9,4(10)        CURRENT TCB ADDRESS TO REGISTER 9
         L     8,112(9)       ADDR OF FIRST PROGRAM SAVE AREA TO REG 8
         L     7,24(8)        ADDRESS OF PARAMETER ADDRESS RO REG 7
         L     6,0(7)         ADDRESS OF PARAMETER LIST TO REGISTER 6
         LH    5,0(6)         LENGTH OF PARAMETER LIST TO REGISTER 5
         LTR   5,5            TEST IF LENGTH ZERO
         BZ    *+10           IF ZERO BYPASS MOVE
         BCTR  5,0            DECREMENT LENGTH BY 1
         EX    5,MOVE         MOVE PARAMETER TO USER AREA
         SPACE 1
         L     13,SAVEAREA+4
        RETURN (14,12)        RETURN CONTROL TO RPG PROGRAM
         SPACE 2
SAVEAREA DS    18F            REGISTER SAVE AREA
APARM    DC    A(PARM)        ADDRESS OF USER'S FIELD
         SPACE 1
MOVE     MVC   0(0,4),2(6)    EXECUTED MOVE INSTRUCTION
         SPACE 2
*    REGISTER ASSIGNMENTS
         SPACE 1
*        1
*        2
*        3
*        4          ADDRESS OF USER'S FIELD
*         5         LENGTH OF PARAMETER LIST
*         6         ADDRESS OF PARAMETER LIST
*         7         ADDRESS OF PARAMETER ADDRESS
*         8         ADDRESS OF FIRST PROGRAM SAVE AREA
*         9         ADDRESS OF CURRENT TCB
*        10         ADDRESS OF NEXT AND CURRENT TCB ADDRESSES
*        11         ADDRESS OF CVT
*        12         BASE REGISTER
*        13         POINTER TO REGISTER SAVE AREA
*        14         BALR RETURN ADDRESS
*        15         BALR ROUTINE ADDRESS
         SPACE 1
         END
