 T580P24: PROC OPTIONS(MAIN);

 /*
         THE PURPOSE OF THIS PROGRAM IS TO CREATE THE CHARGE CODE
          VALIDATION CSECT ('CGRGCTAB') FROM THE RELEVANT ENTRIES
          IN THE PROJECT LOG.  NOTE THAT THE CSECT PRODUCED ('CGRGCTAB'
          IS NOT EXECUTABLE, BUT IS MERELY A TABLE OF VALID CHARGE
          CODES.  IT RESIDES ON SYS1.MLPALIB, AND IS LOADED BY THE
          SMF EXIT IEFUJI EVERY TIME THAT JOB OR STEP ACCOUNTING DATA
          IS TO BE VALIDATED.  THIS PERPETUAL LOADING ENSURES THAT IEFU
          IEFUJI ALWAYS PICKS UP THE LATEST VERSION OF THE TABLE, SO
          THAT CHANGES TO THE PROJECT LOG ARE IMMEDIATELY EFFECTIVE.
  */

     DCL PGMSTRT  FILE RECORD SEQUENTIAL,
         PGMEND   FILE RECORD SEQUENTIAL,
         PROJLOG  FILE RECORD SEQUENTIAL,
         CHRGCT FILE RECORD SEQUENTIAL,

         INREC CHAR(80),

       1 TABNTRY,
       3 FILLER1  CHAR(9) INIT(' '),
       3 FILLER2  CHAR(10) INIT('DC    CL4'''),
       3 CHRGCOUT CHAR(4) INIT(' '),
       3 FILLER3 CHAR(5) INIT(''' '),
       3 TYPEOUT CHAR(8) INIT(' '),
       3 FILLER4 CHAR(2) INIT(' '),
       3 DESCOUT CHAR(29) INIT(' '),
       3 FILLER5 CHAR(13) INIT(' '),

         PROJREC CHAR(80),
         CHRGCIN DEF PROJREC POS(5) CHAR(4),
         TYPEIN DEF PROJREC POS(17) CHAR(8),
         DESCIN DEF PROJREC POS(25) CHAR(29),
         CLSEDATE DEF PROJREC POS(75) CHAR(6);

         OPEN FILE(CHRGCT) OUTPUT;
         ON ENDFILE(PGMSTRT) GOTO OPENLOG;
         OPEN FILE(PGMSTRT) INPUT;
 RSTRT:  READ FILE(PGMSTRT) INTO(INREC);
         WRITE FILE(CHRGCT) FROM(INREC);
         GOTO RSTRT;

 OPENLOG:
         CLOSE FILE(PGMSTRT);
         ON ENDFILE(PROJLOG) GOTO END;
         OPEN FILE(PROJLOG) INPUT;
 RLOG:   READ FILE(PROJLOG) INTO(PROJREC);
         IF CLSEDATE ^= '      '   /* IGNORE CLOSED CHARGE CODES */
         THEN GOTO RLOG;
         IF CHRGCIN = CHRGCOUT     /* IGNORE DUPLICATE CHARGE CODES */
         THEN GOTO RLOG;
         IF CHRGCIN < '1000'       /* IGNORE 'SCOPE' CHARGE CODES */
         THEN GOTO RLOG;
         CHRGCOUT = CHRGCIN;
         DESCOUT = DESCIN;
         TYPEOUT = TYPEIN;
         WRITE FILE(CHRGCT) FROM(TABNTRY);
         GOTO RLOG;

 END:    CLOSE FILE(PROJLOG);
         ON ENDFILE(PGMEND) GOTO EXIT;
         OPEN FILE(PGMEND) INPUT;
 REND:   READ FILE(PGMEND) INTO(INREC);
         WRITE FILE(CHRGCT) FROM(INREC);
         GOTO REND;

 EXIT:   CLOSE FILE(PGMEND);
         CLOSE FILE(CHRGCT);
         END;
