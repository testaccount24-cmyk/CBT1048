UT382    START 0
         LNR   15,15                    USE R15 TO SHOW ENTRY UT382
         ENTRY BSHIFT                   DEFINE EXTERNAL NAME
BSHIFT   SAVE  (14,12)                  SAVE REGISTERS FOR CALLER
         BALR  12,0                     PUT BASE IN R12
         USING *,12                     NOTIFY ASSEMBLER
BASE     LM    2,5,0(1)                 PUT PARAMETER LIST IN R2 - R5
         L     8,0(3)                   PUT SHIFT FACTOR IN R8
         C     15,ZERO                  COMPARE R15 TO ZERO
         BH    SINGLE                   IF POSITIVE - USER WANTS BSHIFT
         CLC   0(4,5),TWO               IS SHIFT LENGTH 2
         BE    DOUBLE                   IF 2 - USER WANTS DOUBLE SHIFT
SINGLE   L     6,0(2)                   PUT INPUT WORD IN R6
         C     8,ZERO                   COMPARE SHIFT COUNT TO ZERO
         BL    LSHIFT                   IF LOW GO TO LEFT SHIFTING
         SRL   6,0(8)                   SHIFT RIGHT BY COUNT IN R8
         B     SRESULT                  GO TO STORE RESULT
LSHIFT   LPR   8,8                      CHANGE COUNT TO POSITIVE
         SLL   6,0(8)                   SHIFT LEFT BY COUNT IN R8
SRESULT  ST    6,0(0,4)                 STORE ONE WORD RESULT FOR USER
         B     USER                     BRANCH TO RETURN
DOUBLE   LM    6,7,0(2)                 PUT INPUT WORDS IN R6 & R7
         C     8,ZERO                   COMPARE SHIFT COUNT TO ZERO
         BL    DLSHIFT                  IF LOW GO TO LEFT SHIFTING
         SRDL  6,0(8)                   SHIFT RIGHT BY COUNT IN R8
         B     DRESULT                  GO TO STORE RESULT
DLSHIFT  LPR   8,8                      CHANGE COUNT TO POSITIVE
         SLDL  6,0(8)                   SHIFT LEFT BY COUNT IN R8
DRESULT  STM   6,7,0(4)                 STORE TWO WORD RESULT FOR USER
USER     RETURN (14,12)                 RETURN TO USER
         DS    0F
ZERO     DC    F'0'
TWO      DC    F'2'
         END
