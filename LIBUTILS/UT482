         MACRO
       PERFORM &N                  DEFINE REPETITIVE CODING
         ENTRY U482R&N
         ENTRY U482S&N
         SPACE 1
U482S&N  EQU   *          USING *,R15          ST    R14,SAVER14&N          BR
U482R&N  EQU   *          USING *,R15          L     R14,SAVER14&N          LA
SAVER14&N DS   F          SPACE 5
         MEND
         TITLE 'SIMULATE PERFORM IN RPG'
UT482    CSECT
         SPACE 5
***********************************************************************
*                                                                     *
*        CONCEIVED BY :            J-P RAINSON                        *
*                                                                     *
*        WRITTEN BY :              D.A.MOORCROFT                      *
*                                                                     *
*        DATE WRITTEN :            25'TH APRIL, 1974                  *
*                                                                     *
*        LOCATION :                EUROPEAN COMPUTER CENTRE           *
*                                                                     *
*        THIS SUB-PROGRAM ENABLES RPG PROGRAMS TO USE A FACILITY      *
*         LOGICALLY EQUIVALENT TO THE COBOL PERFORM.                  *
*                                                                     *
*        THE PROGRAM HAS TWO ENTRY POINTS -
*         UT482S : USED TO SPECIFY THE ADDRESS TO WHICH               *
*              CONTROL IS TO RETURN FROM THE PERFORMED SECTION        *
*         UT482R : RETURNS CONTROL TO THE 'CALLER' FROM               *
*              THE END OF THE PERFORMED SECTION                       *
*                                                                     *
*        TO INVOKE THE PERFORM, THE RPG SEQUENCE                      *
*                   EXIT  UT482S                                      *
*                   GOTO  PARA                                        *
*         IS REQUIRED, WHERE 'PARA' IS THE NAME OF THE 'TAG'          *
*         PRECEDING THE SECTION TO BE PERFORMED.                      *
*        TO RETURN FROM THE PERFORMED SECTION TO THE INSTRUCTION      *
*         FOLLOWING THE ( LOGICAL ) PERFORM, THE CARD                 *
*                   EXIT  UT482R                                      *
*         IS REQUIRED AT THE END OF THE PERFORMED SECTION.            *
*         THIS RETURNS CONTROL TO THE INSTRUCTION FOLLOWING THE       *
*         'EXIT' 'GOTO' SEQUENCE, THUS EQUATING TO THE RETURN         *
*         OF CONTROL FROM A PERFORMED SECTION.                        *
*                                                                     *
*        IN ORDER TO ALLOW NESTED PERFORMS, TEN FURTHER PAIRS         *
*         OF LABELS OF THE FORM U482RN & U482SN EXIST, WHERE          *
*         N IS AN INTEGER IN THE RANGE 0 - 9.                         *
*                                                                     *
*        NOTE THAT ANY PAIR OF LABELS MAY BE USED AS MANY TIMES       *
*         AS DESIRED ( INDEED, THE SAVE LABEL MAY BE USED MANY        *
*         TIMES TO REFERENCE ONE PERFORMED ROUTINE FROM MANY          *
*         SECTIONS OF THE PROGRAM ), BUT FOR NESTED PERFORMS          *
*         DIFFERENT PAIRS OF LABELS ARE OBLIGATORY.                   *
*                                                                     *
***********************************************************************
         EJECT
         ENTRY UT482S
         ENTRY UT482R
         SPACE 1
R14      EQU   14
R15      EQU   15
         SPACE 1
UT482S   EQU   *                   ENTRY TO R14 SAVE ROUTINE
         USING *,R15               ESTABLISH ADDRESSABILITY
         ST    R14,SAVER14         PRESERVE ADDRESS OF INSTRUCTION
*                                   FOLLOWING THE 'BALR' GENERATED BY
*                                   THE 'EXIT' STATEMENT
*                                   THIS ADDRESS + 12 IS THE ADDRESS
*                                   OF THE INSTRUCTION FOLLOWING THE
*                                   'EXIT' ' GOTO' SEQUENCE
         BR    R14                 RETURN TO THE RPG PROGRAM
         DROP  R15
         SPACE 1
UT482R   EQU   *                   ENTRY TO ROUTINE TO RETURN
         USING *,R15               ESTABLISH ADDRESSABILITY
         L     R14,SAVER14         RETRIEVE ADDRESSS OF INSTRUCTION
*                                   FOLLOWING THE ORIGINAL 'EXIT'
         LA    R14,12(,R14)        INRREMENT ADDRESS BY 12 TO
*                                   SPACE OVER THE LM GENERATED
*                                   AS PART OF THE 'EXIT', AND THE
*                                   LA, LR, BALR GENERATED BY THE
*                                   FOLLOWING 'GOTO'
         BR    R14                 RETURN TO INSTRUCTION FOLLOWING
*                                   THE 'GOTO' FOLLOWING THE 'EXIT'
         SPACE 1
SAVER14  DS    F                   SAVE AREA FOR R14
         EJECT
       PERFORM 0                   GENERATE PAIR OF ROUTINES FOR LABEL0
       PERFORM 1                   GENERATE PAIR OF ROUTINES FOR LABEL1
         EJECT
       PERFORM 2                   GENERATE PAIR OF ROUTINES FOR LABEL2
       PERFORM 3                   GENERATE PAIR OF ROUTINES FOR LABEL3
         EJECT
       PERFORM 4                   GENERATE PAIR OF ROUTINES FOR LABEL4
       PERFORM 5                   GENERATE PAIR OF ROUTINES FOR LABEL5
         EJECT
       PERFORM 6                   GENERATE PAIR OF ROUTINES FOR LABEL6
       PERFORM 7                   GENERATE PAIR OF ROUTINES FOR LABEL7
         EJECT
       PERFORM 8                   GENERATE PAIR OF ROUTINES FOR LABEL8
       PERFORM 9                   GENERATE PAIR OF ROUTINES FOR LABEL9
         END
