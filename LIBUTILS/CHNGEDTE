CHANGE1  START 0
*       ***************************************************************
*       *CHANGE1 WRITES AN EXPIRATION DATE INTO A DSCB.  THE DSCB IS  *
*       *READ AND WRITTEN WITH XDAP WITH IDENTIFICATION BY KEY.  THE  *
*       *FIRST TRACK ADDR IS PICKED UP FROM THE DEB ASSOCIATED WITH   *
*       *THE DASD DCB.  THE ADDR IS INCREMENTED UNTIL THE TRACK IS    *
*       *FOUND WHICH CONTAINS THE DSCB.  THE NEW EXPDT IS STORED, AND *
*       *THE DSCB IS WRITTEN BACK INTO ITS ORIGINAL POSITION.  WOW.   *
*       ***************************************************************
         PGMIN EQUATES=NO,USING=2
****     PRINT ON,NOGEN
         BC    15,*+10
         DC    C'CHANGE'
         ST    1,SAVER1            SAVE PARAM LIST ADDRESS
         L     1,0(0,1)            LOAD ADDR OF EXPDT FROM EXEC
         MVC   HWORD,0(1)          PICK UP LENGTH OF PARM FIELD
         CLC   HWORD,=H'1'
         BC    6,TEST5
         OI    SW2,X'40'
         MVC   SW1,2(1)
         BC    15,RDJFCB
TEST5    CLC   HWORD,=H'5'         CHECK LENGTH OF PARM FIELD
         BC    4,ABEND4            ERROR IF PARM LENGTH LT 5
OKCONV   MVC   EXPDT,2(1)          PICK UP NEW EXPDT
         NC    NONNUM,EXPDT        TEST FOR NONNUMERICS
         CLC   NONNUM,=X'F0F0F0F0F0'   CHECK THAT PARM ALL NUMBERS
         BC    6,ABEND5            ERROR IF PARM NOT ALL NUMBERS
         CLC   HWORD,=H'6'         ERROR IF PARM LENGTH GT 5
         BC    4,RDJFCB
         BC    2,ABEND4
         MVC   SW1,7(1)
RDJFCB   EQU   *
*        BEFORE OPENING DATASET, CKECK FOR ANY DDNAME REPLACEMENT
         CALL DDNREPL,(DASD)
         RDJFCB DASD               READ JFCB FOR VOLUME
         MVC   DSNAME,JFCB
         MVC   JFCB(44),Z4         MOVE DSNAME FOR VTOC TO DSCB
         OI    JFCBTSDM,X'08'
         L     1,16                GET CVT
         TM    116(1),X'10'        TEST IF UNDER MVS
         BZ    MFT                 BRANCH IF NO
         OPEN  (DASD,(UPDAT)),TYPE=J
         B     COM
MFT      EQU   *
         OPEN  (DASD),TYPE=J
COM      EQU   *
         TM    DASD+48,X'10'       TEST FOR SUCCESSFUL DASD OPEN
         BC    12,ABEND7           ERROR IF OPEN UNSUCCESSFUL
NOWWHAT1 XC    WORD,WORD
         MVC   WORD+1(3),DASD+45   PICK UP DEB ADDR
         L     6,WORD
         SH    6,=H'16'
         MVC   MBBCCHHR+1(7),1(6)       PICK UP DASD VTOC ADDR
** MVS **
         L     1,16                GET CVT
         TM    116(1),X'10'        TEST IF UNDER MVS
         BZ    MFT00               BRANCH IF NO
***      MODESET KEY=ZERO,MODE=SUP
***      MVI   48(6),X'00'         SET FILE MASK
***      MODESET KEY=NZERO,MODE=PROB
         B     COM00
** MVS **
MFT00    EQU   *
         MVC   DEB(255),0(6)       MOVE DEB FROM SUPERVISOR AREA
         MVC   DEB+255(61),255(6)
         MVC   ADEBP,DASD+45       SAVE OLD DCB POINTER TO DEB
         MVC   DASD+45(3),ADEB     CHANGE DCB POINTER TO DEB
         MVI   DEB+48,X'00'        SET FILE MASK
COM00    EQU   *
         XDAP  ECBLAB,RI,DASD,DSCB,140,,MBBCCHHR
         WAIT  ,ECB=ECBLAB
         TM    ECBLAB,X'7F'        TEST SUCCESSFUL READ
         BC    12,ABEND8
READOK   MVC   DS4DEVSZ,DSCB+64    PICK UP TRKS/CYL FOR VOLUME
         MVC   DS4HPCHR,DSCB+45    PICK UP ADDR OF LAST ACTIVE F1 DSCB
         BC    15,REALREAD
INCRMNT  LH    6,HH          INCREMENT TRK ADDR
         LA    6,1(0,6)      IF TRK ADDR GT TRKS/CYL...
         STH   6,HH
         CLC   HH,DS4DEVSZ
         BC    12,REALRED
         XC    HH,HH         SET TRK ADDR TO ZERO...
         LH    6,CC          AND...
         LA    6,1(0,6)      INCREMENT CYL ADDR...
         STH   6,CC          BURMA SHAVE.
*
*
*
*
REALRED  CLC   MBBCCHH+3(4),DS4HPCHR   IF MBBCCHH GT HIGHEST F1
         BC    2,ABEND3                     DATASET NOT ON VOLUME
REALREAD XDAP  ECBIN,RK,DASD,DATA,96,(DSNAME,44),MBBCCHH
         WAIT  ECB=ECBIN
         TM    ECBIN,X'7F'         READ COMPLETE
         BC    1,GOODREAD
         TM    ECBIN,X'41'         KEY NOT FOUND ON THIS TRK
         BC    1,INCRMNT
         TM    ECBIN,X'42'         ATTEMPT TO READ BEYOND VTOC EXTENT
         BC    1,ABEND3
         BC    15,ABEND1           SOME OTHER DUMB PROBLEM
GOODREAD EQU   *
*
*        BEFORE CARRYING OUT THE REQUESTED FUNCTION, SAVE THE CURRENT
*        STATUS IF REQUESTED BY THE CALLER
*        THIS REQUEST IS DETECTED BY LOOKING FOR A THIRD ADDRESS
*        IN THE PARAM LIST.
*        IT MUST POINT TO A 6-BYTE FIELD IN THE CALLER'S PROGRAM
*
         L     1,SAVER1            GET PARAM LIST ADDRESS
         TM    0(1),X'80'          IS FIRST ADDRESS ALSO LAST?
         BO    NOSAVE              YES. SKIP SAVING CODE
         TM    4(1),X'80'          IS SECOND ADDRESS ALSO LAST?
         BO    NOSAVE              YES. SKIP SAVING CODE
         L     1,8(0,1)            GET ADDRESS OF THIRD PARAM
         USING SAVESECT,1          GET ADDRESSABILITY
         XR    6,6                 CLEAR REG
         IC    6,DSCB+56           GET YEAR
         CVD   6,DTRACKS           CONVERT TO DECIMAL
         UNPK  SAVEYEAR(3),DTRACKS+6(2)  SAVE YEAR
         MVC   SAVEYEAR,SAVEYEAR+1 CORRECT ALIGNMENT
         OI    SAVEYEAR+1,X'F0'    CORRECT ZONE
         MVC   HWORD,DSCB+57       ALIGN DAYS
         LH    6,HWORD             AND PUT IN R6
         CVD   6,DTRACKS           COVERT TO DECIMAL
         UNPK  SAVEDAYS,DTRACKS+6(2)  SAVE DAYS
         OI    SAVEDAYS+2,X'F0'    CORRECT ZONE
         TM    DSCB+93,X'14'       W. PROT?
         BNO   *+12                NO. SKIP N2SI
         MVI   SAVEFLAG,X'02'      YES. MOVE TO PARAM 2
         B     NOSAVE              BRANCH OUT
         SPACE 1
         TM    DSCB+93,X'10'       R/W PROT?
         BNO   *+12                NO. SKIP N2SI
         MVI   SAVEFLAG,X'01'      YES. MOVE TO PARAM2
         B     NOSAVE              BRANCH OUT
         SPACE 1
         MVI   SAVEFLAG,X'03'      NO. PROT
NOSAVE   EQU   *
         TM    SW2,X'40'           DATE NOT PRESENT?
         BO    SECURE              YES. BRANCH
         PACK  DTRACKS+6(2),EXPDT(2)   PICK UP EXPIRATION YEAR
         CVB   6,DTRACKS            CONVERT TO BINARY
         STC   6,DSCB+56           STORE YEAR
         PACK  DTRACKS+6(2),EXPDT+2(3) PICK UP EXPIRATION DAY
         CVB   6,DTRACKS           CONVERT TO BINARY
         STH   6,HWORD
         MVC   DSCB+57(2),HWORD    STORE DAY
         CLI   SW1,X'00'
         BC    8,UPDATE
SECURE   EQU   *
         CLI   SW1,X'01'           R/W PROT REQD?
         BNE   WPROT               NO. BRANCH
         OI    DSCB+93,X'10'       SET FLAG IN DSCB
         NI    DSCB+93,X'FB'       SETOF READ FLAG
         B     UPDATE
         SPACE 1
WPROT    EQU   *
         CLI   SW1,X'02'           WRITE PROT REQD?
         BNE   OFF                 NO. BRANCH
         OI    DSCB+93,X'14'       SET FLAGS
         B     UPDATE
         SPACE 1
OFF      EQU   *
         CLI   SW1,X'03'           TURN OFF REQD?
         BNE   ABEND6              NO. INVALID REQUEST
         NI    DSCB+93,X'EB'       TURN OF PROT.
UPDATE   XDAP  ECBLABEL,WK,DASD,DATA,96,(DSNAME,44),MBBCCHH
         WAIT  ,ECB=ECBLABEL
         TM    ECBLABEL,X'7F'      TEST SUCCESSFUL WRITE
         BC    12,ABEND2           UNSUCCESSFUL WRITE
** MVS **
         L     1,16                GET CVT
         TM    116(1),X'10'        TEST IF UNDER MVS
         BZ    MFT02               BRANCH IF NO
         CLOSE DASD
         B     COM02
** MVS **
MFT02    EQU   *
         MVC   DASD+45(3),ADEBP    RESTORE OLD DEB POINTER TO DCB
         CLOSE DASD
COM02    EQU   *
         PGMOUT
ABEND1   ABEND 1,DUMP,STEP
ABEND2   ABEND 2,DUMP,STEP
ABEND3   ABEND 3,DUMP,STEP
ABEND4   ABEND 4,DUMP,STEP
ABEND5   ABEND 5,DUMP,STEP
ABEND6   ABEND 6,DUMP,STEP
ABEND7   ABEND 7,DUMP,STEP
ABEND8   ABEND 8,DUMP,STEP
SAVER1   DC    F'0'
DTRACKS  DC    D'0'
DEB      DS    CL316
WORD     DS    F
LIST     DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
JFCBTSDM EQU   JFCB+52
DSNAME   DS    CL44
DSCB     DS   0CL140
Z4       DC    11X'04040404'
DATA     DS    CL96
HWORD    DS    H
DS4DEVSZ DS    H
EXPDT    DS    CL5
MBBCCHHR DS    0CL8
MBBCCHH  DS    0CL7
MBB      DC    X'000000'
CC       DS    H
HH       DS    H
       DS    CL1
SW1      DC    X'00'
SW2      DC    X'00'
NONNUM   DC    X'F0F0F0F0F0'
DS4HPCHR DS    CL4
ADEB     DC    AL3(DEB+16)
ADEBP    DS    CL3
DASD     DCB   DDNAME=SYSDISK,MACRF=(E),EXLST=LIST,EODAD=ABEND3
         LTORG
SAVESECT DSECT
SAVEYEAR DS    CL2
SAVEDAYS DS    CL3
SAVEFLAG DS    CL1
         END
