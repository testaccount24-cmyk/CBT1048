000010 ID DIVISION.
000020 PROGRAM-ID.     T586P12.
000030 AUTHOR.         P.S.GRANT   MONSANTO EUROPE S.A.
000040                             PLACE MADOU,1
000050                             1030 BRUXELLES
000060                             BELGIUM  TEL: (02)219-1101.
000070 DATE-WRITTEN.   FEB 1975
000080 DATE-COMPILED.
000090 REMARKS.        SORT AND PRINT THE MOPP SYSTEM
000100                 AUDIT-TRAIL AS THE PRODUCTION
000110                 UPDATE LOG.
000120
000130 ENVIRONMENT DIVISION.
000140 CONFIGURATION SECTION.
000150 SOURCE-COMPUTER. IBM-370.
000160 OBJECT-COMPUTER. IBM-370.
000170 SPECIAL-NAMES.  C01 IS PAGE-EJECT.
000180 INPUT-OUTPUT SECTION.
000190 FILE-CONTROL.
000200*    SELECT MASTER-FILE ASSIGN TO DA-D-MASTER
000210     SELECT HISTORY-INPT ASSIGN TO UT-S-HISTIN.
000220     SELECT HISTORY-OUTPT ASSIGN TO UT-S-HISTOUT.
000230     SELECT PRINT-FILE ASSIGN TO UT-S-SYSPRINT.
000240     SELECT SORT-FILE ASSIGN  TO UT-S-NODDNAME.
000250
000260 DATA DIVISION.
000270 FILE SECTION.
000280 FD  HISTORY-INPT
000290     RECORDING MODE IS F
000300     RECORD CONTAINS 156 CHARACTERS
000310     BLOCK CONTAINS 0 RECORDS
000320     LABEL RECORDS ARE STANDARD
000330     DATA RECORD IS HISTORY-INPT-REC.
000340 01  HISTORY-INPT-REC.
000350     03  HI-TYPEA                PIC X.
000360     03  FILLER                  PIC X(8).
000370     03  HI-MEMBER               PIC X(8).
000380     03  FILLER                  PIC X(12).
000390     03  HI-SYSTEM               PIC X(8).
000400     03  FILLER                  PIC X(43).
000410     03  HI-DATE                 PIC X(6).
000420     03  FILLER                  PIC X(70).
000430
000440 FD  HISTORY-OUTPT
000450     RECORDING MODE IS F
000460     RECORD CONTAINS 156 CHARACTERS
000470     BLOCK CONTAINS 0 RECORDS
000480     LABEL RECORDS ARE STANDARD
000490     DATA RECORD IS HISTORY-OUTPT-REC.
000500 01  HISTORY-OUTPT-REC           PIC X(156).
000510
000520 FD  PRINT-FILE
000530     RECORDING MODE IS F
000540     RECORD CONTAINS 133 CHARACTERS
000550     BLOCK CONTAINS 0 RECORDS
000560     LABEL RECORDS ARE STANDARD
000570     DATA RECORD IS PRINT-REC.
000580 01  PRINT-REC                   PIC X(133).
000590
000600 SD  SORT-FILE
000610     RECORDING MODE IS F
000620     RECORD CONTAINS 156 CHARACTERS
000630     DATA RECORD IS SORT-REC.
000640 01  SORT-REC.
000650     03  SR-TYPE.
000660         05  SR-TYPEA            PIC X.
000670         05  SR-TYPEB            PIC XXX.
000680         05  SR-TYPEC            PIC XXXX.
000690     03  FILLER                  PIC X.
000700     03  SR-MEMBER               PIC X(8).
000710     03  FILLER                  PIC X.
000720     03  SR-PSWD                 PIC XXXX.
000730     03  FILLER                  PIC X.
000740     03  SR-SOURCE               PIC X(5).
000750     03  FILLER                  PIC X.
000760     03  SR-SYSTEM               PIC X(8).
000770     03  SR-SEC-FLAG             PIC X.
000780     03  SR-REQUESTOR            PIC X(10).
000790     03  SR-RMTID                PIC XX.
000800     03  SR-ALIAS1               PIC X(8).
000810     03  SR-ALIAS2               PIC X(8).
000820     03  FILLER                  PIC X.
000830     03  SR-SYSTEM2              PIC X(8).
000840     03  SR-NUMBER               PIC X(5).
000850     03  SR-DATE.
000860         05 SR-YY                PIC XX.
000870         05 SR-MM                PIC XX.
000880         05  SR-DD               PIC XX.
000890     03  SR-NARRATIVE            PIC X(70).
000900 WORKING-STORAGE SECTION.
000910 77  F-RETCD                     PIC 9.
000920 77  F-NAME                      PIC X(8) VALUE 'AUDITTRL'.
000930 77  ABEND-CODE                  PIC 999 VALUE 586.
000940 77  SORT-OUTPUT-SWITCH          PIC X VALUE SPACES.
000950 77  LINE-CTR                    PIC 999 COMP-3 VALUE 66.
000960 77  PAGE-CTR                    PIC 999 COMP-3 VALUE ZERO.
000970 01  HEADINGS.
000980     03  HEAD-1.
000990         05  H1-CTL              PIC X.
001000         05  H1-PROGNO           PIC X(8) VALUE 'T586P12'.
001010         05  FILLER              PIC X(10) VALUE SPACES.
001020         05  H1-MOPP             PIC X(4) VALUE 'MOPP'.
001030         05  FILLER              PIC X(22) VALUE SPACES.
001040         05  H1-TITLE            PIC X(34)
001050             VALUE 'PRODUCTION LIBRARY UPDATE REGISTER'.
001060         05  FILLER              PIC X(6) VALUE SPACES.
001070         05  H1-FROM-DATE        PIC X(9) VALUE SPACES.
001080         05  H1-TO               PIC X(4) VALUE ' TO '.
001090         05  H1-TO-DATE          PIC X(9) VALUE SPACES.
001100         05  FILLER              PIC X(13) VALUE SPACES.
001110         05  FILLER              PIC X(5) VALUE SPACES.
001120         05  H1-PAGE             PIC X(5) VALUE 'PAGE '.
001130         05  H1-PAGE-NO          PIC ZZZ  VALUE ZERO.
001140     03  HEAD-2.
001150         05  H2-CTL              PIC X.
001160         05  H2-DATE             PIC X(6) VALUE '  DATE'.
001170         05  FILLER              PIC X(8) VALUE SPACES.
001180         05  H2-MEMBER           PIC X(11) VALUE 'MEMBER'.
001190         05  H2-REQ              PIC X(14) VALUE 'REQUESTED'.
001200         05  H2-UPDATE           PIC X(11) VALUE ' UPDATE '.
001210         05  H2-SEC              PIC X(4)  VALUE 'SEC'.
001220         05  FILLER              PIC X(8) VALUE SPACES.
001230         05  FILLER              PIC X(27) VALUE SPACES.
001240         05  H2-NARRATIVE        PIC X(16)
001250             VALUE 'UPDATE NARRATIVE'.
001260         05  FILLER              PIC X(27) VALUE SPACES.
001270
001280     03  HEAD-3.
001290         05  H3-CTL              PIC X.
001300         05  FILLER              PIC X(28) VALUE SPACES.
001310         05  H3-BY               PIC X(11) VALUE 'BY'.
001320         05  H3-TYPE             PIC X(11) VALUE 'TYPE  NO'.
001330         05  H3-FLAG             PIC X(12) VALUE 'FLAG'.
001340         05  H3-HYPHENS          PIC X(70) VALUE ALL '-'.
001350
001360     03  HEAD-1B.
001370         05  H1B-CTL             PIC X.
001380         05  FILLER              PIC X(48) VALUE SPACES.
001390         05  H1B-FOR             PIC X(5) VALUE 'FOR'.
001400         05  H1B-SYSTEM          PIC X(8) VALUE SPACES.
001410         05  FILLER              PIC  XX VALUE SPACES.
001420         05  H1B-TYPE            PIC X(12) VALUE SPACES.
001430         05  FILLER              PIC X(57) VALUE SPACES.
001440
001450 01  DETAIL-LINE.
001460     03  DETAIL-1.
001470         05  D1-CTL              PIC X.
001480         05  D1-DATE             PIC X(9) VALUE SPACES.
001490         05  FILLER              PIC X(4) VALUE SPACES.
001500         05  D1-MEMBER           PIC X(8) VALUE SPACES.
001510         05  FILLER              PIC X(4) VALUE SPACES.
001520         05  D1-REQUESTOR        PIC X(10) VALUE SPACES.
001530         05  FILLER              PIC X(4) VALUE SPACES.
001540         05  D1-UPDATE-TYPE      PIC XXX  VALUE SPACES.
001550         05  FILLER              PIC X    VALUE SPACES.
001560         05  D1-UPDATE-NO        PIC X(5) VALUE SPACES.
001570         05  FILLER              PIC XXX  VALUE SPACES.
001580         05  D1-SEC-FLAG         PIC X VALUE SPACE.
001590         05  FILLER              PIC X(10) VALUE SPACES.
001600         05  D1-NARRATIVE        PIC X(70) VALUE SPACES.
001610
001620 01  KEYS.
001630     03  AUDITTRAIL-KEY.
001640         05  AK-SYSTEM           PIC X(8).
001650         05  AK-TYPEA            PIC X.
001660         05  AK-MEMBER           PIC X(8).
001670         05  AK-DATE.
001680             07  AK-YY           PIC XX.
001690             07  AK-MM           PIC XX.
001700             07  AK-DD           PIC XX.
001710     03  HISTORY-KEY.
001720         05  HK-SYSTEM           PIC X(8).
001730         05  HK-TYPEA            PIC X.
001740         05  HK-MEMBER           PIC X(8).
001750         05  HK-DATE.
001760             07  HK-YEAR         PIC XX.
001770             07  HK-MMDD         PIC XXXX.
001780     03 WORK-KEY.
001790         05  WK-SYSTEM           PIC X(8) VALUE SPACES.
001800         05  WK-TYPEA            PIC X VALUE SPACES.
001810         05  WK-MEMBER           PIC X(8) VALUE SPACES.
001820 01  DATE-AREA.
001830     03  DATE-OPCODE             PIC X.
001840     03  DATE-1-MMDDYY           PIC X(6).
001850     03  DATE-2-DDMMYY           PIC X(6).
001860     03  DATE-3-YYMMDD           PIC X(6).
001870     03  DATE-4-DWWYY            PIC X(5).
001880     03  DATE-5-YYWWD            PIC X(5).
001890     03  DATE-6-YYDDD            PIC X(5).
001900     03  DATE-REPORT             PIC X(9).
001910     03  DATE-LASTMONTH          PIC X(9).
001920     03  DATE-THISMONTH          PIC X(9).
001930     03  DATE-NEXTMONTH          PIC X(9).
001940     03  DATE-DAY                PIC X(9).
001950     03  DATE-HHMMSS             PIC X(6).
001960     03  DATE-RTCODE             PIC X.
001970 01  DATE-OVERLAY REDEFINES DATE-AREA.
001980     03  FILLER                  PIC X(13).
001990     03  DATE-YY                 PIC XX.
002000     03  DATE-MM                 PIC XX.
002010     03  DATE-DD                 PIC XX.
002020     03  DATE-D                  PIC X.
002030     03  DATE-WW                 PIC XX.
002040     03  FILLER                  PIC X(9).
002050     03  DATE-DDD                PIC XXX.
002060
002070 01  FAIR-AREA.
002080     03  F-MODULE-HEADER.
002090         05  FH-PSWD             PIC XXXX.
002100         05  FH-DATE-ADDED       PIC X(6).
002110         05  FH-DATE-LASTUPDT    PIC X(6).
002120         05  FH-TIME-LASTUPDT    PIC X(4).
002130         05  FH-CARDCOUNT        PIC 9(5) COMP-3.
002140         05  FH-PROGRAMMER       PIC X(15).
002150         05  FH-PROC             PIC X(8).
002160         05  FH-DESCRIPTION      PIC X(30).
002170*    NOTE THE NEXT 8 FIELDS DEFINED CONTAIN BINARY VALUES.
002180         05  FH-SEQ-START        PIC X.
002190         05  FH-SEQ-LENGTH       PIC X.
002200         05  FH-SEQ-INCREMENT    PIC XX.
002210         05  FH-SEQ-INITIAL      PIC XX.
002220         05  FH-ACCESSES         PIC XX.
002230         05  FH-UPDATES          PIC XX.
002240         05  FH-EXTENTS          PIC X.
002250         05  FH-BLOCKS           PIC X.
002260         05  FILLER              PIC X(12).
002270
002280
002290     03  F-UPDATE-RECORD REDEFINES F-MODULE-HEADER.
002300         05  FR-TYPE.
002310             07  FR-TYPEA        PIC X.
002320          06 FR-TYPEBC.
002330             07  FR-TYPEB        PIC XXX.
002340             07  FR-TYPEC        PIC XXXX.
002350         05  FILLER              PIC X.
002360         05  FR-MEMBER           PIC X(8).
002370         05  FILLER              PIC X.
002380         05  FR-PSWD             PIC X(4).
002390         05  FILLER              PIC X.
002400         05  FR-SOURCE           PIC X(5).
002410         05  FILLER              PIC X.
002420         05  FR-SYSTEM           PIC X(8).
002430         05  FR-SEC-FLAG         PIC X.
002440         05  FR-REQUESTOR        PIC X(10).
002450         05  FR-RMTID            PIC XX.
002460         05  FR-ALIAS1           PIC X(8).
002470         05  FR-ALIAS2           PIC X(8).
002480         05  FILLER              PIC X.
002490         05  FR-SYSTEM2          PIC X(8).
002500         05  FR-NUMBER           PIC X(5).
002510         05  FILLER              PIC X(10).
002520         05  FR-DATE.
002530             07  FR-MM           PIC XX.
002540             07  FILLER          PIC X.
002550             07  FR-DD           PIC XX.
002560             07  FILLER          PIC X.
002570             07  FR-YY           PIC XX.
002580         05  FILLER              PIC XX.
002590     03  F-NARRATIVE-RECORD REDEFINES F-MODULE-HEADER.
002600         05  FN-HST              PIC X(4).
002610         05  FILLER              PIC X.
002620         05  FN-NARRATIVE        PIC X(70).
002630         05  FN-NUMBER           PIC X(5).
002640         05  FILLER              PIC X(20).
002650
002660 01  WORK-AREA.
002670     03  WA-TYPE.
002680         05  WA-TYPEA            PIC X.
002690         05  WA-TYPEB            PIC XXX.
002700         05  WA-TYPEC            PIC XXXX.
002710     03  FILLER                  PIC X.
002720     03  WA-MEMBER               PIC X(8).
002730     03  FILLER                  PIC X.
002740     03  WA-PSWD                 PIC XXXX.
002750     03  FILLER                  PIC X.
002760     03  WA-SOURCE               PIC X(5).
002770     03  FILLER                  PIC X.
002780     03  WA-SYSTEM               PIC X(8).
002790     03  WA-SEC-FLAG             PIC X.
002800     03  WA-REQUESTOR            PIC X(10).
002810     03  WA-RMTID                PIC XX.
002820     03  WA-ALIAS1               PIC X(8).
002830     03  WA-ALIAS2               PIC X(8).
002840     03  FILLER                  PIC X.
002850     03  WA-SYSTEM2              PIC X(8).
002860     03  WA-NUMBER               PIC X(5).
002870     03  WA-DATE.
002880         05  WA-YY               PIC XX.
002890         05  WA-MM               PIC XX.
002900         05  WA-DD               PIC XX.
002910     03  WA-NARRATIVE            PIC X(70).
002920 01  RECORD-TYPE-DATA.
002930     03  FILLER      PIC X(20) VALUE '1TSTPROGPROGRAMS'.
002940     03  FILLER      PIC X(20) VALUE '1ADDPROGPROGRAMS'.
002950     03  FILLER      PIC X(20) VALUE '1REPPROGPROGRAMS'.
002960     03  FILLER      PIC X(20) VALUE '2ADDPRC PROCEDURES'.
002970     03  FILLER      PIC X(20) VALUE '2REPPRC PROCEDURES'.
002980     03  FILLER      PIC X(20) VALUE '2CHGPRC PROCEDURES'.
002990     03  FILLER      PIC X(20) VALUE '3ADDCTL CNTROL CARDS'.
003000     03  FILLER      PIC X(20) VALUE '3REPCTL CNTROL CARDS'.
003010     03  FILLER      PIC X(20) VALUE '4ADDTAB TABLES'.
003020     03  FILLER      PIC X(20) VALUE '4REPTAB TABLES'.
003030     03  FILLER      PIC X(20) VALUE '4CHGTAB TABLES'.
003040     03  FILLER      PIC X(20) VALUE '5ADDFD  FILE DEFNS.'.
003050     03  FILLER      PIC X(20) VALUE '5REPFD  FILE DEFNS.'.
003060     03  FILLER      PIC X(20) VALUE '5CHGFD  FILE DEFNS.'.
003070     03  FILLER      PIC X(20) VALUE '6ADDBIRDPARROT-BUDGI'.
003080     03  FILLER      PIC X(20) VALUE '6REPBIRDPARROT-BUDGI'.
003090     03  FILLER      PIC X(80) VALUE SPACES.
003100
003110 01  RECORD-TYPE-TABLE REDEFINES RECORD-TYPE-DATA.
003120     03  RT-ENTRY OCCURS 20 TIMES INDEXED BY RT-SUB.
003130     05  RT-TYPEA                PIC X.
003140     05  RT-TYPEBC.
003150         07  RT-TYPEB            PIC XXX.
003160         07  RT-TYPEC            PIC XXXX.
003170     05  RT-TITLE                PIC X(12).
003180     EJECT
003190 PROCEDURE DIVISION.
003200     MOVE '0' TO DATE-OPCODE.
003210     PERFORM CALL-SUPERDATE.
003220     MOVE DATE-REPORT TO H1-TO-DATE.
003230     CALL 'MASTOPN' USING F-RETCD, F-RETCD.
003240     CALL 'MASTMOD' USING F-NAME, F-RETCD, FAIR-AREA.
003250     IF F-RETCD NOT EQUAL TO ZERO
003260         MOVE F-RETCD TO ABEND-CODE
003270         GO TO PROGRAM-ABORT.
003280     OPEN INPUT HISTORY-INPT.
003290     OPEN OUTPUT HISTORY-OUTPT.
003300     OPEN OUTPUT PRINT-FILE.
003310     PERFORM READ-MASTER.
003320     PERFORM READ-HISTORY.
003330     IF AK-TYPEA = HIGH-VALUES
003340         AND HK-TYPEA = HIGH-VALUES
003350         DISPLAY 'T586P12 - NO INPUT'
003360         GO TO EOJ.
003370     IF  AK-TYPEA = HIGH-VALUES
003380         DISPLAY 'T586P12 - NO MASTER FILE INPUT'
003390         PERFORM SELECT-OUTPUT-RECORD THRU LIST-AND-MERGE-EXIT
003400         GO TO EOJ.
003410     IF  HK-TYPEA = HIGH-VALUES
003420         DISPLAY 'T586P12 - NO HISTORY INPUT'.
003430
003440     SORT SORT-FILE ON ASCENDING KEY SR-SYSTEM
003450                    ON ASCENDING KEY SR-TYPEA
003460                    ON ASCENDING KEY SR-MEMBER
003470                    ON ASCENDING KEY SR-DATE
003480                    ON ASCENDING KEY SR-NUMBER
003490     INPUT PROCEDURE SORT-MASTER-INPUT
003500     OUTPUT PROCEDURE LIST-AND-MERGE-LOG.
003510
003520 EOJ.
003530     MOVE ' END OF T586P12' TO DETAIL-LINE
003540     WRITE PRINT-REC FROM DETAIL-LINE AFTER 3 LINES.
003550     CLOSE PRINT-FILE.
003560     CLOSE HISTORY-OUTPT.
003570     CLOSE HISTORY-INPT.
003580     CALL 'MASTCLS' USING F-RETCD.
003590     STOP RUN.
003600     CALL 'FAIR'.
003610     EJECT
003620 SORT-MASTER-INPUT SECTION.
003630
003640 NEXT-INPUT-RECORD.
003650     IF  AK-TYPEA = HIGH-VALUES
003660             GO TO CLOSE-MASTER.
003670     SET RT-SUB TO 1.
003680     SEARCH RT-ENTRY
003690         AT END GO TO READ-NEXT-MASTER
003700         WHEN FR-TYPEBC = RT-TYPEBC (RT-SUB)
003710         NEXT SENTENCE.
003720     MOVE RT-TYPEA (RT-SUB) TO FR-TYPEA
003730     MOVE F-UPDATE-RECORD TO SORT-REC.
003740     MOVE FR-YY TO SR-YY.
003750     MOVE FR-MM TO SR-MM.
003760     MOVE FR-DD TO SR-DD.
003770     PERFORM READ-MASTER.
003780     MOVE SPACES TO SR-NARRATIVE.
003790     IF FR-NUMBER = SR-NUMBER
003800         MOVE FN-NARRATIVE TO SR-NARRATIVE
003810         PERFORM READ-MASTER.
003820     EXAMINE SR-NUMBER REPLACING LEADING ZEROES BY SPACES.
003830     RELEASE SORT-REC.
003840     GO TO NEXT-INPUT-RECORD.
003850 READ-NEXT-MASTER.
003860     PERFORM READ-MASTER.
003870     GO TO NEXT-INPUT-RECORD.
003880 CLOSE-MASTER.
003890 MASTER-INPUT-EXIT.
003900     EXIT.
003910     SKIP3
003920 LIST-AND-MERGE-LOG SECTION.
003930 CHECK-RETURN-CODE.
003940     IF SORT-RETURN = 16 GO TO PROGRAM-ABORT.
003950 GET-SORTED-RECORD.
003960     RETURN SORT-FILE AT END
003970         MOVE HIGH-VALUES TO AUDITTRAIL-KEY,
003980         GO TO SELECT-OUTPUT-RECORD.
003990     MOVE SR-SYSTEM TO  AK-SYSTEM.
004000     MOVE SR-TYPEA  TO  AK-TYPEA.
004010     MOVE SR-DATE   TO  AK-DATE.
004020     MOVE SR-MEMBER TO  AK-MEMBER.
004030 SELECT-OUTPUT-RECORD.
004040     IF AUDITTRAIL-KEY = HIGH-VALUES
004050         AND HISTORY-KEY = HIGH-VALUES
004060         GO TO LIST-AND-MERGE-EXIT.
004070     IF HISTORY-KEY IS LESS THAN AUDITTRAIL-KEY
004080        MOVE HISTORY-INPT-REC TO WORK-AREA,
004090        PERFORM READ-HISTORY
004100     ELSE
004110        MOVE SORT-REC TO WORK-AREA
004120        MOVE  '1' TO SORT-OUTPUT-SWITCH.
004130 WRITE-HISTORY-OUTPUT.
004140     WRITE HISTORY-OUTPT-REC FROM WORK-AREA.
004150
004160 FORMAT-UPDATE-LOG.
004170     IF  WK-SYSTEM = WA-SYSTEM
004180         AND WK-TYPEA = WA-TYPEA
004190         GO TO NO-SYSTEM-CHANGE.
004200     MOVE WA-SYSTEM TO WK-SYSTEM.
004210     MOVE WA-TYPEA TO WK-TYPEA.
004220     MOVE WA-SYSTEM TO H1B-SYSTEM.
004230     SET RT-SUB TO 1.
004240     SEARCH RT-ENTRY AT END MOVE 'UNKNOWN' TO H1B-TYPE
004250         WHEN WA-TYPEA = RT-TYPEA (RT-SUB)
004260         MOVE RT-TITLE (RT-SUB) TO H1B-TYPE.
004270     PERFORM PAGE-HEADINGS.
004280
004290 NO-SYSTEM-CHANGE.
004300     IF WA-MEMBER = WK-MEMBER
004310         MOVE SPACES TO D1-MEMBER
004320         GO TO NO-PROGRAM-CHANGE.
004330     MOVE SPACES TO DETAIL-1.
004340     WRITE PRINT-REC FROM DETAIL-1 AFTER 1 LINES.
004350     ADD 1 TO LINE-CTR.
004360     MOVE WA-MEMBER TO WK-MEMBER, D1-MEMBER.
004370
004380 NO-PROGRAM-CHANGE.
004390     IF LINE-CTR IS GREATER THAN 60
004400         PERFORM PAGE-HEADINGS.
004410     MOVE WA-DATE TO  DATE-3-YYMMDD.
004420     MOVE '3' TO DATE-OPCODE.
004430     PERFORM CALL-SUPERDATE.
004440     MOVE DATE-REPORT TO D1-DATE.
004450     MOVE WA-REQUESTOR TO D1-REQUESTOR.
004460     MOVE WA-TYPEB TO D1-UPDATE-TYPE.
004470     MOVE WA-NUMBER TO D1-UPDATE-NO.
004480     MOVE SPACES TO D1-SEC-FLAG.
004490     IF WA-SEC-FLAG = SPACES
004500         AND WA-TYPEB = 'REP'
004510         MOVE 'Y' TO D1-SEC-FLAG.
004520     MOVE WA-NARRATIVE TO D1-NARRATIVE.
004530
004540 PRINT-REPORT-LINE.
004550     WRITE PRINT-REC FROM DETAIL-1 AFTER 1 LINES.
004560     ADD 1 TO LINE-CTR.
004570
004580 NEXT-RECORD-DECISION.
004590     IF SORT-OUTPUT-SWITCH = '1'
004600         MOVE SPACES TO SORT-OUTPUT-SWITCH
004610         GO TO GET-SORTED-RECORD.
004620     GO TO SELECT-OUTPUT-RECORD.
004630
004640 LIST-AND-MERGE-EXIT.
004650     EXIT.
004660     EJECT
004670 PROGRAM-ABORT SECTION.
004680     CALL 'COBDUM' USING ABEND-CODE.
004690     SKIP3
004700 READ-MASTER SECTION.
004710     CALL 'MASTRCD' USING FAIR-AREA F-RETCD.
004720     IF F-RETCD IS GREATER THAN ZERO
004730         MOVE HIGH-VALUES TO AUDITTRAIL-KEY
004740         GO TO READ-MASTER-EXIT.
004750     MOVE FR-SYSTEM TO AK-SYSTEM.
004760     MOVE FR-TYPEA TO AK-TYPEA.
004770     MOVE FR-YY TO AK-YY.
004780     MOVE FR-MM TO AK-MM.
004790     MOVE FR-DD TO AK-DD.
004800     MOVE FR-MEMBER TO AK-MEMBER.
004810
004820 READ-MASTER-EXIT.
004830     EXIT.
004840     SKIP3
004850 READ-HISTORY SECTION.
004860     READ HISTORY-INPT
004870         AT END MOVE HIGH-VALUES TO HISTORY-KEY
004880         GO TO READ-HISTORY-EXIT.
004890     MOVE HI-SYSTEM TO HK-SYSTEM
004900     MOVE HI-TYPEA TO HK-TYPEA.
004910     MOVE HI-DATE TO HK-DATE.
004920     MOVE HI-MEMBER TO HK-MEMBER.
004930
004940 READ-HISTORY-EXIT.
004950     EXIT.
004960     SKIP3
004970 PAGE-HEADINGS SECTION.
004980     ADD 1 TO PAGE-CTR.
004990     MOVE PAGE-CTR TO H1-PAGE-NO.
005000     WRITE PRINT-REC FROM HEAD-1 AFTER PAGE-EJECT.
005010     WRITE PRINT-REC FROM HEAD-1B AFTER 2 LINES.
005020     WRITE PRINT-REC FROM HEAD-2 AFTER 3 LINES.
005030     WRITE PRINT-REC FROM HEAD-3 AFTER 1 LINES.
005040     MOVE SPACES TO DETAIL-1.
005050     WRITE PRINT-REC FROM DETAIL-1 AFTER 1 LINES.
005060     MOVE WA-MEMBER TO D1-MEMBER.
005070     MOVE 9 TO LINE-CTR.
005080     SKIP3
005090 CALL-SUPERDATE SECTION.
005100     CALL 'UT458' USING DATE-AREA.
005110     IF DATE-RTCODE NOT = ZERO
005120         MOVE '458' TO ABEND-CODE
005130         GO TO PROGRAM-ABORT.
005140 CALL-SUPERDATE-EXIT. EXIT.
