//STEP01       EXEC ASMFCL
//ASM.SYSIN    DD   *
         TITLE 'RIGAM INTERFACE FOR PL/1'
UT412    CSECT
         ENTRY PL1RIGM
         PRINT ON,NOGEN,NODATA     DEFINE PRINT
         SPACE 1
*        THIS SUB-PROGRAM WAS WRITTEN TO ENABLE PL/1 PROGRAMS
*         TO USE RIGAM.  PL/1 CANNOT DYNAMO RIGAM DIRECTLY
*         BECAUSE A SPURIOUS FIXED OVERFLOW CONDITION
*         VALIDLY TRIGGERED BY RIGAM PROCESSING IS
*         INTERCEPTED BY THE PL/1  ERROR HANDLER, AND ALSO
*         BECAUSE PL/1 CANNOT PASS AN EXCEPTION-POINTER
*         ADDRESS IN A FORM ACCEPTABLE TO RIGAM.
*        THIS PROGRAM THUS SERVES TWO  PURPOSES :-
*        1. IT SETS OFF FIXED OVERFLOW IN THE PROGRAM MASK
*             IN THE PSW BEFORE CALLING RIGAM, AND SETS IT
*             ON AGAIN AFETR RIGAM HAS FINISHED,        AND
*        2. IT PRESERVES AND RESETS THE RETURN ADDRESS IN
*             ITS SAVE AREA IN ORDER TO OVER-WRITE THE
*             INVALID RETURN ADDRESS ESTABLISHED BY RIGAM
*             WHEN IT TRIES TO PASS CONTROL TO THE ERROR
*             ROUTINE.  THIS EFFECTIVELY NOPS THE EXCEPTION
*             POINTER ROUTINE ADDRESS, AND THE PL/1 PROGRAM
*             MUST CHECK THE RIGAM RETURN CODE FOR NON-ONE.
*        NOTE THAT THIS MODULE MAY BE USED TO DYNAMICALLY
*         INVOKE ANY SUB-ROUTINE THAT REQUIRES THE FIXED
*         OVERFLOW MASK BIT TO BE OFF.
         SPACE 1
PL1RIGM  PGMIN USING=13            HOUSEKEEPING
         ST    R14,R14SAVE         PRESERVE RETURN ADDR FOR LATER
         L     R4,0(,R1)           PICK UP POINTER TO MODULE NAME
         MVC   EPOINT,0(R4)        SET UP MODULE NAME FOR LOAD
         LA    R4,4(,R1)           SET  UP PARM POINTER
         BALR  R6,0                PICK UP PGM MASK FROM PSW
         ST    R6,SPMSAVE          PREVSERVE PGM MASK
         N     R6,SPMMASK          TURN OFF FIXED OVERFLOW
         SPM   R6                   AND SET IN PSW
         LOAD  EPLOC=EPOINT        LOAD MODULE ( IF NECEQQARY )
         LR    R15,R0              SET R15 TO ENTRY POINT
         LR    R1,R4               STE  UP PARM POINTER
         BALR  R14,R15             EXECUTE SELECTED MODULE
        DELETE EPLOC=EPOINT        DELETE MODULE ( IF NECESSARY )
         L     R6,SPMSAVE          PICK UP ORIGINAL PGM MASK
         SPM   R6                   AND RESET IT IN PSW
         L     R10,4(,R13)         PICK UP ADDRESS OF PREVIOUS S/A
         MVC   12(4,R10),R14SAVE   RESET POSSIBLY CORRUPTED RET ADDR
        PGMOUT ,                   RETRUN TO CALLER
         SPACE 1
R14SAVE  DS    F                   SAVE AREA  FOR RETRUN ADDRESS
SPMSAVE  DS    F                   SAVE AREA FOR PROGRAM MASK
SPMMASK  DC    X'F7FFFFFF'         MASK TO SET OFF FIXED OVERFLOW
EPOINT   DC    CL8' '              AREA FOR MODULE NAME
         END
