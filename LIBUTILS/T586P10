T586P10  START 0
         SPACE 2
***********************************************************************
*        PROGRAM ID :              T586P10                            *
*        DATE WRITTEN :            JUNE 1974                          *
*        LOCATION :                EUROPEAN COMPUTER CENTER           *
*        AUTHOR :                  J. NOLS                            *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*              PURPOSE                                                *
*             =========                                               *
*                                                                     *
*         TO CREATE RECORDS DESCRIBING THE SECURITY TAPES             *
*         IN ORDER TO SUPPLY A PULLING LIST.                          *
*        DDNAMES :   SYSPRINT - ERROR MESSAGES IF ANY.                *
*                    SYSUT1   - POINTS TO TAPE FILE WHOSE JFCB        *
*                               IS TO BE ACCESSED                     *
*                    SYSUT2   - DATA SET TO WHICH HISTORY RECORDS     *
*                               ARE WRITTEN.                          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        REGISTERS EQUATES      /  HOUSEKEEPING                       *
***********************************************************************
         SPACE 1
         PGMIN EQUATES=YES,USING=(R13)  ESTABLISH ADDRESSABILITY
         SPACE 3
         PRINT NOGEN
         EJECT
***********************************************************************
*                                                                     *
*        READ JFCB OF SYSUT1, CHECK IF THE DSNAME OF THIS JFCB        *
*        START WITH 'LIBRARYN' AND IF IS PART OF A GDG. READ THE      *
*        CATALOG FOR THIS DSNAME BUT FOR GENERATION (-1) AND          *
*        FOR EACH VOLSER IN THE JFCB , CREATE AN OUTPUT RECORD        *
*        ON SYSUT2 WITH VOLSER OF EQUIVALENT TAPE IN (-1) GENERATION. *
*                                                                     *
***********************************************************************
         SPACE 1
         RDJFCB (SYSUT1)                READ JFCB FOR SYSUT1
         CLC   JFCBDSNM(8),=C'LIBRARYN' TEST IF DSNAME OF SYSUT1 START-
         BNE   ERROR1                   NO - ERROR    / WITH 'LIBRARYN'
         TIME  ,                        REQUEST IPL DATE AND TIME
         STM   R0,R1,DECIMAL            STORE PACKED DATE AND TIME
         UNPK  IPLDATE,DECIMAL+5(3)     UNPACK DATE
         OI    IPLDATE+4,SIGN           FORCE SIGN TO 'F'
         UNPK  IPLTIME(7),DECIMAL(4)    UNPACK TIME
         MVC   DSNAME,JFCBDSNM          MOVE DSNAME TO OUTPUT RECORD
         MVC   VOLSEQTT,JFCBFLSQ   MOVE VOL SEQ TO OUTPUT RECORD
         MVC   LOCDSN,JFCBDSNM     MOVE DSNAME FROM JFCB TO WK DSN FOR-
         LA    R5,LOCDSN+9         ADDRESS LOCDSN+9     / LOCATE MACRO
         LA    R6,27               ZERO AND ADD 27
LOOP1    CLC   0(2,R5),=C'.G'      TEST IF GDG NAME START A(R5)
         BNE   NT1                 NO - PREPARE FOR TEST NEXT POSITION
         CLC   6(3,R5),=C'V00'     CONFIRME IF GDG NAME START A(R5)
         BE    NT2                 YES - PREPARE WK DSNAME FOR LOCATE-
NT1      LA    R5,1(R5)            ADD ONE                   /  MACRO
         BCT   R6,LOOP1            REPEAT 26 TIMES
         B     ERROR2              ERROR THIS DS IS NOT A GDG
NT2      EQU   *
NT4      OPEN  (SYSUT2,(OUTPUT))   OPEN SYSUT2 FILE
         MVC   VOLSERTT,0(R6)      MOVE VOL SER FROM JFCB TO RECOUT
         PUT   SYSUT2,RECOUT       WRITE RECOUT TO SYSUT2
         B     END                 GO TO END OF JOB
         EJECT
***********************************************************************
*        ERRORS ROUTINE                                               *
***********************************************************************
         SPACE 1
ERROR1   MVC   PRINTOUT+1(L'MSGE1),MSGE1     MOVE MESSAGE ERROR 1
         B     OPCA
ERROR2   MVC   PRINTOUT+1(L'MSGE2),MSGE2     MOVE MESSAGE ERROR 2
         B     OPCA
ERROR3   MVC   PRINTOUT+1(L'MSGE3),MSGE3     MOVE MESSAGE ERROR 3
         B     OPCA
ERROR4   MVC   PRINTOUT+1(L'MSGE4),MSGE4     MOVE MESSAGE ERROR 4
OPCA     OPEN  (SYSPRINT,(OUTPUT))      OPEN SYSPRINT FILE
         PUT   SYSPRINT,PRINTOUT        PRINT ERROR MESSAGES
         CLOSE (SYSPRINT)               CLOSE SYSPRINT FILE
         ABEND 581,DUMP                 ABEND U0581
         SPACE 3
***********************************************************************
*        CLOSE FILE AND END OF PROGRAM                                *
***********************************************************************
         SPACE 1
END      CLOSE (SYSUT2)            CLOSE SYSUT2 FILE
         PGMOUT ,                  END OF PROGRAM
         EJECT
***********************************************************************
*        DATA CONTROL BLOCKS                                          *
***********************************************************************
         SPACE 1
SYSPRINT DCB   DDNAME=SYSPRINT,                                        C
               DSORG=PS,                                               C
               RECFM=FM,                                               C
               LRECL=133,                                              C
               BLKSIZE=133,                                            C
               MACRF=PM
         SPACE 1
SYSUT1   DCB   DDNAME=SYSUT1,                                          C
               MACRF=GM,                                               C
               DSORG=PS,                                               C
               RECFM=F,                                                C
               BLKSIZE=140,                                            C
               EXLST=STRJFCB
         SPACE 1
SYSUT2   DCB   DDNAME=SYSUT2,                                          C
               DSORG=PS,                                               C
               RECFM=FB,                                               C
               LRECL=80,                                               C
               BLKSIZE=1680,                                           C
               MACRF=PM
         SPACE 3
***********************************************************************
*        DEFINITION OUTPUT RECORD (SYSUT2) AND OUTPUT PRINT (SYSPRINT)*
***********************************************************************
         SPACE 1
RECOUT   DS    0CL80
         DC    C'LIB01'            RECORD ID
DSNAME   DC    CL44' '             DATA SET NAME
IPLDATE  DS    CL5                 IPL DATE (YYDDD)
IPLTIME  DS    CL6                 IPL TIME (HHMMSS)
VOLSERTT DS    CL6                 VOLSER OF THIS TAPE
VOLSEQTT DS    CL2                 VOL SEQ NO OF THIS TAPE
VOLSEREQ DS    CL6                 VOLSER OF EQUIVALENT TAPE IN (-1)GEN
FILLER   DC    CL6' '              SPACES
         SPACE 1
PRINTOUT DC    CL133' '
         EJECT
***********************************************************************
*        JOB FILE CONTROL BLOCK  (SYSUT1)                             *
***********************************************************************
         SPACE 1
         DS    0D
STRJFCB  DC    X'87'               READ JFCB
         DC    AL3(INFMJFCB)
         PRINT GEN
         IEFJFCBN
         PRINT NOGEN
         EJECT
***********************************************************************
*        WORK AREAS                                                   *
***********************************************************************
         SPACE 1
DECIMAL  DS    D
         SPACE 1
VOLIST   CAMLST NAME,LOCDSN,,LOCAREA
LOCDSN   DS    CL44
LOCAREA  DS    0D
         DS    CL265
         SPACE 1
NVOLIST  CAMLST BLOCK,LOCTTR,,LOCAREA
LOCTTR   EQU   LOCAREA+252
         SPACE 3
***********************************************************************
*        CONSTANTS , EQUATES AND LITTERAL POOL                        *
***********************************************************************
         SPACE 1
MSGE1    DC    C'DSNAME OF SYSUT1 NOT START WITH ''LIBRARYN'''
MSGE2    DC    C'DATA SET OF SYSUT1 IS NOT A GDG. '
MSGE3    DC    C'RETURN CODE OF LOCATE MACRO IS NOT = 0 (SEE DUMP R8)'
MSGE4    DC    C'NO OF SERIAL NUMBER DS SYSUT1 IS NOT = TO GEN(-1)'
FIVE     DC    H'5'
TWENTY   DC    H'20'
DUMVOL   DC    5CL6' '
SIGN     EQU   X'F0'
         END

