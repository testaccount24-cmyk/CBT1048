COPY           CSECT
               SAVE (14,12),,*
               BALR 12,0
               USING *,12
               ST   13,SAVEREGS+4
               LR   11,13
               ST   13,8(11)
               LA   13,SAVEREGS
*
*                             CHECK FOR SYSIN DATA-SET.
*
               OPEN (SYSIN,,SYSPRINT,(OUTPUT))
         OPEN  (DUMPDATA,(OUTPUT))
               PUT   SYSPRINT,PRTAREA-1
               MVI  PRTCTL,X'19'  WRITE WITH TRIPLE SPACE
               BAL  11,PRINT    PRINT START MESSAGE
               GET  SYSIN       ANY SYSIN
               MVC  PRTAREA,MSG1   YES  ERROR MSG
               MVC   PRTAREA+45(16),MSG4   TERMIATE MSG
               BAL  11,PRINT  PRINT IT
               B    BADEND    BA
*
*                             NO SYSIN DATA SET SO CARRY ON.
*
ENDSYSIN       EQU  *    BRANCH FROM EODAD  SYSIN DCB
               OPEN (SYSUT1,,SYSUT2,(OUTPUT))
MAINLOOP       EQU  *
               GET  SYSUT1    READ A RECORD
               LR   0,1
               PUT  SYSUT2,(0) WRITE A RECORD
               AP   COUNTER,=P'1' COUNT IT
               B    MAINLOOP   GO AN READ ANOTHER
*
*                             END OF FILE PROCESSING
*
ENDIP          EQU  *    BRANCH FROM EODAD SYSUT1 DCB
               CLOSE (SYSUT1,,SYSUT2)
         CLOSE (DUMPDATA)
               ED   PATRN,COUNTER   PUT COUNT IN PRINTLINE
               MVC  PRTAREA(39),MSG3  GOOD END MESSAGE
               BAL  11,PRINT  BRANCH TO PRINT ROUTINE
               CLOSE (SYSIN,,SYSPRINT)
               L    13,SAVEREGS+4
               RETURN (14,12),RC=0  GOO END
*
*                                  I/O ERRORS AND BAD END.
*
ERRUT2         EQU  *    BRANCH FROM SYNAD EXIT SYSUT2 DCB
               MVI  MSG2+18,C'2'   MODIFY I10 ERR MESSAGE
ERRUT1         EQU  *    BRANCH FROM SYNAD EXIT SYSUT1 DCB
               MVC  PRTAREA,MSG2   MOVE MESSAGE TO PRINTAREA
               MVC   PRTAREA+31(16),MSG4   MOVE TERINATION MESSAGE
               BAL  11,PRINT  BRANCH TO PRINT ROUTINE
BADEND         EQU  *    BRANCH FROM SYSIN CHECK
               L    13,SAVEREGS+4
               RETURN (14,12),RC=16
ERROR    SNAP  DCB=DUMPDATA,ID=1,SDATA=(TRT,CB),PDATA=ALL
         BR    14
*
*                                  PRINT ROUTINE
*
PRINT          EQU  *    BRANCH FROM MAINLINE
               PUT  SYSPRINT,PRTAREA-1  PRINT OUTPUT
               MVI  PRTCTL,C' '  PREPARE TO CLEAR PRINTLINE
               MVC  PRTAREA(121),PRTCTL  CLEAR PRINTLINE
               MVI  PRTCTL,X'19'   RESTORE CARRIAGE CONTROL CHARACTER
               BR   11   RETURN TO MAINLINE
*
*                             CONSTANTS SAVERREAS DCBS ETC.
*
SYSUT1         DCB  DDNAME=SYSUT1,                                     C
               DSORG=PS,MACRF=GL,                                      C
               EODAD=ENDIP,SYNAD=ERROR EROPT=ACC
*
SYSUT2         DCB  DDNAME=SYSUT2,                                     C
               DSORG=PS,MACRF=PM,                                      C
               SYNAD=ERRUT2
*
SYSIN          DCB  DDNAME=SYSIN,                                      C
               DSORG=PS,MACRF=GL,                                      C
               EODAD=ENDSYSIN,                                         C
               RECFM=F,LRECL=80,BLKSIZE=80,BUFNO=1
*
DUMPDATA DCB   DSORG=PS,RECFM=VBA,MACRF=(W),BLKSIZE=882,LRECL=125,     *
               DDNAME=DUMPDATA
SYSPRINT       DCB  DDNAME=SYSPRINT,                                   C
               DSORG=PS,MACRF=PM,                                      C
               RECFM=FBM,LRECL=121,BLKSIZE=121,BUFNO=1
SAVEREGS       DS   18F
COUNTER        DC  PL4'0'
PRTCTL         DC   X'8B'
PRTAREA        DC  CL121'MISD COPY UTILITY'
MSG1           DC   C'SYSIN DATA SET FOUND. USE IEBGENER UTILITY.'
MSG2           DC   C'I/O ERROR ON SYSUT1 DATA SET.'
MSG3           DC   C'COPY COMPLETE. RECORD COUNT ='
PATRN          DC   X'40206B2020206B202120'
MSG4           DC   C'COPY TERMINATED.'
*
               END
