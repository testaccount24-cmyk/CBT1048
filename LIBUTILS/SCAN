         MACRO
&NAME    SCAN  &FIELD,&KEY,&REG=,&YES=,&NO=,&LIMIT=
         LCLA  &K,&L,&LL
         LCLC  &R
         AIF   ('&NAME' EQ '').C1
&NAME    DS    0H
.C1      AIF   ('&FIELD' NE '').C2
         MNOTE 16,'NO FIELD TO BE SEARCHED WAS SUPPLIED'
         MEXIT
.C2      AIF   ('&KEY' NE '').C3
         MNOTE 16,'NO KEY ON WHICH TO SEARCH WAS SUPPLIED'
         MEXIT
.C3      ANOP
         AIF   ('&KEY'(1,1) EQ '=').C15
         AIF   (L'&FIELD GT L'&KEY).C15
         MNOTE 16,'FIELD TO BE SEARCHED NOT LARGER THAN KEY'
         MEXIT
.C15     ANOP
&R       SETC  '&REG'
         AIF   ('&REG' NE '').C4
         MNOTE 1,'NO POINTER REGISTER WAS SUPPLIED - R1 IS ASSUMED'
&R       SETC  'R1'
.C4      LA    &R,&FIELD
         AIF   ('&KEY'(1,1) EQ '=').C5
&L       SETA  L'&KEY
&LL      SETA  &L
SCAN&SYSNDX CLC   0(&L,&R),&KEY
         AGO   .C6
.C5      ANOP
&K       SETA  K'&KEY
&K       SETA  &K-4
         AIF   ('&KEY'(2,1) EQ 'C').C16
         AIF   ('&KEY'(2,1) EQ 'X').C17
         AIF   ('&KEY'(2,1) EQ 'P').C18
         AIF   ('&KEY'(2,1) EQ 'B').C19
         MNOTE 16,'UNRECOGNISABLE LITERAL IN ''KEY'' FIELD'
         MEXIT
.C19     ANOP
&K       SETA  &K/8
         AGO   .C16
.C18     ANOP
&K       SETA  &K/2
&K       SETA  &K+1
         AGO   .C16
.C17     ANOP
&K       SETA  &K/2
.C16     ANOP
&LL      SETA  &K
SCAN&SYSNDX CLC   0(&K,&R),&KEY
.C6      AIF   ('&YES' NE '').C7
         MNOTE *,'NO ''FOUND'' ADDRESS SUPPLIED - MACRO END +4 ASSUMED'
         BE    EXIT&SYSNDX.+4
         AGO   .C9
.C7      AIF   ('&YES'(1,1) EQ '(').C8
         BE    &YES
         AGO   .C9
.C8      BE    0(&YES(1))
.C9      LA    &R,1(&R)
         AIF   ('&LIMIT' NE '').C10
&L       SETA  L'&FIELD
         C     &R,=A(&FIELD.+&L.+1-&LL)
         AGO   .C11
.C10     C     &R,=A(&LIMIT)
.C11     BL    SCAN&SYSNDX
         AIF   ('&NO' NE '').C12
         MNOTE *,'NO ''NOT-FOUND'' ADDRESS SUPPLIED - MACRO END USED'
EXIT&SYSNDX DS    0H
         MEXIT
.C12     AIF   ('&NO'(1,1) EQ '(').C13
         B     &NO
         AGO   .C14
.C13     BR    &NO(1)
.C14     ANOP
EXIT&SYSNDX DS    0H
         MEND
