START    START
         PRINT ON,NODATA,NOGEN
         SAVE  (14,12),,*
         BALR  R2,0
         USING *,R2,R3
         LA    R3,4095(R2)
         LA    R3,1(R3)
         ST    R13,SAVE+4
         LR    R12,R13
         LA    R13,SAVE
         ST    R13,8(R12)
* REGISTERNAMES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
* OPEN FILES
         OPEN  (SNAPDUMP,(OUTPUT))
         OPEN  (DCBP,(OUTPUT))
         OPEN  (DCBC,(INPUT))
         OPEN  (DCBF,(OUTPUT))
         OPEN  (DCBS,(OUTPUT))
         OPEN  (DCBL,(OUTPUT))
* INITIALIZE POINTERS
         LA    R8,TITLE
         ST    R8,POINTER
         LA    R4,SBUF
         LA    R5,FBUF
* READ MASTERCARD INTO MAREA
         GET   DCBC,MAREA
         CLC   FOO,MF
         BNE   ERROR27
         CLC   NRF(1),=C'0'
         BL    ERROR30
         CLC   NRF+1(1),=C'0'
         BL    ERROR30
         CLC   NRF(1),=C'3'
         BH    ERROR30
         CLC   NRF+1(1),=C'9'
         BH    ERROR30
* INITIALIZE AVAILABLE PRINTINGSPACE
         PACK  PNRF,NRF
         ZAP   LLIM,PNRF
         AP    LLIM,PNRF
         MVC   TITLEO,BLANK
         MVC   UPDATE,UPDDATE
         MVC   TITLEO+35(63),MTITLE
NEXTCARD EQU   *
* READ HEADERCARD INTO AREA
         GET   DCBC,AREA
         CLC   F,FF
         BNE   ERROR21
         LA    R9,SFL
         CLC   FNR(1),=C'0'
         BL    ERROR31
         CLC   FNR+1(1),=C'0'
         BL    ERROR31
         CLC   FNR(1),=C'3'
         BH    ERROR31
         CLC   FNR+1(1),=C'9'
         BH    ERROR31
         PACK  FRR,FNR
         AP    PCNT,PONE
         CP    PCNT,FRR
         BNE   ERROR23
         SP    SPACK,SPACK
         SP    CCNT,CCNT
* CHECK ON DUMMY FIELD
         CLC   FFFF,CNR
         BE    DUMR
         CLC   CNR,=C'0'
         BNE   ERROR20
         CLC   FL(1),=C'0'
         BL    ERROR32
         CLC   FL+1(1),=C'0'
         BL    ERROR32
         CLC   FL(1),=C'9'
         BH    ERROR32
         CLC   FL+1(1),=C'9'
         BH    ERROR32
         PACK  PFL,FL
         ZAP   PACK,PFL
         SP    EIGHTY,PFL
* CALCULATE HEXADECIMAL FIELDLENGTH
         SP    PFL,PONE
         ZAP   BFL,PFL
         CVB   R6,BFL
         ST    R6,PFL
         MVC   0(4,R5),PFL
         LA    R5,4(R0,R5)
         AP    LLIM,PACK
SUBF     EQU   *
* PROCESS SUBFIELD LENGTHS
         CLC   0(1,R9),=C'0'
         BL    ERROR33
         CLC   1(1,R9),=C'0'
         BL    ERROR33
         CLC   0(1,R9),=C'9'
         BH    ERROR33
         CLC   1(1,R9),=C'9'
         BH    ERROR33
         PACK  PSFL,0(2,R9)
         AP    SPACK,PSFL
         SP    PSFL,PONE
         ZAP   BFL,PSFL
         CVB   R6,BFL
         ST    R6,PSFL
         MVC   0(4,R4),PSFL
         LA    R4,4(R0,R4)
         CP    SPACK,PACK
         BE    BINO
         LA    R9,2(R9)
         CLC   0(2,R9),BLANK
         BE    ERROR25
         B     SUBF
* MOVE ENDMAK AFTER LAST SUBFIELD LENGTH
BINO     MVC   0(4,R4),BZEROS
         LA    R4,4(R0,R4)
* READ TITLECARD INTO TAREA
TITLER    GET   DCBC,TAREA
         CLC   TF,FF
         BNE   ERROR22
         CLC   TFNR,FNR
         BNE   ERROR26
         AP    CCNT,PONE
         CLC   TCNR,=C'1'
         BL    ERROR34
         CLC   TCNR,=C'5'
         BH    ERROR34
         PACK  PCNR,TCNR
         CP    CCNT,PCNR
         BNE   ERROR24
         L     R7,PFL
         EX    R7,MVC
         CP    FIVE,PCNR
         BE    NEXTF
         LA    R8,132(R8)
         B     TITLER
NEXTF    EQU   *
* RESET POINTERS FOR NEXT FIELD
         L     R8,POINTER
         LA    R8,0(R7,R8)
         A     R8,HONE
         ST    R8,POINTER
SCARD    EQU   *
         CP    EIGHTY,PNINE
         BNH   ERROR29
* SEVENTH FIELD ?
         CLC   SEVEN,FNR
         BNE   ENDTEST
         ZAP   BFL,EIGHTY
         CVB   R6,BFL
         ST    R6,EIGHTY
         MVC   0(4,R5),EIGHTY
         LA    R5,4(R0,R5)
         ZAP   EIGHTY,=P'500'
* LAST FIELD ?
ENDTEST  CP    PNRF,FRR
         BH    NEXTCARD
         CP    LLIM,PRLIM
         BH    ERROR28
         GET   DCBC,AREA
         B     ERROR35
* ROUTINE FOR DUMMY FIELD
DUMR     MVC   0(4,R5),FOOO
         LA    R5,4(R0,R5)
         SP    LLIM,PTWO
         B     SCARD
* TEST ON THE TOTAL NUMBER OF CARDS
TEST     DS    0H
         CP    PNRF,FRR
         BE    SNAP
         BH    ERROR36
* GIVE A SHORTDUMP OF THE F-TABLE, S-TABLE AND L-TABLE
SNAP     DS    0H
         MVC   0(4,R5),FFOO
         SNAP  DCB=SNAPDUMP,ID=50,STORAGE=(FBUF,FBUF+84,SBUF,SBUF+640, X
               TITLEO,TITLEO+792)
* PUT THE TABLES ON DISK
         PUT   DCBL,TITLEO
         PUT   DCBS,SBUF
         PUT   DCBF,FBUF
         B     STOP
STOP     EQU   *
         L     R13,4(R13)
         LM    14,12,12(13)
         BR    14
* ERRORROUTINES
ERROR20  DS    0H
         PUT   DCBP,MERR20
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
         B     STOP
ERROR21  DS    0H
         PUT   DCBP,MERR21
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
ERROR22  DS    0H
         PUT   DCBP,MERR22
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,TAREA
         PUT   DCBP,PRINT
         B     STOP
ERROR23  DS    0H
         PUT   DCBP,MERR23
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
         B     STOP
ERROR24  DS    0H
         PUT   DCBP,MERR24
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,TAREA
         PUT   DCBP,PRINT
         B     STOP
ERROR25  DS    0H
         PUT   DCBP,MERR25
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
         B     STOP
ERROR26  DS    0H
         PUT   DCBP,MERR26
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,TAREA
         PUT   DCBP,PRINT
         B     STOP
ERROR27  DS    0H
         PUT   DCBP,MERR27
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,MAREA
         PUT   DCBP,PRINT
         B     STOP
ERROR28  DS    0H
         PUT   DCBP,MERR28
         B     STOP
ERROR29  DS    0H
         PUT   DCBP,MERR29
         B     STOP
ERROR30  DS    0H
         PUT   DCBP,MERR30
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,MAREA
         PUT   DCBP,PRINT
         B     STOP
ERROR31  DS    0H
         PUT   DCBP,MERR31
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
         B     STOP
ERROR32  DS    0H
         PUT   DCBP,MERR32
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
         B     STOP
ERROR33  DS    0H
         PUT   DCBP,MERR33
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
         B     STOP
ERROR34  DS    0H
         PUT   DCBP,MERR34
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,TAREA
         PUT   DCBP,PRINT
         B     STOP
ERROR35  DS    0H
         MVC   MERR35+91(2),NRF
         PUT   DCBP,MERR35
         PUT   DCBP,PRINT
         MVC   PRINTA+5(4),=C'CARD'
         MVC   PRINTB,AREA
         PUT   DCBP,PRINT
         B     STOP
ERROR36  DS    0H
         MVC   MERR36+68(2),NRF
         PUT   MERR36
         B     STOP
* SYNADROUTINES
RSYNADC  DS    0H
         PUT   DCBP,MERRC
         B     STOP
RSYNADF  DS    0H
         PUT   DCBP,MERRF
         B     STOP
RSYNADL  DS    0H
         PUT   DCBP,MERRL
         B     STOP
RSYNADS  DS    0H
         PUT   DCBP,MERRS
         B     STOP
RSYNADP  DS    0H
         SNAP  DCB=SNAPDUMP,ID=55,PDATA=(PSW,REGS),                    X
               STORAGE=(MERRP,MERRP+64)
         B     STOP
* CONSTANTST & STORAGE
SAVE     DS    18F
PNRF     DS    PL2
MAREA    DS    0CL80
MF       DS    CL1
MNR      DS    CL2
NRF      DS    CL2
UPDDATE  DS    CL6
MTITLE   DS    CL63
MIDENT   DS    CL6
AREA     DS    0CL80
F        DS    CL1
FNR      DS    CL2
CNR      DS    CL1
FL       DS    CL2
SFL      DS    CL74
TAREA    DS    0CL80
TF       DS    CL1
TFNR     DS    CL2
TCNR     DS    CL1
TT       DS    CL76
POINTER  DS    F
BFL      DS    D
PFL      DS    F
PSFL     DS    F
PACK     DS    F
EIGHTY   DC    PL4'74'
FOO      DC    C'F00'
PONE     DC    P'1'
SPACK    DC    PL4'0'
BZEROS   DC    BL2'1111111111111111',BL2'0'
LLIM     DS    PL2
PCNT     DC    PL2'0'
FRR      DS    CL2
SEVEN    DC    CL2'07'
FF       DC    C'F'
CCNT     DC    PL1'0'
         DS    0F
FBUF     DS    CL84
SBUF     DS    CL640
FFOO     DC    XL4'FF000000'
PRLIM    DC    PL2'127'
BLANK    DC    CL132' '
FIVE     DC    PL1'5'
         DS    0F
HONE     DC    XL4'1'
MVC      MVC   0(0,R8),TT
FFFF     DC    C'OFF'
PCNR     DS    PL1
PNINE    DC    PL4'9'
PTWO     DC    PL2'2'
FOOO     DC    XL4'F0000000'
* ERRORMESSAGES
MERR20   DS    0CL144
         DC    CL5' ',C'ID=20',CL27' ',C'*****  '
         DC    C'CARD NUMBER OF HEADER CARD NOT EQUAL TO 0'
         DC    C'  *****',CL56' '
MERR21   DS    0CL144
         DC    CL5' ',C'ID=21',CL27' ',C'*****  '
         DC    C'CARD CODE OF HEADER CARD NOT EQUAL TO F'
         DC    C'  *****',CL58' '
MERR22   DS    0CL144
         DC    CL5' ',C'ID=22',CL27' ',C'*****  '
         DC    C'CARD CODE NOT EQUAL TO F'
         DC    C'  *****',CL73' '
MERR23   DS    0CL144
         DC    CL5' ',C'ID=23',CL27' ',C'*****  '
         DC    C'FIELD NUMBER OF HEADER CARD NOT IN SEQUENCE'
         DC    C'  *****',CL56' '
MERR24   DS    0CL144
         DC    CL5' ',C'ID=24',CL27' ',C'*****  '
         DC    C'CARD NUMBER OF TITLE CARD NIT IN SEQUENCE'
         DC    C'  *****',CL56' '
MERR25   DS    0CL144
         DC    CL5' ',C'ID=25',CL27' ',C'*****  '
         DC    C'FIELD LENGTH NOT EQUAL TO SUM OF SUBFIELDLENGTHS'
         DC    C'  *****',CL49' '
MERR26   DS    0CL144
         DC    CL5' ',C'ID=26',CL20' ',C'*****  '
         DC    C'FIELD NUMBER OF TITLE CARD NOT EQUAL TO FIELD NUMBER OX
               F HEADER CARD'
         DC    C'  *****'
         DC    CL40' '
MERR27   DS    0CL144
         DC    CL5' ',C'ID=27',CL27' ',C'*****  '
         DC    C'CARD CODE OF MASTERCARD NOT EQUAL TO F00'
         DC    C'  *****',CL59' '
MERR28   DS    0CL144
         DC    CL5' ',C'ID=28',CL20' ',C'*****  '
         DC    C'SUM OF ALL FIELDLENGTHS EXCEEDS THE AVAILABLE PAGESIZEX
               '
         DC    C'  *****'
         DC    CL50' '
MERR29   DS    0CL144
         DC    CL5' ',C'ID=29',CL20' ',C'*****  '
         DC    C'SUM OF FIELDLENGTHS OF FIRST D-CARD EXCEEDS THE AVAILAX
               BLE SPACE ON CARD'
         DC    C'  *****'
         DC    CL13' '
MERR30   DS    0CL144
         DC    CL5' ',C'ID=30',CL27' ',C'*****  '
         DC    C'NUMBER OF FIELDS INCORRECT CODED'
         DC    C'  *****',CL65' '
MERR31   DS    0CL144
         DC    CL5' ',C'ID=31',CL27' ',C'*****  '
         DC    C'FIELDNUMBER INCORRECT CODED'
         DC    C'  *****',CL70' '
MERR32   DS    0CL144
         DC    CL5' ',C'ID=32',CL27' ',C'*****  '
         DC    C'FIELD LENGTH INCORRECT CODED'
         DC    C'  *****',CL69' '
MERR33   DS    0CL144
         DC    CL5' ',C'ID=33',CL27' ',C'*****  '
         DC    C'ONE OF THE SUBFIELDLENGTHS INCORRECT CODED'
         DC    C'  *****',CL55' '
MERR34   DS    0CL144
         DC    CL5' ',C'ID=34',CL27' ',C'*****  '
         DC    C'CARD NUMBER INCORRECT CODED'
         DC    C'  *****',CL70' '
MERR35   DS    0CL144
         DC    CL5' ',C'ID=35',CL20' ',C'*****  '
         DC    C'MORE CARDS CODED THAN ALLOWED BY THE NUMBER OF FIELDS X
                  CODED ON THE MASTERCARD'
         DC    C'  *****'
         DC    CL22' '
MERR36   DS    0CL144
         DC    CL5' ',C'ID=36',C'   *****  '
         DC    C'NOT ENOUGH CARDS CODED TO DESCRIBE ENTIRELY THE    FIEX
               LDS INDICATED BY THE NUMBER OF FIELDS ON THE MASTERCARD'
         DC    C'  *****       '
MERRC    DS    0CL144
         DC    CL23' ',C'*****  '
         DC    C'ERROR OCCURED WHILE READING A CARD'
         DC    C'  *****',CL73' '
MERRL    DS    0CL144
         DC    CL23' ',C'*****  '
         DC    C'ERROR OCCURED WHILE CREATING THE L-TABLE'
         DC    C'  *****',CL70' '
MERRS    DS    0CL144
         DC    CL23' ',C'*****  '
         DC    C'ERROR OCCURED WHILE CREATING THE S-TABLE'
         DC    C'  *****',CL70' '
MERRF    DS    0CL144
         DC    CL23' ',C'*****  '
         DC    C'ERROR OCCURED WHILE CREATING THE F-TABLE'
         DC    C'  *****',CL70' '
MERRP    DC    C'SOME ERROR OCCURED WHILE PRINTING A MESSAGE ON THE SYSX
               OUT DEVICE'
PRINT    DS    0CL144
PRINTA   DC    CL30' '
PRINTB   DC    CL80' '
PRINTC   DC   CL34' '
* DATA CONTROL BLOCKS
SNAPDUMP DCB   DDNAME=SYSNDUMP,DSORG=PS,RECFM=VBA,MACRF=(W),LRECL=125, X
               BLKSIZE=882
DCBC     DCB   DDNAME=DDCARD,DSORG=PS,MACRF=GM,SYNAD=RSYNADC,          X
               EODAD=TEST,LRECL=80,RECFM=F,BLKSIZE=80
DCBF     DCB   DDNAME=DISKF,DSORG=PS,MACRF=PM,LRECL=84,RECFM=F,        X
               BLKSIZE=84,SYNAD=RSYNADF
DCBS     DCB   DDNAME=DISKS,DSORG=PS,MACRF=PM,LRECL=640,RECFM=F,       X
               BLKSIZE=640,SYNAD=RSYNADS
DCBL     DCB   DDNAME=DISKL,DSORG=PS,MACRF=PM,LRECL=792,RECFM=F,       X
               BLKSIZE=792,SYNAD=RSYNADL
DCBP     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=144,RECFM=F,    X
               BLKSIZE=144,SYNAD=RSYNADP
         LTORG
TITLEO   DS    0CL132
UPDATE   DS    CL6
         DS    CL120
KEYWORD  DS    CL6
TITLE   DS    5CL132
         END
