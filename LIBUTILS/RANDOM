         TITLE 'RANDOM NUMBER GENERATOR - UT365'
         ISEQ  73,80
         SPACE
* WRITTEN BY D. R. HURTGEN
*
* MONSANTO COMPANY - CREVE COEUR
*
* DECEMBER 1966
*
* UT365 CONSISTS OF TWO SEPARATELY COMPILED AND CALLABLE SUBROUTINES.
* EACH ONE WILL GENERATE A NEW PSEUDO-RANDOM VALUE EACH TIME IT IS
* ENTERED AND WILL RESTART THE CYCLE WHENEVER THE USER PASSES ANY
* NEGATIVE VALUE TO IT. ALL COMPUTATIONS ARE DONE IN THE LOGICAL MODE
* TO PREVENT INTERRUPTS DUE TO A FIXED-POINT OVERFLOW. THE RESULT IS
* THEN CONVERTED TO THE PROPER FORM PRIOR TO PASSING IT TO THE USER.
* THE FORM AND RANGE OF THE RESULT IS UNIQUE FOR EACH SUBROUTINE AND IS
* DISCUSSED SEPARATELY. CYCLE LENGTH OF BOTH SUBROUTINES IS 2**29.
*
* REGISTER ASSIGNMENTS -
*   0, 2, 3, 4, 5, 10, 11, 12, 14, AND 15 - NOT USED
*   1 - ADDRESS OF PARAMETER LIST
*   6 AND 7 - USED IN COMPUTATION
*   8 - ADDRESS OF COMMUNICATION REGION
*   9 - SUBROUTINE BASE ADDRESS
*   13 - ADDRESS OF USERS REGISTER SAVE AREA
         EJECT
* BEGIN SUBROUTINE 'RANDOM'
*
* THIS SUBROUTINE RETURNS A PSEUDO-RANDOM VALUE IN INTEGER BINARY FORM.
* THE VALUES ARE EVENLY DISTRIBUTED OVER THE RANGE OF 1 THROUGH
* (2**31)-1.
         SPACE 2
RANDOM   START
         STM   6,9,44(13)     SAVE REGISTERS
         BALR  9,0  ESTABLISH BASE ADDRESS
         USING *,9
         L     8,0(1)    LOAD ADDRESS OF RANDOM NUMBER HOLD AREA TO R8
         TM    0(8),128  Q.-USER HAS SPECIFIED CYCLE RESTART
         BZ    *+10      B.-NO, GO TO GENERATE NEXT NUMBER
         MVC   NUMBER,RESTORE RESTART GENERATION
         L     7,NUMBER  CALCULATE NEW NUMBER
         LR    6,7
         SLL   7,16
         ALR   7,6
         ALR   7,6
         ALR   7,6
         ST    7,NUMBER  NEW NUMBER IS BASIS FOR NEXT CALCULATION
         SRL   7,1  CONVERT FROM LOGICAL TO INTEGER BINARY FORM AND
         ST    7,0(8) RETURN TO USER
         LM    6,9,44(13)     RESTORE REGISTERS
         BR    14   RETURN TO USERS PROGRAM
         SPACE 2
NUMBER   DC    F'131074'
RESTORE  DC    F'131074'
         END
