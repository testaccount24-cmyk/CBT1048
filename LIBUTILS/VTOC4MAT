VTOC4MAT CSECT
         SAVE  (14,12)                  SAVE REGISTERS
         BALR  12,0                     ESTABLISH VALUE
         USING *,12                     AS A BASE REGISTER
         ST    13,SAVE+4                BACK CHAIN
         LA    9,SAVE
         ST    9,8(0,13)                FORWARD CHAIN
         LR    13,9                     SAVE AREA ADDRESS IN 13
         USING FORMAT1,11               BASE REGISTER FOR DSECT
         USING FORMAT2,10               BASE REGISTER FOR DSECT
         USING FORMAT3,9                BASE REGISTER FOR DSECT
         LA    11,WORK1-44              LOAD THE REGISTER
         LA    10,WORK3                 LOAD THE REGISTER
         LR    9,10                     LOAD THE REGISTER
         LA    8,2048(12)               ADDITIONAL MAINLINE BASEREG
         LA    8,2048(8)
         USING VTOC4MAT+4102,8
         OPEN  (CONTROL,(INPUT),LIST,(OUTPUT),LISTONLY,(OUTPUT))
         TM    CONTROL+48,X'10'         OPENED OK
         BZ    SYSINMSG                 NO
         TM    LIST+48,X'10'            OPENED OK
         BZ    SYPRTMSG                 NO
         TM    LISTONLY+48,X'10'        OPENED OK
         BZ    SYLSTMSG                 NO
         SR    0,0                      ZERO OUT REGISTER
         STH   0,LINECNT                MOVE ZERO TO LINE COUNTER
         PUT   LISTONLY,EJECTRCD        SKIP TO A NEW PAGE
STARTUP  GET   CONTROL,CARDIN           READ A CONTROL CARD
         PUT   LIST,EJECTRCD            SKIP TO A NEW PAGE
         PUT   LIST,CARDIN-1            PRINT THE CONTROL CARD
         PUT   LIST,SPACE3              SPACE 3 LINES
         CLI   DSN,C' '                 ENTIRE VTOC OR 1 DATA SET
         BE    DOVTOC                   ENTIRE VTOC
         LA    7,STARTUP                LOAD RETURN REGISTER
         OBTAIN DSCBNAME                GET A DSCB
         B     HERE(15)                 BRANCH TABLE
HERE     B     NEXTSTEP                 OK
         B     NM                       DEVICE NOT MOUNTED
         B     NF                       DSCB NOT FOUND
         B     IO                       I/O ERROR READING VTOC
NEXTSTEP EQU   *
         MVC   DSNAME1,DSN              MOVE DSNAME TO OUTPUT AREA
         PUT   LIST,DSNAME-1            PRINT DSNAME
PUTDSS   EQU   *
         MVC   DSSERNO1,DS1DSSN         MOVE VOLUME SERIAL TO OUTPUT
         PUT   LIST,DSSERNO-1           PRINT VOLUME SERIAL NUMBER
         SR    5,5                      ZERO OUT REGISRER 5
         MVC   HWORD,DS1VOLSQ           CONVERT VOLUME
         LH    5,HWORD                  SEQUENCE NUMBER
         CVD   5,WORK2                  TO PRINTABLE
         UNPK  VOLSQNO1,WORK2           FORM
         OI    VOLSQNO1+1,X'F0'         MAKE IT NUMERIC
         PUT   LIST,VOLSQNO-1           PRINT IT
         SR    5,5                      ZERO OUT REGISTER 5
         IC    5,DS1CREDT               CONVERT CREATION
         CVD   5,WORK2                  DATE TO
         UNPK  DATE1(2),WORK2           PRINTABLE FORM
         OI    DATE1+1,X'F0'            MAKE IT NUMERIC
         MVC   HWORD,DS1CREDT+1
         LH    5,HWORD
         CVD   5,WORK2
         UNPK  DATE1+2(3),WORK2
         OI    DATE1+4,X'F0'
         MVI   DATE1+5,C' '
         SR    5,5                      DO SAME
         IC    5,DS1EXPDT               WITH
         CVD   5,WORK2                  EXPIRATION
         UNPK  DATE1+6(2),WORK2         DATE
         OI    DATE1+7,X'F0'
         MVC   HWORD,DS1EXPDT+1
         LH    5,HWORD
         CVD   5,WORK2
         UNPK  DATE1+8(3),WORK2
         OI    DATE1+10,X'F0'
         PUT   LIST,DATE-1              PRINT DATES
         SR    5,5                      ZERO OUT REGISTER K
         IC    5,DS1NOEPV               CONVERT
         CVD   5,WORK2                  NUMBER  OF
         UNPK  EXT1,WORK2+6(2)          EXTENTS TO
         OI    EXT1+1,X'F0'             PRINTABLE FORM
         PUT   LIST,EXT-1               PRINT IT
         SR    5,5                      ZERO OUT REGISTER 5
         IC    5,DS1NOBDB               CONVERT NUMBER OF BYTES
         CVD   5,WORK2                  IN LAST DIRECTORY BLOCK TO
         UNPK  DBLK1,WORK2+6(2)         PRINTABLE FORM
         OI    DBLK1+2,X'F0'            MAKE IT NUMERIC
         PUT   LIST,DBLK-1              PRINT IT
         TM    DS1DSORG,X'80'           ID DSORG *IS*
         BO    MOVEIS                   YES
         TM    DS1DSORG,X'40'           IS DSORG *PS*
         BO    MOVEPS                   YES
         TM    DS1DSORG,X'20'           IS DSORG *DA*
         BO    MOVEDA                   YES
         TM    DS1DSORG,X'02'           IS DSORG *PO*
         BO    MOVEPO                   YES
         MVC   DSORG1(3),=C'XXX'        MOVE X'S TO DSORG
UTEST    TM    DS1DSORG,X'01'           IS DSORG UNMOVEABLE
         BZ    DODSORG                  NO
         MVI   DSORG1+2,C'U'            FLAG AS UNMOVEABLE
         B     DODSORG
MOVEDA   MVC   DSORG1(2),=C'DA'
         B     UTEST
MOVEIS   MVC   DSORG1(2),=C'IS'
         B     UTEST
MOVEPS   MVC   DSORG1(2),=C'PS'
         B     UTEST
MOVEPO   MVC   DSORG1(2),=C'PO'
         B     UTEST
DODSORG  PUT   LIST,DSORG-1             PRINT DSORG
         MVC   DSORG1,=CL3' '           CLEAN IT OUT FOR NEXT TIME
DORECFM  CLI   DS1RECFM,X'00'           IS RECFM AVAILABLE
         BE    MOVXXXXX                 NO MOVE X'S TO IT
         TM    DS1RECFM,X'80'           IS RECFM *V*
         BZ    MOVEV                    YES
         TM    DS1RECFM,X'40'           IS RECFM *F*
         BZ    MOVEF                    YES
         MVI   RECFM1,C'U'              RECFM IS *U*
         B     DORECFM2
MOVEV    MVI   RECFM1,C'V'
         B     DORECFM2
MOVEF    MVI   RECFM1,C'F'
DORECFM2 TM    DS1RECFM,X'10'           ARE RECORDS BLOCKED
         BZ    DORECFM3                 NO
         MVI   RECFM1+1,C'B'
DORECFM3 TM    DS1RECFM,X'08'           ANY SHORT BLOCKS
         BZ    DORECFM4                 YES
         MVI   RECFM1+2,C'S'
DORECFM4 TM    DS1RECFM,X'04'           ASA CONTROL CHARACTERS
         BZ    DORECFM5                 NO
         MVI   RECFM1+3,C'A'
DORECFM5 TM    DS1RECFM,X'02'           MACHINE CONTROL CHARACTERS
         BZ    DORECFM6                 NO
         MVI   RECFM1+3,C'M'
DORECFM6 TM    DS1RECFM,X'20'           TRACK OVERFLOW
         BZ    DORECFM7                 NO
         MVI   RECFM1+4,C'T'
DORECFM7 PUT   LIST,RECFM-1             PRINT RECFM
         MVC   RECFM1,=CL5' '           CLEAR IT OUT FOR NEXT TIME
         LH    5,DS1BLKL                CONVERT
         CVD   5,WORK2                  BLOCKSIZE TO
         UNPK  BLKSIZE1,WORK2           PRINTABLE FORM
         OI    BLKSIZE1+4,X'F0'         MAKE IT NUMERIC
         PUT   LIST,BLKSIZE-1           PRINT IT
         LH    5,DS1LRECL               CONVERT LOGICAL
         CVD   5,WORK2                  RECORD LENGTH TO
         UNPK  LRECL1,WORK2             PRINTABLE FORM
         OI    LRECL1+4,X'F0'           MAKE IT NUMERIC
         PUT   LIST,LRECL-1             PRINT IT
         SR    5,5                      ZERO OUT REGISTER 5
         IC    5,DS1KEYL                CONVERT KEY
         CVD   5,WORK2                  LENGTH TO
         UNPK  KEYLEN1,WORK2            PRINTABLE FORM
         OI    KEYLEN1+2,X'F0'          MAKE IT NUMERIC
         PUT   LIST,KEYLEN-1            PRINT IT
         MVC   WORK2(2),DS1RKP          CONVERT RELATIVE
         LH    5,WORK2                  KEY POSITION
         CVD   5,WORK2                  TO PRINTABLE
         UNPK  RKP1,WORK2               FORM
         OI    RKP1+4,X'F0'             MAKE IT NUMERIC
         PUT   LIST,RKP-1               PRINT IT
         UNPK  WORK2(7),DS1LSTAR(4)     CONVERT LAST
         TR    WORK2(6),BITOHEX-240     RECORD ADDRESS
         MVC   LB1(6),WORK2             TO PRINTABLE HEX
         PUT   LIST,LB-1                PRINT IT
         CLC   DS1LSTAR(3),=XL3'00'     IS LAST ADDRESS ZERO
         BE    MOVZERO                  MOVE ZERO TO TRACKS USED
         MVC   HWORD,DS1LSTAR           CONVERT IT
         LH    5,HWORD                  TO PRINTABLE FORM
         LA    5,1(5)                   INCREMENT BY U
         CVD   5,WORK2
         UNPK  TRXU1,WORK2
         OI    TRXU1+4,X'F0'            MAKE IT NUMERIC
PUTTRXU  PUT   LIST,TRXU-1              PRINT IT
         CLC   DS1PTRDS,=XL5'0'         LAST DSCB
         BE    GETTRX                   YES
         MVC   PTR,DS1PTRDS             MOVE POINTER TO PARAMETER
         MVC   DSSN,DS1DSSN             MOVE SERIAL NUMBER TO PARAMETER
         OBTAIN FIND                    GET NEXT DSCB
TYPE2OR3 CLI   WORK3,X'02'              IS IT TYPE *2* OR *3*
         BE    ISAMDSCB                 TYPE *2*
         B     GETTRX1
MOVXXXXX MVC   RECFM1(5),=CL5'XXXXX'
         B     DORECFM7
MOVZERO  MVI   TRXU1,C'0'
         MVC   TRXU1+1(4),TRXU1
         B     PUTTRXU
GETTRX   MVI   DS3EXTNT,X'00'           MOVE ZERO TO EXTRA EXTENTS
GETTRX1  EQU   *
         MVC   EXTAREA(30),DS1EXT1      MOVE EXTRA EXTENTS TO WORKAREA
         MVC   EXTAREA+30(40),DS3EXTNT  MOVE EXTRA EXTENTS TO WORKAREA
         MVC   EXTAREA+70(90),DS3ADEXT  MOVE EXTRA EXTENTS TO WORKAREA
         LA    6,16                     LOOP CONTROL
         LA    5,EXTAREA                LOOP CONTROL
         MVC   CYLCOUNT,=XL2'0'         SET COUNT TO ZERO
         MVC   TRKCOUNT,=XL2'0'         SET COUNT TO ZERO
ADDEMUP  CLI   0(5),X'00'               LAST ONE
         BE    EDITTRX                  YES
         LH    4,6(5)                   LOAD END CYLINDER ADDRESS
         LH    3,2(5)                   LOAD START CYLINDER ADDRESS
         SR    4,3                      FIND DIFFERENCE
         AH    4,CYLCOUNT               ADD TO PREVIOUS TOTAL
         STH   4,CYLCOUNT               SAVE IT
         LH    4,8(5)                   LOAD END TRACK ADDRESS
          LH   3,4(5)                   LOAD START TRACK ADDRESS
         SR    4,3                      FIND DIFFERENCE
         LA    4,1(4)                   INCREMENT BY 1
         AH    4,TRKCOUNT               ADD IT TO PREVIOUS TOTAL
         STH   4,TRKCOUNT               SAVE IT
         LA    5,10(5)                  GO TO NEXT EXTENT
         BCT   6,ADDEMUP                SUBTRACT 1 FROM COUNTER
EDITTRX  LH    5,CYLCOUNT               GET NUMBER OF CYLINDERS
         CLC   VTYP,=C'2314'            *2314* DEVICE
         BE    MBY20                    YES
          MH    5,=H'10'                MULTIPLY BY TRACKS/CYLINDER
ADDUP    AH    5,TRKCOUNT               ADD TRACKS TO CYLS*10OR20
         CVD   5,WORK2                  CONVERT TO
         UNPK  TRXA1,WORK2              PRINTABLE FORM
         OI    TRXA1+4,X'F0'            MAKE IT NUMERIC
         PUT   LIST,TRXA-1              PRINT TRACKS USED
         BR    7                        RETURN TO STARTING POINT
MBY20    MH    5,=H'20'                 MULTIPLY BY TRACKS/CYLINDER
         B     ADDUP
ISAMDSCB SR    5,5                      ZERO OUT REGISTER 5
         IC    5,DS2NOLEV               CONVERT NUMBER
         CVD   5,WORK2                  OF INDEX LEVELS TO
         UNPK  XLEVELS1,WORK2           PRINTABLE FORM
         OI    XLEVELS1+2,X'F0'         MAKE IT NUMERIC
         PUT   LIST,XLEVELS-1           PRINT IT
         SR    5,5                      ZERO OUT REGISTER 5
         IC    5,DS2DVIND               CONVERT NUMBER OF
         CVD   5,WORK2                  RECORDS DEVELOPING A
         UNPK  TRXMI1,WORK2             MASTER INDEX TO PRINTABLE FORM
         OI    TRXMI1+2,X'F0'           MAKE IT NUMERIC
         PUT LIST,TRXMI-1               PRINT IT
         SR    5,5                      ZERO OUT REGISTER 5
         IC    5,DS2CYLOV               CONVERT NUMBER OF TRACKS
         CVD   5,WORK2                  PER CYLINDER FOR OVERFLOW
         UNPK  TCLOFLO1,WORK2           TO PRINTABLE FORM
         OI    TCLOFLO1+2,X'F0'         MAKE IT NUMERIC
         PUT   LIST,TCLOFLO-1           PRINTIT
         MVC   HWORD,DS2TAGDT           CONVERT NUMBER OF RECORDS
         LH    5,HWORD                  TAGGED FOR DELETION TO
         CVD   5,WORK2                  PRINTABLE FORM
         UNPK  RECDEL1,WORK2
         OI    RECDEL1+4,X'F0'          MAKE IT NUMERIC
         PUT   LIST,RECDEL-1            PRINT IT
         MVI   WORK2,X'00'              CONVERT NUMBER
         MVC   WORK2+1(3),DS2RORG3      OF OVERFLOW
         L     5,WORK2                  REFERENCES
         CVD   5,WORK2                  TO PRINTABLE
         UNPK  OREF1,WORK2              FORM
         OI    OREF1+4,X'F0'            MAKE IT NUMERIC
         PUT   LIST,OREF-1              PRINT IT
         MVC   HWORD,DS2NOBYT           CONVERT HIGH LEVEL
         LH    5,HWORD                  INDEX SIZE
         CVD   5,WORK2                  TO PRINTABLE FORM
         UNPK  HLIS1,WORK2
         OI    HLIS1+4,X'F0'            MAKE IT NUMERIC
         PUT   LIST,HLIS-1              PRINT IT
         MVC   FWORD,DS2PRCTR           CONVERT NUMBER OF
         L     5,FWORD                  PRIME RECORDS
         CVD   5,WORK2                  TO
         UNPK  NPDR1,WORK2              PRINTABLE FORM
         OI    NPDR1+4,X'F0'            MAKE IT NUMERIC
         PUT   LIST,NPDR-1              PRINT IT
         UNPK  LPRA1(9),DS2LPRAD(5)     CONVERT LAST PRIME
         UNPK  LPRA1+8(9),DS2LPRAD+4(5) RECORD ADDRESS
         TR    LPRA1,BITOHEX-240        TO PRINTABLE
         MVI   LPRA1+16,C' '            HEX
         PUT   LIST,LPRA-1              PRINT IT
         UNPK  LORA1(9),DS2LOVAD(5)     CONVERT LAST
         UNPK  LORA1+8(9),DS2LOVAD+4(5) OVERFLOW RECORD ADDRESS TO
         TR    LORA1,BITOHEX-240        PRINTABLE HEX
         MVI   LORA1+16,C' '
         PUT   LIST,LORA-1              PRINT IT
         MVC   HWORD,DS2RORG2           CONVERT REMAINING TRACKS
         LH    5,HWORD                  IN OVERFLOW
         CVD   5,WORK2                  AREA TO
         UNPK  RTOA1,WORK2              PRINTABLE FORM
         OI    RTOA1+4,X'F0'            MAKE IT NUMERIC
         PUT   LIST,RTOA-1              PRINT IT
         MVC   HWORD,DS2OVRCT           CONVERT OVERFLOW
         LH    5,HWORD                  RECORD COUNT TO
         CVD   5,WORK2                  PRINTABLE
         UNPK  ROA1,WORK2               FORM
         OI    ROA1+4,X'F0'             MAKE IT NUMERIC
         PUT   LIST,ROA-1               PRINT IT
         MVC   HWORD,DS2RORG1           CONVERT NUMBER OF
         LH    5,HWORD                  FULL OVERFLOW AREAS
         CVD   5,WORK2                  TO PRINTABLE
         UNPK  FOA1,WORK2               FORM
         OI    FOA1+4,X'F0'             MAKE IT NUMERIC
         PUT   LIST,FOA-1               PRINT IT
         CLC   DS2PTRDS,=XL5'0'         ANY ADDITIONAL DSCB'S
         BE    GETTRX                   NO
         MVC   PTR,DS2PTRDS             SET UP PARAMETER
         OBTAIN FIND                    GET ADDITIONAL DSCB
         B     TYPE2OR3                 GO TO ANALYZE IT
ENDUP    CLOSE (LIST,,CONTROL,,LISTONLY)
ENDUP1   EQU   *
         L     13,SAVE+4                RELOAD REGISTER 13
         RETURN (14,12)                 RETURN
NM EQU *
         MVC   MSGNM+29(44),DSN         MOVE DSNAME TO MESSAGE
         PUT   LIST,MSGNM-1             PRINT MESSAGE
         B     STARTUP                  GO BACK TO RESTART
NF EQU *
         MVC   MSGNF+9(44),DSN          MOVE DSNAME TO MESSAGE
         PUT   LIST,MSGNF-1             PRINT THE MESSAGE
         B     STARTUP                  RETURN TO RESTART
IO EQU *
         MVC   MSGIO+27(44),DSN         MOVE DSNAME TO MESSAGE
         PUT   LIST,MSGIO-1             PRINT IT
         B     STARTUP                  RETURN TO RESTART
DOVTOC   MVC   DCB4VTOC+40(8),=CL8' '     CLEAN OUT DDNAME
         MVC   DCB4VTOC+40(6),DSS       PUT IN CORRECT DDNAME
         RDJFCB (DCB4VTOC)              READ THE JFCB TO MODIFY IT
         MVC   JFCB1(44),=44X'04'       MAKE THE DSNAME 44X'04'S
         OI    JFCB1+76,X'80'           SET BIT SO JFCB WILL BE UPDATED
         OPEN  (DCB4VTOC,(INPUT)),TYPE=J    OPEN WITH UPDATED JFCB
         TM    DCB4VTOC+48,X'10'        OPEN CORRECTLY
         BZ    PUTERMES                 NO  GIVE MESSAGE
         SR    0,0
         STH   0,DSCBCTR                SET COUNTER TO ZERO
         MVI   F5SW,X'00'               SET FORMAT 5 SWITCH TO ZERO
         PUT   LISTONLY,CARDIN-1        PRINT CONTROL CARD
         PUT   LISTONLY,SPACE3          SPACE O LINES
         LH    1,LINECNT                INCREMENT
         LA    1,4(1)                   LINE
         STH   1,LINECNT                COUNTER
KEEPON   READ  DECBVTOC,SF,DCB4VTOC,WORK1-44,'S'
         CHECK DECBVTOC
         CLI   DS1FMTID,X'00'           FORMAT ZERO
         BE    ADDSCB                   YES
         CLI   DS1FMTID,C'5'            FORMAT 5
         BE    DOF5                     YES
         CLI   DS1FMTID,C'1'            FORMAT 1
         BNE   KEEPON                   NO  GET ANOTHER DSCB
         MVC   DSNAME1,DS1DSNAM         MOVE DSNAME TO MESSAGE
         PUT   LIST,DSNAME-1            PRINT DSNAME MESSAGE
         PUT   LISTONLY,DSNAME-1        PRINT DSNAME MESSAGE
         LH    1,LINECNT                INCREMENT
         LA    1,1(1)                   LINE COUNTER
         LA    2,44                     SET UP COMPARAND
         CR    1,2                      COMPARE
         BNL   NUPAGE                   PAGE OVERFLOW ROUTINE
         STH   1,LINECNT                RESTORE LINECOUNTER
DOALL    EQU   *
         BAL   7,PUTDSS                 BRANCH INTO MAINLINE
         PUT   LIST,EJECTRCD            SKIP TO A NEW PAGE
         B     KEEPON                   GET ANOTHER DSCB
ADDSCB   LH    6,DSCBCTR                INCREMENT DSCB
         LA    6,1(6)                   COUNTER
         STH   6,DSCBCTR                BY  1
         B     KEEPON                   GET ANOTHER DSCB
CLOSEUP  CLOSE (DCB4VTOC)
         LH    5,DSCBCTR                CONVERT DSCB
         CVD   5,WORK2                  COUNTER TO
         UNPK  CNTDSCB,WORK2            PRINTABLE FORM
         OI    CNTDSCB+4,X'F0'          MAKE IT NUMERIC
         MVC   DSCBCNT,DSS              MOVE VOLID TO MESSAGE
         PUT   LIST,DSCBMESS-1          PRINT THE MESSAGE
         PUT   LIST,AVTRX-1             PRINT AVAILABLE TRACKS MESSAGE
         PUT   LISTONLY,DSCBMESS-1      PRINT AVAILABLE DSCBS MESSAGE
         PUT   LISTONLY,AVTRX-1         PRINT AVAILABLE TRACKS MESSAGE
         PUT   LISTONLY,SPACE3          SPACE 3
         LH    1,LINECNT                INCREMENT
         LA    1,5(1)                   LINE
         STH   1,LINECNT                COUNTER
         B     STARTUP                  RETURN TO RESTART
DOF5     EQU   *
         MVC   WORK3(140),WORK1-44      MOVE DSCB TO OTHER WORKAREA
         LA    6,WORK3                  SET UP DSECT ADDRESSABILITY
         USING FORMAT5,6
         CLI   F5SW,X'00'               IS THIS FIRST FORMAT 5
         BNE   KEEPON                   NO
         MVI   F5SW,X'01'          SET SWITCH TO AVOID NEXT FORMAT 5'S
         MVC   EXTAREA(5),DS5AVEXT      MOVE FREE EXTENTS TO WORK AREA
         MVC   EXTAREA+5(35),DS5EXTAV   MOVE FREE EXTENTS TO WORK AREA
         MVC   EXTAREA+40(90),DS5MAVET  MOVE FREE EXTENTS TO WORK AREA
         LA    5,26                     LOOP CONTROL
         LA    4,EXTAREA                LOOP CONTROL
         SR    3,3
         SR    2,2
LOOPF5   MVC   HWORD,2(4)
         AH    3,HWORD                  ADD NUMBER OF CONTIGUOUS CYLS
         SR    1,1
         IC    1,4(4)                   ADD NUMBER OF ADDITIONAL TRACKS
         AR    2,1                      TO REGISTER 2
         LA    4,5(4)                   UPDATE LOOP POINTER
         BCT   5,LOOPF5                 CONTINUE LOOP
         CLC   DS5PTRDS,=XL5'00'        ADDITIONAL DSCBS
         BE    EDITAV                   NO EDIT THE AMOUNT
         MVC   PTR(5),DS5PTRDS          MOVE POINTER TO PARAMETER
         OBTAIN FIND                    GET NEXT FORMAT 5
         LA    5,26                     LOOP CONTROL
         LA    4,EXTAREA                LOOP CONTROL
         B     LOOPF5                   BRANCH INTO LOOP
EDITAV   CLC   VTYP(4),=C'2314'         IS DEVICE A *2314*
         BE    MX20                     YES
         MH    3,=H'10'                 MULTIPLY BY TRACKS/CYLINDER
EDITAV1  AR    3,2                      ADD ADDITIONAL TRACKS
         CVD   3,WORK2                  CONVERT IT TO
         UNPK  AVTRX1,WORK2             PRINTABLE FORM
         OI    AVTRX1+4,X'F0'           MAKE IT NUMERIC
         B     KEEPON                   GET NEXT DSCB
MX20     MH    3,=H'20'                 MULTIPLY BY TRACKS/CYLINDER
         B     EDITAV1                  BRANCH INTO EDIT
SYSINMSG WTO   'UNABLE TO OPEN *SYSIN*  JOB TERMINATED'
         B     ENDUP1                   RETURN DUE TO ERROR
SYPRTMSG WTO   'UNABLE TO OPEN *SYSPRINT*  JOB TERMINATED'
         B     ENDUP1                   RETURN DUE TO ERROR
SYLSTMSG WTO   'UNABLE TO OPEN *SYSLIST*  JOB TERMINATED'
         B     ENDUP1                   RETURN DUE TO ERROR
NUPAGE   PUT   LISTONLY,EJECTRCD        SKIP TO A NEW PAGE
         SR    0,0
         STH   0,LINECNT                SET LINE COUNTER TO ZERO
         B     DOALL                    RETURN TO PROCESS
PUTERMES PUT   LIST,NODD-1              NO DD MESSAGE
         PUT   LISTONLY,CARDIN-1        PRINT CONTROL CARD
         PUT   LISTONLY,NODD-1          PRINT NO DD MESSAGE
         PUT   LISTONLY,SPACE3          SPACE 3
         LH    1,LINECNT                INCREMENT
         LA    1,5(1)                   LINE
         STH   1,LINECNT                COUNTER
         B     STARTUP                  RETURN TO RESTART
         DS    0F
JFCBEXIT DC    X'87'
         DC    AL3(JFCB1)
         DS    0F
JFCB1    DS    CL176
SAVE     DS    18F
         DC    X'09'
CARDIN   DS    0CL80
DSS      DS    CL6
         DS    C
VTYP     DS    CL4
         DS    C
DSN      DS    CL44
         DS    CL24
         DC    CL4' '
         DS    CL52
         DS    0D
WORK1    DS    CL350
DSCBCTR  DS    H
LINECNT  DS    H
HWORD    DS    H
FWORD    DS    F
WORK2    DS    D
WORK3    DS    CL350
CYLCOUNT DS    H
TRKCOUNT DS    H
F5SW     DS    C
         DC    X'09'
DSNAME   DC    CL40'DATA SET NAME'
DSNAME1  DS    CL44
         DC    X'09'
DSSERNO  DC    CL40'DATA SET SERIAL NUMBER'
DSSERNO1 DS    CL6
         DC    CL38' '
         DC    X'09'
VOLSQNO  DC    CL40'VOLUME SEQUENCE NUMBER'
VOLSQNO1 DS    CL2
         DC    CL42' '
         DC    X'09'
DATE     DC    CL40'CREATION-EXPIRATION DATES'
DATE1    DS    CL11
         DC    CL33' '
         DC    X'09'
EXT      DC    CL40'NUMBER OF EXTENTS'
EXT1     DS    CL2
         DC    CL42' '
         DC    X'09'
DBLK     DC    CL40'BYTES IN LAST DIRECTORY BLOCK'
DBLK1    DS    CL3
         DC    CL41' '
         DC    X'09'
DSORG    DC    CL40'DATA SET ORGANIZATION'
DSORG1   DC    CL3' '
         DC    CL41' '
         DC    X'09'
RECFM    DC    CL40'RECORD FORMAT'
RECFM1   DC    CL5' '
         DC    CL39' '
         DC    X'09'
BLKSIZE  DC    CL40'BLOCKSIZE'
BLKSIZE1 DS    CL5
         DC    CL39' '
         DC    X'09'
LRECL    DC    CL40'LOGICAL RECORD LENGTH'
LRECL1   DS    CL5
         DC    CL39' '
         DC    X'09'
KEYLEN   DC    CL40'KEY LENGTH'
KEYLEN1  DS    CL3
         DC    CL41' '
         DC    X'09'
RKP      DC    CL40'RELATIVE KEY POSITION'
RKP1     DS    CL5
         DC    CL39' '
         DC    X'09'
LB       DC    CL40'LAST BLOCK POINTER (HEX)'
LB1      DS    CL6
         DC    CL38' '
         DC    X'09'
TRXU     DC    CL40'NUMBER OF TRACKS USED'
TRXU1    DS    CL5
         DC    CL39' '
         DC    X'09'
TRXA     DC    CL40'NUMBER OF TRACKS ALLOCATED'
TRXA1    DS    CL5
         DC    CL39' '
         DC    X'09'
XLEVELS  DC    CL40'NUMBER OF INDEX LEVELS'
XLEVELS1 DS    CL3
         DC    CL41' '
         DC    X'09'
TRXMI    DC    CL40'TRACKS PER MASTER INDEX'
TRXMI1   DS    CL3
         DC    CL41' '
         DC    X'09'
TCLOFLO  DC    CL40'TRACKS PER CYLINDER FOR OVERFLOW'
TCLOFLO1 DS    CL3
         DC    CL41' '
         DC    X'09'
RECDEL   DC    CL40'NUMBER OF RECORDS FOR DELETION'
RECDEL1  DS    CL5
         DC    CL39' '
         DC    X'09'
OREF     DC    CL40'NUMBER OF OVERFLOW REFERENCES'
OREF1    DS    CL5
         DC    CL39' '
         DC    X'09'
HLIS     DC    CL40'HIGH LEVEL INDEX SIZE'
HLIS1    DS    CL5
         DC    CL39' '
         DC    X'09'
NPDR     DC    CL40'NUMBER OF PRIME DATA RECORDS'
NPDR1    DS    CL5
         DC    CL39' '
         DC    X'09'
LPRA     DC    CL40'LAST PRIME RECORD ADDRESS MBBCCHHR'
LPRA1    DS    CL16
         DC    CL28' '
         DC    X'09'
LORA     DC    CL40'LAST OVERFLOW RECORD ADDRESS MBBCCHHR'
LORA1    DS    CL16
         DC    CL28' '
         DC    X'09'
RTOA     DC    CL40'REMAINING TRACKS IN OVERFLOW AREA'
RTOA1    DS    CL5
         DC    CL39' '
         DC    X'09'
ROA      DC    CL40'NUMBER OF RECORDS IN OVERFLOW AREA'
ROA1     DS    CL5
         DC    CL39' '
         DC    X'09'
FOA      DC    CL40'NUMBER OF FULL OVERFLOW AREAS'
FOA1     DS    CL5
         DC    CL39' '
         DC    X'09'
DSCBMESS DC    C'THERE ARE '
CNTDSCB  DS    CL5
         DC    CL20' AVAILABLE DSCBS ON '
DSCBCNT  DS    CL6
         DC CL43' '
         DC    X'09'
AVTRX    DC    C'THERE ARE '
AVTRX1   DS    CL5
         DC    CL69' AVAILABLE TRACKS ON THIS VOLUME'
         DS    0D
EXTAREA  DS    16CL10
DSCBNAME CAMLST SEARCH,DSN,DSS,WORK1
PTR      DS    CL5
DSSN     DS    CL6
         DC    X'09'
MSGNM    DC    C'DEVICE NOT MOUNTED. DATA SET '
         DS    CL44
         DC    C' BYPASSED. '
         DC    X'09'
MSGNF    DC    C'DATA SET '
         DS    CL44
         DC    C' NOT FOUND. DATA SET BYPASSED.  '
         DC    X'09'
MSGIO    DC    C'I/O ERROR READING VTOC FOR '
         DS    CL44
         DC    C'. BYPASSED.  '
         DC    X'09'
NODD     DC    CL84'UNABLE TO ACCESS ABOVE VOLUME. CHECK FOR MISSING DD*
                STATEMENT'
BITOHEX  DC    C'0123456789ABCDEF'
FIND     CAMLST SEEK,PTR,DSSN,WORK3
EJECTRCD DC    X'8B'
SPACE3   DC    X'1B'
         LTORG
CONTROL  DCB   DSORG=PS,MACRF=GM,LRECL=80,BLKSIZE=80,DDNAME=SYSIN,     X
               EODAD=ENDUP
LIST     DCB   DSORG=PS,MACRF=PM,LRECL=85,BLKSIZE=85,DDNAME=SYSPRINT,  *
               RECFM=FM
LISTONLY DCB   DSORG=PS,MACRF=PM,LRECL=85,BLKSIZE=85,DDNAME=SYSLIST,   *
               RECFM=FM
DCB4VTOC DCB   DSORG=PS,MACRF=R,BLKSIZE=140,KEYLEN=44,EODAD=CLOSEUP,   *
               RECFM=U,                                                *
               EXLST=JFCBEXIT,DDNAME=XXXXXXXX
FORMAT1  DSECT
DS1DSNAM DS    CL44
DS1FMTID DS    CL1
DS1DSSN  DS    CL6
DS1VOLSQ DS    CL2
DS1CREDT DS    CL3
DS1EXPDT DS    CL3
DS1NOEPV DS    CL1
DS1NOBDB DS    CL1
         DS    CL1
DS1SYSCD DS    CL13
         DS    CL7
DS1DSORG DS    CL2
DS1RECFM DS    CL1
DS1OPTCD DS    CL1
DS1BLKL  DS    CL2
DS1LRECL DS    CL2
DS1KEYL  DS    CL1
DS1RKP   DS    CL2
DS1DSIND DS    CL1
DS1SCALO DS    CL4
DS1LSTAR DS    CL3
DS1TRBAL DS    CL2
         DS    CL2
DS1EXT1  DS    CL10
DS1EXT2  DS    CL10
DS1EXT3  DS    CL10
DS1PTRDS DS    CL5
FORMAT2  DSECT
         DS    CL1
DS22MIND DS    CL7
DS2L2MIN DS    CL5
DS23MIND DS    CL7
DS2L3MIN DS    CL5
         DS    CL19
DS2FMTID DS    CL1
DS2NOLEV DS    CL1
DS2DVIND DS    CL1
DS21RCYL DS    CL3
DS2LTCYL DS    CL2
DS2CYLOV DS    CL1
DS2HIRIN DS    CL1
DS2HIRPR DS    CL1
DS2HIROV DS    CL1
DS2RSHTR DS    CL1
DS2HIRTI DS    CL1
         DS    CL1
DS2TAGDT DS    CL2
DS2RORG3 DS    CL3
DS2NOBYT DS    CL2
DS2NOTRK DS    CL1
DS2PRCTR DS    CL4
DS2STIND DS    CL1
DS2CYLAD DS    CL7
DS2ADLIN DS    CL7
DS2ADHIN DS    CL7
DS2LPRAD DS    CL8
DS2LTRAD DS    CL5
DS2LCYAD DS    CL5
DS2LMSAD DS    CL5
DS2LOVAD DS    CL8
DS2BYOVL DS    CL2
DS2RORG2 DS    CL2
DS2OVRCT DS    CL2
DS2RORG1 DS    CL2
         DS    CL3
DS2PTRDS DS    CL5
FORMAT3  DSECT
         DS    CL4
DS3EXTNT DS    4CL10
DS3FMTID DS    CL1
DS3ADEXT DS    9CL10
DS3PTRDS DS    CL5
FORMAT5  DSECT
DS5KEYID DS    CL4
DS5AVEXT DS    CL5
DS5EXTAV DS    CL35
DS5FMTID DS    CL1
DS5MAVET DS    CL90
DS5PTRDS DS    CL5
         END
