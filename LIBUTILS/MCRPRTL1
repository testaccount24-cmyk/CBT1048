PRTL TITLE 'PRINT ROUTINE MCRPRTL1 TO SERVICE PRINTLIN MACRO CALLS'
*        THIS ROUTINE WAS CODED BY
*        JOHN R. EHRMAN, SLAC COMPUTATION GROUP, STANFORD, CALIF.
*        NOVEMBER 1, 1967
*        REVISED FOR RELEASE 16 JANUARY 15, 1969
*
*        CALLING SEQUENCE GENERATED BY PRINTLIN MACRO EXPANSION...
*        PRINTLIN  LOC,UNIT=U,LEN=L
*   OR   PRINTLIN  (LOC),UNIT=U,LEN=L
*
*        CNOP  2,4
*        STM   14,15,*+10
*        L     15,*+14
*        BALR  14,15
*        DC    2F'0'
*        DC    V(MCRPRTLN)
*        DC    B'FLAGS',AL3(LOC)
*
*        WHERE THE FLAGS INDICATE THE FOLLOWING...
*  (1)   B'001' MEANS THAT THE FOLLOWING FULLWORD CONTAINS A UNIT
*        SPECIFICATION OF THE FORM
*        DC    B'X',AL3(UNIT)  WHERE X IS 0 IF UNIT IS A SELF-DEFINING
*        VALUE, AND IS 1 IF UNIT IS THE NAME OF A FULLWORD WHICH CON-
*        TAINS THE INTEGER UNIT NUMBER.  THE FORTRAN I/O ROUTINE FIOCS
*        CHECKS THE BIT 'X' FOR INDIRECTNESS OF SPECIFICATION, SO IT
*        MUST REMAIN IN THE STATED POSITION AND NO OTHERS MAY BE SET.
*  (2)   B'010'  MEANS THAT THE FOLLOWING FULLWORD CONTAINS A LENGTH
*        SPECIFICATION OF THE FORM
*        DC    B'X0000000',AL3(LEN)  WHERE X IS ZERO IF LEN IS A SELF-
*        DEFINING VALUE, AND IS 1 IF LEN IS THE NAME OF A FULLWORD
*        WHICH CONTAINS THE INTEGER LINE LENGTH TO BE PRINTED.
*        IF THE LENGTH SPECIFIED IS NOT POSITIVE, OR EXCEEDS 10000
*        ( WHICH IS ABOUT A PAGE AND A HALF OF CHARACTERS), NO INFOR-
*        MATION WILL BE PRINTED, AND CONTROL RETURNS TO THE CALLER.
*  (3)   IF BOTH A UNIT AND A LENGTH SPECIFICATION ARE PRESENT, AS
*        INDICATED BY B'011', THE UNIT SPECIFICATION IS FIRST.
*  (4)   B'X00'   IF X IS ZERO, LOC IS THE NAME OF THE FIRST BYTE TO BE
*        PLACED IN THE OUTPUT LINE, AND IF X IS 1, LOC IS THE NAME OF A
*        FULLWORD ADDRESS CONSTANT WHICH CONTAINS THE ADDRESS OF THE
*        FIRST BYTE OF THE LINE TO BE PRINTED, AS SPECIFIED BY PAREN-
*        THESES AROUND THE LOCATION NAME LOC.
*
*        LM    14,15,0(14)   CONTROL IS RETURNED HERE TO RELOAD R14-15
*
*        NOTES   (1) THE DEFAULT LINE LENGTH FOR THE MACRO EXPANSION IS
*                133 BYTES, AND THE DEFAULT UNIT IS 6.  IF THESE TWO
*                VALUES ARE SPECIFIED, NO CONSTANTS ARE GENERATED.
*                (2) IF THE NUMBER OF BYTES SPECIFIED FOR THE LINE
*                LENGTH IS GREATER THAN THE NUMBER PROVIDED FOR BY THE
*                DEVICE, MULTIPLE LINES WILL BE 'PRINTED', AND NO BYTES
*                WILL BE THROWN AWAY.
*                (3) THE FORTRAN I/O PACKAGE WHICH PERFORMS THE ACTUAL
*                INTERFACE WITH THE ACCESS METHOD ROUTINES REQUIRES
*                DDNAMES OF THE FORM FTUUF001 FOR UNIT UU, AND WILL
*                NORMALLY USE THE FIRST CHARACTER OF EACH LINE FOR
*                CARRIAGE CONTROL.
         EJECT
*
MCRPRTL1 CSECT
         ENTRY MCRPRTLN
INIT     EQU   0
WRITE    EQU   2
         USING *,15
MCRPRTLN STM   0,14,SAVE     SAVE ALL REGISTERS
         LR    12,15         MOVE BASE REGISTER TO R12
         DROP  15
         USING MCRPRTLN,12
         LA    4,133         SET UP DEFAULT LENGTH IN R4
         LA    2,UNIT        SET DEFAULT UNIT POINTER TO 6
         L     5,12(0,14)    GET LOCATION OF CHARACTER STRING
         TM    12(14),4      TEST FOR REMOTELY SPECIFIED LOCATION
         BC    8,*+8         JUMP IF NOT REMOTE
         L     5,0(0,5)      GET TRUE LOCATION
         LA    11,16(0,14)   SET UP RETURN ADDRESS IN R11
         TM    12(14),1      TEST UNIT SPECIFICATION BIT
         BC    8,OKUNIT      BRANCH IF NO UNIT SPECIFICATION
         LA    2,16(0,14)    GET UNIT SPEC PTR, FIOCS CHECKS INDIRECT
         LA    11,4(0,11)    INCREMENT RETURN ADDRESS TO SKIP ADCON
         TM    12(14),2      TEST LENGTH SPECIFICATION BIT
         BC    8,OKLEN       BRANCH IF NO LENGTH SPEC
         L     4,20(0,14)    GET LENGTH SPECIFICATION
         B     RMTLEN        JUMP TO CHECK IF REMOTELY SPECIFIED
OKUNIT   TM    12(14),2      CHECK LENGTH SPECIFICATION BIT
         BC    8,OKLEN       JUMP IF NONE
         L     4,16(0,14)    GET LENGTH SPEC
RMTLEN   LA    11,4(0,11)    INCREMENT RETURN ADDRESS
         LTR   4,4           SEE IF REMOTE BIT IS ON
         BC    11,OKLEN      BRANCH IF NOT
         L     4,0(0,4)      OTHERWISE PICK UP REMOTELY SPECIFIED LEN
OKLEN    LTR   4,4           CHECK FINAL VALUE OF LENGTH
         BC    12,EXIT       IF NOT POSITIVE, EXIT
         CH    4,BIGLINE     SEE IF CHARACTER COUNT IS EXCESSIVE
         BC    2,EXIT        EXIT IF TOO MANY CHARACTERS WANTED
         L     1,VFIOCS      GET ADDRESS OF FORTRAN OUTPUT DRIVER
*     ADDRESS OF UNIT OR UNIT POINTER MUST BE IN R2
         BALR  0,1           BRANCH TO IT
         DC    AL1(INIT),AL1(255) INITIALIZE FOR FORMATTED OUTPUT
*     RETURNS BUFFER LOCATION IN R2, LENGTH IN R3
         NOP   0             IGNORE ERROR RETURN FROM FIOCS
MULTIPLE LA    0,256         SET UP FOR MOVE
         CR    3,4           SEE IF MOVE AMOUNT IS BIGGER THAN BUFFER
         BC    12,*+6        BRANCH IF NOT
         LR    3,4           OTHERWISE SET FOR MOVING WHAT WE'VE GOT
         LR    6,3           SAVE AMOUNT TO MOVE
MOVEB    CR    3,0           DO IN CHUNKS OF 256 BYTES AT A TIME
         BC    12,MOVEBX     JUMP IF LESS THAN 256
         MVC   0(256,2),0(5) MOVE 256 BYTES
         AR    2,0           INCREMENT BUFFER POINTER
         AR    5,0           INCREMENT MESSAGE POINTER
         SR    3,0           DECREASE LENGTH COUNT
         B     MOVEB         BRANCH BACK TO TEST AGAIN
MOVEAMVC MVC   0(0,2),0(5)   EXECUTED MOVE INSTRUCTION
MOVEBX   BCTR  3,0           SET UP FOR EXECUTE
         LTR   3,3           CHECK FOR NEGATIVE NOW
         BC    4,WRITEIT     OUTPUT IT ALL IF DONE
         EX    3,MOVEAMVC    MOVE THE REST OF THE LINE INTO THE BUFFER
         LA    5,1(3,5)      BUMP POINTER TO NEXT OUTPUT CHARACTER
         LR    2,6           INDICATE NUMBER OF BYTES TO OUTPUT
WRITEIT  L     1,VFIOCS      GET FIOCS ADDRESS
         BALR  0,1           BRANCH TO PUTPUT ROUTINE
         DC    AL1(WRITE),AL1(0)
         NOP   0             IGNORE ERROR RETURN FROM FIOCS
         SR    4,6           SEE IF THERE'S MORE TO DO
         BC    2,MULTIPLE    JUMP BACK IF LINE IS LONGER THAN A BUFFER
EXIT     LR    15,11         SET UP RETURN ADDRESS IN R15
         LM    0,14,SAVE     RESTORE ALL REGISTERS
         SPM   14            RESTORE THE CALLER'S CONDITION CODE TOO
         BR    15            RETURN TO CALLER
BIGLINE  DC    H'10000'
UNIT     DC    F'6'          DEFAULT UNIT SPECIFICATION
VFIOCS   DC    V(FIOCS#)
SAVE     DS    15F
         END
