UT477    START 0
         PRINT NOGEN               SUPPRESS MACRO EXPANSION
         PGMIN EQUATES=YES,USING=(R13)  ESTABLISH ADDRESSABILITY
         L     R3,0(R1)            SAVE ADDRESS OF PARM FROM EXEC CARD
         LH    R4,0(R3)            LOAD LENGTH OF PARM
         CH    R4,ONE              TEST IF LENGTH OF PARM IS EQUAL TO 1
         BE    OK                  YES - BRANCH TO OK
NO       ABEND 477,DUMP            NO  - ABEND
OK       MVC   SUPPRESS,2(R3)      SAVE SUPPRESS CHARACTER
         OPEN  (SYSIN,,SYSPUNCH,(OUTPUT)) OPEN THE FILES
REFICA   GET   SYSIN               READ FIRST CARD
         MVC   FIRSTCAR(80),0(R1)  SAVE FIRST CARD
READ     GET   SYSIN               READ NEXT CARDS
         CLC   0(9,R1),=C'ENDOFDECK'    TEST IF END OF DECK
         BE    REFICA              YES - BRANCH TO READ A OTHER FIRST C
         MVC   OUTCARD,0(R1)       MOVE IN OUTPUT AREA
         SR    R5,R5               RESET
         LA    R6,FIRSTCAR-1
         LA    R7,OUTCARD-1
LOOP     AH    R5,ONE              ADD ONE TO REG.5
         CH    R5,COL81            TEST IF COL. 81
         BE    WRITE               YES - BRANCH TO WRITE
         LA    R8,0(R5,R6)         NO - ADDRESS OF COL. FOR FIRST CARD
         LA    R9,0(R5,R7)         ADDRESS OF COL. FOR OUTPUT CARD
         CLI   0(R8),BLANK         TEST IF THIS COL. CHANGE
         BE    LOOP                NO - BRANCH TO TEST NEXT COL.
         CLC   0(1,R8),SUPPRESS    TEST IF THIS COL. IS TO SUPPRESS
         BE    MOVEBL              YES - BRANCH TO MOVE BLANK
         MVC   0(1,R9),0(R8)       NO - MOVE NEW CHARACTER TO THIS COL.
         B     LOOP                BRANCH TO TEST NEXT COL.
MOVEBL   MVI   0(R9),BLANK         MOVE BLANK TO THIS COL.
         B     LOOP                BRANCH TO TEST NEXT COL.
WRITE    PUT   SYSPUNCH,OUTCARD    WRITE
         B     READ                BRANCH TO NEXT READ
EOF      CLOSE (SYSIN,,SYSPUNCH)   CLOSE THE FILES
         PGMOUT ,                  END OF PROGRAM
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,RECFM=F,LRECL=80,        C
               BLKSIZE=80,EODAD=EOF
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM,RECFM=F,LRECL=80,     C
               BLKSIZE=80
COL81    DC    H'81'
BLANK    EQU   X'40'
SUPPRESS DS    C
OUTCARD  DS    CL80
FIRSTCAR DS    CL80
ONE      DC    H'1'
         END
