SETLIMIT CSECT
         ENTRY UT422
*
* THIS IS AN ASSEMBLER LANGUAGE ROUTINE THAT CAN BE CALLED BY A COBOL
* PROGRAM TO POSITION AN INDEXED SEQUENTIAL FILE THAT IS TO BE READ
* AND UPDATED (BUT NO RECORDS INSERTED) TO ANY POINT (SPECIFIED BY A
* RECORD KEY) IN THE MIDDLE OF THE FILE.
*
*
* TO CALL SETLIMIT CODE
*
*        CALL  SETLIMIT USING FILENAME,SEARCHTYPE,KEY
*
*   WHERE
*
*   FILENAME   = THE NAME OF THE FILE TO BE POSITIONED
*   SEARCHTYPE = 1 FOR A SPECIFIC KEY SEARCH (ZONED DECIMAL)
*                2 FOR A KEY CLASS SEARCH (ZONED DECIMAL)
*                3 IF THE FIRST ITEM IN THE DATA SET IS REQUIRED
*   KEY        = KEY OR KEY CLASS
*
* NOTE
* ----
*    IF SEARCHTYPE 3 IS SPECIFIED, THE KEY PARAMETER MAY BE OMITTED
*
* ALL THE ROUTINE DOES IS TO PICK UP THE PARAMETERS FROM THE PARAMETER
* LIST ADDRESS  (IN REG ONE), IDENTIFY THE SEARCH TYPE REQUIRED AND
* ISSUE THE APPROPRIATE 'SETL' INSTRUCTION.
*
UT422    SAVE  (14,12)
         BALR  12,0
         USING *,12
*
* PROVIDE NEW SAVE AREA
*
         ST    13,SAVE01+4
         LR    11,13
         LA    13,SAVE01
         ST    13,8(11)
*
* OBTAIN LOCATION OF FILENAME (DCB) KEY AND SEARCHTYPE
*
         L     3,0(0,1)                DCB (FILENAME) TO REG 3
         L     9,56(0,3)               SYNAD ENTRY TO REG 9
         C     9,=F'1'                 IS IT EMPTY
         BE    SYNAD
SEARCH   L     5,4(0,1)                SEARCH TYPE ADDRESS TO REG 5
         TM    4(1),X'80'              IS SEARCHTYPE LAST PARAM. IN LST
         BO    CONT01                  YES
         L     6,8(0,1)                NO, KEY ADDRESS TO REG 6
*
*
CONT01   ESETL (3)
         CLI   0(5),C'1'               SEARCHTYPE 1 SPECIFIED
         BE    SPEC0101
         CLI   0(5),C'3'               SEARCHTYPE 3 SPECIFIED
         BE    SPEC0301
         SETL  (3),KC,(6)              SEARCH ON KEY CLASS
RETURN01 L     13,SAVE01+4
         RETURN (14,12)
SPEC0301 SETL  (3),B                   FIRST ITEM REQUIRED
         B     RETURN01
SPEC0101 SETL  (3),K,(6)               SEARCH ON SPECIFIC KEY
         B     RETURN01
SYNAD    LA    9,BR14                  SET SYNAD TO RETURN AFTER
         ST    9,56(0,3)               ERROR EXIT TAKEN
         B     SEARCH
BR14     BC   0,ABEND
         BR    14
ABEND    ABEND 999,DUMP
*
*
*
SAVE01   DS    18F
*
         LTORG
         END
