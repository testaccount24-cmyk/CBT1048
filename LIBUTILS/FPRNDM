         TITLE 'RANDOM NUMBER GENERATOR - UT365'
         ISEQ  73,80
         SPACE
* BEGIN SUBROUTINE 'FPRNDM'
*
* THIS SUBROUTINE RETURNS A PSEUDO-RANDOM VALUE IN NORMALIZED FLOATING-
* POINT FORM. THE VALUES ARE EVENLY DISTRIBUTED OVER THE RANGE OF ZERO
* THROUGH LESS THAN ONE.
*
* IN ADDITION TO THE REGISTERS SPECIFIED IN THE GENERAL COMMENTS, THIS
* SUBROUTINE USES FLOATING-POINT REGISTER SIX.
         SPACE 2
FPRNDM   START
         STM   6,9,44(13)     SAVE REGISTERS
         BALR  9,0  ESTABLISH BASE ADDRESS
         USING *,9
         L     8,0(1)    LOAD ADDRESS OF RANDOM NUMBER HOLD AREA TO R8
         TM    0(8),128  Q.-USER HAS SPECIFIED CYCLE RESTART
         BZ    *+10      B.-NO, GO TO GENERATE NEXT NUMBER
         MVC   NUMBER,RESTORE RESTART GENERATION
         L     7,NUMBER  CALCULATE NEW NUMBER
         LR    6,7
         SLL   7,16
         ALR   7,6
         ALR   7,6
         ALR   7,6
         ST    7,NUMBER  NEW NUMBER IS BASIS FOR NEXT CALCULATION
         SRL   7,8  CONVERT FROM LOGICAL TO FLOATING-POINT
         ST    7,WORKAREA
         MVI   WORKAREA,64    SET CHARACTERISTIC
         SUR   6,6  RESET FLOATING-POINT REGISTER
         AE    6,WORKAREA     NORMALIZE VALUE
         STE   6,0(8)    RETURN TO USER
         LM    6,9,44(13)     RESTORE REGISTERS
         BR    14   RETURN TO USERS PROGRAM
         SPACE 2
NUMBER   DC    F'131074'
RESTORE  DC    F'131074'
WORKAREA DS    F
         END
