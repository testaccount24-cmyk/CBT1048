         TITLE '   IMAP  PRINT  TAPE  ROUTINE  -  BY  J.L.PYLE.'
         ISEQ  76,80
*                                                                     *
***********************************************************************
*              PROGRAM WRITTEN BY J.L.PYLE, MISD - LONDON.            *
*                            DECEMBER 1967.                           *
***********************************************************************
*                                                                     *
STARTA   START 0
         SAVE  (14,12)                  STORE REGISTERS.
         BALR  11,0                     SET UP BASE REGISTER.
         USING *,11
         USING CARDIN,3                 ALLOCATE R3 BASE FOR CARD AREA.
         USING TAPEIN,4                 ALLOCATE R4 BASE FOR TAPE AREA.
         ST    13,SAVEAREA+4            STORE REGISTER 13.
         LA    13,SAVEAREA
         B     BA020
SAVEAREA DS    18F                      STORE FOR REGISTERS.
*                 ********************************                    *
*                 *  DEFINE  ALL  DATA  SETS.    *                    *
*                 ********************************                    *
         SPACE 2
*                             INPUT  STATIONERY  CODE  TABLE.         *
TABLE18  DCB   DSORG=PS,                                               ,
               BFALN=D,                                                ,
               BLKSIZE=800,                                            ,
               DDNAME=UTL3C,                                           ,
               DEVD=DA,                                                ,
               MACRF=GL,                                               ,
               RECFM=FB,                                               ,
               EODAD=EOFRTNC,                                          ,
               LRECL=80
         SPACE 2
*                             INPUT  CARRIAGE  TAPE  CODE  TABLE.     *
TABLE19  DCB   DSORG=PS,                                               ,
               BFALN=D,                                                ,
               BLKSIZE=800,                                            ,
               DDNAME=UTL3D,                                           ,
               DEVD=DA,                                                ,
               MACRF=GL,                                               ,
               RECFM=FB,                                               ,
               EODAD=EOFRTND,                                          ,
               LRECL=80
         SPACE 2
*                             INPUT  CONTROL  CARDS.                  *
CARDS    DCB   BFALN=D,                                                ,
               BFTEK=S,                                                ,
               DDNAME=UTL3A,                                           ,
               DSORG=PS,                                               ,
               EODAD=EOFRTNA,                                          ,
               MACRF=GL,                                               ,
               RECFM=F
         SPACE 2
*                             INPUT  PRINTAPE  DATA  SET.             *
PRINTAPE DCB   BFALN=D,                                                ,
               BFTEK=S,                                                ,
               BLKSIZE=1500,                                           ,
               DDNAME=UTL3B,                                           ,
               DEVD=DA,                                                ,
               DSORG=PS,                                               ,
               EODAD=EOFRTNB,                                          ,
               LRECL=150,                                              ,
               MACRF=GL,                                               ,
               RECFM=FB
         SPACE 2
*                             OUTPUT  PRINTER  DATA  SET.             *
PRINTER  DCB   DSORG=PS,                                               ,
               BLKSIZE=133,                                            ,
               MACRF=PM,                                               ,
               DDNAME=SYSPRINT,                                        ,
               RECFM=FM,                                               ,
               LRECL=133,                                              ,
               BFTEK=S
         SPACE 2
*                   END  OF  DATA  SETS  DEFINITIONS.                 *
         EJECT
*                             DEFINE  WORKAREAS, CONSTANTS  ETC.
PRINT    DS    CL133                    PRINTER WORKAREA.
TRANSTAB DC    X'01'                    CONTROL CHARACTER TABLE.
         DC    10X'00'
         DC    X'D9E1'
         DC    20X'00'
         DC    X'09'
         DC    25X'00'
         DC    X'DBE3'
         DC    67X'00'
         DC    X'D1899199A1A9B1B9C1C9'
         DC    7X'00'
         DC    X'0B131B'
         DC    14X'00'
         DC    X'1119'
         DC    12X'00'
         DC    X'D38B939BA3ABB3BBC3CB'
SWITCH   DS    CL1
REPSTORE DS    CL144
ERRMSG1  DC    C'************** THERE  IS  AN  ERROR  IN  THE  CONTROL X
               CARDS -- JOB  HAS  TERMINATED. **************'
REPNOSTO DS    CL6                      REPORT NO. STORE.
ERRMSG2  DC    C'************** INPUT  TAPE  IS  OUT  OF  SEQUENCE - JOX
               B  HAS  TERMINATED. **************'
NONOSTOR DS    CL6                      NOT SELECTED STORE.
ERRMSG3  DC    C'*** INVALID  SKIP  CODES - JOB  HAS  TERMINATED ***'
ERRMSG4  DC    C'*** INVALID  SKIP  CODES  PRESENT  IN  THE  FOLLOWING X
                LINE  RECORD - SPACE  1  AFTER  PRINT  ASSUMED. ***'
ERRMSG5  DC    C'*** SEQUENCE  ERROR  IN  INPUT  TAPE  FILE - RECORD  HX
               AS  BEEN  PRINTED  BELOW. ***'
REPLY    DC    CL2'  '
ECBA     DC    F'0'
ALGNMSG1 DC    C'THIS LINE IS PRINTED ON 1ST PRINTABLE LINE OF STATIONEX
               RY. THE FIRST'
ALGNMSG2 DC    C'LETTER -T- OF THE FIRST WORD -THIS- SHOULD BE ALIGNED X
               ON POSTN 1.'
REPLY2   DC    CL1' '
RP       DC    CL1' '
CARRIAGE DS    CL2
STATNERY DS    CL2
TAB18    DSECT                          STATIONERY CODE TABLE WORKAREA.
         DS    CL4
STATCODE DS    CL2                      STATIONERY CODE.
         DS    CL6
STATDESC DS    CL41                     STATIONERY DESCRIPTION.
         DS    CL27
*                                                                     *
TAB19    DSECT                          CARRIAGE TAPE CODE WORKAREA.
         DS    CL4
CARRCODE DS    CL2                      CARRIAGE TAPE CODE.
         DS    CL6
CARRDESC DS    CL41                     CARRIAGE TAPE DESCRIPTION.
         DS    CL27
*                                                                     *
CARDIN   DSECT                          INPUT CARD WORKAREA.
CCODE    DS    CL3                      CARD CODE.
CFORM    DS    CL1                      CARD FORMAT.
CMODE    DS    CL1                      MODE OF RUN.
CREPNO   DS    CL6                      REPORT NO. - OCCURS
         DS    CL66                                  12 TIMES.
CCONT    DS    CL1                      CARD CONTINUED.
         DS    CL2                      BLANK.
*                                                                     *
TAPEIN   DSECT                          INPUT TAPE WORKAREA.
TBLANK   DS    CL1                      BLANK.
TREPNO   DS    CL6                      REPORT NUMBER.
TCARTAPE DS    CL2                      CARRIAGE TP CODE.
TSTATYPE DS    CL2                      STATIONERY CODE.
TREPLINO DS    CL3                      REPORT LINE NO.
TAFTPRIN DS    CL1                      AFTER PRINT SKIP/SPACE CODE.
TBEFPRIN DS    CL1                      BEFORE PRINT SKIP/SPACE CODE.
TPRINTLN DS    CL132                    LINE TO BE PRINTED.
TCONST   DS    CL1
TBLAN    DS    CL1
STARTA   CSECT
BA020    L     5,0(1)                   LOAD PARM. LIST ADDRESS TO R5.
         LA    10,REPSTORE+144
         USING TAB18,6                  ALLOCATE R6 BASE FOF TABLE18.
         USING TAB19,7                  ALLOCATE R7 BASE FOF TABLE19.
         OPEN  (TABLE18,,TABLE19,,CARDS,,PRINTAPE,,PRINTER,(OUTPUT))
         MVC   CARRIAGE(2),=C'00'       INITIALISE STATIONERY
         MVC   STATNERY(2),=C'00'        AND CARRIAGE TAPE CODES.
         MVI   PRINT+1,X'40'            CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTAREA.
         MVI   PRINT,X'89'              SKIP TO
         PUT   PRINTER,PRINT            CHANNEL 1.
         LA    9,STATABLE
GET18    GET   TABLE18
         LR    6,1
         MVC   0(49,9),STATCODE
         LA    9,49(9)
         B     GET18
EOFRTNC  MVI   0(9),C'*'
         LA    9,STATABLE
         GET   CARDS                    A050-READ CARD.
         LR    3,1                      POSITION DUMMY SECTION.
         LA    8,REPSTORE
         CLC   CCODE(3),=C'A06'         ENSURE CORRECT CARD FORMAT.
         BE    *+8
         BAL   14,BA170
         CLI   CFORM,C'B'               TEST FOR NORMAL PROCESSING.
         BE    BA060
         B     BA080
BA060    MVI   SWITCH,C'N'              SPECIFY NORMAL.
         B     BB010
BA080    CLI   CFORM,C'A'               TEST CORRECT FORMAT.
         BE    *+8
         BAL   14,BA170
         CLI   CMODE,C'S'               TEST FOR SELECTIVE REPORTS.
         BNE   BA120
         MVI   SWITCH,C'S'              SPECIFY SELECTIVE.
BA110    CLI   CCONT,X'40'              TEST FOR A SECOND CARD.
         BE    BA210
         B     BA140
BA120    CLI   CMODE,C'O'              TEST FOR OMISSIVE REPORTS.
         BE    *+8
         BAL   14,BA170
         MVI   SWITCH,C'O'             SPECIFY OMISSIVE.
         B     BA110
BA140    MVC   0(72,8),CREPNO           STORE REPORT NUMBERS.
         LA    8,72(8)
         GET   CARDS
         LR    3,1
         CLC   CCODE(3),=C'A06'
         BE    *+8
         BAL   14,BA170
         CLI   CFORM,C'A'
         BE    BA200
BA170    CLC   2(4,5),=C'TEST'
         BNE   BA175
         MVC   PRINT+4(100),ERRMSG1
         MVI   PRINT,X'09'
         PUT   PRINTER,PRINT
         MVC   PRINT+2(131),PRINT+1
         BR    14
BA175    MVI   PRINT,X'09'
         PUT   PRINTER,PRINT
         MVI   PRINT,X'89'
         PUT   PRINTER,PRINT
         ABEND 999,DUMP
BA200    CLC   CMODE(1),SWITCH          TEST FOR CORRECT MODE.
         BE    *+8
         BAL   14,BA170
BA210    MVC   0(72,8),CREPNO           STORE REPORT NUMBERS.
         GET   CARDS
         LR    3,1
         CLC   CCODE(3),=C'A06'
         BE    *+8
         BAL   14,BA170
         CLI   CFORM,C'B'               TEST FOR LAST CARD.
         BE    *+8
         BAL   14,BA170
BB010    MVC   REPNOSTO(6),=C'000000'   SET REPORT NO. STORE TO ZEROS.
BB020    GET   PRINTAPE
         LR    4,1
BB030    CLC   TREPNO(6),REPNOSTO       B030.-SEQUENCE TEST.
         BE    BC010                    O.K.
         BH    BB040
         CLC   TSTATYPE(2),STATNERY     CHANGED STATIONERY.*
         BNE   BB040                    YES.
         CLC   TCARTAPE(2),CARRIAGE     CHANGED CARRIAGE TAPE.*
         BNE   BB040                    YES.
         CLC   2(4,5),=C'TEST'          IS TEST PARAMETER PRESENT.*
         BE    BB035                    YES.
         CLC   2(4,5),=C'NSEQ'     SEQUENCE ERRORS IGNORED - Q
         BE    BB040               YES - AVOID ERROR MESSAGES
         MVC   PRINT+4(88),ERRMSG2      PRINT SEQUENCE ERROR MESSAGE.
         B     BA175
BB035    MVC   PRINT+4(83),ERRMSG5      INSERT MESSAGE.
         MVI   PRINT,X'09'
         PUT   PRINTER,PRINT
         MVI   PRINT+1,X'40'            CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTAREA.
         B     BB040
BB040    MVC   REPNOSTO(6),=C'000000'   SET REPORT NO. STORE TO ZEROS.
         CLI   SWITCH,C'N'              TEST FOR NORMAL PROCESSING.
         BE    BC010                    YES.
         CLI   SWITCH,C'O'             TEST FOR OMISSIVE PROCESSING.
         BE    BB070                    YES.
         LA    8,REPSTORE               B120.
BB160    CLC   0(6,8),TREPNO            SELECTIVE REPORT.*
         BE    BC010                    YES.
         LA    8,6(8)                   INCREMENT STORE REGISTER BY 6.
         CR    10,8
         BNE   BB160
         CLI   0(8),X'5C'               END OF CARD STORE.*
         BNE   BB160                    NO.
         LA    8,REPSTORE               RE-INITIALISE REGISTER,
         MVC   NONOSTOR(6),TREPNO       MOVE REP NO TO NOT-WANTED STOR.
BB260    GET   PRINTAPE
         LR    4,1
         CLC   TREPNO(6),NONOSTOR       SAME NO.*
         BE    BB260                    YES.
         B     BB030                    RETURN FOR NEW NUMBER.
BB070    LA    8,REPSTORE
BB080    CLC   0(6,8),TREPNO            OMMISSIVE REPORT.*
         BE    BB090                    YES.
         LA    8,6(8)                   INCREMENT STORE REGISTER BY 6.
         CR    10,8
         BE    BC010                    END OF REPSTORE.
         CLI   0(8),X'5C'               END OF CARD STORE.*
         BNE   BB080                    NO.
         B     BC010                    REPORT TO BE PRINTED.
BB090    LA    8,REPSTORE               RE-INITIALISE REGISTER.
         MVC   NONOSTOR(6),TREPNO       MOVE REPNO TO NOT-WANTED STORE.
BB190    GET   PRINTAPE
         LR    4,1
         CLC   TREPNO(6),NONOSTOR       SAME NO.*
         BE    BB190                    YES.
         B     BB030                    RETURN FOR NEW NUMBER.
BC010    CLC   REPNOSTO(6),=C'000000'   NEW REPORT.*
         BE    BC020                    YES.
BC050    MVI   PRINT+1,X'40'            CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTLINE.
         CLC   TAFTPRIN(2),=X'4040'     INVALID SKIP COD.S9*
         BNE   BC150                    NO.
         CLC   2(4,5),=C'TEST'          IS TEST PARAMETER PRESENT.*
         BE    BC140                    YES.
         MVI   PRINT,X'89'              NO-SKIP TO
         PUT   PRINTER,PRINT            CHANNEL 1.
         MVC   PRINT+4(51),ERRMSG3      PRINT INVALID SKIP CODES MSG.
         B     BA175
BC140    MVC   PRINT+4(106),ERRMSG4     PRINT INVALID (TESTING) MSG.
         MVI   PRINT,X'09'
         PUT   PRINTER,PRINT
         MVC   PRINT+2(131),PRINT+1     CLEAR PRINTLINE LEAVING ASSUMED
         B     BC290                    CONTROL CHARACTER-9-FOR PRINT.
ALLRIGHT MVI   TRANSTAB,X'03'           INSERT NO-OP SKIP CODE.
         TR    TBEFPRIN(1),TRANSTAB-64 TRANSLATE BEFORE-PRINT CODES.
         MVC   PRINT(1),TBEFPRIN        INSERT RESULTING CTL CHARACTER.
         PUT   PRINTER,PRINT
         MVI   TRANSTAB,X'01'           INSERT WRITE-NO-SPACE CODE.
         TR    TAFTPRIN(1),TRANSTAB-64  TRANSLATE AFTER PRINT CODES.
         MVC   PRINT(1),TAFTPRIN        INSERT RESULTING CTL CHARACTER.
BC290    MVC   PRINT+1(132),TPRINTLN    INSERT RECORD FOR PRINTING.
         PUT   PRINTER,PRINT            PRINT RECORD.
         MVI   PRINT+1,X'40'            CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTAREA.
         B     BB020
BC150    CLI   TBEFPRIN,C'9'            *   ENSURE         THAT       *
         BH    TESTTEST                 *   B E F O R E               *
         CLI   TBEFPRIN,C'0'            *   P R I N T                 *
         BNL   CARRYON                  *   S P A C E  /  S K I P     *
         CLI   TBEFPRIN,C'#'            *   C O D E                   *
         BE    CARRYON                  Â¾   I S                       *
         TM    TBEFPRIN,X'7D'           *   W I T H I N               *
         BE    CARRYON                  *   T H E                     *
         CLI   TBEFPRIN,C'J'            *   C O D E S                 *
         BE    CARRYON                  *   A L L O W E D ,           *
         CLI   TBEFPRIN,C'K'            *   N A M E L Y -             *
         BE    CARRYON                  *   0  T H R O U G H  9 ,     *
         CLI   TBEFPRIN,C'L'            *   # , J , K , L  A N D      *
         BE    CARRYON                  *   ' .                       *
         CLI   TBEFPRIN,C' '            *                             *
         BE    CARRYON                  *******************************
TESTTEST CLC   2(4,5),=C'TEST'
         BE    BC140
         MVI   PRINT,X'89'
         PUT   PRINTER,PRINT
         MVC   PRINT+4(51),ERRMSG3
         B     BA175
CARRYON  CLI   TAFTPRIN,C'/'            *******************************
         BE    ALLRIGHT                 *  E N S U R E    T H A T     *
         CLI   TAFTPRIN,C'S'            *      A F T E R              *
         BE    ALLRIGHT                 *      P R I N T              *
         CLI   TAFTPRIN,C'T'            *   S P A C E  / S K I P      *
         BE    ALLRIGHT                 *        C O D E              *
         CLI   TAFTPRIN,C'.'            *          I S                *
         BE    ALLRIGHT                 *      W I T H I N            *
         CLI   TAFTPRIN,C'<'            *         T H E               *
         BE    ALLRIGHT                 *       C O D E S             *
         CLI   TAFTPRIN,C' '            *       T H A T               *
         BE    ALLRIGHT                 *        A R E                *
         CLI   TAFTPRIN,C'?'            *     A L L O W E D           *
         BL    TESTTEST                 *      N A M E L Y            *
         CLI   TAFTPRIN,C'I'            * {-T H R O U G H-I,.,<,/,S+T.*
         BH    TESTTEST                 *******************************
         B     ALLRIGHT
BC020    CLC   TCARTAPE(2),CARRIAGE     SAME CARRIAGE TAPE.*
         BE    BC030                    YES.
BC060    GET   TABLE19                  NO -READ CARRIAGE TAPE TABLE.
         LR    7,1                         -READ CARRIAGE TAPE TABLE.
         MVC   CARRIAGE(2),CARRCODE
         CLC   TCARTAPE(2),CARRIAGE     REQUIRED CARRIAGE TAPE TABLE.*
         BNE   BC060                    NO.                        E.
         MVC   BC210+24(2),CARRDESC     INSERT C-TAPE NO IN MESSAGE.
BC210    WTOR  'M TAPE -  - REPLY -Y-',REPLY2,1,ECBA
         CLI   REPLY2,C'Y'              IS REPLY AFFIRMATIVE*
         BNE   BC210                    NO.
BC030    CLC   TSTATYPE(2),STATNERY     SAME STATIONERY.*
         BE    BC270                    YES.
BC070    LA    9,STATABLE
BC0705   MVC   STATNERY(2),0(9)
         CLC   TSTATYPE(2),STATNERY     REQUIRED STATIONERY.*
         BE    PUTIN
         LA    9,49(9)
         CLI   STATNERY,C'*'        END OF STATIONERY STORE.
         BNE   BC0705               NO - TRY AGAIN.
         MVC   WRONGUN+25(2),TSTATYPE INSERT WRONG CODE IN MESSAGE.
WRONGUN  WTO   'STATIONERY CODE -  - NOT IN TABLE 18'
         CLC   2(4,5),=C'TEST'      PARM = TEST
         BNE   ASKOP                NO - ASK OPERATOR.
         MVC   BC220+25(2),=C'01'   INSERT 1-PART CODE BY DEFAULT.
         B     BC220
ASKOP    WTOR  'DO YOU WANT TO CANCEL, Y OR N{',REPLY2,1,ECBA
         CLI   REPLY2,C'Y'          CANCEL*
         BNE   *+8                  NO - BRANCH.
         B     BA175+36             YES - BRANCH.
         CLI   REPLY2,C'N'
         BE    INSTRUCT
         MVC   CHASTISE+26(1),REPLY2
CHASTISE WTO   'WHAT DO YOU MEAN - -, YOU CLOD{'
         B     ASKOP
INSTRUCT MVC   MESSAGE+28(6),TREPNO INSERT REPORT NO IN MESSAGE.
MESSAGE  WTO   'NEXT REPORT OUT IS -      -. CONSULT OPERATIONS GUIDE'
         WTO   'TO DETERMINE WHAT XX SHOULD BE IN FOLLOWING MESSAGE.'
         MVC   BC220+25(2),=C'XX'
         B     BC220
PUTIN    MVC   BC220+25(2),8(9)         INSERT STAT. CODE IN MESSAGE.
BC220    WTOR  'M PAPER -  - REPLY -Y-',REPLY2,1,ECBA
         CLI   REPLY2,C'Y'              IS REPLY AFFIRMATIVE.*
         BNE   BC220                    N/.
BC250    MVI   PRINT,X'89'              SKIP TO
         PUT   PRINTER,PRINT            CHANNEL 1.
         MVI   PRINT,X'09'
         MVC   PRINT+1(67),ALGNMSG1     MOVE IN 1ST LINE OF
         PUT   PRINTER,PRINT            ALIGNMENT MESSAGE.
         MVI   PRINT+1,X'40'            CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTAREA.
         MVC   PRINT+1(65),ALGNMSG2     MOVE IN 2ND LINE OF
         MVI   PRINT,X'19'              ALIGNMENT MESSAGE.
         PUT   PRINTER,PRINT
         MVI   PRINT+1,X'40'            CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTAREA.
         WTOR  'ALIGNED OK?  Y  OR  N',REPLY2,1,ECBA
         CLI   REPLY2,C'Y'              ALIGNMENT O.K.*
         BNE   BC250                    NO.-REPEAT ALIGNMENT PHASE.
BC270    MVI   PRINT,X'89'              YES.-SKIP TO
         MVI   PRINT+1,X'40'           -CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTAREA,-
         PUT   PRINTER,PRINT            CHANNEL 1.
         MVC   REPNOSTO(6),TREPNO       STORE REPORT NO.
         B     BC050
EOFRTNA  MVC   PRINT+4(96),=C'NO FORMAT B CARD PRESENT AS INPUT. JOB HAX
               S TERMINATED. INSERT CORRECT CARD(S) AND RELOAD PROGRAM'
         B     BA175
EOFRTNB  MVI   PRINT+1,X'40'            CLEAR
         MVC   PRINT+2(131),PRINT+1     PRINTAREA.
         MVI   PRINT,X'89'              SKIP TO
         PUT   PRINTER,PRINT            CHANNEL 1.
BD010    CLOSE (TABLE18,,TABLE19,,CARDS,,PRINTAPE,,PRINTER)
         WTO   'GOOD END UTL003 - PRINT-TAPE UTILITY'
         L     13,SAVEAREA+4
         RETURN (14,12),RC=0
EOFRTND  MVC   WTOR4+23(2),TCARTAPE INSERT CARR.TAPE CODE IN MSG.
WTOR4    WTO   'CARRIAGE CODE *  * HAS NO TABLE RECORD.'
         WTO   ' *****  BAD END UTL003 - PRINT-TAPE UTILITY  *****'
         CLOSE (TABLE18,,TABLE19,,CARDS,,PRINTAPE,,PRINTER)
         L     13,SAVEAREA+4
         RETURN (14,12),RC=12
         LTORG
         DS    0F
STATABLE DS    CL4900                   STATIONERY CODE TABLE AREA.
         END   STARTA
