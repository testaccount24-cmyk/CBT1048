000100 ID DIVISION.
000200 PROGRAM-ID. T586P06.
000300 AUTHOR. P.S.GRANT FROM AN ADR USER  PROGRAM BY EARL RYAN.
000400 DATE-WRITTEN. JUNE 1973.
000500 DATE-COMPILED.
000600*REMARKS.    INDEX LISTING BY PROGRAMMER WITHIN TEAM.
000700*            PRODUCTION PROGRAMS ARE NOT PRINTED,
000800*            BUT THE COMLETE SORTED FILE IS AVAILABLE AS
000900*            AN ADDITIONAL OUTPUT.
001000*            A COMPLETE LISTING BY PROGRAMMER-FIELD ONLY
001100*            MAY BE ACHIEVED BY DUMMYING OUT BOTH TABLES.
001200*            ADDITIONAL INFORMATION SUPPLIED:-
001300*                1. INACTIVE PROGRAM FLAG. PERIOD FROM PARM.
001400*                2. SOURCE LANGUAGE FROM HISTORY CARDS.
001500*                3. ALIAS NAMES FROM HISTORY CARDS.
001600*                4. SYSTEM NAME FROM HISTORY CARDS. NOT PRINTED.
001700*            PARM DATA REQUIRED:- PARM='DDDFILE    '
001800*                WHERE DDD = NO. OF DAYS BEYOND WHICH A
001900*                            MODULE IS CONSIDERED INACTIVE.
002000*                AND FILE = NAME OF LIBRARYN.FILE TO BE LISTED.
002100*                           MUST BE PADDED TO 8 CHARS. BY
002200*                            USE OF THE APOSTROPHE AS ABOVE.
002300
002400*               SUBROUTINES USED - ADR FAIR ROUTINES
002500*                   MASTOPN - MASTMOD - MASTHST - MASTCLS.
002600*                   MONSANTO UT458 UNIVERSAL DATE ROUTINE.
002700
002800 ENVIRONMENT DIVISION.
002900 CONFIGURATION SECTION.
003000 SOURCE-COMPUTER. IBM-370.
003100 OBJECT-COMPUTER. IBM-370.
003200 SPECIAL-NAMES. C01 IS TO-TOP-OF-PAGE.
003300 INPUT-OUTPUT SECTION.
003400 FILE-CONTROL.
003500     SELECT INDEX-LISTING ASSIGN TO UT-S-SYSPRINT.
003600     SELECT INDEX-FILE ASSIGN TO UT-S-INDEXREC.
003700     SELECT SYSTEM-FILE ASSIGN TO UT-S-TABLT1.
003800     SELECT PGMR-FILE ASSIGN TO UT-S-TABLT2.
003900     SELECT SORTFIL ASSIGN TO UT-S-O9CA3X2.
004000 DATA DIVISION.
004100 FILE SECTION.
004200 FD  INDEX-LISTING BLOCK CONTAINS 0 RECORDS LABEL RECORD IS
004300       STANDARD RECORD CONTAINS 133 CHARACTERS RECORDING MODE IS
004400       F DATA RECORD IS PRINT-LINE.
004500 01  PRINT-LINE.
004600     05  FILLER          PIC X.
004700     05  P-INA-FLAG      PIC X.
004800     05  P-MODULE-NAME   PIC X(8).
004900     05  FILLER          PIC XX.
005000     05  P-PASSWORD      PIC X(4).
005100     05  FILLER          PIC XXX.
005200     05  P-ALIAS         PIC X(8).
005300     05  FILLER          PIC XXX.
005400     05  P-SLANG         PIC X(5).
005500     05  FILLER          PIC XXX.
005600     05  P-DESCRIPTION   PIC X(30).
005700     05  FILLER          PIC X.
005800     05  P-DATE-ADDED    PIC X(8).
005900     05  FILLER          PIC X(4).
006000     05  P-LAST-UPDATE   PIC X(13).
006100     05  FILLER          PIC X(4).
006200     05  P-VERS.
006300         07  P-VERS-MM   PIC XX.
006400         07  P-VERS-DD   PIC XX.
006500         07  P-VERS-TIME PIC X(4).
006600     05  FILLER          PIC XXX.
006700     05  P-RECORDS       PIC Z(5).
006800     05  FILLER          PIC X(4).
006900     05  P-SEQ-PARAM     PIC X(15).
007000 SD  SORTFIL RECORDING MODE IS F DATA RECORD IS SORTREC RECORD
007100     CONTAINS 135 CHARACTERS.
007200 01  SORTREC.
007300     05 MODULE-NAME     PIC X(8).
007400     05 MODULE-INPUT-AREA.
007500       10 M-PASSWORD      PIC X(4).
007600       10 DATE-ADDED.
007700         15 MONTH-ADDED PIC XX.
007800         15 DAY-ADDED   PIC XX.
007900         15 YEAR-ADDED  PIC XX.
008000       10 LAST-UPDATED.
008100         15 MONTH-UPDATED PIC XX.
008200         15 DAY-UPDATED PIC XX.
008300         15 YEAR-UPDATED PIC XX.
008400         15 TIME-UPDATED PIC X(4).
008500       10 NUMBER-OF-CARDS PIC S9(5) USAGE COMP-3.
008600       10 PROGRAMMER.
008700         15 PGMR         PIC X(10).
008800       10  FILLER        PIC XX.
008900       10  TEAM          PIC XXX.
009000       10 PROC-PARM     PIC X(8).
009100       10 DESCRIPTION   PIC X(30).
009200       10 SEQ-COL-X     PIC X.
009300       10 SEQ-LENGTH-X  PIC X.
009400       10 SEQ-INCREMENT PIC 9(4) USAGE COMPUTATIONAL.
009500       10 SEQ-START-VAL PIC 9(4) USAGE COMPUTATIONAL.
009600       10  ACCESSES      PIC 9(4) COMP.
009700       10  UPDATES       PIC 9(4) COMP.
009800       10  EXTENTS       PIC X.
009900       10  BLOCKS        PIC X.
010000       10  ALIAS1        PIC X(8).
010100       10  ALIAS2        PIC X(8).
010200       10  SLANG         PIC X(5).
010300       10  SYSTEM        PIC X(8).
010400       10  SORT-NAME     PIC X(10).
010500 FD  INDEX-FILE
010600     RECORDING MODE IS F
010700     LABEL RECORDS ARE STANDARD
010800     RECORD CONTAINS 125 CHARACTERS
010900     BLOCK CONTAINS 0 RECORDS
011000     DATA RECORD IS INDEXREC.
011100 01  INDEXREC            PIC X(125).
011200 FD  SYSTEM-FILE
011300         RECORDING MODE IS F
011400         LABEL RECORDS ARE STANDARD
011500         RECORD CONTAINS 80 CHARACTERS
011600         BLOCK CONTAINS 0 RECORDS
011700         DATA RECORD IS SYSTEM-REC.
011800 01  SYSTEM-REC.
011900     03  SR-ID           PIC X(4).
012000     03  SR-SYSTEM       PIC X(8).
012100     03  FILLER          PIC X(68).
012200 FD  PGMR-FILE
012300         RECORDING MODE IS F
012400         LABEL RECORDS ARE STANDARD
012500         RECORD CONTAINS 80 CHARACTERS
012600         BLOCK CONTAINS 0 RECORDS
012700         DATA RECORD IS PGMR-REC.
012800 01  PGMR-REC.
012900     03  PR-ID           PIC X(4).
013000     03  PR-DATA         PIC X(32).
013100     03  FILLER          PIC X(44).
013200 WORKING-STORAGE SECTION.
013300 77  PT-LIMIT            PIC S999 COMP VALUE +200.
013400 77  ST-LIMIT            PIC S999 COMP VALUE +200.
013500 77  BLOCK-COUNT         PIC S9(5) COMP VALUE +0.
013600 77  RETCD               PIC 9.
013700 77  TPHDR               PIC X.
013800 77  FIRST-TIME          PIC X VALUE '1'.
013900 77  LAST-TEAM           PIC XXX VALUE SPACES.
014000 77  LAST-PGMR           PIC X(10) VALUE SPACES.
014100 77  LINE-CTR            PIC 999 COMP-3 VALUE  0.
014200 77  PAGE-CTR            PIC 999 COMP-3 VALUE  0.
014300 77  MODULE-COUNT        PIC 999 COMP-3 VALUE  0.
014400 77  HOLD-LINE           PIC X(133).
014500 77  UT458               PIC X(8) VALUE 'UT458'.
014600 01  MESSAGE1.
014700     05 FILLER          PIC X(29) VALUE
014800         'OPEN FAILED - RETURN CODE IS '.
014900     05 OPENCD          PIC X VALUE ' '.
015000     05 FILLER          PIC X(15) VALUE ' - MODULE NAME '.
015100     05 NAME1           PIC X(8).
015200 01  MESSAGE2.
015300     05 FILLER          PIC X(28) VALUE
015400         'GET FAILED - RETURN CODE IS '.
015500     05 RETCD-G         PIC X VALUE SPACE.
015600     05 FILLER          PIC X(15) VALUE ' - MODULE NAME '.
015700     05 NAME2           PIC X(8).
015800 01  MESSAGE3.
015900     03  FILLER          PIC X(7) VALUE ' TABLE '.
016000     03  MSG3-TABNO      PIC XXX.
016100     03  FILLER          PIC X(42) VALUE
016200         ' OVERFLOWED. EXPAND AND RE-COMPILE T586P06'.
016300 01  HEADER1.
016400     05  FILLER          PIC X(30) VALUE
016500         ' T586P06             LIBRARYN.'.
016600     05  HDR-FILE        PIC X(8).
016700     05  FILLER          PIC X(12) VALUE SPACES.
016800     05  FILLER          PIC X(27) VALUE
016900         'INDEX LISTING BY PROGRAMMER'.
017000     05  FILLER          PIC X(10) VALUE SPACES.
017100     05  FILLER          PIC X(5) VALUE 'DATE '.
017200     05 HDR-DATE         PIC X(9).
017300     05  FILLER          PIC X(8) VALUE '   TIME '.
017400     05  HDR-TIME        PIC X(4).
017500     05  FILLER          PIC X(11) VALUE SPACES.
017600     05 FILLER          PIC X(5) VALUE 'PAGE '.
017700     05 PAGEID          PIC XXX VALUE SPACES.
017800     05 FILLER          PIC X VALUE SPACE.
017900 01  HEADER1A.
018000     05  FILLER          PIC X(46) VALUE SPACES.
018100     05 FILLER           PIC X(7) VALUE 'TEAM - '.
018200     05 HDR-TEAM         PIC XXX.
018300     05 FILLER           PIC X(18) VALUE '     PROGRAMMER - '.
018400     05  PGMRID          PIC X(10).
018500     05  FILLER          PIC X(49) VALUE SPACES.
018600 01  HEADER2.
018700     05  FILLER          PIC X VALUE SPACES.
018800     05  FILLER          PIC X(44) VALUE
018900         ' MODULE    PSWD   ALIAS     S.LANG     MODUL'.
019000     05  FILLER          PIC X(43) VALUE
019100          'E DESCRIPTION            ADDED       LAST U'.
019200 05  FILLER              PIC X(44) VALUE
019300         'PDATE      VERS=    RECORDS   SEQUENCE PARAM'.
019400     05  FILLER          PIC X VALUE SPACES.
019500 05  FILLER              PIC X(44) VALUE
019600         'PDATE      VERS=    RECORDS   SEQUENCE PARAM'.
019700 01  TRAILER.
019800     05 FILLER          PIC X(26) VALUE
019900         '  TOTAL NUMBER OF MODULES '.
020000     05 TRL-NO          PIC ZZZ.
020100     05  FILLER          PIC X(6) VALUE '    - '.
020200     05  TRL-BLKS        PIC Z(5).
020300     05  FILLER          PIC X(7) VALUE ' BLOCKS'.
020400 01  HST-AREA.
020500     03  FILLER          PIC X(5) VALUE '0'.
020600     03  HST-ID          PIC X(6).
020700     03  FILLER          PIC X.
020800     03  HST-SYS.
020900         05  FILLER      PIC X.
021000         05 HST-SYSTEM   PIC X(8).
021100     03  HST-AL REDEFINES HST-SYS.
021200         05  HST-ALIAS   PIC X(8).
021300         05  FILLER      PIC X.
021400     03  HST-SL REDEFINES HST-AL.
021500         05  HST-SLANG   PIC X(5).
021600         05  FILLER      PIC X(4).
021700     03 FILLER           PIC X(67).
021800 01  DATE-AREA  REDEFINES HST-AREA.
021900     03  D-ACTION-CODE   PIC X.
022000     03  DUS-MMDD        PIC X(4).
022100     03  DUS-YEAR        PIC XX.
022200     03  FILLER          PIC X(22).
022300     03  DATE-JULIAN.
022400         05  DJ-YEARS    PIC 99.
022500         05  DJ-DAYS     PIC 999.
022600     03  D-REPORT        PIC X(9).
022700     03  FILLER          PIC X(36).
022800     03  D-TIME          PIC X(4).
022900     03  FILLER          PIC XX.
023000     03  D-RTCODE        PIC X.
023100 01  TEST-DATE.
023200     03  TD-YEAR         PIC XX.
023300     03  TD-MMDD         PIC XXXX.
023400 01  RECORD-DATE.
023500     03  RD-YEAR         PIC XX.
023600     03  RD-MONTH        PIC XX.
023700     03  RD-DAY          PIC XX.
023800 01  BUILD-P-DATE.
023900     05 BUILD-DATE.
024000       10 MONTH         PIC XX.
024100       10 FILLER        PIC X VALUE '/'.
024200       10 D-A-Y         PIC XX.
024300       10 FILLER        PIC X VALUE '/'.
024400       10 YEAR          PIC XX.
024500     05 FILLER          PIC X VALUE SPACE.
024600     05 BUILD-TIME      PIC X(4).
024700 01  BUILD-SEQ-PARAM.
024800     05 B-SEQ-START     PIC XX.
024900     05 FILLER          PIC X VALUE IS '/'.
025000     05 B-SEQ-LENGTH    PIC XX.
025100     05 FILLER          PIC X VALUE IS '/'.
025200     05 B-SEQ-INCREMENT PIC X(4).
025300     05 FILLER          PIC X VALUE IS '/'.
025400     05 B-SEQ-START-VAL PIC X(4).
025500 01  SEQ-B              PIC 99 USAGE COMPUTATIONAL.
025600 01  SEQ-FIELD          REDEFINES SEQ-B.
025700     05 FILLER          PIC X.
025800     05 SEQ-X           PIC X.
025900 01  SYSTEM-TABLE.
026000     03  ST-ENTRY OCCURS 200 TIMES DEPENDING ON ST-LIMIT
026100         INDEXED BY ST-SUB.
026200         05  ST-SYSTEM   PIC X(8).
026300 01  PGMR-TABLE.
026400     03  PT-ENTRY OCCURS 200 TIMES DEPENDING ON PT-LIMIT
026500         INDEXED BY PT-SUB.
026600         05  PT-NAME6    PIC X(6).
026700         05  PT-NAME10   PIC X(10).
026800         05  PT-NAME3    PIC XXX.
026900         05  PT-TEAM     PIC XXX.
027000         05  PT-SRTNAME  PIC X(10).
027100 LINKAGE SECTION.
027200 01  PARM-DATA.
027300     03  PARM-LNGTH      PIC 999 COMP.
027400     03  PARM-DAYS       PIC 999.
027500     03  PARM-FILE       PIC X(8).
027600 PROCEDURE DIVISION USING PARM-DATA.
027700     MOVE PARM-FILE TO HDR-FILE.
027800     CALL 'DYNAMO' USING UT458, DATE-AREA.
027900     MOVE D-REPORT TO HDR-DATE.
028000     MOVE D-TIME TO HDR-TIME.
028100     IF DJ-DAYS NOT > PARM-DAYS
028200         ADD 365 TO DJ-DAYS
028300         SUBTRACT 1 FROM DJ-YEARS.
028400     SUBTRACT PARM-DAYS FROM DJ-DAYS.
028500     MOVE '6' TO D-ACTION-CODE.
028600     CALL 'DYNAMO' USING UT458, DATE-AREA.
028700     MOVE DUS-YEAR TO TD-YEAR.
028800     MOVE DUS-MMDD TO TD-MMDD.
028900     OPEN OUTPUT INDEX-LISTING.
029000     OPEN INPUT SYSTEM-FILE.
029100     SET ST-SUB TO 1.
029200 BUILD-SYSTAB.
029300     READ SYSTEM-FILE AT END GO TO SYSTAB-COMPLETE.
029400     IF SR-ID NOT = 'LT1 '
029500         GO TO BUILD-SYSTAB.
029600     IF ST-SUB > ST-LIMIT
029700         GO TO ST-OVERFLOW.
029800     MOVE SR-SYSTEM TO ST-SYSTEM (ST-SUB).
029900     SET ST-SUB UP BY 1.
030000     GO TO BUILD-SYSTAB.
030100 ST-OVERFLOW.
030200     MOVE 'LT1 ' TO MSG3-TABNO.
030300 TAB-OVERFLOW.
030400     MOVE SPACES TO PRINT-LINE.
030500     MOVE MESSAGE3 TO PRINT-LINE.
030600     WRITE PRINT-LINE AFTER ADVANCING TO-TOP-OF-PAGE.
030700     MOVE 16 TO RETURN-CODE.
030800     CLOSE INDEX-LISTING.
030900     STOP RUN.
031000 SYSTAB-COMPLETE.
031100     SET ST-SUB DOWN BY 1.
031200     SET ST-LIMIT TO ST-SUB.
031300     CLOSE SYSTEM-FILE.
031400     OPEN INPUT PGMR-FILE.
031500     SET PT-SUB TO 1.
031600 BUILD-PGMRTAB.
031700     READ PGMR-FILE AT END GO TO PGMRTAB-COMPLETE.
031800     IF PR-ID NOT = 'LT2 '
031900         GO TO BUILD-PGMRTAB.
032000     IF PT-SUB > PT-LIMIT
032100         GO TO PT-OVERFLOW.
032200     MOVE PR-DATA TO  PT-ENTRY (PT-SUB).
032300     SET PT-SUB UP BY 1.
032400     GO TO BUILD-PGMRTAB.
032500 PT-OVERFLOW.
032600     MOVE 'LT2' TO MSG3-TABNO.
032700     GO TO TAB-OVERFLOW.
032800 PGMRTAB-COMPLETE.
032900     SET PT-SUB DOWN BY 1.
033000     SET PT-LIMIT TO PT-SUB.
033100     CLOSE PGMR-FILE.
033200 OPEN-REMAINING-DATASETS.
033300     CALL 'MASTOPN' USING TPHDR, RETCD.
033400     OPEN OUTPUT INDEX-FILE.
033500     MOVE +2000 TO SORT-FILE-SIZE.
033600 0001-SORT-PROC.
033700     SORT SORTFIL ON ASCENDING KEY TEAM,
033800                  ON ASCENDING KEY SORT-NAME,
033900                  ON ASCENDING KEY MODULE-NAME,
034000     INPUT PROCEDURE 0002-BUILD-SORTREC
034100     OUTPUT PROCEDURE 0011-LIST-INDICIES.
034200     STOP RUN.
034300 DUMMY-CALL-OF-FAIR-FOR-LKED.
034400     CALL 'FAIR' USING RETCD.
034500 0002-BUILD-SORTREC SECTION.
034600 0003-OPEN-LIST-FILE.
034700     MOVE SPACES TO PRINT-LINE.
034800     MOVE SPACES TO HDR-TEAM, PGMRID.
034900     MOVE 1 TO PAGEID.
035000     WRITE PRINT-LINE FROM HEADER1 AFTER ADVANCING
035100         TO-TOP-OF-PAGE.
035200     WRITE PRINT-LINE FROM HEADER1A AFTER ADVANCING 2 LINES.
035300     MOVE 5 TO LINE-CTR.
035400     WRITE PRINT-LINE FROM HEADER2 AFTER ADVANCING 2 LINES.
035500     MOVE SPACES TO PRINT-LINE.
035600     MOVE 0 TO SEQ-B.
035700 0004-OPEN-MASTER.
035800*    NOTE: THE ACTUAL OPEN OF THE MASTER DATASET HAS BEEN
035900*          MOVED TO PARA OPEN-REMAINING-DATASETS BEFORE THE
036000*          SORT IS INVOKED: THIS ALLOWS SUFFICIENT SPACE TO
036100*          BE FOUND FOR BUFFERS IN 52K.
036200 0005-CHECK-OPEN.
036300     IF RETCD EQUAL TO ZERO GO TO 0007-GET-A-MODULE.
036400     MOVE RETCD TO OPENCD.
036500     MOVE MODULE-NAME TO NAME1.
036600     MOVE SPACES TO PRINT-LINE.
036700     MOVE MESSAGE1 TO PRINT-LINE.
036800     WRITE PRINT-LINE AFTER ADVANCING TO-TOP-OF-PAGE.
036900 0006-SET-RETURN-CODE.
037000     MOVE 16 TO RETURN-CODE.
037100     GO TO 0009-CLOSE-MASTER.
037200 0007-GET-A-MODULE.
037300     MOVE SPACES TO MODULE-NAME.
037400     CALL 'MASTMOD' USING MODULE-NAME, RETCD, MODULE-INPUT-AREA.
037500     IF RETCD = ZERO GO TO 0072-GET-HSTS.
037600     IF RETCD EQUAL TO 1 GO TO 0009-CLOSE-MASTER.
037700     MOVE RETCD TO RETCD-G MOVE MODULE-NAME TO NAME2.
037800     MOVE SPACES TO PRINT-LINE.
037900     MOVE MESSAGE2 TO PRINT-LINE.
038000     WRITE PRINT-LINE AFTER ADVANCING TO-TOP-OF-PAGE.
038100     GO TO 0006-SET-RETURN-CODE.
038200 0072-GET-HSTS.
038300     MOVE SPACES TO ALIAS1, ALIAS2, SYSTEM, SLANG.
038400 0072-GET-HSTS-LOOP.
038500     CALL 'MASTHST' USING HST-AREA, RETCD.
038600     IF RETCD NOT = ZERO GO TO 0074-PGMR-LOOKUP.
038700     IF HST-ID = 'SYSTEM'
038800          MOVE HST-SYSTEM TO SYSTEM.
038900     IF HST-ID = 'SLANG'
039000         MOVE HST-SLANG TO SLANG.
039100     IF HST-ID = 'ALIAS '
039200         AND ALIAS1 = SPACES
039300         MOVE HST-ALIAS TO ALIAS1
039400         GO TO 0072-GET-HSTS-LOOP.
039500     IF HST-ID = 'ALIAS '
039600         AND ALIAS1 NOT = SPACES
039700         MOVE HST-ALIAS TO ALIAS2.
039800     GO TO 0072-GET-HSTS-LOOP.
039900 0074-PGMR-LOOKUP.
040000     SET PT-SUB TO 1.
040100     SEARCH PT-ENTRY
040200         AT END GO TO 0076-SYSTEM-LOOKUP,
040300         WHEN PGMR = PT-NAME3 (PT-SUB) NEXT SENTENCE,
040400         WHEN PGMR = PT-NAME10 (PT-SUB) NEXT SENTENCE.
040500     MOVE PT-TEAM (PT-SUB) TO TEAM.
040600     MOVE PT-SRTNAME (PT-SUB) TO SORT-NAME.
040700     GO TO 0008-RELEASE-PROC.
040800 0076-SYSTEM-LOOKUP.
040900     SET ST-SUB TO 1.
041000     SEARCH ST-ENTRY,
041100         AT END GO TO 0078-NO-MATCH,
041200         WHEN PGMR = ST-SYSTEM (ST-SUB) NEXT SENTENCE.
041300     MOVE 'PRD' TO TEAM.
041400     MOVE PGMR TO SORT-NAME.
041500     GO TO 0008-RELEASE-PROC.
041600 0078-NO-MATCH.
041700     MOVE PGMR TO SORT-NAME.
041800     MOVE SPACES TO TEAM.
041900 0008-RELEASE-PROC.
042000     RELEASE SORTREC.
042100     GO TO 0007-GET-A-MODULE.
042200 0009-CLOSE-MASTER.
042300     CALL 'MASTCLS'.
042400 0010-EXIT-CLOSE.
042500     EXIT.
042600 0011-LIST-INDICIES SECTION.
042700 0012-CHECK-SORT-CODE.
042800     IF SORT-RETURN = 16 MOVE 16 TO RETURN-CODE GO TO 0022-EOJ.
042900 0013-RETURN-A-RECORD.
043000     RETURN SORTFIL AT END GO TO 0021-SET-GOOD-RETURN.
043100 0014-CHECK-PGMR-ID.
043200     IF TEAM = 'PRD' GO TO WRITE-INDEX-RECORD.
043300     IF FIRST-TIME = '1'
043400         MOVE TEAM TO LAST-TEAM
043500         MOVE SORT-NAME TO LAST-PGMR
043600         MOVE '0' TO FIRST-TIME.
043700     IF TEAM = LAST-TEAM
043800         AND SORT-NAME = LAST-PGMR,
043900         GO TO 0015-BUILD-PRINT-LINE.
044000     MOVE SPACES TO PRINT-LINE.
044100     PERFORM 0020-PUT-TRAILER.
044200     MOVE PGMR TO PGMRID.
044300     MOVE SORT-NAME TO LAST-PGMR.
044400     IF TEAM = LAST-TEAM GO TO NO-TEAM-CHANGE.
044500     MOVE TEAM TO LAST-TEAM, HDR-TEAM.
044600     MOVE 0 TO PAGE-CTR.
044700 NO-TEAM-CHANGE.
044800     ADD 1 TO PAGE-CTR.
044900     PERFORM 0019-PUT-HEADERS.
045000 0015-BUILD-PRINT-LINE.
045100     MOVE SPACES TO P-INA-FLAG.
045200     MOVE ALIAS1 TO P-ALIAS.
045300     MOVE SLANG TO P-SLANG.
045400     MOVE MODULE-NAME TO P-MODULE-NAME.
045500     MOVE M-PASSWORD TO P-PASSWORD.
045600     MOVE DESCRIPTION TO P-DESCRIPTION.
045700     MOVE MONTH-ADDED TO MONTH.
045800     MOVE DAY-ADDED TO D-A-Y.
045900     MOVE YEAR-ADDED TO YEAR.
046000     MOVE BUILD-DATE TO P-DATE-ADDED.
046100     MOVE MONTH-UPDATED TO MONTH.
046200     MOVE DAY-UPDATED TO D-A-Y.
046300     MOVE YEAR-UPDATED TO YEAR.
046400     IF BUILD-DATE = '00/00/00' GO TO 0016-BUILD-CONSTANT.
046500     MOVE TIME-UPDATED TO BUILD-TIME.
046600     MOVE BUILD-P-DATE TO P-LAST-UPDATE.
046700     MOVE YEAR-UPDATED TO RD-YEAR.
046800     MOVE MONTH-UPDATED TO P-VERS-MM, RD-MONTH.
046900     MOVE DAY-UPDATED TO P-VERS-DD, RD-DAY.
047000     MOVE TIME-UPDATED TO P-VERS-TIME.
047100     GO TO 0017-RESUME-BUILD.
047200 0016-BUILD-CONSTANT.
047300     MOVE ' NOT UPDATED ' TO P-LAST-UPDATE.
047400     MOVE ZERO TO P-VERS-MM P-VERS-DD.
047500     MOVE SPACES TO P-VERS-TIME.
047600     MOVE DAY-ADDED TO RD-DAY.
047700     MOVE MONTH-ADDED TO RD-MONTH.
047800     MOVE YEAR-ADDED TO RD-YEAR.
047900 0017-RESUME-BUILD.
048000     MOVE NUMBER-OF-CARDS TO P-RECORDS.
048100     IF RECORD-DATE < TEST-DATE
048200         MOVE '*' TO P-INA-FLAG.
048300     MOVE SEQ-COL-X TO SEQ-X.
048400     MOVE SEQ-B TO B-SEQ-START.
048500     MOVE SEQ-LENGTH-X TO SEQ-X.
048600     MOVE SEQ-B TO B-SEQ-LENGTH.
048700     MOVE SEQ-INCREMENT TO B-SEQ-INCREMENT.
048800     MOVE SEQ-START-VAL TO B-SEQ-START-VAL.
048900     MOVE BUILD-SEQ-PARAM TO P-SEQ-PARAM.
049000     MOVE BLOCKS TO SEQ-X.
049100     ADD SEQ-B TO BLOCK-COUNT.
049200 0018-WRITE-PRINT-LINE.
049300     IF LINE-CTR > 58 ADD 1 TO PAGE-CTR,
049400       MOVE PRINT-LINE TO HOLD-LINE, PERFORM 0019-PUT-HEADERS
049500       MOVE HOLD-LINE TO PRINT-LINE.
049600     WRITE PRINT-LINE AFTER ADVANCING 2 LINES.
049700     ADD 2 TO LINE-CTR.
049800     IF ALIAS2 NOT = SPACES,
049900         MOVE SPACES TO PRINT-LINE,
050000         MOVE ALIAS2 TO P-ALIAS,
050100         WRITE PRINT-LINE AFTER ADVANCING 1 LINES,
050200         ADD 1 TO LINE-CTR.
050300     ADD 1 TO MODULE-COUNT.
050400 WRITE-INDEX-RECORD.
050500     MOVE SORTREC TO INDEXREC.
050600     WRITE INDEXREC.
050700     GO TO 0013-RETURN-A-RECORD.
050800 0019-PUT-HEADERS.
050900     MOVE PAGE-CTR TO PAGEID.
051000     WRITE PRINT-LINE FROM HEADER1 AFTER ADVANCING
051100       TO-TOP-OF-PAGE.
051200     WRITE PRINT-LINE FROM HEADER1A AFTER ADVANCING 2 LINES.
051300     MOVE 5 TO LINE-CTR.
051400     WRITE PRINT-LINE FROM HEADER2 AFTER ADVANCING 2 LINES.
051500     MOVE SPACES TO PRINT-LINE.
051600 0020-PUT-TRAILER.
051700     MOVE MODULE-COUNT TO TRL-NO.
051800     MOVE 0 TO MODULE-COUNT.
051900     MOVE BLOCK-COUNT TO TRL-BLKS.
052000     MOVE 0 TO BLOCK-COUNT.
052100     MOVE TRAILER TO PRINT-LINE.
052200     WRITE PRINT-LINE AFTER ADVANCING 2 LINES.
052300 0021-SET-GOOD-RETURN.
052400     MOVE 0 TO RETURN-CODE.
052500 0022-EOJ.
052600     PERFORM 0020-PUT-TRAILER.
052700     CLOSE INDEX-LISTING.
052800     CLOSE INDEX-FILE.
052900 0023-EXIT-EOJ.
053000     EXIT.
