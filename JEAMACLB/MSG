         MACRO
&LABEL   MSG   &TXT,&RPLY,&START=0000,&INTVL=0100,&TIME=,&CMND=NO,     X
               &DSECT=NO,&PGM=NO,&PARM=,&AUTOR=NO,&RPYID=,&WAIT=15
         GBLA  &RPLYNO             USED TO GENERATE ENTRY NAMES
         GBLA  &ENTYNO             USED TO GENERATE ENTRY NAMES
         GBLB  &STRTSW
         LCLA  &FLAG               WORK REGISTER FOR FLAG VALUE
         LCLA  &RPYLNTH            LENGTH OF EXPECTED REPLY
         AIF   ('&DSECT' NE 'NO').DSECT
         AIF   (&STRTSW EQ 1).STRTDN
         LCLC  &WORKC
T115P510 CSECT
.*       DC    A(IHB0)             ADDRESS OF COMMAND TABLE
.*       DC    A(JHB0)             ADDRESS OF REPLY TABLE
&STRTSW  SETB  1
.STRTDN  ANOP
         AIF   ('&AUTOR' NE 'NO').REPLYS
IHB&ENTYNO DS  0F                  START OF THE TABLE ENTRY
&ENTYNO  SETA  &ENTYNO+1           INCREMENT ENTRY NUMBER COUNTER
         DC    A(IHB&ENTYNO)       ADDRESS OF NEXT ENTRY IN TABLE
         AIF   (T'&TIME EQ 'O').NOTTOD NORMAL INTERVAL TYPE
&FLAG    SETA  &FLAG+128           SET TOD INDICATOR IN FLAG
         DC    XL4'&TIME.000C'     TIME OF EXECUTION OF THIS ITEM
         DC    XL4'0000000C'       NO INCREMENT TO BE ADDED
         AGO   .SETFLG
.NOTTOD  ANOP
         DC    XL4'&START.000C'    TIME OF FIRST EXECUTION
         DC    XL4'&INTVL.000C'    INTERVAL BETWEEN EXECUTIONS
.SETFLG  ANOP
         AIF   ('&CMND' NE 'NO').ISACMND
&FLAG    SETA  &FLAG+64            SET NORMAL MESSAGE INDICATOR
.ISACMND ANOP
         AIF   (T'&RPLY EQ 'O').NORPLY
&FLAG    SETA  &FLAG+32            SET REPLY EXPECTED INDICATOR
.NORPLY  ANOP
         AIF   ('&PGM' EQ 'NO').DEFFLG
&FLAG    SETA  &FLAG+16            SET PROGRAM INDICATOR
.DEFFLG  ANOP
         DC    AL1(&FLAG)          CONTROL ENTRY FLAG
         DS    XL3                 PADDING
         AIF   ('&PGM' NE 'NO').ATTACH
         AIF   (T'&RPLY NE 'O').WTOR
         DS    4F                  PADDING
         WTO   &TXT,MF=L           BUILD WTO PARM LIST
         MEXIT
.WTOR    ANOP
         DC    A(IHB&SYSNDX.E)     ADDRESS OF WAIT ECB
         DC    A(IHB&SYSNDX)       ADDRESS OF REPLY RECEIVED AREA
         DC    A(IHB&SYSNDX.X)     ADDRESS OF REPLY EXPECTED AREA
&RPYLNTH SETA  K'&RPLY-2           PICK UP LENGTH OF EXPECTED REPLY
         DC    F'&RPYLNTH'         LENGTH OF REPLY AREA
         WTOR  &TXT,IHB&SYSNDX,&RPYLNTH,IHB&SYSNDX.E,MF=L
IHB&SYSNDX.E DC F'0'               WAIT ECB FOR REPLY
IHB&SYSNDX DC C&RPLY
IHB&SYSNDX.X DC C&RPLY
         MEXIT
.ATTACH  ANOP
         DC    A(IHB&SYSNDX.P)     ADDRESS OF PARM FIELD
         DS    3F                  PADDING
         ATTACH EP=&TXT,SF=L
IHB&SYSNDX.P DS 0H
         AIF   ('&PARM' EQ '').ZERO
         DC    AL2(L'IHB&SYSNDX.Q)
IHB&SYSNDX.Q DC C&PARM
         MEXIT
.ZERO    DC    AL2(0)
         MEXIT
.REPLYS  ANOP
JHB&RPLYNO DS  0F                  START OF THE TABLE ENTRY
&RPLYNO  SETA  &RPLYNO+1           INCREMENT ENTRY NUMBER COUNTER
         DC    A(JHB&RPLYNO)       ADDRESS OF NEXT ENTRY IN TABLE
         DC    A(&WAIT*100)        TIME TO WAIT BEFORE REPLYING
         DC    CL8'&RPYID'         IDENTIFIER OF THE MESSAGE
&WORKC   SETC  'R 99,'.''''.'&TXT'.''''.' '
         WTO   '&WORKC',MF=L         BUILD WTO PARM LIST
         MEXIT
.DSECT   ANOP
TABENTRY DSECT ,                   MESSAGE TABLE ENTRY DSECT
TABCHAIN DS    F                   ADDRESS OF NEXT ENTRY ON CHAIN
NEXTTIME DS    F                   NEXT PROJECTED TIME OF EXECUTION
NEXTITVL DS    F                   INTERVAL BETWEEN EXECUTIONS
TABFLG   DS    X                   FLAG FOR CONTROL LOGIC
FLGTOD   EQU   X'80'               'NEXTTIME' IS A REAL TOD VALUE
FLGMSG   EQU   X'40'               THIS IS NOT A COMMAND TYPE ENTRY
FLGRPLY  EQU   X'20'               A REPLY IS TO BE EXPECTED FOR MSG
FLGPGM   EQU   X'10'               A PROGRAM IS TO BE EXECUTED
         DS    XL3                 PADDING
ECBADDR  DS    F                   ADDRESS OF WTOR ECB IF PRESENT
PGMPARM  EQU   ECBADDR             ADDRESS OF PROGRAM PARMS
RPLYRCVD DS    F                   ADDRESS OF REPLY RECEIVED
RPLYEXPD DS    F                   ADDRESS OF REPLY EXPECTED
RPLYLNTH DS    F                   LENGTH OF REPLY TEXT
TXTAREA  DS    0F                  START OF WTO/WTOR PARAMETER LIST
         SPACE 2
         ORG   TABENTRY
RPYENTRY DS    0F                  REPLY PROCESSOR ENTRY
RPYCHAIN DS    F                   REPLY QUEUE CHAIN FIELD
RPYWAIT  DS    F                   WAIT TIME IN 1/100THS OF A SECOND
IDTEXT   DS    CL8                 IDENTIFIER FOR THIS MESSAGE
RPYTEXT  DS    CL128               REPLY COMMAND FORMAT AREA
         MEND
