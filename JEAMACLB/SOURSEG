         MACRO
         SOURSEG &PARM,&OPTION
         GBLA  &S,&SL,&PNBR,&EXTDBN
         GBLA  &SLDP(255),&SD(300),&FT1(1000)                  @BA38747
         GBLB  &QUITB,&ERROR,&GB1(16)
         GBLC  &ACC,&DBN,&PLIST(100)
         GBLC  &SS(300),&SN(500),&FN1(1000),&FSF1(1000)
         LCLA  &A,&AAA,&B,&C,&E,&N
         LCLA  &X,&Y,&OFF(5)
         LCLB  &BUG,&DATA,&KEY,&FL(5)
         LCLC  &D
.*
.*
.*
         AIF   ('&OPTION' NE 'ADD').G
&N       SETA  0-2
         AIF   (&SL+N'&PARM LE 300).A
         MNOTE 8,'SOUR100 - SOURCE SEGMENT TABLE LIMIT EXCEEDED. '
&GB1(16) SETB  1
&SL      SETA  &SL-N'&PARM
.A       EXPARMS PARM=&PARM,NBR=3       EXTRACT SOURCE SEGMENT VALUES
&GB1(16) SETB  (&GB1(16) OR &ERROR)
         AIF   (NOT &ERROR).B
         MEXIT
.B       ANOP
&N       SETA  &N+3           VALIDATE SOURCE SEGMENT OPERAND VALUES.
&SL      SETA  &SL+1          BUILT TABLES &SD AND &SS CONTAINING
&SS(&SL) SETC  '&PLIST(&N)'   NAME AND RELATED VALUES.
&D       SETC  '&DBN'
&KEY     SETB  ('&PLIST(&N+1)' EQ 'KEY' OR '&PLIST(&N+1)' EQ 'K')
&DATA    SETB  ('&PLIST(&N+1)' EQ 'DATA' OR '&PLIST(&N+1)' EQ 'D')
&DATA    SETB  (&DATA OR '&PLIST(&N+1)' EQ '')
         AIF   ('&SS(&SL)' NE '').C
         MNOTE 8,'SOUR110 - SOURCE SEGMENT NAME IS OMITTED. '
&GB1(16) SETB  1
.C       AIF   (&KEY OR &DATA).D
         MNOTE 8,'SOUR120 - SOURCE SEGMENT DATA OPTION IS INVALID. '
         MNOTE *,'          OPTION=&PLIST(&N+1)..'
&GB1(16) SETB  1
.D       AIF   ('&ACC' EQ 'LOGICAL' AND ('&PLIST(&N+2)' EQ '' OR '&PLIS,
               T(&N+2)' EQ '&DBN')).E
         AIF   ('&PLIST(&N+2)' EQ '').F
&D       SETC  '&PLIST(&N+2)'
         ALPHA FIELD=&D
&GB1(16) SETB  (&GB1(16) OR &QUITB)
         AIF   (NOT &QUITB).F
.E       MNOTE 8,'SOUR130 - SOURCE SEGMENT DATABASE IS INVALID - &D..'
&GB1(16) SETB  1
.F       EXTDBD &D
&SD(&SL) SETA  &S*65536+&KEY*32768+&DATA*8192+&EXTDBN
         AIF   (&PNBR*3 GT &N+2).B
         MEXIT
.*
.*
.*
.G       AIF   ('&OPTION' NE 'FIND').J
&N       SETA  &N+1
         AIF   (&SD(&N)/65536 NE &PARM).G    FIND 1ST SOURCE ENTRY
&X       SETA  &N                            FOR ENTERED SEGMENT.
.H       ANOP
&N       SETA  &N+1
         AIF   (&N GT &SL).I                 FIND LAST ENTRY
         AIF   (&SD(&N)/65536 EQ &PARM).H    FOR ENTERED SEGMENT.
.I       DC    AL2(&N-&X,@$#&X-SORTAB)  NBR SS, OFFSET TO 1ST ENTRY.
         MEXIT
.*
.*
.*
.J       AIF   ('&OPTION' EQ 'LIST').K
         MEXIT
.K       SPACE 3
SORTAB   DS    0F
&N       SETA  1
.L       AIF   (&N GT &SL).P
         AIF   (&SD(&N)/65536/16384 NE 1).N
&N       SETA  &N+1
         AGO   .L
.M       SPACE
.N       ANOP
@$#&N    DC    CL8'&SS(&N)'        SOURCE SEGMENT NAME
&X       SETA  (&SD(&N)-&SD(&N)/65536*65536)/256  SOURCE SEGMENT FLAG
&Y       SETA  (&SD(&N)-&SD(&N)/256*256-1)*12     SOURCE DB OFFSET
         DC    AL1(&X),AL3(&Y)     FLAG, DATABASE ENTRY OFFSET
&N       SETA  &N+1
.O       AIF   (&N LE &SL AND &SD(&N)/65536/16384 NE 1).M
&N       SETA  &N+1
         AIF   (&N LE &SL).O
.P       DC    F'-1'
         SPACE 3
INDXTAB  DS    0F
         DC    A($NDX0-INDXTAB-4)
&N       SETA  1
.Q       AIF   (&N GT &SL).AW
         AIF   (&SD(&N)/65536/16384 EQ 1).R
&N       SETA  &N+1
         AGO   .Q
.R       ANOP
&X       SETA  &SD(&N)/65536-16384   &X EQUALS XDFLD STMT NUMBER
&E       SETA  &N                    &N EQ 1ST &SL NUMBER
.S       ANOP
&E       SETA  &E+1
         AIF   (&SD(&E)/65536-16384 EQ &X).S
&E       SETA  &E-1                  &E EQUALS LAST &SL NUMBER
&Y       SETA  0
         AIF   (&FT1(&X)/65536/256-&FT1(&X)/65536/256/2*2 NE 1).T
&Y       SETA  &FT1(&X)-&FT1(&X)/65536*65536  &Y EQ ISS/XDS NUMBER
         AGO   .W
.T       ANOP
&Y       SETA  &Y+1                         THESE INSTRUCTIONS
         AIF   (&Y GT 255).V                 SET &Y EQUAL TO
         AIF   ('&SN(&Y)' NE '&FSF1(&X)').T     ISS NUMBER
&B       SETA  &Y                               SET ISS-PC
&C       SETA  &FT1(&X)-&FT1(&X)/256*256        GET ISS-PC
.U       AIF   (&B EQ &C).W                     ISS-PC = IS-PC ?
&B       SETA  &SLDP(&B)/1048576                SET ISS-PC TO ISS-PPC
         AIF   (&B GE &C).W                     ISS-PC GE IS-PC ?
         MNOTE 8,'XDFLD021 - INDEX SOURCE SEGMENT (&FSF1(&X).) IS NOT '
         MNOTE *,'           HIERARCHICALLY BELOW INDEXED SEGMENT (&SN(,
               &C).).'
&GB1(16) SETB  1
         AGO   .W
.V       MNOTE 8,'XDFLD016 - SPECIFIED INDEX SOURCE SEGMENT NAME '
         MNOTE *,'           NOT FOUND.  NAME EQUALS - &FSF1(&X)..'
&GB1(16) SETB  1
.W       ANOP
&AAA     SETA  (&Y-1)*48                   &AAA EQ OFFSET TO ISS
&B       SETA  &FT1(&X)/65536/256          &B EQ XDFLD FLAGS
&C       SETA  0                           &C WILL CONTAIN CONSTANT
         AIF   ((&B-&B/16*16)/8 NE 1).X
&C       SETA  &FT1(&X)/65536-&FT1(&X)/65536/256*256
.X       DC    X'64',AL3(0)
         DC    X'01',AL3(0)
         DC    A(&AAA)
         DC    AL1(&B,&C,0,0)
         AIF   ((&SD(&E)-&SD(&E)/65536*65536)/256 LT 64).Y
&A       SETA  ((&SD(&E)-&SD(&E)/65536*65536)/256)-64
&C       SETA  &SD(&E)-&SD(&E)/256*256
&D       SETC  '&SS(&E).        '(1,8)
         DC    X'40',AL1(&A),AL2(0)     CODE BYTE, FLAGS, UNUSED
         DC    AL1(&C),VL3($NDX&E)      NULL VALUE, EXTRTN CSECT
         DC    CL8'&D'                  NAME OF EXIT ROUTINE
         SPACE 1
$NDX&E   CSECT
&Y       SETA  &FT1(&X)-&FT1(&X)/256*256
         DC    CL8'&SN(&Y)'         SEGMENT NAME
         DC    CL8'&FN1(&X)'        XDFLD NAME
         DC    CL8'&D',A(0)         MAINT ROUTINE NAME & EP
         DC    AL2(32,0)            CSECT LENGTH, RESERVED
&DBN     CSECT                      RETURN TO DBD CSECT
         SPACE 1
.Y       ANOP
&Y       SETA  1
&BUG     SETB  0
.Z       ANOP
&OFF(&Y) SETA  0
&FL(&Y)  SETB  0
&Y       SETA  &Y+1
         AIF   (&Y LE 5).Z
&Y       SETA  0                        CURRENT FIELD STATEMENT@BA38752
&A       SETA  1
.AA      AIF   ((&SD(&N)-&SD(&N)/65536*65536)/256 NE 4).AE
&Y       SETA  &Y+1                                            @BA38747
         AIF   (&Y GT 999 AND NOT &BUG).AD                     @BA38747
         AIF   (&Y GT 999 AND &BUG).AB                         @BA38747
         AIF   ('&SS(&N)' NE '&FN1(&Y)').AA
&OFF(&A) SETA  (&FT1(&Y)-&FT1(&Y)/65536*65536)-1
         AIF   (&AAA EQ &OFF(&A)*48).AC
&BUG     SETB  1
         AGO   .AA
.AB      MNOTE 8,'XDFLD017 - SPECIFIED SRCH FIELD NAME - &SS(&N)..'
         MNOTE *,'           NON-EXISTENT IN INDEX SOURCE SEGMENT. '
&GB1(16) SETB  1
.AC      ANOP
&OFF(&A) SETA  (&Y-1)*24
&FL(&A)  SETB  1
&A       SETA  &A+1
         AIF   (&N EQ &E).AE
&N       SETA  &N+1
&Y       SETA  0
         AGO   .AA
.AD      MNOTE 8,'XDFLD111 - SPECIFIED SRCH FIELD NAME NON-EXISTENT. '
         MNOTE *,'           FIELD NAME EQUALS - &SS(&N)..'
&GB1(16) SETB  1                                               @BA38747
         MEXIT
.AE      AIF   (&N NE &E).AF
         AIF   ((&SD(&E)-&SD(&E)/65536*65536)/256 LT 64 AND (&SD(&E)-&S,
               D(&E)/65536*65536)/256 NE 4).AF
         DC    X'84',AL1(&FL(1),&FL(2),&FL(3))    CODE, FL1, FL2, FL3
         AGO   .AG
.AF      DC    X'04',AL1(&FL(1),&FL(2),&FL(3))    CODE, FL1, FL2, FL3
.AG      DC    AL1(&FL(4),&FL(5)),H'&OFF(1)'      FL4, FL5, OFF1
         DC    H'&OFF(2)',H'&OFF(3)'    OFF2, OFF3
         DC    H'&OFF(4)',H'&OFF(5)'    OFF4, OFF5
         AIF  (&N EQ &E AND (&SD(&E)-&SD(&E)/65536*65536)/256 GT 64).AW
&Y       SETA  1
&BUG     SETB  0
.AH      ANOP
&OFF(&Y) SETA  0
&FL(&Y)  SETB  0
&Y       SETA  &Y+1
         AIF   (&Y LE 5).AH
         AIF   ((&SD(&N)-&SD(&N)/65536*65536)/256 NE 36).AP
&Y       SETA  0                        CURRENT FIELD STATEMENT@BA38752
&A       SETA  1
.AI      AIF   ((&SD(&N)-&SD(&N)/65536*65536)/256 NE 36).AM
&Y       SETA  &Y+1                                            @BA38747
         AIF   (&Y GT 999 AND NOT &BUG).AL                     @BA38747
         AIF   (&Y GT 999 AND &BUG).AJ                         @BA38747
         AIF   ('&SS(&N)' NE '&FN1(&Y)').AI
&OFF(&A) SETA  (&FT1(&Y)-&FT1(&Y)/65536*65536)-1
         AIF   (&AAA EQ &OFF(&A)*48).AK
&BUG     SETB  1
         AGO   .AI
.AJ      MNOTE 8,'XDFLD018 - SPECIFIED SUBSEQ FIELD NAME - &SS(&N)..'
         MNOTE *,'           NON-EXISTENT IN INDEX SOURCE SEGMENT. '
&GB1(16) SETB  1
.AK      ANOP
&OFF(&A) SETA  (&Y-1)*24
&FL(&A)  SETB  1
&A       SETA  &A+1
         AIF   (&N EQ &E).AN
&N       SETA  &N+1
&Y       SETA  0
         AGO   .AI
.AL      MNOTE 8,'XDFLD112 - SPECIFIED SUBSEQ FIELD NAME NON-EXIXTANT.'
         MNOTE *,'           FIELD NAME EQUALS - &SS(&N)..'
&GB1(16) SETB  1                                               @BA38747
         MEXIT
.AM      AIF  (&N EQ &E AND (&SD(&E)-&SD(&E)/65536*65536)/256 NE 32).AN
         DC    X'24',AL1(&FL(1),&FL(2),&FL(3))   CODE, FL1, FL2, FL3
         AGO   .AO
.AN      DC    X'A4',AL1(&FL(1),&FL(2),&FL(3))   CODE, FL1, FL2, FL3
.AO      DC    AL1(&FL(4),&FL(5)),H'&OFF(1)'     FL4, FL5, OFF1
         DC    H'&OFF(2)',H'&OFF(3)'    OFF2, OFF3
         DC    H'&OFF(4)',H'&OFF(5)'    OFF4, OFF5
         AIF  (&N EQ &E AND (&SD(&E)-&SD(&E)/65536*65536)/256 GT 64).AW
.AP      ANOP
&Y       SETA  1
&BUG     SETB  0
.AQ      ANOP
&OFF(&Y) SETA  0
&FL(&Y)  SETB  0
&Y       SETA  &Y+1
         AIF   (&Y LE 5).AQ
         AIF   ((&SD(&N)-&SD(&N)/65536*65536)/256 NE 32).AW
&Y       SETA  0                        CURRENT FIELD STATEMENT@BA38752
&A       SETA  1
.AR      AIF   ((&SD(&N)-&SD(&N)/65536*65536)/256 NE 32).AV
&Y       SETA  &Y+1                                            @BA38747
         AIF   (&Y GT 999 AND NOT &BUG).AU                     @BA38747
         AIF   (&Y GT 999 AND &BUG).AS                         @BA38747
         AIF   ('&SS(&N)' NE '&FN1(&Y)').AR
&OFF(&A) SETA  (&FT1(&Y)-&FT1(&Y)/65536*65536)-1
         AIF   (&AAA EQ &OFF(&A)*48).AT
&BUG     SETB  1
         AGO   .AR
.AS      MNOTE 8,'XDFLD019 - SPECIFIED SOURCE FIELD NAME - &SS(&N)..'
         MNOTE *,'           NON-EXISTENT IN INDEX SOURCE SEGMENT. '
&GB1(16) SETB  1
.AT      ANOP
&OFF(&A) SETA  (&Y-1)*24
&FL(&A)  SETB  1
&A       SETA  &A+1
         AIF   (&N EQ &E).AV
&N       SETA  &N+1
&Y       SETA  0
         AGO   .AR
.AU      MNOTE 8,'XDFLD113 - SPECIFIED SOURCE FIELD NAME NON-EXISTENT.'
         MNOTE *,'           FIELD NAME EQUALS - &SS(&N)..'
&GB1(16) SETB  1                                               @BA38747
         MEXIT
.AV      DC    X'A0',AL1(&FL(1),&FL(2),&FL(3))    CODE, FL1, FL2, FL3
         DC    AL1(&FL(4),&FL(5)),H'&OFF(1)'      FL4, FL5, OFF1
         DC    H'&OFF(2)',H'&OFF(3)'    OFF2, OFF3
         DC    H'&OFF(4)',H'&OFF(5)'    OFF4, OFF5
.AW      ANOP
&N       SETA  &N+1
         AIF   (&N LE &SL AND &SD(&N)/65536/16384 EQ 1).R
         AIF   (&N LE &SL).AW
$NDX0    DC    F'-1'
         SPACE 2
         MEXIT
         MEND
