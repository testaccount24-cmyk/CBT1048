         MACRO
&LABEL   REPLACE &DATA,&LENGTH,&DATA2,&LENGTH2
         LCLC  &WORKC,&WORKC2
&WORKC2  SETC  'L'''
&WORKC   SETC  'IHB&SYSNDX'        USED TO OVERCOME COL ALIGNMENT
         AIF   (T'&DATA NE 'O').TESTDA
         MNOTE *,'NO DATA ADDRESS. REG14 ASSUMED'
         AGO   .LOADLEN
.TESTDA  AIF   ('&DATA' EQ 'START').START
         AIF   ('&DATA' EQ 'END').END
         AIF   ('&DATA' EQ 'NEXT').NEXT
         AIF   ('&DATA'(1,1) NE '''').DATALD
         BAL   14,&WORKC           JUMP AROUND ARGUMENT
&WORKC.D DC    C&DATA
&WORKC   DS    0H
         AGO   .LOADLEN
.DATALD  AIF   ('&DATA'(1,1) EQ '(').DATAREG
         LA    14,&DATA            LOAD ADDRESS OF ARGUMENT
         AGO   .LOADLEN
.DATAREG LA    14,0&DATA           LOAD ADDRESS OF ARGUMENT
         AGO   .LOADLEN
.LOADLEN AIF   (T'&LENGTH NE 'O').LENHERE
         AIF   (T'&DATA NE 'O').TESTLEN
         MNOTE *,'NO LENGTH SPECIFIED. REG15 ASSUMED'
         AGO   .CALL
.TESTLEN AIF   ('&DATA'(1,1) NE '(').LATTRIB
         MNOTE *,'NO LENGTH AVAILABLE FOR DATA. REG15 ASSUMED'
         AGO   .CALL
.LATTRIB AIF   ('&DATA'(1,1) NE '''').NOTLIT
         LA    15,&WORKC2&WORKC.D  LOAD LENGTH OF SCAN LITERAL
         AGO   .CALL
.NOTLIT  LA    15,&WORKC2&DATA     LOAD LENGTH OF SCAN DATA
         AGO   .CALL
.LENHERE AIF   ('&LENGTH'(1,1) EQ '(').LENREG
         LA    15,&LENGTH.(0,0)    LOAD LENGTH OF DATA
         AGO   .CALL
.LENREG  LA    15,0&LENGTH         LOAD LENGTH OF DATA
.CALL    ST    15,$WKSAVE          SAVE LENGTH OF ARGUMENT
         SETSCAN
         BNE   &WORKC.X            ARGUMENT NOT FOUND. EXIT
         L     15,$WKSAVE          RELOAD LENGTH OF DATA
         DELETE
         AIF   (T'&DATA2 EQ 'O' AND T'&LENGTH2 EQ 'O').MEND
         INSERT &DATA2,&LENGTH2
.MEND    ANOP
&WORKC.X EQU   *
         MEND
