         MACRO
         CKOPT &OPT,&M
         GBLA  &P,&S               PCB & SENSEG NUMBERS.
         GBLB  &E                  PSBGEN ERROR SWITCH.
         GBLC  &PO(255),&SPO(500)  PCB & SENSEG PROCESSING OPTIONS.
         LCLA  &N
         LCLB  &B(8)               OPTION CHECK BITS.
.*
.*
         AIF   ('&OPT' NE '').ST
         MEXIT
.ST      AIF   (K'&OPT GT 4).ERR
         AIF   ('&OPT' EQ 'A' OR '&OPT' EQ 'L' OR '&OPT' EQ 'K').SET
         AIF (('&OPT' EQ 'GS' OR '&OPT' EQ 'GO' OR '&OPT' EQ 'GOP' OR  *
               '&OPT' EQ 'LS' OR '&OPT' EQ 'LSP') AND '&M'(1,3) EQ     *
               'PCB').SET
         AIF   ('&PO(&P)' EQ 'GO' AND '&M'(1,3) EQ 'SEG').GOCHK
         AIF   ('&M'(1,3) EQ 'SEG' AND ('&PO(&P)' EQ 'LS' OR           *
               '&PO(&P)' EQ 'L') AND ('&OPT' EQ 'L' OR                 *
               '&OPT' EQ 'LS')).SET
.LOOP    ANOP
&N       SETA  &N+1
         AIF   ('&OPT'(&N,1) NE 'P').G                             3909
         AIF   (&B(8)).ERR                                         3909
&B(8)    SETB  1                                                   3909
         AGO   .ENDLOOP                                            3909
.G       AIF   ('&OPT'(&N,1) NE 'G').I
         AIF   (&B(1)).ERR
&B(1)    SETB  1
         AGO   .ENDLOOP
.I       AIF   ('&OPT'(&N,1) NE 'I').R
         AIF   (&B(2)).ERR
&B(2)    SETB  1
         AGO   .ENDLOOP
.R       AIF   ('&OPT'(&N,1) NE 'R').D
         AIF   (&B(3)).ERR
&B(3)    SETB  1
         AGO   .ENDLOOP
.D       AIF   ('&OPT'(&N,1) NE 'D').A
         AIF   (&B(4)).ERR
&B(4)    SETB  1
.ENDLOOP AIF   (&N LT K'&OPT).LOOP
.SET     AIF   ('&M'(1,3) EQ 'SEG').SEG
&PO(&P)  SETC  '&OPT'
         AIF   (NOT &B(6) AND NOT &B(7)).Z                         2434
         AIF   (NOT &B(1) AND NOT &B(2) AND NOT &B(3) AND NOT &B(4)).ER,
               R                                                   2434
.Z       ANOP                                                      2434
         AIF   ('&OPT' EQ 'K').ERR                                 2428
         MEXIT
.A       AIF   ('&OPT'(&N,1) NE 'A').E
         AIF   (&B(1) OR &B(2) OR &B(3) OR &B(4) OR &B(5)).ERR
&B(1)    SETB  1
&B(2)    SETB  1
&B(3)    SETB  1
&B(4)    SETB  1
&B(5)    SETB  1
         AGO   .ENDLOOP
.E       AIF   ('&OPT'(&N,1) NE 'E').S
         AIF   (&B(6)).ERR
&B(6)    SETB  1
         AGO   .ENDLOOP
.S       AIF   ('&OPT'(&N,1) NE 'S').ERR
         AIF   (&B(7)).ERR
&B(7)    SETB  1
         AGO   .ENDLOOP
.ERR     MNOTE 8,'&M - PROCOPT OPERAND &OPT IS INVALID WITH &PO(&P)'
&E       SETB  1
         MEXIT
.GOCHK   ANOP                           CHECK VALID OPTIONS FOR@BA26340
         AIF   ('&OPT' NE 'K' AND '&OPT' NE 'G' AND '&OPT' NE 'GP'     *
               AND '&OPT' NE 'GOP').ERR  ONLY VALID WITH GO ON PCB
.SEG     ANOP
&SPO(&S) SETC  '&OPT'
         AIF  (('&SPO(&S)' EQ 'L' OR '&SPO(&S)' EQ 'LS') AND           ,
               ('&PO(&P)' NE 'L' AND '&PO(&P)' NE 'LS')).ERR   @BA42983
         MEND
