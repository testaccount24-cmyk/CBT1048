         MACRO
&LABEL   GOBACK &MODE,&DATA
&LABEL   DS    0H
         AIF   ('&MODE' EQ 'NORMAL' OR T'&MODE EQ 'O').NORMAL
         AIF   ('&MODE' EQ 'INSERT').INSERT
         AIF   ('&MODE' EQ 'DELETE').DELETE
         AIF   ('&MODE' EQ 'SCRATCH').SCRATCH
         MNOTE 4,'INVALID GOBACK TYPE - &MODE. ''NORMAL'' ASSUMED'
.NORMAL  XR    15,15               SET ZERO RETURN CODE
         AGO   .ZRETURN
.INSERT  ANOP
         LA    15,4(0,0)           SET INSERT RETURN CODE
         AIF   (T'&DATA EQ 'O').NOLOAD
         AIF   ('&DATA'(1,1) EQ '(').LOADREG
         LA    1,&DATA             LOAD ADDRESS OF DATA RECORD
         AGO   .ZRETURN
.LOADREG LA    1,0&DATA            LOAD ADDRESS OF DATA RECORD
         AGO   .ZRETURN
.NOLOAD  MNOTE 0,'NO INSERT DATA ADDRESS. REG1 ASSUMED'
         AGO   .ZRETURN
.DELETE  LA    15,8(0,0)           SET DELETE RETURN CODE
         AGO   .ZRETURN
.SCRATCH LA    15,12(0,0)          SET SCRATCH PROC RETURN CODE
         AGO   .ZRETURN
.ZRETURN L     13,4(13)            LOAD ADDRESS CALLER'S SAVE AREA
         L     14,12(13)           RELOAD LINK REGISTER
         LM    2,12,28(13)         RELOAD CALLERS'S REGISTERS
         BR    14                  RETURN TO CALLER
         MEND
