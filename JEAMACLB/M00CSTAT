         MACRO
&NAME    M00CSTAT &PCB=D1PC,     1ST 4 CHAR OF PCB-STATUS FIELD        *
               &TEST=     LIST OF ELEMENTS IN FOLLOWING FORMAT:
.*                        (STAT,LABEL,STAT,LABEL,.............)
.*                        WHERE STAT=STATUSCODE AND
.*                        LABEL IS WHERE-TO-GO ADDRESS
.*                              -----------------
.*                           CHECK PCB-STATUSCODE
.*                           --------------------
         LCLA  &INDEX
         LCLC  &ENDLBL
         LCLC  &LABEL
         LCLC  &STAT
&INDEX   SETA  1
&ENDLBL  SETC  'M00'.'&SYSNDX'
         ACTR  100
.B000    ANOP
      AIF   (T'&TEST(&INDEX) EQ 'O' AND T'&TEST(&INDEX+1) EQ 'O').OUT
.*                           STATUS SUBPARAMETER:
.*                           ----------------------
&STAT    SETC  '  '          DEFAULT AND EQUIVALENT OF 'OK'
         AIF   (T'&TEST(&INDEX) EQ 'O').B200
         AIF   ('&TEST(&INDEX)' EQ 'OK').B200
&STAT    SETC  '&TEST(&INDEX)'
.*
.*
.*                           WHERE-TO-GO SUBPARAMETER:
.*                           -------------------------
.B200    ANOP
&INDEX   SETA  &INDEX+1
&LABEL   SETC  '&ENDLBL'
         AIF   (T'&TEST(&INDEX) EQ 'O').B300
&LABEL   SETC  '&TEST(&INDEX)'
.B300    ANOP
&INDEX   SETA  &INDEX+1
         CLC   &PCB.STAT,=C'&STAT'
         BE    &LABEL
         AGO   .B000
.OUT     ANOP
&ENDLBL.D DS   0H
               SPACE 1
         MEXIT
         MEND
