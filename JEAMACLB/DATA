         MACRO
&LABEL   DATA  &LINE,&COL,&DATA,&BRIGHT=NO,&CHANGE=NO,&CURSOR=NO,      X
               &PASS=NO,&INDEX=0,&SECRET=NO,&RJ=NO
.*
.*       GLOBAL DEFINITIONS FOR LOCAL LINKAGE PARAMETERS
.*
         GBLA  &SCRNDS             LINKAGE SECTION NUMBER
         GBLA  &SCINDEX            GLOBAL INDEX FOR FOLLOWING:
         GBLC  &SCARRY1(256)       NAME OF THE FIELD
         GBLA  &SCARRY2(256)       LENGTH OF THE FIELD
         GBLA  &SCARRY3(256)       OFFSET OF THE FIELD
         GBLA  &SCARRY4(256)       LINKAGE INDEX NUMBER OF FIELD
.*
.*       ARRAY FIELD ADDITIONAL GLOBAL ARRAYS
.*
         GBLA  &AROFFST(256)       START OFFSET OF THE ARRAY
         GBLA  &ARLENTH(256)       LENGTH OF AN ARRAY ENTRY
         GBLA  &SCARRAY(256)       INDEX TO ARRAY FOR THIS FIELD
         GBLA  &SCAROFF(256)       OFFSET WITHIN THE ARRAY
         GBLA  &ARINDEX            GLOBAL ARRAY INDEX FOR ARRAY ENTRIES
         GBLB  &ARSW               IF ON - WE ARE PART OF AN ARRAY
.*
.*       REMAINING DEFINITIONS
.*
         GBLA  &GBLSCRN,&BASE,&DLINE,&DCOL,&DLENTH
         LCLA  &WORK,&WORKB,&WORKF,&WORKD,&WORKE
         LCLC  &WORKC,&WORKC2,&WRKLINE
         LCLB  &SCSW               LOCAL 'LINKAGE' SWITCH
.*
.*       SEARCH THE GLOBAL TABLES FOR A 'LINKAGE' ENTRY
.*
&SCSW    SETB  0
&WORKB   SETA  0
.LKLOOP  ANOP
         AIF   (&WORKB EQ &SCINDEX).NOLINK NOT A VALID LINKAGE NAME
&WORKB   SETA  &WORKB+1            INDEX NEXT TABLE ENTRY
         AIF   ('&SCARRY1(&WORKB)' NE '&DATA(1)').LKLOOP TEST FOR NAME
&SCSW    SETB  1                   FOUND IT. SO SET SWITCH
.NOLINK  ANOP  ,                   FIELD IS NOT LINKAGE FIELD
.*
.*
.*
&WORKC   SETC  'L'''
&WORK    SETA  0
&GBLSCRN SETA  &GBLSCRN+1
X&BASE.X&GBLSCRN DS  0F
         AIF   ('&BRIGHT' NE 'YES').NOBRITE
&WORK    SETA  &WORK+128           DISPLAY IS TO BE BRIGHT
.NOBRITE ANOP
         AIF   ('&CHANGE' NE 'YES').NOCHNG
&WORK    SETA  &WORK+64            DATA IN FIELD CAN BE CHANGED
.NOCHNG  ANOP
         AIF   ('&CURSOR' NE 'YES').NOCURS
&WORK    SETA  &WORK+32
.NOCURS  ANOP
         AIF   ('&PASS' NE 'YES' AND &SCSW NE 1).NOPASS
&WORK    SETA  &WORK+16
.NOPASS  ANOP
         AIF   ('&SECRET' NE 'YES').NOSECR
&WORK    SETA  &WORK+8
.NOSECR  ANOP
         AIF   ('&RJ' NE 'YES').NORJ
&WORK    SETA  &WORK+4
.NORJ    ANOP
         AIF   ('&LINE' NE '*').TESTS
&DLINE   SETA  &DLINE+1
         DC    AL2(&DLINE)          LINE NUMBER
         AGO   .DOCOL
.TESTS   AIF   ('&LINE' NE 'S').NORLIN
         DC    AL2(&DLINE)          LINE NUMBER
         AGO   .DOCOL
.NORLIN  ANOP
         AIF   ('&LINE'(1,2) NE '*+').NORLIN2
&WRKLINE SETC  '&LINE'(3,6)
&DLINE   SETA  &DLINE+&WRKLINE
         DC    AL2(&DLINE)         LINE NUMBER
         AGO   .DOCOL
.NORLIN2 ANOP
&DLINE    SETA  &LINE
         DC    AL2(&LINE)          LINE NUMBER
.DOCOL   ANOP
         AIF   ('&COL' NE '*').NORCOL
&DCOL    SETA  &DCOL+&DLENTH+1
         DC    AL2(&DCOL)          COLUMN NUMBER
         AGO   .COLEND
.NORCOL  ANOP
         DC    AL2(&COL)           COLUMN NUMBER
&DCOL    SETA  &COL
.COLEND  ANOP
         DC    AL1(&WORK)          PROCESSING OPTION
         AIF   (&SCSW EQ 1).DEFLINK GO DEFINE LINK DEFNTN FIELD
         AIF   (N'&DATA EQ 1 AND '&DATA'(1,1) EQ '''').SUPLY
         AIF   (N'&DATA EQ 1).ASSUME
&DLENTH  SETA  &DATA(1)
         DC    AL3(&DATA(1))       LENGTH OF THE FIELD
         AIF   ('&PASS' EQ 'YES').MEXIT2
         DC    A(&DATA(2))         ADDRESS OF THE DATA
         MEXIT
.ASSUME  ANOP
&DLENTH  SETA  L'&DATA
         DC    AL3(&WORKC&DATA)    LENGTH OF THE DATA FIELD
         AIF   ('&PASS' EQ 'YES').MEXIT2
         DC    A(&DATA)            ADDRESS OF THE DATA ITSELF
         MEXIT
.SUPLY   ANOP
&WORKC2  SETC  'IHB&SYSNDX'
         DC    AL3(L'IHB&SYSNDX)   LENGTH OF THE DATA FIELD
         DC    A(IHB&SYSNDX)       ADDRESS OF THE DATA FIELD
&LABEL   DS    0CL(L'IHB&SYSNDX)
IHB&SYSNDX DC C&DATA
&DLENTH  SETA  L'&WORKC2
         MEXIT
.MEXIT2  ANOP
         DC    A(&INDEX)           OFFSET TO PASS LIST ENTRY
         MEXIT
.DEFLINK ANOP
         AIF   (&SCARRAY(&WORKB) NE 0).ARRAY FIELD IS PART OF AN ARRAY
         DC    AL3(&SCARRY2(&WORKB)) LENGTH OF THE DATA FIELD
         DC    AL2(&SCARRY3(&WORKB)) LINKAGE OFFSET
         DC    AL2(&SCARRY4(&WORKB)) LINKAGE INDEX OF THE FIELD
&DLENTH  SETA  &SCARRY2(&WORKB)    SET UP THE GLOBAL LENGTH
         MEXIT
.ARRAY   ANOP
.*
.*       FIELD IS IN AN ARRAY. CALCULATE THE OFFSET TO IT
.*
.*       GET THE INDEX NUMBER AND GET OFFSET FROM START AF ARRAY
.*
&WORKF   SETA  &SCARRAY(&WORKB)    INDEX FOR THIS ARRAY
&WORKD   SETA  &ARLENTH(&WORKF)    LENGTH OF EACH ENTRY IN ARRAY
&WORKE   SETA  &DATA(2)-1          USER SUPPLIED INDEX
&WORKD   SETA  &WORKE*&WORKD+&SCAROFF(&WORKB) OFFSET WITHIN ARRAY
.*
.*       ADD THE OFFSET OF THE ARRAY ITSELF TO GET REAL OFFSET
.*
&WORKD   SETA  &WORKD+&AROFFST(&WORKF) ADD IN START OFFSET OF ARRAY
.*
.*       LAY OUT THE FIELD DEFINTION
.*
         DC    AL3(&SCARRY2(&WORKB)) LENGTH OF THE FIELD
         DC    AL2(&WORKD)         OFFSET WITHIN LINKAGE
         DC    AL2(&SCARRY4(&WORKB)) LINKAGE INDEX
         MEND
