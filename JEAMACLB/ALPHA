         MACRO
         ALPHA &AN,&FIELD=,&CHAR=1
         GBLB  &QUITB
         LCLB  &A,&H,&N,&N1                                    @BM02927
         LCLA  &V
.*
.*
&QUITB   SETB  0
&A       SETB  ('&AN' EQ 'ALL')         TEST ALL CHARS (1ST FOR A-Z)
&H       SETB  ('&AN' EQ 'HEX')         TEST ALL FOR HEX.      @BM02927
&N       SETB  (&A OR '&AN' EQ 'AN')    TEST FOR ALPHA/NUMERIC
&N1      SETB  ('&AN' EQ 'AN1')    TEST FIRST FOR ALPHA/NUM        6518
         AIF   ('&FIELD' EQ '').B
         AIF   (&CHAR GT K'&FIELD).E
         AIF   (&CHAR GE 1 AND &CHAR LE 255).T
.E       ANOP
&QUITB   SETB  1
         MNOTE 0,'*** CHAR OPERAND RANGE IS INVALID - &CHAR..'
         MEXIT
.T       ANOP
&V       SETA  &CHAR
.L       AIF   (&H AND                                                 ,
               (('&FIELD'(&V,1) GE 'A' AND '&FIELD'(&V,1) LE 'F') OR   ,
               ('&FIELD'(&V,1) GE '0' AND '&FIELD'(&V,1) LE '9'))).G
         AIF   (&H).B                                          @BM02927
         AIF   ('&FIELD'(&V,1) GE 'A' AND '&FIELD'(&V,1) LE 'I').G
         AIF   ('&FIELD'(&V,1) GE 'J' AND '&FIELD'(&V,1) LE 'R').G
         AIF   ('&FIELD'(&V,1) GE 'S' AND '&FIELD'(&V,1) LE 'Z').G
         AIF   ('&FIELD'(&V,1) EQ '$').G
         AIF   ('&FIELD'(&V,1) EQ '#').G
         AIF   ('&FIELD'(&V,1) EQ '@').G
         AIF   (&A AND &V EQ &CHAR).B
         AIF (&N AND '&FIELD'(&V,1) GE '0' AND '&FIELD'(&V,1) LE '9').G
        AIF (&N1 AND '&FIELD'(&V,1) GE '0' AND '&FIELD'(&V,1) LE '9').G
.B       ANOP
&QUITB   SETB  1
         MEXIT
.G       ANOP
&V       SETA  &V+1
         AIF   ((&H OR &N) AND K'&FIELD GE &V).L               @BM02927
         MEND
