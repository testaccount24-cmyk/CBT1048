         MACRO
&NAME    ITEM  &LENGTH=1
.*
.*       NORMAL FIELD DEFINITION ARRAYS
.*
         GBLA  &SCRNDS             LINKAGE SECTION NUMBER
         GBLA  &SCINDEX            GLOBAL INDEX FOR FOLLOWING:
         GBLC  &SCARRY1(256)       NAME OF THE FIELD
         GBLA  &SCARRY2(256)       LENGTH OF THE FIELD
         GBLA  &SCARRY3(256)       OFFSET OF THE FIELD
         GBLA  &SCARRY4(256)       LINKAGE INDEX NUMBER OF FIELD
.*
.*       ARRAY FIELD ADDITIONAL GLOBAL ARRAYS
.*
         GBLC  &ARNAME(256)        NAME OF THE ARRAY
         GBLA  &ARLENTH(256)       LENGTH OF AN ARRAY ENTRY
         GBLA  &SCARRAY(256)       INDEX TO ARRAY FOR THIS FIELD
         GBLA  &SCAROFF(256)       OFFSET WITHIN THE ARRAY
         GBLA  &ARINDEX            GLOBAL ARRAY INDEX FOR ARRAY ENTRIES
         GBLB  &ARSW               IF ON - WE ARE PART OF AN ARRAY
.*
.*
&SCINDEX SETA  &SCINDEX+1
&SCARRY1(&SCINDEX) SETC '&NAME'
&SCARRY2(&SCINDEX) SETA &LENGTH
&SCARRY3(&SCINDEX) SETA &SCARRY3(&SCINDEX-1)+&SCARRY2(&SCINDEX-1)
&SCARRY4(&SCINDEX) SETA &SCRNDS-1
.*
.*       TEST FOR BEING IN AN ARRAY AND PROCESS
.*
&SCARRAY(&SCINDEX) SETA 0          ASSUME WE WILL NOT BE IN ARRAY
         AIF   (&ARSW NE 1).MEXIT  NOT IN ARRAY. EXIT
&SCARRAY(&SCINDEX) SETA &ARINDEX   YES WE ARE. SO SAVE ID
&SCAROFF(&SCINDEX) SETA &ARLENTH(&ARINDEX) GET MY OFFSET WITHIN ARRAY
&ARLENTH(&ARINDEX) SETA &ARLENTH(&ARINDEX)+&LENGTH LENGTH OF ARRAY SO F
.MEXIT   MEND
