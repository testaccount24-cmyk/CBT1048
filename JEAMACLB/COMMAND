//JEAMACRO JOB (1580,5),'0000 J ABBEY',CLASS=A,MSGLEVEL=(2,0)
// EXEC PGM=IEBUPDTE,REGION=80K
//SYSPRINT DD SYSOUT=A
//SYSUT1 DD DSN=JEA.MACLIB,DISP=OLD
//SYSUT2 DD DSN=JEA.MACLIB,DISP=OLD
>> REPL NAME=COMMAND
>> NUMBER NEW1=1000,INCR=1000
         MACRO
         COMMAND &COMMAND,&PRCSSR,&TXT=' ',&ADCON=V
.*       THIS MACRO IS UNIQUE TO COMMAND PROCESSING
.*       ITS PURPOSE IS TO DEFINE THE TABLE OF VALID COMMANDS USED
.*       BY THIS MODULE.
.*
.*       IF THE FIRST POSITIONAL OPERAND IS 'DSECT', A MAPPING DSECT
.*       OF THE TABLE ENTRIES WILL BE PRODUCED.
.*
.*       &COMMAND  IS THE NAME OF THE COMMAND ITSELF
.*       &PRCSSR   IS THE NAME OF THE PROCESSING MODULE FOR THE COMMAND
.*       &TXT      IS DESCRIPTIVE TEXT FOR THE COMMAND
.*       &ADCON    DICTATES WHETHER THE COMMAND PROCESSOR ADDRESS IS
.*                 DEFINED AS A V-TYPE OR A-TYPE ADDRESS CONSTANT.
.*                 THE OPERAND HAS TWO PARTS, CODED IN BRACKETS. 1ST
.*                 OPERAND IS THE ADCON TYPE: V OR A. THE 2ND OPERAND
.*                 IS OPTIONAL AND IS THE ADDRESS THAT THE ADCON SHOULD
.*                 REFER TO. DEFAULT IS SAME NAME AS &&PRCSSR.
.*
         LCLC  &WORKC
.*
.*       TEST TO SEE IF THIS IS A MAPPING REQUEST
.*
         AIF   ('&COMMAND' NE 'DSECT').CONT1
CMDLEN   DS    AL1                 LENGTH OF THE COMMAND
CMDNAME  DS    C                   NAME OF THE COMMAND
CMDPRCSR DS    CL8                 NAME OF COMMAND PROCESSOR
CMDPRCAD DS    AL4                 ADDRESS OF COMMAND PROCESSOR
CMDTXLEN DS    AL1                 LENGTH OF TEXT
CMDTEXT  DS    C                   DESCRIPTIVE TEXT
         AGO   .END
.CONT1   ANOP
.*
.*
&WORKC   SETC  'L'''               USED TO GENERATE LENGTH ATTRIBUTES
.*
         DC    AL1(&WORKC.IHB&SYSNDX.C-1)  LENGTH-1 OF COMMAND
IHB&SYSNDX.C DC    C'&COMMAND'            COMMAND NAME
         DC    CL8'&PRCSSR'        NAME OF PROCESSING ROUTINE
         AIF   (N'&ADCON EQ 1).DEFAULT
         DC    &ADCON(1).L4(&ADCON(2)) ADDRESS COMMAND PROCESSOR
         AGO   .CONTX
.DEFAULT ANOP
         DC    &ADCON.L4(&PRCSSR)  ADDRESS COMMAND PROCESSOR
.CONTX   ANOP
         DC    AL1(&WORKC.IHB&SYSNDX-1) LENGTH DESCRIPTIVE TEXT
IHB&SYSNDX DC  C&TXT
.END     MEND
