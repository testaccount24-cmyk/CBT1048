         MACRO
         XDFLD &NAME=,&SEGMENT=,&SRCH=,&CONST=,                        ,
               &SUBSEQ=,&DDATA=,&NULLVAL=,&EXTRTN=
         GBLA  &S,&SL,&SLC,&F1,&F2,&F3,&HEX,&ORG,&MT           @BA41949
         GBLA  &SD(300),&FT1(1000),&SLDP(255)
         GBLA  &SFLD(255),&SLCF(255),&SLCS(255)
         GBLB  &GB1(16),&QUITB,&SYMB(255),&FK1(1000),&SCIDXSW  @BA46262
         GBLC  &FLDNAM(255),&SN(500),&SS(300)
         GBLC  &FN1(1000),&FSF1(1000)
         LCLA  &N,&C(2)
         LCLB  &B(8)                                           @BA46262
.**********************************************************************
.**********************************************************************
.**                     APARLOG 1.1.1                                **
.**                                                                  **
.**      PP36120  PREVENT LCHILD CARD FOR SECONDARY INDEX FOLLOWING  **
.**               ITS ASSOCIATED XDFLD CARD.                         **
.**      PP46262  ALLOWES SYMBOLIC POINTING TO PASS FOR SECONDARY    **
.**               INDEXS. MSG XDFLD002 ISSUED IN ERROR.              **
.*
.**                                                                  **
.**********************************************************************
.**********************************************************************
&N       SETA  &S
         AIF   (&GB1(1)).A
         MNOTE 8,'DBD001 - DBD CARD MUST OCCUR 1ST IN A DBDGEN. '
&GB1(16) SETB  1
.A       AIF   ((&ORG NE 2 OR &GB1(2)) AND &ORG NE 3 AND &ORG NE 4 AND ,
               &ORG NE 7 AND &ORG NE 12).B
         MNOTE 8,'XDFLD001 - XDFLD STATEMENT INVALID FOR HSAM, INDEX,'
         MNOTE *,'           AND (HISAM,ISAM) MULTIPLE DATASET DBDS. '
&GB1(16) SETB  1
.B       ANOP
         AIF   (&SCIDXSW).C   LCHILD  PRECEEDES SECONDARY INDEX@BA46262
         MNOTE 8,'XDFLD002 - LCHILD CARD FOR SECONDARY INDEX MUST PRECE,
               DE XDFLD CARD. '
&GB1(16) SETB  1
&SLC     SETA  1
.C       ANOP
&MT      SETA  6              INDICATE MACRO TYPE - XDFLD      @BA41949
         AIF   (&S GT 0).C1                                    @BA41949
         MNOTE 8,'XDFLD003 - SEGM CARD MUST PRECEDE XDFLD CARD. '
&GB1(16) SETB  1
&B(2)    SETB  1
.C1     AIF   (&SYMB(&S+&B(2)) AND NOT &FK1(&SFLD(&N+&B(2))/65536)).C2
 AIF (&SYMB(&S+&B(2)) AND &FT1(&SFLD(&N+&B(2))/65536)/65536/32 EQ 1).C2
&N       SETA  &SLDP(&N)/1048576
         AIF   (&SYMB(&S+&B(2)) AND &N NE 0).C1
&N       SETA  0
         AIF   (&SYMB(&S+&B(2))).D
         AIF   (&SLCF(&SLC)-&SLCF(&SLC)/8*8 EQ 4).D
.C2      MNOTE 8,'XDFLD020 - PREVIOUS LCHILD POINTER OPERAND INVALID. '
&GB1(16) SETB  1
.D       AIF   ('&NAME' EQ '' OR N'&NAME NE 1 OR K'&NAME GT 8).F
&N       SETA  &N+1
         AIF   (&N GT &F2).E
         AIF   (&N GT 255).G
         AIF   ('&NAME' NE '&FLDNAM(&N)').D
         MNOTE 8,'FLD190 - DUPLICATE FIELD NAME IN SEGMENT &SN(&S+&B(2),
               )..'
&GB1(16) SETB  1
.E       ANOP
&F2      SETA  &N
&FLDNAM(&N) SETC '&NAME'
&F3      SETA  &F3+1
         AIF   (&F3 LE 32).H
         MNOTE 8,'XDFLD013 - SEGMENT XDFLD LIMIT EXCEEDED. '
&GB1(16) SETB  1
&F3      SETA  &F3-1
         AGO   .H
.F       MNOTE 8,'XDFLD004 - NAME OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
         AGO   .H
.G       MNOTE 8,'FLD200 - SEGMENT FIELD LIMIT EXCEEDED. '
&GB1(16) SETB  1
.H       ANOP
&N       SETA  1
.H1      AIF   (&N GT &SL).H3
         AIF   ('&NAME' EQ '&SS(&N)').H2
&N       SETA  &N+1
         AGO   .H1
.H2      MNOTE 8,'XDFLD014 - SPECIFIED NAME MAY NOT BE THE OBJECT OF '
         MNOTE *,'           PREVIOUS XDFLD SRCH, DDATA, OR SUBSEQ. '
&GB1(16) SETB  1
.H3      ANOP
&B(3)    SETB  ('&SEGMENT' EQ '' OR '&SEGMENT' EQ '&SN(&S)')
&B(4)    SETB  ('&CONST' NE '')
&B(5)    SETB  ('&SUBSEQ' NE '')
&B(6)    SETB  ('&DDATA' NE '')
&B(7)    SETB  ('&NULLVAL' NE '')
&B(8)    SETB  ('&EXTRTN' NE '')
&GB1(8)  SETB  0
         AIF   ('&SRCH' EQ '' OR N'&SRCH GT 5).U
&N       SETA  1
.I       AIF   (K'&SRCH(&N) GT 8).U
         AIF   (K'&SRCH(&N) LT 3).I1
         AIF ('&SRCH(&N)'(1,3) EQ '/CK' OR '&SRCH(&N)'(1,3) EQ '/SX').U
.I1      ANOP
&N       SETA  &N+1
         AIF   (&N LE N'&SRCH).I
         AIF   (NOT &B(8)).K
         AIF   (N'&EXTRTN NE 1 OR K'&EXTRTN GT 8).AD
         ALPHA FIELD=&EXTRTN,CHAR=1
         AIF   (&QUITB).AD
.K       AIF   (NOT &B(6)).M
         AIF   (N'&DDATA GT 5).AA
&N       SETA  1
.L       AIF   (K'&DDATA(&N) GT 8).AA
         AIF   (K'&DDATA(&N) LT 3).L1
         AIF   ('&DDATA(&N)'(1,3) EQ '/SX').AA
.L1      ANOP
&N       SETA  &N+1
         AIF   (&N LE N'&DDATA).L
.M       AIF   (NOT &B(5)).O
         AIF   (N'&SUBSEQ GT 5).X
&N       SETA  1
.N       AIF   (K'&SUBSEQ(&N) GT 8).X
&N       SETA  &N+1
         AIF   (&N LE N'&SUBSEQ).N
.O       AIF   (NOT &B(4)).P
         AIF   (T'&CONST NE 'N').AE
         AIF   (&CONST GT 255).AE
&C(1)    SETA  &CONST
.P       AIF   (NOT &B(7)).Q
         AIF   ('&NULLVAL' EQ 'ZERO').Q
&C(2)    SETA  64
         AIF   ('&NULLVAL' EQ 'BLANK').Q
         AIF   (T'&NULLVAL NE 'N').AF
         AIF   (&NULLVAL GT 255).AF
&C(2)    SETA  &NULLVAL
.Q       AIF   (&B(3)).QA
         AIF   (N'&SEGMENT NE 1 OR K'&SEGMENT GT 8).AG
.QA      AIF   (&B(3)+&GB1(6) NE 2).QB
         MNOTE 8,'XDFLD022 - INDEX SOURCE SEGMENT A LOGICAL SEGMENT. '
&GB1(16) SETB  1
.QB      AIF   (&S EQ 0).QB3
&N       SETA  &S
.QB1     AIF   (&N EQ 0).QB3
         AIF   (&SLCS(&N)-&SLCS(&N)/65536*65536 NE 0 AND (&SLCF(&SLCS(&,
               N)/65536)-&SLCF(&SLCS(&N)/65536)/16*16)/8 EQ 1).QB2
&N       SETA  &SLDP(&N)/1048576
         AGO   .QB1
.QB2     MNOTE 8,'XDFLD023 - XDFLD STATEMENT IS INVALID FOR A '
         MNOTE *,'           LOGICAL CHILD SEGMENT OR A PHYSICAL '
         MNOTE *,'           DEPENDENT OF A LOGICAL CHILD SEGMENT. '
         MNOTE *,'           REFERENCE SEGM STATEMENT -  &SN(&N)  '
&GB1(16) SETB  1
.QB3     ANOP
&F1      SETA  &F1+1
         AIF   (&F1 LE 1000).R
         MNOTE 8,'FLD180 - FIELD SPECIFICATION LIMIT EXCEEDED. '
&GB1(16) SETB  1
&F1      SETA  &F1-1
.R       ANOP
&N       SETA  &S-1
.R1      AIF   (&N EQ 0).R3
         AIF   ('&SEGMENT' EQ '&SN(&N)').R2
&N       SETA  &N-1
         AGO   .R1
.R2      MNOTE 8,'XDFLD015 - SEGMENT KEYWORD MAY NOT SPECIFY '
         MNOTE *,'           A PREVIOUSLY DEFINED SEGMENT NAME. '
&GB1(16) SETB  1
.R3      ANOP
&SFLD(&S+&B(2))  SETA  &SFLD(&S+&B(2))+1   INCREMENT NBR FIELDS
&FSF1(&F1) SETC '&SEGMENT'
&FN1(&F1) SETC '&NAME'
&FT1(&F1) SETA (&SYMB(&S+&B(2))*16384+4096+&B(4)*2048+&B(5)*1024+&B(3)*,
               256)*65536+&S
         AIF   (NOT &B(4)).S
&FT1(&F1) SETA &FT1(&F1)+&C(1)*65536
.S       AIF   (&SL+N'&SRCH GT 300).AH
&N       SETA  0
.T       ANOP
&N       SETA  &N+1
&SL      SETA  &SL+1
&SS(&SL) SETC  '&SRCH(&N)'
&SD(&SL) SETA  (16384+&F1)*65536+1024
         AIF   (&N LT N'&SRCH).T
         AGO   .V
.U       MNOTE 8,'XDFLD005 - SRCH OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
.V       AIF   (NOT &B(5)).Y
         AIF   (&SL+N'&SUBSEQ GT 300).AH
&N       SETA  1
.W       ANOP
&SL      SETA  &SL+1
&SS(&SL) SETC  '&SUBSEQ(&N)'
&SD(&SL) SETA  (16384+&F1)*65536+9216
&N       SETA  &N+1
         AIF   (&N LE N'&SUBSEQ).W
         AGO   .Y
.X       MNOTE 8,'XDFLD006 - SUBSEQ OPERAND IS INVALID. '
&GB1(16) SETB  1
.Y       AIF   (NOT &B(6)).AB
         AIF   (&SL+N'&DDATA GT 300).AH
&N       SETA  1
.Z       ANOP
&SL      SETA  &SL+1
&SS(&SL) SETC  '&DDATA(&N)'
&SD(&SL) SETA  (16384+&F1)*65536+8192
&N       SETA  &N+1
         AIF   (&N LE N'&DDATA).Z
         AGO   .AB
.AA      MNOTE 8,'XDFLD007 - DDATA OPERAND IS INVALID. '
&GB1(16) SETB  1
.AB      AIF   (NOT &B(7) AND NOT &B(8)).AC
&SL      SETA  &SL+1
         AIF   (&SL GT 300).AH
&SD(&SL) SETA  (16384+&F1)*65536+16384+&B(8)*512+&B(7)*256+&C(2)
&SS(&SL) SETC  ''
         AIF   (NOT &B(8)).AC
&SS(&SL) SETC  '&EXTRTN'
.AC      MEXIT
.AD      MNOTE 8,'XDFLD008 - EXTRTN OPERAND IS INVALID. '
&GB1(16) SETB  1
         MEXIT
.AE      MNOTE 8,'XDFLD009 - CONST OPERAND IS INVALID. '
&GB1(16) SETB  1
         MEXIT
.AF      MNOTE 8,'XDFLD010 - NULLVAL OPERAND IS INVALID. '
&GB1(16) SETB  1
         MEXIT
.AG      MNOTE 8,'XDFLD011 - SEGMENT OPERAND IS INVALID. '
&GB1(16) SETB  1
         MEXIT
.AH      MNOTE 8,'XDFLD012 - SRCH / DDATA / SUBSEQ / EXTRTN / NULLVAL'
         MNOTE *,'           NAME TABLE LIMIT EXCEEDED. '
&GB1(16) SETB  1
         MEXIT
         MEND
