         MACRO
         EXTDBD &DB,&CODE
         GBLA  &S,&EXTDB,&EXTDBN,&EXTDBR
         GBLB  &GB1(16)
         GBLC  &DBN,&SN(500),&DBNAME(255)
.*
.*
.*
&EXTDBN  SETA  0
         AIF   ('&CODE' NE 'RTN').A
         AIF   (&EXTDB GE &EXTDBR-1).F
&EXTDBR  SETA  &EXTDBR-1
&DBNAME(&EXTDBR)  SETC  '&DB'
&EXTDBN  SETA  &EXTDBR
         MEXIT
.A       AIF   ('&CODE' NE 'SEGM').D
.B       AIF   (&EXTDBN EQ &S).C
&EXTDBN  SETA  &EXTDBN+1
         AIF   ('&DB' NE '&SN(&EXTDBN)').B
&EXTDBN  SETA  ((1*65536*256)+(&EXTDBN-1)*48)  OFFSET INTO SEGTAB
         MEXIT
.C       MNOTE 8,'DGEN120 - SEGMENT &DB NOT IN DATABASE &DBN..'
&GB1(16) SETB  1
         MEXIT
.D       AIF   (&EXTDBN GT &EXTDB).E
&EXTDBN  SETA  &EXTDBN+1
         AIF   ('&DB' NE '&DBNAME(&EXTDBN)').D
         MEXIT
.E       AIF   (&EXTDBN GT &EXTDBR-1).F
&DBNAME(&EXTDBN)  SETC  '&DB'
&EXTDB   SETA  &EXTDBN
         MEXIT
.F       MNOTE 8,'EXT001 - EXTERNAL REFERENCE TABLE FULL. '
&GB1(16) SETB  1
         MEXIT
.*
.*
.*
.**********************************************************************
.*                                                                    *
.*       THIS MACRO SEARCHES THE EXTERNAL DATABASE TABLE FOR AN ENTRY *
.* CONTAINING THE SPECIFIED NAME AND, IF FOUND, THE MACRO EXITS WITH  *
.* THE GLOBAL SYMBOL &EXTDBN CONTAINING THE REFERENCED ENTRY. IF THE  *
.* SPECIFIED NAME IS NOT IN THE TABLE, IT IS ADDED AND THE TOTAL ENTRY*
.* COUNT IS ADJUSTED TO INCLUDE THE NEW ENTRY.                        *
.*                                                                    *
.*       THE MACRO IS ALSO USED BY THE DBDGEN MACRO TO FIND A SEGMENT *
.* IN THE &SN MATRIX AND SET THE GLOBAL SYMBOL &EXTDBN TO CONTAIN THE *
.*  OFFSET IN SEGTAB FOR THAT SEGMENT, AND SET THE HIGH-ORDER BYTE OF *
.* &EXTDBN TO CONTAIN 01.  AN ERROR MESSAGE IS PRODUCED AND &GB1(16)  *
.* IS SET IF THE SPECIFIED SEGMENT IS NOT FOUND.                      *
.*                                                                    *
.**********************************************************************
         MEND
