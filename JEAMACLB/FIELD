         MACRO
         FIELD &NAME=,&TYPE=C,&BYTES=,&START=
         GBLA  &S,&F1,&F2,&ORG,&SEGS,&CDNBR,&RTKEY             @BA44469
         GBLA  &DFL(10),&FB1(1000),&FT1(1000)
         GBLA  &SB(255),&SB1(255),&SFLD(255),&SLDP(255)
         GBLB  &IQFB,&GB1(16),&PRD(255),&FK1(1000)
         GBLC  &OVF(10),&FLDNAM(255),&SN(500),&FN1(1000)
         LCLA  &A,&N,&T,&CK,&PN
         LCLB  &SEQ,&MULT
.**********************************************************************
.**********************************************************************
.**                                                                  **
.**                         APARLOG 1.1.1                            **
.**                                                                  **
.**      PP49306  PREVENTS THE CODING OF AN INDEX DBD WITH A SEQ     **
.**               FIELD AND START NOT EQUAL TO ONE.                  **
.**      PP49150  FIXES FLD160 MSG AND ADDS MSGS FLD140 AND FLD145   **
.**                                                                  **
.**********************************************************************
.**********************************************************************
         AIF   (&GB1(1)).A
         MNOTE 8,'DBD001 - DBD CARD MUST OCCUR 1ST IN A DBDGEN. '
&GB1(16) SETB  1
.A       AIF   (&S GT 0).B
         MNOTE 8,'FLD100 - SEGM CARD MUST PRECEDE FIELD. '
&GB1(16) SETB  1
.B       AIF   ('&NAME(1)' EQ '' OR K'&NAME(1) GT 8).C
&SEQ     SETB  ('&NAME(2)' EQ 'SEQ')
&MULT    SETB  ('&NAME(3)' EQ 'M')
&GB1(13) SETB (&ORG EQ 7 AND &GB1(2) AND '&OVF(1)' NE '' AND NOT &MULT ,
               AND ((NOT &GB1(13) AND &F1 EQ 0) OR &GB1(13)))
         AIF   (NOT &SEQ AND N'&NAME GT 1).C
         AIF   (&MULT AND (&ORG EQ 11 OR &ORG EQ 12)).C2       @BA49150
         AIF   (&MULT AND &GB1(4)).C3        DOS COMPAT INVALID@BA49150
         AIF   (&MULT OR '&NAME(3)' EQ '' OR '&NAME(3)' EQ 'U').D
.C       MNOTE 8,'FLD110 - NAME OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
         AGO   .D             CHECK MULTIPLE SEQ FIELDS        @BA49150
.C2      MNOTE 8,'FLD140 - NON-UNIQUE KEYS REQUIRE AN OVERFLOW DATASET'
&GB1(16) SETB  1              SET QUIT BIT                     @BA49150
         AGO   .D             CHECK MULTIPLE SEQ FIELDS        @BA49150
.C3      MNOTE 8,'FLD145 - NON-UNIQUE KEYS WITH DOSCOMP ARE INVALID'
&GB1(16) SETB  1              SET QUIT BIT                     @BA49150
.D       AIF   (NOT &SEQ).F
         AIF   (NOT &GB1(5)).E
         AIF   (&GB1(9) AND &PRD(&S)).F      MULTIPLE SEQ FIELDS OK ?
         MNOTE 8,'FLD120 - SEGMENT &SN(&S) HAS MULTIPLE SEQUENCE FIELDS,
               .'
&GB1(16) SETB  1
.E       ANOP
&GB1(5)  SETB  1
.F       AIF   (NOT &GB1(9)).G          PHANTOM SEGMENT ?
         AIF   (&PRD(&S)).G             PAIRED ?
         MNOTE 8,'FLD210 - FIELD STMT IS INVALID FOR PRECEDING SEGM.'
&GB1(16) SETB  1
.G       AIF   (NOT &SEQ).H             SEQUENCE FIELD ?
         AIF   (NOT &GB1(7)).H          SEQ FIELDS ALLOWED ?
         MNOTE 8,'FLD220 - SEQUENCE FIELD INVALID FOR PRECEDING SEGM. '
&GB1(16) SETB  1
.H       ANOP
&N       SETA  &N+1
         AIF   (&N GT &F2).I
         AIF   (&N GT 255).J
         AIF   ('&NAME(1)' NE '&FLDNAM(&N)').H
         MNOTE 8,'FLD190 - DUPLICATE FIELD NAME IN SEGMENT &SN(&S)..'
&GB1(16) SETB  1
.I       ANOP
&F2      SETA  &N
&FLDNAM(&N)  SETC  '&NAME(1)'
         AGO   .K
.J       MNOTE 8,'FLD200 - SEGMENT FIELD LIMIT EXCEEDED. '
&GB1(16) SETB  1
.K       ANOP
&SFLD(&S) SETA &SFLD(&S)+1    INCREMENT NUMBER OF FIELDS IN SEGMENT
&T       SETA  1
         AIF   ('&TYPE' EQ 'X').L
&T       SETA  2
         AIF   ('&TYPE' EQ 'P').L
&T       SETA  3
         AIF   ('&TYPE' EQ '' OR '&TYPE' EQ 'C').L
         MNOTE 8,'FLD130 - TYPE OPERAND IS INVALID. '
&GB1(16) SETB  1
.L       AIF   (K'&NAME(1) LT 3).L4
   AIF ('&FLDNAM(&N)'(1,3) NE '/CK' AND '&FLDNAM(&N)'(1,3) NE '/SX').L4
         AIF   (NOT &SEQ).L1
         MNOTE 8,'FLD225 - SEQUENCE FIELD SPECIFICATION IS INVALID. '
&GB1(16) SETB  1
.L1      AIF   ('&FLDNAM(&N)'(1,3) NE '/CK').L2
     AIF (T'&BYTES EQ 'N' AND '&BYTES' GE '1' AND '&BYTES' LE '3825').M
.L2      AIF   ('&FLDNAM(&N)'(1,3) NE '/SX').L5
         AIF   (&F1+1 LE 1000).L3
         MNOTE 8,'FLD180 - FIELD SPECIFICATION LIMIT EXCEEDED. '
&GB1(16) SETB  1
&F1      SETA  &F1-1
.L3      ANOP
&F1      SETA  &F1+1
&FB1(&F1) SETA 4
         AGO   .X
.L4   AIF (T'&BYTES EQ 'N' AND '&BYTES' GE '1' AND '&BYTES' LE '256').M
         AIF   ('&BYTES' NE '' AND &IQFB).M
.L5      MNOTE 8,'FLD150 - BYTES OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
.M       AIF   ((&ORG NE 7 AND &ORG NE 15) OR NOT &SEQ).M1     @BA49150
         AIF   (&START EQ 1).M1        START EQUAL TO ONE?     @BA49150
         AGO   .M3            INDEX DBD WITH SEQ, START MUST EQ@BA49306
.M1      AIF   (K'&NAME(1) LT 3).M2                            @BA49306
         AIF   ('&FLDNAM(&N)'(1,3) NE '/CK').M2                @BA49306
     AIF (T'&START EQ 'N' AND '&START' GE '1' AND '&START' LE '3825').R
         AGO   .M3                                             @BA49306
.M2 AIF (T'&START EQ 'N' AND '&START' GE '1' AND '&START' LE '32767').N
         AIF   ('&START' NE '' AND &IQFB).N
.M3      MNOTE 8,'FLD160 - START OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
.N       AIF   (NOT &IQFB AND (T'&START NE 'N' OR T'&BYTES NE 'N')).R
         AIF   (&GB1(9) OR &CDNBR EQ 0).R
         AIF   (&START+&BYTES-1 GT &SB(&S)-&SB(&S)/65536*65536).Q
         AIF   ('&NAME(2)' NE 'SEQ').R
         AIF   (NOT &GB1(2) OR &SB1(&S)-&SB1(&S)/65536*65536 EQ 0).N1
         AIF   (&SB1(&S)/65536/256 GE 14).N1
         AIF   ((&SB1(&S)/65536/256-&SB1(&S)/65536/256/8*8)/4 EQ 0).N1
         AIF   (&START+&BYTES-1 GT &SB1(&S)-&SB1(&S)/65536*65536).Q
.N1      AIF   (&BYTES LT &DFL(&CDNBR)/65536).O
&DFL(&CDNBR) SETA (&DFL(&CDNBR)-&DFL(&CDNBR)/65536*65536)+&BYTES*65536
.O       AIF   (&DFL(&CDNBR)-&DFL(&CDNBR)/65536*65536 EQ 0).P
         AIF   (&BYTES GT &DFL(&CDNBR)-&DFL(&CDNBR)/65536*65536).R
.P       ANOP
&DFL(&CDNBR) SETA &DFL(&CDNBR)/65536*65536+&BYTES
         AGO   .R
.Q       MNOTE 8,'FLD170 - FIELD EXTENDS BEYOND DEFINED SEGMENT LENGTH'
&GB1(16) SETB  1
.R       ANOP
&F1      SETA  &F1+1
         AIF   (&F1 LE 1000).S
         MNOTE 8,'FLD180 - FIELD SPECIFICATION LIMIT EXCEEDED. '
&GB1(16) SETB  1
&F1      SETA  &F1-1
.S       AIF   (NOT &IQFB AND (T'&BYTES NE 'N' OR T'&START NE 'N')).W
&FB1(&F1) SETA (&START-1)*65536+&BYTES
         AIF  ((&ORG NE 7 AND &ORG NE 12) OR (&GB1(5) AND NOT &SEQ) OR ,
               NOT &GB1(5)).V
&A       SETA  &F1-1
.T       AIF   (&A EQ 0).V
         AIF   (&FK1(&A)).U
&A       SETA  &A-1
         AGO   .T
.U       AIF   (&FB1(&F1) EQ &FB1(&A)).V
         MNOTE 8,'FLD997 - START AND BYTES OPERANDS FOR EACH SEQUENCE '
         MNOTE *,'         FIELD IN A MULTI-INDEX DBD MUST EQUAL START'
         MNOTE *,'         AND BYTES OPERANDS OF EACH PREVIOUSLY      '
         MNOTE *,'         DEFINED SEGMENT SEQUENCE FIELD.            '
&GB1(16) SETB  1
.V       AIF   (&CDNBR NE 1 OR &S NE 1 OR NOT &SEQ).W
&RTKEY   SETA  &BYTES           SET ROOT KEY LENGTH
.W       AIF   (&GB1(8) EQ &SEQ OR NOT &SEQ).X
         MNOTE 8,'FLD230 - SEQUENCE FIELDS MUST IMMEDIATELY FOLLOW SEGM,
               .'
&GB1(16) SETB  1
.X       ANOP
&FN1(&F1) SETC '&NAME(1)'
&FK1(&F1) SETB (&SEQ)
&T       SETA  &T+&MULT*32
&FT1(&F1) SETA (&T*65536)+&S
&GB1(8)  SETB  (&SEQ)
         AIF   (NOT &GB1(16) AND K'&NAME(1) GE 3).Y
         MEXIT
.Y       AIF   ('&FN1(&F1)'(1,3) EQ '/CK').Z
         MEXIT
.Z       ANOP
&PN      SETA  &S
.AA      AIF   (NOT &FK1(&SFLD(&PN)/65536)).BB
&CK      SETA  &CK+(&FB1(&SFLD(&PN)/65536)-&FB1(&SFLD(&PN)/65536)/65536,
               *65536)
.BB      ANOP
&PN      SETA  &SLDP(&PN)/1048576
         AIF   (&PN NE 0).AA
         AIF   (&START+&BYTES-1 LE &CK).CC
         MNOTE 8,'FLD999 - START+BYTES EXCEEDS CONCATENATED KEY LENGTH'
         MNOTE *,'         CONCATENATED KEY LENGTH = &CK '
&GB1(16) SETB  1
.CC      ANOP
&FB1(&F1) SETA (&START-1)*65536+&BYTES
         MEXIT
         MEND
