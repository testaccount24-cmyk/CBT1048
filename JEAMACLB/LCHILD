         MACRO
         LCHILD &NAME=,&POINTER=,&PTR=,&PAIR=,&INDEX=,&RULES=
         GBLA  &S,&SLC,&ORG,&EXTDBN,&LCWIC,&MT                 @BA41949
         GBLA  &SB(255),&SF1(255),&IB(255)
         GBLA  &SLCF(255),&SLCS(255),&SLDP(255)
         GBLB  &GB1(16),&QUITB
         GBLB  &PARNT(255),&SYMB(255),&SCIDXSW                 @BA46262
         GBLC  &DBN,&SI(255),&SN(500)
         GBLC  &SLCN(255),&SLCP(255)
         LCLA  &N
         LCLC  &C,&P
.**********************************************************************
.**********************************************************************
.**                     APARLOG 1.1.1                                **
.**                                                                  **
.**      PP36120  PREVENT LCHILD CARD FOR SECONDARY INDEX FOLLOWING  **
.**               ITS ASSOCIATED XDFLD CARD.                         **
.**      PP46262  ALLOWES SYMBOLIC POINTING TO PASS FOR SECONDARY    **
.**               INDEXS. MSG XDFLD002 ISSUED IN ERROR.              **
.**                                                                  **
.**********************************************************************
.**********************************************************************
         AIF   (&GB1(1)).A
         MNOTE 8,'DBD001 - DBD CARD MUST OCCUR 1ST IN A DBDGEN. '
&GB1(16) SETB  1
.A       AIF   (&S GT 0).B
         MNOTE 8,'LCHD100 - SEGM STATEMENT MUST PRECEDE LCHILD STMT.'
&GB1(16) SETB  1
&S       SETA  1
.B       AIF   (&ORG EQ 11).AA
&SLC     SETA  &SLC+1
         AIF   (&SLC LE 255).C
         MNOTE 8,'LCHD110 - LCHILD SPECIFICATION LIMIT EXCEEDED. '
&GB1(16) SETB  1
&SLC     SETA  &SLC-1
.C       AIF   (K'&NAME(1) LE 8 AND N'&NAME LE 2 AND '&NAME(1)' NE '' A,
               ND K'&NAME(2) LE 8).D
         MNOTE 8,'LCHD120 - NAME OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
.D       AIF   ('&NAME(2)' EQ '').E
         ALPHA FIELD=&NAME(2)
         AIF   (NOT &QUITB AND ('&NAME(2)' NE '&DBN' OR ('&POINTER' NE ,
               'INDX' AND '&PTR' NE 'INDX'))).E
         MNOTE 8,'LCHD130 - DATABASE NAME PARAMETER IS INVALID. '
&GB1(16) SETB  1
.E       AIF   (NOT &GB1(6)).F
         MNOTE 8,'LCHD180 - SEGMENTS WITH A LOGICAL PARENT CANNOT '
         MNOTE *,'          ALSO HAVE LOGICAL CHILDREN. '
&GB1(16) SETB  1
.F       ANOP
         AIF   ('&POINTER' NE 'INDX' AND '&PTR' NE 'INDX').F5  @BA41949
         AIF   (&ORG NE 6 AND &ORG NE 14 ).F3   NOT HIDAM      @BA44469
         AIF   (&S NE 1).F3            NOT A ROOT SEGMENT      @BA44469
&LCWIC   SETA  &LCWIC+1     UPDATE LCHILD W/PTR=INDX COUNT     @BA41949
         AIF   (&LCWIC EQ 1).F5    FIRST LCHILD W/PTR=INDX?    @BA41949
.F3      ANOP
&MT      SETA  10        INDICATE LCHILD FOR SECONDARY INDEX   @BA41949
.F5      ANOP
         AIF   ('&PTR' NE '' AND '&POINTER' NE '').I           @BA41949
&P       SETC  '&PTR'
         AIF   ('&POINTER' EQ '').G
&P       SETC  '&POINTER'
.G       AIF   ((&ORG EQ 7 OR &ORG EQ 12) AND '&INDEX' NE '' AND '&P' N,
               E 'SNGL' AND '&P' NE 'SYMB' AND '&P' NE '').I
         AIF   ((&ORG LE 2 OR &ORG EQ 11) AND '&P' EQ 'INDX').I    1331
.*                                      PTR=INDX INVALID  FOR HISAM
  AIF (NOT &GB1(2) AND (&ORG EQ 7 OR &ORG EQ 12) AND '&P' EQ 'SYMB').I
&SYMB(&S) SETB ('&P' EQ 'SYMB')
&GB1(12) SETB  (&GB1(12) OR (&ORG EQ 6 AND &S EQ 1 AND '&P' EQ 'INDX'))
&GB1(12) SETB  (&GB1(12) OR (&ORG EQ 7 AND &S EQ 1))
&GB1(12) SETB  (&GB1(12) OR (&ORG EQ 12 AND &S EQ 1))
         AIF   ('&P' NE 'INDX' AND NOT &SYMB(&S)).GX EXIT      @BA46262
         AIF   (&ORG NE 6).G1 NOT HIDAM SET SEC SWITCH         @BA46262
&N       SETA  &SLDP(&S)/1048576  PARENTS SEGMENT NUMBER       @BA46262
         AIF   (&N GT 1).G1   NOT A ROOT SEGMENT               @BA46262
&LCWIC   SETA  &LCWIC+1       UPDATE SEC LCHILD COUNT          @BA46262
         AIF   (&LCWIC EQ 1).GX  FIRST LCHILD FOR SEC INDEX    @BA46262
.G1      ANOP                 SET SWITCH FOR SECONDARY INDEX   @BA46262
&SCIDXSW SETB  1              LCHILD FOR SECONDARY INDEX       @BA46262
.GX      AIF   (&SYMB(&S)).J                                   @BA46262
         AIF   ((&ORG NE 7 AND &ORG NE 12) OR '&INDEX' EQ '').H
&P       SETC  'SNGL'
         AGO   .J
.H       AIF   ('&P' EQ '' OR '&P' EQ 'INDX' OR '&P' EQ 'DBLE' OR '&P' ,
               EQ 'SNGL' OR '&P' EQ 'NONE').J
.I       MNOTE 8,'LCHD140 - POINTER OR PTR OPERAND IS INVALID. '
&GB1(16) SETB  1
.J       AIF   ('&PAIR' EQ '' OR K'&PAIR LE 8).K
         MNOTE 8,'LCHD150 - PAIR OPERAND IS INVALID. '
&GB1(16) SETB  1
.K       AIF   (NOT &GB1(9)).L
         MNOTE 8,'LCHD170 - LCHILD STMT INVALID FOR LOGICAL SEGMENTS. '
&GB1(16) SETB  1
.L       AIF   (&PARNT(&S)).N
         AIF   (&ORG LE 4 OR &S EQ 1).N
         AIF   ((&ORG EQ 7 OR &ORG EQ 12) AND &S GT 1).N
&PARNT(&S) SETB 1
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF POINTER
&SF1(&S) SETA  &SF1(&S)+4096       SET PARENT POINTER FLAG
&N       SETA  &SLDP(&S)/1048576   PARENT SEGMENT NUMBER
.M       AIF   (&N LE 1).N         ROOT SEGMENT ?
         AIF   (&PARNT(&N)).N      PARENT PTR REQUIRED ?
&PARNT(&N) SETB 1
&SB(&N)  SETA  &SB(&N)+4*65536
&SF1(&N) SETA  &SF1(&N)+4096
         SEGPTRS &N                CHECK SEGMENT SIZE
&N       SETA  &SLDP(&N)/1048576   NEXT HIGHER PARENT
         AGO   .M
.N       ANOP
&N       SETA  0
&C       SETC  '&DBN'              CURRENT DATABASE
&SLCN(&SLC)  SETC  '&NAME(1)'
         AIF   ('&NAME(2)' EQ '').O
         EXTDBD &NAME(2)
&SLCF(&SLC)  SETA  &EXTDBN*1048576
&C       SETC  '&NAME(2)'          REFERENCED DATABASE
.O       AIF   ('&NAME(1)' NE '&SN(&S)' OR '&C' NE '&DBN').P
         MNOTE 8,'LCHD200 - REFERENCED LCHILD CANNOT PRECEDE LCHILD STM,
               T.'
&GB1(16) SETB  1
.P       AIF   ('&P' NE '' AND '&P' NE 'NONE').Q
         AIF   (&SF1(&S)/32768 EQ 1).S  PARENT HAS CTR ?
&SF1(&S) SETA  &SF1(&S)+32768           SET CTR FLAG
&SB(&S)  SETA  &SB(&S)+4*65536          ADD SIZE TO PREFIX
         AGO   .S
.Q     AIF (&GB1(2) AND (&ORG EQ 7 OR &ORG EQ 12) AND '&P' EQ 'SYMB').S
&N       SETA  4
         AIF   ('&P' NE 'DBLE').R
&N       SETA  1
&SB(&S)  SETA  &SB(&S)+8*65536          ADD SIZE OF POINTER
         AGO   .S
.R       AIF   ('&P' NE 'SNGL').S
&N       SETA  2
&SB(&S)  SETA  &SB(&S)+4*65536          ADD SIZE OF POINTER TO PREFIX
.S       SEGPTRS &S                     CHECK SEGMENT SIZE
&SLCF(&SLC)  SETA  &SLCF(&SLC)+(&S*256)+&N
&SLCP(&SLC) SETC '&PAIR'
&SLCS(&S)   SETA &SLCS(&S)+1
         AIF   ('&INDEX' EQ '').Y
         AIF   (&ORG NE 7 AND &ORG NE 12).X
         AIF   ('&INDEX(1)' EQ '' OR K'&INDEX(1) GT 8).X
         AIF   (N'&INDEX GT 2 OR '&PAIR' NE '').X
&SI(&SLC) SETC '&INDEX(1)'
&SLCF(&SLC) SETA &SLCF(&SLC)/256*256
&SLCF(&SLC) SETA &SLCF(&SLC)+32          SET INDEX ENTRY FLAG
         AIF   ('&INDEX(2)' NE '').U
         MEXIT
.U       AIF   ('&INDEX(2)' NE 'ZERO').V
&SLCF(&SLC) SETA &SLCF(&SLC)+2          ZERO ENTRIES NOT INDEXED
         MEXIT
.V       AIF   ('&INDEX(2)' NE 'BLANK').W
&SLCF(&SLC) SETA &SLCF(&SLC)+4          BLANK ENTRIES NOT INDEXED
&IB(&SLC) SETA 64
         MEXIT
.W       AIF   (T'&INDEX(2) NE 'N').X
         AIF   (K'&INDEX(2) LT 4 OR K'&INDEX(2) GT 5).X
         AIF   ('&INDEX(2)'(1,1) NE 'X').X
&SLCF(&SLC) SETA &SLCF(&SLC)+1          SET NON-INDEX CHARACTER FLAG
&IB(&SLC) SETA &INDEX(2)                SET NON-INDEX CHARACTER
         MEXIT
.X       MNOTE 8,'LCHD160 - INDEX OPERAND IS INVALID. '
&GB1(16) SETB  1
         MEXIT
.Y       ANOP
&N       SETA  4096
         AIF   ('&RULES' EQ 'FIRST').Z
&N       SETA  8192
         AIF   ('&RULES' EQ '' OR '&RULES' EQ 'LAST').Z
&N       SETA  12288
         AIF   ('&RULES' EQ 'HERE').Z
         MNOTE 4,'LCHD190 - RULES OPERAND IS INVALID; DEFAULT ASSUMED.'
&N       SETA  256
.Z       ANOP
&IB(&SLC) SETA &N
         MEXIT
.AA      MNOTE 8,'LCHD210 - LCHILD STATEMENT IS INVALID. '
&GB1(16) SETB  1
         MEXIT
         MEND
