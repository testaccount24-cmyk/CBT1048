         MACRO
         ENDARRAY
.*
.*       NORMAL FIELD DEFINITION ARRAYS
.*
         GBLA  &SCRNDS             LINKAGE SECTION NUMBER
         GBLA  &SCINDEX            GLOBAL INDEX FOR FOLLOWING:
         GBLC  &SCARRY1(256)       NAME OF THE FIELD
         GBLA  &SCARRY2(256)       LENGTH OF THE FIELD
         GBLA  &SCARRY3(256)       OFFSET OF THE FIELD
         GBLA  &SCARRY4(256)       LINKAGE INDEX NUMBER OF FIELD
.*
.*       ARRAY FIELD ADDITIONAL GLOBAL ARRAYS
.*
         GBLA  &AROFFST(256)       START POSITION OF THE ARRAY
         GBLA  &ARLENTH(256)       LENGTH OF AN ARRAY ENTRY
         GBLA  &SCARRAY(256)       INDEX TO ARRAY FOR THIS FIELD
         GBLA  &SCAROFF(256)       OFFSET WITHIN THE ARRAY
         GBLA  &ARINDEX            GLOBAL ARRAY INDEX FOR ARRAY ENTRIES
         GBLB  &ARSW               IF ON - WE ARE PART OF AN ARRAY
         GBLA  &AOCCURS(256)       OCCURS COUNT FOR ARRAY
.*
.*       LOCAL WORK DEFINITIONS
.*
         LCLA  &WORKA,&WORKB
.*
.*       SET THE 'LOCATION COUNTER' TO END OF ARRAY
&WORKA   SETA  &AROFFST(&ARINDEX)+&AOCCURS(&ARINDEX)*&ARLENTH(&ARINDEX)
&SCINDEX SETA  &SCINDEX+1          GENERATE A DUMMY FIELD
&SCARRY1(&SCINDEX) SETC 'NULL'     CALLED 'NULL'
&SCARRY2(&SCINDEX) SETA 0          OF ZERO LENGTH
&SCARRY3(&SCINDEX) SETA &WORKA     AND AN OFFSET TO END OF ARRAY
&SCARRY4(&SCINDEX) SETA &SCRNDS    AND IN THE CURRENT LINKAGE
&ARSW    SETB  0                   SET OFF THE ARRAY FLAG
         MEND
