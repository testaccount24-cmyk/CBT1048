         MACRO
&LABEL   SETSCAN &DATA,&LENGTH,&DELETE
         LCLC  &WORKC,&WORKC2
&WORKC2  SETC  'L'''
&LABEL   DS    0H
         AIF   (T'&DELETE EQ 'O').NODEL
         AIF   ('&DELETE' EQ 'DELETE').DELETE
         MNOTE 4,'INVALID OPTION - &DELETE - ''DELETE'' ASSUMED'
.DELETE  ANOP
         MVC   $WKSAVE,$SCANPTR    SAVE SCAN POINTER
         SETSCAN &DATA,&LENGTH     LOCATE END OF DATA
         MVC   $SCANPTR,$WKSAVE    RESTORE SCAN POINTER
         DELETE ,                  DELETE THE DATA
         MEXIT
.NODEL   ANOP
         AIF   (T'&DATA EQ 'O').LOADLEN
.TESTDA  AIF   ('&DATA' EQ 'START').START
         AIF   ('&DATA' EQ 'END').END
         AIF   ('&DATA' EQ 'NEXT').NEXT
         AIF   ('&DATA'(1,1) NE '''').DATALD
&WORKC   SETC  'IHB&SYSNDX'
         BAL   14,&WORKC           JUMP AROUND ARGUMENT
&WORKC.D DC    C&DATA
&WORKC   DS    0H
         AGO   .LOADLEN
.DATALD  AIF   ('&DATA'(1,1) EQ '(').DATAREG
         LA    14,&DATA            LOAD ADDRESS OF ARGUMENT
         AGO   .LOADLEN
.DATAREG LA    14,0&DATA           LOAD ADDRESS OF ARGUMENT
         AGO   .LOADLEN
.LOADLEN AIF   (T'&LENGTH NE 'O').LENHERE
         AIF   (T'&DATA EQ 'O').CALL
.TESTLEN AIF   ('&DATA'(1,1) NE '(').LATTRIB
         MNOTE *,'NO LENGTH AVAILABLE FOR DATA. REG15 ASSUMED'
         AGO   .CALL
.LATTRIB AIF   ('&DATA'(1,1) NE '''').NOTLIT
         LA    15,&WORKC2&WORKC.D  LOAD LENGTH OF SCAN LITERAL
         AGO   .CALL
.NOTLIT  LA    15,&WORKC2&DATA     LOAD LENGTH OF SCAN DATA
         AGO   .CALL
.LENHERE AIF   ('&LENGTH'(1,1) EQ '(').LENREG
         LA    15,&LENGTH.(0,0)    LOAD LENGTH OF DATA
         AGO   .CALL
.LENREG  LA    15,0&LENGTH         LOAD LENGTH OF DATA
         AGO   .CALL
.CALL    L     11,JCLSCAN          LOAD ADDRESS OF ROUTINE
         BALR  11,11               CALL THE SCAN ROUTINE
         MEXIT
.START   XC    $SCANPTR,$SCANPTR   RESET SCAN TO START OF RECORD
         XR    1,1                 CLEAR LENGTH MOVED REGISTER
         MEXIT
.END     L     15,$RECEND          LOAD LAST BYTE OF DATA ADDRESS
         LA    15,1(0,15)          LOAD ADDRESS OF NEXT BYTE AFTER
         S     15,$RECADD          CALCULATE NEW SCAN OFFSET
         ST    15,$SCANPTR         STORE OFFSET IN SCANPTR
         MEXIT
.NEXT    L     15,$SCANPTR         LOAD CURRENT SCAN POINTER
         AIF   ('&LENGTH'(1,1) EQ '(').NEXTREG
         LA    15,&LENGTH.(0,15)   BUMP UP SCANPOINTER
         AGO   .STORIT
.NEXTREG LA    15,0&LENGTH         BUMP UP SCANPOINTER
         AGO   .STORIT
.STORIT  ST    15,$SCANPTR         UPDATE SCAN POINTER
         MEND
