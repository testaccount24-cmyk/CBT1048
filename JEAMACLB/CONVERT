         MACRO
         CONVERT &VALUE=,&DIGITS=,&TO=
         GBLA  &HEX
         GBLB  &QUITB
         GBLC  &EBCDIC
         LCLA  &A,&B,&C(8),&D
         LCLC  &CA
&EBCDIC  SETC  ''
&QUITB   SETB  0
&HEX     SETA  0
         AIF   ('&TO' EQ 'HEX').TOHEX
         AIF   ('&TO' EQ 'EBCDIC').TOEBCDC
         MNOTE 0,'*** CONVERT ''TO'' VALUE NOT SPECIFED OR INVALID'
&QUITB   SETB  1
         MEXIT
.TOEBCDC ANOP
&A       SETA  &VALUE
&B       SETA  &DIGITS+1
&D       SETA  &A/65536           SHIFT RIGHT 16 POSITIONS.
&C(8)    SETA  &D/4096            SHIFT RIGHT 12 POSITIONS.
&C(7)    SETA  &D/256-16*&C(8)  SHIFT D RIGHT 8 & C LEFT 4-GET 4 ZEROS.
&C(6)    SETA  &D/16-&D/256*16  SHIFT D RIGHT 8 & 4 MORE TO GET 4 0'S.
&C(5)    SETA  &D-&D/16*16
&C(4)    SETA  &A/4096-16*&D
&C(3)    SETA  &A/256-&A/4096*16
&C(2)    SETA  &A/16-&A/256*16
&C(1)    SETA  &A-&A/16*16
.BACK    AIF   (&B EQ 1).FINI
&B       SETA  &B-1
&EBCDIC  SETC  '&EBCDIC'.'0123456789ABCDEF'(&C(&B)+1,1)
         AGO   .BACK
.TOHEX   AIF   (&B EQ &DIGITS).FINI
&B       SETA  &B+1
&CA      SETC  '&VALUE'(&B,1)
         AIF   ('&CA' GE '0').ZERTO9
         AIF   ('&CA' EQ 'A').AX
         AIF   ('&CA' EQ 'B').BX
         AIF   ('&CA' EQ 'C').CX
         AIF   ('&CA' EQ 'D').DX
         AIF   ('&CA' EQ 'E').EX
         AIF   ('&CA' EQ 'F').FX
         MNOTE 0,'*** CONVERT VALUE=&VALUE IS NOT CONVERTABLE'
&QUITB   SETB  1
         MEXIT
.ZERTO9  ANOP
&HEX     SETA  &HEX*16+&CA
         AGO   .TOHEX
.AX      ANOP
&HEX     SETA  &HEX*16+X'A'
         AGO   .TOHEX
.BX      ANOP
&HEX     SETA  &HEX*16+X'B'
         AGO   .TOHEX
.CX      ANOP
&HEX     SETA  &HEX*16+X'C'
         AGO   .TOHEX
.DX      ANOP
&HEX     SETA  &HEX*16+X'D'
         AGO   .TOHEX
.EX      ANOP
&HEX     SETA  &HEX*16+X'E'
         AGO   .TOHEX
.FX      ANOP
&HEX     SETA  &HEX*16+X'F'
         AGO   .TOHEX
.FINI    ANOP
         MEXIT
         MEND
