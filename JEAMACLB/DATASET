         MACRO
&DMAN    DATASET  &L,&DD1=,&DD2=,&DEVICE=,&DEVADDR=,&OVFLW=,           *
               &BLOCK=,&SIZE=,&RECORD=,&SCAN=,&RECFM=,&FRSPC=,&MODEL=
         GBLA  &ORG,&SEGS,&RAPS,&DNBR,&CDNBR,&LCWIC,&MT        @BA41949
         GBLA  &REC(10),&BLK(10),&OREC(10),&OBLK(10),&FRS(10),&SCN(10)
         GBLA  &TRK(10),&TRK1(10),&THRD(10),&CYL(10),&DAE(10),&DLEV(10)
         GBLB  &GB1(16),&GB2(16),&GB3(16),&GB4(16),&QUITB      @BI5ZZ
         GBLC  &ACC,&DEV(10),&DMN(10),&DS1(10),&DS2(10),&OVF(10)
         LCLA  &N,&X,&Y,&Z,&TRAPS                              @BA26083
         LCLB  &KWS,&END,&SSI,&NSIZE                           @BA38223
         LCLC  &C
.*
.*
.**********************************************************************
.**********************************************************************
.**                                                                  **
.**                                                                  **
.**                            DATASET                               **
.**                                                                  **
.**       EACH DATASET STATEMENT DEFINES A DATA SET GROUP WITHIN     **
.**       A DATA BASE.  AT LEAST ONE DATASET STATEMENT IS REQUIRED   **
.**       FOR EACH DBD GENERATION.  THE MAXIMUM NUMBER DEPENDS ON    **
.**       THE TYPE OF DATA BASE BEING DEFINED.                       **
.**       HSAM SHSAM SHISAM INDEX AND LOGICAL DATA BASES CAN HAVE    **
.**       ONLY ONE DATA SET GROUP.  HISAM HDAM AND HIDAM DATA        **
.**       BASES CAN BE DIVIDED INTO 1 TO 10 DATA SET GROUPS          **
.**       SUBJECT TO SPECIAL RULES IN THE UTILITIES MANUAL.          **
.**                                                                  **
.**********************************************************************
.**********************************************************************
.**                                                                  **
.**                      APARLOG REL 1.1.1                           **
.**                                                                  **
.**      PP38223  CORRECTS THE CALCULATION OF WRONG DBDLRECL FOR     **
.**              3330 AND 3340 DA DEVICES                            **
.**                                                                  **
.**********************************************************************
&MT      SETA  2              INDICATE MACRO TYPE - DATASET    @BA38223
         AIF   (&GB1(1)).A
         MNOTE 8,' DBD001 - DBD CARD MUST OCCUR 1ST IN A DBDGEN. '
&GB1(16) SETB  1
.A       ANOP
&KWS     SETB  ('&DD1' NE '' OR '&DD2' NE '' OR '&DEVICE' NE '' OR '&DE*
               VADDR' NE '' OR '&OVFLW' NE '' OR '&BLOCK' NE '')
&KWS     SETB  (&KWS OR '&SIZE' NE '' OR '&RECORD' NE '' OR '&SCAN' NE *
               '' OR '&FRSPC' NE '' OR '&MODEL' NE '')
         AIF   ('&DMAN' EQ '' AND NOT &KWS).B                  @BA32813
         AIF   ('&DMAN' EQ '').F
         AIF   (K'&DMAN GT 8).D
         ALPHA FIELD=&DMAN
         AIF   (&QUITB).D
.B       ANOP
&N       SETA  &N+1
         AIF   (&N GT &DNBR).E
         AIF   ('&DMAN' NE '&DMN(&N)').B
         AIF   ('&L' NE '' OR &KWS).C
&CDNBR   SETA  &N        RE-ESTABLISH CURRENT DATASET GROUP.
         MEXIT
.C       MNOTE 8,'DMAN180 - DATASET NAME IS A DUPLICATE. '
&GB1(16) SETB  1
         AGO   .F
.D       MNOTE 8,'DMAN190 - DATASET NAME FIELD IS INVALID. '
&GB1(16) SETB  1
         AIF   ('&L' NE '' OR &KWS).F
         MEXIT
.E       AIF   ('&L' NE '' OR &KWS).F
         MNOTE 8,'DMAN250 - DATASET NOT PREVIOUSLY ESTABLISHED. '
&GB1(16) SETB  1
         MEXIT
.F       ANOP                                                  @BA22679
&TRAPS   SETA  &RAPS          SAVE OFF RAPS VALUE.             @BA26083
         AIF   ('&L' EQ '').H                                  @BA26083
         AIF   (&ORG EQ 3).F1           IS ACCESS METHOD LOGICAL   1503
         MNOTE 8,'DMAN215 - ACCESS METHOD LOGICAL MUST BE CODED FOR A  *
               LOGICAL DATASET.'                                   1503
&GB1(16) SETB  1                                                   1503
&CDNBR   SETA  0         ASSUME LOGICAL DATASET.
.F1      AIF   ('&L' NE 'LOGICAL' OR &KWS).G                       1503
         MEXIT
.G       MNOTE 4,'DMAN220 - POSITIONAL OPERAND ASSUMES LOGICAL DATASET'
         AIF   (NOT &KWS).G1
         MNOTE 8,'DMAN230 - KEYWORD SPECIFICATIONS ARE INVALID  '
         MNOTE *,'          FOR A LOGICAL DATA SET DEFINITION. '
&GB1(16) SETB  1
.G1      MEXIT
.H       ANOP
&DNBR    SETA  &DNBR+1
         AIF   (&DNBR EQ 1).H2  FIRST DATA SET IN GROUP?       @BA41949
&LCWIC   SETA  0              RESET LCHILD W/PTR=INDX INDICATOR@BA41949
.H2      ANOP
         AIF   (&DNBR LE 10).I
&DNBR    SETA  &DNBR-1
         MNOTE 8,'DMAN100 - DATASET SPECIFICATION LIMIT EXCEEDED. '
&GB1(16) SETB  1
.I       AIF   ('&DD1' EQ '' OR K'&DD1 GT 8).J
         ALPHA FIELD=&DD1
         AIF   (NOT &QUITB).K
.J       MNOTE 8,'DMAN110 - DD1 OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
.K       ANOP
&DS1(&DNBR) SETC '&DD1'
&DMN(&DNBR) SETC '&DMAN'
         AIF   (&ORG EQ 16 AND '&DD2' NE '').L                 @BI5ZZ
         AIF   (&ORG EQ 16).T                                  @BI5ZZ
         AIF   (&ORG NE 1 AND &ORG NE 3 AND &ORG NE 4 AND &ORG NE 7    *
               AND &ORG NE 16).O
         AIF   (&ORG EQ 1 AND NOT &GB1(2)).O
         AIF   ((&ORG EQ 1 OR &ORG EQ 7) AND &DNBR EQ 1).O
         AIF   (&DNBR EQ 1).L
         MNOTE 8,'DMAN100 - DATASET SPECIFICATION LIMIT EXCEEDED. '
&GB1(16) SETB  1
         AIF   (&ORG EQ 1 OR &ORG EQ 7).O
.L       AIF   ('&DD2' EQ '' OR K'&DD2 GT 8).M
         ALPHA FIELD=&DD2
         AIF   (NOT &QUITB).N
.M       MNOTE 8,'DMAN120 - DD2 OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
.N       ANOP
&DS2(&DNBR) SETC '&DD2'
         AIF   ('&OVFLW' EQ '').T
         MNOTE 8,'DMAN130 - OVFLW AND DD2 OPERANDS ARE CONFLICTING. '
&GB1(16) SETB  1
         AGO   .T
.O       AIF   (T'&DD2 NE 'O').M        DD2 ONLY VALID FOR HSAM    1503
         AIF   (&ORG EQ 2).P                                       1503
         AIF   (&ORG GT 1 AND &ORG LT 7).T
         AIF   (&DNBR EQ 1 AND NOT &GB1(2)).P
 AIF ((&ORG NE 1 AND &ORG NE 11) OR (&ORG EQ 1 AND T'&OVFLW NE 'O')).O1
&SEGS    SETA  &SEGS-&SEGS*&GB1(2)+&GB1(2)
         AIF   (&ORG EQ 1 AND &GB1(2)).T
.O1      AIF   (T'&OVFLW EQ 'O' AND &ORG EQ 11).T
         AIF   (T'&OVFLW NE 'O' AND &ORG EQ 11).R
&SSI     SETB  (T'&OVFLW EQ 'O' AND &ORG EQ 7 AND &GB1(2))
&ORG     SETA  &ORG+5*&SSI
         AIF   (&SSI).T
         AIF   (&DNBR EQ 1).P
&ORG     SETA  2
.P       AIF   ('&OVFLW' EQ '' OR K'&OVFLW GT 8).R
         AIF   ('&DD2' EQ '').Q
         MNOTE 8,'DMAN130 - OVFLW AND DD2 OPERANDS ARE CONFLICTING. '
&GB1(16) SETB  1
.Q       ALPHA FIELD=&OVFLW
         AIF   (NOT &QUITB).S
.R       MNOTE 8,'DMAN140 - OVFLW OPERAND IS OMITTED OR INVALID. '
&GB1(16) SETB  1
.S       ANOP
&OVF(&DNBR) SETC '&OVFLW'
.T       ANOP
&C       SETC  '&DS1(&DNBR)'
         AIF   ('&C' EQ '').AF
&X       SETA  &DNBR-1
&Y       SETA  &DNBR
&Z       SETA  &DNBR
.X       AIF   ('&C' EQ 'IEFRDER' OR '&C' EQ 'IMSLOGR' OR '&C' EQ 'QBLK*
               S' OR '&C' EQ 'SHMSG' OR '&C' EQ 'LGMSG' OR '&C' EQ 'IMS*
               ACB' OR '&C' EQ 'IMSDILIB').AE
         AIF   ('&C' EQ 'IMSSPA' OR '&C' EQ 'IMSDBL' OR '&C' EQ        *
               'SYSUDUMP' OR '&C' EQ 'STEPLIB').AE
.Y       AIF   (&X EQ 0).Z
         AIF   ('&C' EQ '&DS1(&X)').AD
&X       SETA  &X-1
         AGO   .Y
.Z       AIF   (&Y EQ 0).AA
         AIF   ('&C' EQ '&DS2(&Y)').AD
&Y       SETA  &Y-1
         AGO   .Z
.AA      AIF   (&Z EQ 0).AB
         AIF   ('&C' EQ '&OVF(&Z)').AD
&Z       SETA  &Z-1
         AGO   .AA
.AB      AIF   (&END).AF
         AIF   ('&C' EQ '&DS2(&DNBR)').AC
&C       SETC  '&DS2(&DNBR)'
&X       SETA  &DNBR
&Y       SETA  &DNBR-1
&Z       SETA  &DNBR
         AIF   ('&C' NE '').X
.AC      ANOP
&C       SETC  '&OVF(&DNBR)'
&X       SETA  &DNBR
&Y       SETA  &DNBR
&Z       SETA  &DNBR-1
&END     SETB  1
         AIF   ('&C' NE '').X
         AGO   .AF
.AD      MNOTE 8,'DMAN170 - ENTERED DDNAME IS A DUPLICATE - &C..'
&GB1(16) SETB  1
         AGO   .AF
.AE      MNOTE 8,'DMAN260 - ENTERED DDNAME IS RESERVED - &C..'
&GB1(16) SETB  1
.AF      AIF   ('&FRSPC' EQ '').AI
         AIF   (N'&FRSPC GT 2).AH
         AIF   ('&FRSPC(1)' EQ '').AG
         AIF   (T'&FRSPC(1) NE 'N').AH
         AIF   (&FRSPC(1) EQ 0).AG
         AIF   (&FRSPC(1) LT 2 OR &FRSPC(1) GT 100).AH
&FRS(&DNBR) SETA &FRSPC(1)*256
.AG      AIF   ('&FRSPC(2)' EQ '').AI
         AIF   (T'&FRSPC(2) NE 'N').AH
         AIF   (&FRSPC(2) EQ 0).AI
         AIF   (&FRSPC(2) LT 1 OR &FRSPC(2) GT 99).AH
&FRS(&DNBR) SETA &FRS(&DNBR)+&FRSPC(2)
         AGO   .AI
.AH      MNOTE 8,'DMAN290 - FRSPC OPERAND IS INVALID. '
&GB1(16) SETB  1
.AI      ANOP
&SCN(&DNBR) SETA 3            SET DEFAULT CYLINDER SCAN.
         AIF   ('&SCAN' EQ '').AL
         AIF   (T'&SCAN EQ 'N').AK
.AJ      MNOTE 8,'DMAN280 - SCAN OPERAND IS INVALID. '
&GB1(16) SETB  1
         AGO   .AL
.AK      AIF   (&SCAN GT 255).AJ
&SCN(&DNBR) SETA &SCAN
.AL      AIF   ((&ORG EQ 3 OR &ORG EQ 4) AND '&SIZE' NE '').AM3
 AIF (&ORG GT 4 AND &ORG LT 7 AND '&BLOCK' NE '' AND '&SIZE' NE '').AM3
         AIF   ((&ORG LE 2 OR &ORG GE 7) AND (('&SIZE(1)' NE '' AND '&B,
               LOCK(1)' NE '') OR ('&SIZE(2)' NE '' AND '&BLOCK(2)' NE ,
               ''))).AM3
         AIF   (&ORG EQ 16).AU                                 @BI5ZZ
&GB2(&DNBR)  SETB  ('&SIZE(1)' NE '')
&GB3(&DNBR)  SETB  ('&SIZE(2)' NE '')
    AIF ((&ORG LE 2 OR &ORG GE 7) AND (&GB2(&DNBR) OR &GB3(&DNBR))).AM1
         AIF   ((&ORG EQ 5 OR &ORG EQ 6) AND &GB2(&DNBR)).AM2
         AIF   (N'&BLOCK GT 2).AN
         AIF   ('&BLOCK' EQ '').AO
         AIF   ('&BLOCK(1)' EQ '').AM
         AIF   (T'&BLOCK(1) NE 'N').AN
&BLK(&DNBR) SETA &BLOCK(1)
.AM      AIF   ('&BLOCK(2)' EQ '').AO
         AIF   (T'&BLOCK(2) NE 'N').AN
&OBLK(&DNBR) SETA &BLOCK(2)
         AGO   .AO
.AM1     AIF   (N'&SIZE GT 2 OR (&GB2(&DNBR) AND T'&SIZE(1) NE 'N') OR ,
               (&GB3(&DNBR) AND T'&SIZE(2) NE 'N')).AM3
         AIF   (NOT &GB2(&DNBR)).AM1A
         AIF   (&GB1(2) AND &SIZE(1) GT 30720).AM3             @BA23432
&BLK(&DNBR)  SETA  &SIZE(1)
         AIF (NOT &GB1(2) OR &SIZE(1)-(&SIZE(1)+511)/512*512 EQ 0).AM1A
&BLK(&DNBR)  SETA  (&SIZE(1)+511)/512*512
         MNOTE 4,'DMAN217 - SIZE SPECIFICATION ROUNDED TO NEXT 512 MULT,
               IPLE. '
         MNOTE *,'          &SIZE(1)  TO  &BLK(&DNBR) '
.AM1A    AIF   (NOT &GB3(&DNBR)).AO
         AIF   (&GB1(2) AND &SIZE(2) GT 30720).AM3             @BA23432
&OBLK(&DNBR) SETA  &SIZE(2)
         AIF   (NOT &GB1(2) OR &SIZE(2)-(&SIZE(2)+511)/512*512 EQ 0).AO
&OBLK(&DNBR) SETA  (&SIZE(2)+511)/512*512
         MNOTE 4,'DMAN217 - SIZE SPECIFICATION ROUNDED TO NEXT 512 MULT,
               IPLE. '
         MNOTE *,'          &SIZE(2)  TO  &OBLK(&DNBR) '
         AGO   .AO
.AM2     AIF   (N'&SIZE GT 1 OR T'&SIZE(1) NE 'N').AM3
         AIF   (&GB1(2) AND &SIZE(1) GT 30720).AM3             @BA23432
&REC(&DNBR)  SETA  &SIZE(1)
&BLK(&DNBR)  SETA  1
         AIF   (NOT &GB1(2) OR &SIZE(1)-(&SIZE(1)+511)/512*512 EQ 0).AU
&REC(&DNBR)  SETA  (&SIZE(1)+511)/512*512
         MNOTE 4,'DMAN217 - SIZE SPECIFICATION ROUNDED TO NEXT 512 MULT,
               IPLE. '
         MNOTE *,'          &SIZE(1)  TO  &REC(&DNBR) '
         AGO   .AU
.AM3     MNOTE 8,'DMAN215 - SIZE OPERAND IS INVALID. '
&GB1(16) SETB  1
         AGO   .AU
.AN      MNOTE 8,'DMAN210 - BLOCK OPERAND IS INVALID. '
&GB1(16) SETB  1
.AO      AIF   (N'&RECORD GT 2).AQ
         AIF   ('&RECORD(1)' EQ '').AP
         AIF   (T'&RECORD(1) NE 'N').AQ
 AIF ((&ORG LE 2 OR &ORG GE 7) AND &GB1(2) AND &RECORD(1) GT 30713).AQ
&REC(&DNBR) SETA &RECORD(1)
.AP      AIF   ('&RECORD(2)' EQ '').AR
         AIF   (T'&RECORD(2) NE 'N').AQ
 AIF ((&ORG LE 2 OR &ORG GE 7) AND &GB1(2) AND &RECORD(2) GT 30713).AQ
&OREC(&DNBR) SETA &RECORD(2)
         AIF   (&REC(&DNBR) EQ 0 OR &OREC(&DNBR) EQ 0 OR &OREC(&DNBR)  *
               GE &REC(&DNBR)).AR
         MNOTE 8,'DMAN400 - OSAM RECORD CANNOT BE LESS THAN PRIME RECOR*
               D.'
&GB1(16) SETB  1
         AGO   .AR
.AQ      MNOTE 8,'DMAN200 - RECORD OPERAND IS INVALID. '
&GB1(16) SETB  1
.AR      AIF   (&ORG NE 5 AND &ORG NE 6).AU
         AIF   ('&RECORD' NE '').AS       RECORDS PARM. OMMITTE@BA22679
         AIF   (N'&BLOCK GT 1).AT
         AIF   ('&BLOCK(1)' EQ '').AU
&REC(&DNBR) SETA &BLK(&DNBR)
&BLK(&DNBR) SETA 1
         AIF   (&GB1(2) AND &REC(&DNBR) GT 30720).AT           @BA23432
         AGO   .AU
.AS      MNOTE 8,'DMAN200 - RECORD OPERAND IS INVALID. '
&GB1(16) SETB  1
         AGO   .AU
.AT      MNOTE 8,'DMAN210 - BLOCK OPERAND IS INVALID. '
&GB1(16) SETB  1
.AU      ANOP
         AIF   (&ORG NE 16).DEVCK                             @BI5ZZ
&GB4(1)  SETB  ('&RECFM' EQ 'F')                               @BI5ZZ
&GB4(2)  SETB  ('&RECFM' EQ 'V')                               @BI5ZZ
&GB4(3)  SETB  ('&RECFM' EQ 'U')                               @BI5ZZ
&GB4(4)  SETB  ('&RECFM' EQ 'FB')                              @BI5ZZ
&GB4(5)  SETB  ('&RECFM' EQ 'VB')                              @BI5ZZ
&GB4(6)  SETB  ('&RECFM' EQ 'V' OR '&RECFM' EQ 'VB')           @BI5ZZ
         AIF   (&GB1(2) AND &GB4(3)).AUX0                      @BI5ZZ
     AIF (&GB4(1) OR &GB4(2) OR &GB4(3) OR &GB4(4) OR &GB4(5)).GSAMWRD
.AUX0    MNOTE 8,'DMAN430 - RECORD FORMAT IS OMMITTED OR INVALID.'
&GB1(16) SETB  1                                               @BI5ZZ
.GSAMWRD ANOP                                                      GSAM
         AIF   (&GB1(16)).GSOUT                                @BI5ZZ
         AIF   ('&SIZE' EQ '').AUX1                            @BI5ZZ
         AIF   (N'&SIZE GT 1 OR T'&SIZE EQ 'C' OR &SIZE GT 30720).AM3
&BLK(&DNBR) SETA &SIZE(1)                                      @BI5ZZ
         AGO   .AUX3                                           @BI5ZZ
.AUX1    AIF   ('&BLOCK' NE '').AUX2                           @BI5ZZ
&BLK(&DNBR) SETA &RECORD(1)+&GB4(6)*4                          @BI5ZZ
         AGO   .AUX3                                           @BI5ZZ
.AUX2    AIF   (N'&BLOCK GT 1 OR K'&BLOCK GT 2 OR T'&BLOCK EQ 'C').AT
.AUX3    AIF   ('&RECORD' EQ '').AUX4                          @BI5ZZ
         AIF   (N'&RECORD GT 2 OR &RECORD(1) GT 30720).AS      @BI5ZZ
         AIF   (T'&RECORD(1) EQ 'C' OR T'&RECORD(2) EQ 'C').AS @BI5ZZ
&REC(&DNBR) SETA &RECORD(1)                                    @BI5ZZ
         AIF   (N'&RECORD EQ 1).AUX31                          @BI5ZZ
&REC(&DNBR+1) SETA &RECORD(2)                                  @BI5ZZ
.AUX31   AIF   (&REC(&DNBR+1) GT &REC(&DNBR)).AS               @BI5ZZ
         AIF   (&GB4(3) OR &GB4(6) OR N'&RECORD EQ 1).AUX4     @BI5ZZ
         AIF   (&REC(&DNBR+1) NE &REC(&DNBR)).AS               @BI5ZZ
.AUX4    AIF   (('&SIZE' NE '') OR ('&BLOCK' EQ '')).AUX5      @BI5ZZ
&BLK(&DNBR) SETA &BLOCK(1)*&RECORD(1)+&GB4(6)*4                @BI5ZZ
.AUX5    ANOP                                                      GSAM
         AIF   ('&DD2' NE '').GSDEVC                           @BI5ZZ
&DS2(&DNBR) SETC '&DD1'                                        @BI5ZZ
.GSDEVC  ANOP                                                      GSAM
&DEV(&DNBR) SETC '    '                                        @BI5ZZ
         AIF   ('&DEVICE' EQ '').GSOUT                         @BI5ZZ
         MNOTE 8,'DMAN440 - DEVICE OPERAND IS INAPPLICABLE TO GSAM.'
&GB1(16) SETB  1                                               @BI5ZZ
.GSOUT   MEXIT                                                     GSAM
.DEVCK   ANOP
.**********************************************************************
.**********************************************************************
.**                                                                  **
.**       CHECK FOR VALID DEVICE AND MODEL AND SET UP THE            **
.**       PHYSICAL CHARACTERISTICS OF THE DEVICE FOR SPACE           **
.**       CALCULATION.                                               **
.**       VALID DEVICES ARE:                                         **
.**            TAPES                                                 **
.**             2400                                                 **
.**               2401                                               **
.**               2402                                               **
.**               2403                                               **
.**             3400 - ALL MODELS                                    **
.**            DIRECT ACCESS                                         **
.**             2305- MODELS 1 AND 2                                 **
.**             2314                                                 **
.**             2319                                                 **
.**             3330 - MODELS 1 AND 11                               **
.**             3340                                                 **
.**             3350                                                 **
.**                                                                  **
.**                                                                  **
.**********************************************************************
.**********************************************************************
&NSIZE   SETB  1              INITIALIZE SIZE GLOBAL           @BA38223
.**********************************************************************
.**                       3400 MAGNETIC TAPE DRIVES                  **
.**********************************************************************
         AIF   ('&DEVICE' NE '3400' AND '&DEVICE' NE 'TAPE' AND        *
               '&DEVICE' NE '2400').C2314
         MNOTE *,'             &DEVICE MAGNETIC TAPE DRIVE'
&TRK(&DNBR)    SETA    (32000*65536)+32000
         AIF   (&ORG EQ 3 OR &ORG EQ 4).AV
         MNOTE 8,'DMAN160 - DEVICE OPERAND IS INVALID FOR ACCESS=&ACC '
&GB1(16) SETB  1
         AGO   .AV
.**********************************************************************
.**********************************************************************
.**                2314 AND 2319 DISK STORAGE                        **
.**********************************************************************
.**********************************************************************
.C2314   AIF   ('&DEVICE' NE '2314' AND '&DEVICE' NE '2319').C2305
         MNOTE *,'             &DEVICE DISK STORAGE'
&TRK1(&DNBR)   SETA    7294                       OSAM CAPACITY.
&TRK(&DNBR)    SETA    (7249*65536)+3476          CAPACITIES
&THRD(&DNBR)   SETA    (2254*65536)+1649          WITH KEYS.
&CYL(&DNBR)    SETA    (20*65536*256)+(45*4096)+146  CYL. CAPACITIES
&DAE(&DNBR)    SETA    (2137*65536)+2048             AND ISAM VARIABLES
         AIF   (&ORG LE 2 OR &ORG GE 7).AV
&TRK(&DNBR)    SETA    (7294*65536)+3520          CAPACITIES
&THRD(&DNBR)   SETA    (2298*65536)+1693          WITHOUT KEYS.
         AGO   .AV
.**********************************************************************
.**********************************************************************
.**                 2305 FIXED HEAD STORAGE                          **
.**********************************************************************
.**********************************************************************
.C2305   AIF   ('&DEVICE' NE '2305').C3330
         MNOTE *,'             2305 FIXED HEAD STORAGE'
         AIF   ('&MODEL' NE '1').MOD2
         MNOTE *,'             MODEL 1'
&TRK1(&DNBR)   SETA    14136
&TRK(&DNBR)    SETA    (13934*65536)+6650                      @BA38223
&THRD(&DNBR)   SETA    (4222*65536)+3008
&CYL(&DNBR)    SETA    (8*65536*256)+(120*4096)+2
&DAE(&DNBR)    SETA    65537
         AIF   (&ORG LE 2 OR &ORG GE 7).AV
&TRK(&DNBR)    SETA    (14136*65536)+6852
&THRD(&DNBR)   SETA    (4424*65536)+3210
         AGO   .AV
.MOD2    AIF   ('&MODEL' EQ '2').M2
         MNOTE 4,'DMAN155 - MODEL &MODEL INVALID MODEL 2 ASSUMED FOR   *
               SPACE CALCULATION.'
.M2      ANOP
         MNOTE *,'             MODEL 2'
&TRK1(&DNBR)   SETA    14660
&TRK(&DNBR)    SETA    (14569*65536)+7140
&THRD(&DNBR)   SETA    (4663*65536)+3425
&CYL(&DNBR)    SETA    (8*65536*256)+(33*4096)+1
&DAE(&DNBR)    SETA    65537
         AIF   (&ORG LE 2 OR &ORG GE 7).AV
&TRK(&DNBR)    SETA    (14660*65536)+7231
&THRD(&DNBR)   SETA    (4754*65536)+3516
         AGO   .AV
.**********************************************************************
.**********************************************************************
.**                  3330 DISK STORAGE                               **
.**********************************************************************
.**********************************************************************
.C3330   AIF   ('&DEVICE' NE '3330').C3340
         MNOTE *,'             3330 DISK STORAGE'
         AIF   ('&MODEL' NE '11').MOD1
         MNOTE *,'             MODEL 11'
&TRK1(&DNBR)   SETA    13030
&TRK(&DNBR)    SETA    (12974*65536)+6391
&THRD(&DNBR)   SETA    (4197*65536)+3100
&CYL(&DNBR)    SETA    (19*65536*256)+(56*4096)+191            @BA38223
&DAE(&DNBR)    SETA    65537
         AIF   (&ORG LE 2 OR &ORG GE 7).AV
&TRK(&DNBR)    SETA    (13030*65536)+6447
&THRD(&DNBR)   SETA    (4253*65536)+3156
         AGO   .AV
.MOD1   AIF   ('&MODEL' EQ '1' OR '&MODEL' EQ '').M1
         MNOTE 8,'DMAN157 - MODEL &MODEL INVALID, MODEL 1  ASSUMED FOR *
               SPACE CALCULATIONS.'
&GB1(16) SETB  1
         AGO   .M1A
.M1      AIF   (T'&MODEL EQ 'O').M1A
         MNOTE *,'             MODEL 1'
         AGO   .M1B
.M1A     MNOTE *,'             MODEL 1 ASSUMED'
.M1B     ANOP
&TRK1(&DNBR)   SETA                13030
&TRK(&DNBR)    SETA                (12974*65536)+6391
&THRD(&DNBR)   SETA                (4197*65536)+3100
&CYL(&DNBR)    SETA                (19*65536*256)+(56*4096)+191
&DAE(&DNBR)    SETA                65537
         AIF   (&ORG LE 2 OR &ORG GE 7).AV
&TRK(&DNBR)    SETA                (13030*65536)+6447
&THRD(&DNBR)   SETA                (4253*65536)+3156
         AGO   .AV
.**********************************************************************
.**********************************************************************
.**                     3340 DISK STORAGE                            **
.**********************************************************************
.**********************************************************************
.C3340   AIF   ('&DEVICE' NE '3340').C3350                     @BA51104
         MNOTE *,'             3340 DISK STORAGE'
&TRK1(&DNBR)   SETA                8368                        @BA38223
&TRK(&DNBR)    SETA                (8293*65536)+4025           @BA38223
&THRD(&DNBR)   SETA                (2603*65536)+1891           @BA38223
&CYL(&DNBR)    SETA                (12*65536*256)+(75*4096)+242
&DAE(&DNBR)    SETA                65537
         AIF   (&ORG LE 2 OR &ORG GE 7).AV
&TRK(&DNBR)    SETA                (8368*65536)+4100           @BA38223
&THRD(&DNBR)   SETA                (2678*65536)+1966           @BA38223
         AGO   .AV
.**********************************************************************
.**********************************************************************
.**                     3350 DISK STORAGE                            **
.**********************************************************************
.**********************************************************************
.C3350   AIF   ('&DEVICE' NE '3350').DE                        @BA51104
       MNOTE *,'               3350 DISK STORAGE'
&TRK1(&DNBR)   SETA                19069                       @BA51104
&TRK(&DNBR)    SETA                (18987*65536)+9360          @BA51104
&THRD(&DNBR)   SETA                (6151*65536)+4546           @BA51104
&CYL(&DNBR)    SETA                (30*65536*256)+(82*4096)+267
&DAE(&DNBR)    SETA                65537                       @BA51104
         AIF   (&ORG LE 2 OR &ORG GE 7).AV                     @BA51104
&TRK(&DNBR)    SETA                (19069*65536)+9442          @BA51104
&THRD(&DNBR)   SETA                (6233*65536)+4628           @BA51104
         AGO   .AV                                             @BA51104
.DE      MNOTE 8,'DMAN150 - DEVICE OPERAND  &DEVICE IS OMITTED         *
               OR INVALID.'
               MNOTE *,'A 3330 WITH INDEXING WILL BE ASSUMED FOR SPACE *
               CALCULATIONS.'
&GB1(16) SETB  1
&TRK1(&DNBR)   SETA    13030
&TRK(&DNBR)    SETA    (13030*65536)+6447
&THRD(&DNBR)   SETA    (4253*65536)+3156
&CYL(&DNBR)    SETA    (19*65536*256)+(56*4096)+191            @BA38223
&DAE(&DNBR)    SETA    65537
&DEV(&DNBR)    SETC    '3330'
         AGO   .AV1
.AV      ANOP
&DEV(&DNBR) SETC '&DEVICE'.'    '
.AV1     ANOP
&DEV(&DNBR)  SETC  '&DEV(&DNBR)'(1,4)    INSURE LENGTH OF FOUR
&CDNBR   SETA  &DNBR              ESTABLISH CURRENT DATASET GROUP.
         AIF   (&GB1(2) OR '&DEVICE' EQ '2400' OR '&DEVICE' EQ '3400' O*
               R '&DEVICE' EQ 'TAPE').AW
         AIF  (&REC(&CDNBR) LE &TRK(&CDNBR)/65536 AND &OREC(&CDNBR) LE *
               &TRK(&CDNBR)/65536).AV2
         MNOTE 8,'DMAN420 - RECORD EXCEEDS TRACK LENGTH. '
&GB1(16) SETB  1
.AV2     AIF   (NOT &GB2(&CDNBR) AND NOT &GB3(&CDNBR)).AW
         AIF  (&BLK(&CDNBR) LE &TRK(&CDNBR)/65536 AND &OBLK(&CDNBR) LE ,
               &TRK(&CDNBR)/65536).AW
         MNOTE 8,'DMAN425 - SIZE SPECIFICATION EXCEEDS TRACK LENGTH. '
&GB1(16) SETB  1
.AW      AIF   (&ORG LT 5 OR &ORG GE 7).AY                     @BA22673
         AIF   ('&DD2' NE '' OR '&OVFLW' NE '').AZ
         AIF   (&GB1(2) OR ('&BLOCK(1)' EQ '' AND '&SIZE(1)' EQ '')).AY
         AIF   (&DNBR EQ 1 AND '&SIZE' EQ 'O').AW1 PRIMARY     @BA38223
&NSIZE   SETB  0              NO OVERHEAD BYTES ADDED          @BA38223
.AW1     AIF   (&REC(&CDNBR)+&NSIZE*((&TRAPS+1)*4) LE                  *
                &TRK(&CDNBR)/65536).AY
         MNOTE 8,'DMAN210 - BLOCK OPERAND IS INVALID. '
&GB1(16) SETB  1
.AY      MEXIT
.AZ      MNOTE 8,'DMAN240 - DD2 AND OVFLW OPERANDS INVALID FOR &ACC..'
&GB1(16) SETB  1
         MEXIT
         MEND
