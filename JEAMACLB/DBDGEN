         MACRO
         DBDGEN
         GBLA  &S,&SL,&SLC,&SEGS,&F1,&F2,&F3
         GBLA  &DNBR,&CDNBR,&EXTDB,&EXTDBN,&EXTDBR
         GBLA  &HDB,&HDRBN,&HEX,&LEV,&ORG,&RAPS,&RTKEY
         GBLA  &BLK(10),&CYL(10),&DAE(10),&DFL(10)
         GBLA  &DSC(10),&DSL(10),&FRS(10)                      @BA22673
         GBLA  &OBLK(10),&OREC(10),&REC(10)
         GBLA  &SCN(10),&THRD(10),&TRK(10),&TRK1(10)
         GBLA  &IB(255),&SB(255),&SB1(255),&SFLD(255)
         GBLA  &SFQ(255),&SF1(255),&SF2(255),&SLCF(255)
         GBLA  &SLCS(255),&SLDP(255),&SD(300),&FB1(1000),&FT1(1000)
         GBLB  &GB1(16),&GB4(16),&FK1(1000),&HIER(255)         @BI5ZZ
         GBLB  &PARNT(255),&PRD(255),&SYMB(255)
         GBLC  &ACC,&DBN,&EBCDIC,&RMN
         GBLC  &DMN(10),&DEV(10),&DS1(10),&DS2(10),&OVF(10)
         GBLC  &SN(500),&FN1(1000),&FSF1(1000),&FLDNAM(255)
         GBLC  &SI(255),&SLCN(255),&SLCP(255),&DBNAME(255),&SS(300)
         LCLA  &A,&B,&D,&E,&F,&G,&H(5),&TRAPS                  @BA22679
         LCLB  &LAST
         LCLC  &C,&CODE
.**********************************************************************
.**                         APARLOG 1.1.1                            **
.**                                                                  **
.**      PP43399  SEGMENT LENGTH INCORRECT FROM DBDGEN OF            **
.**               AN HSAM ROOT ONLY DBD.                             **
.**      PP38223  PREFIX LENGTH ONE BYTE SHORT FOR SINGLE SEGMENT    **
.**               HISAM-VSAM.  UNABLE TO OPEN DATASET !              **
.**      PP46252  PREFIX LENGTH ONE BYTE LONG FOR HD ORGNIZATION     **
.**               AND NON-UNIQUE KEYS.                               **
.**      PP48216  PREFIX LENGTH ONE BYTE TO LONG FOR SHISAM          **
.**                                                                  **
.**********************************************************************
&C       SETC  '&DBN.       '(1,8)
         AIF   (&GB1(1)).A
         MNOTE 8,'DBD001 - DBD CARD MUST OCCUR 1ST IN A DBDGEN. '
&GB1(16) SETB  1
.A       AIF   (&S GE 1 OR &ORG EQ 16).B                       @BI5ZZ
         MNOTE 8,'DGEN100 - DBD CONTAINS NO SEGMENTS. '
&GB1(16) SETB  1
.B       AIF   (&GB1(12) OR (&ORG NE 6 AND &ORG NE 7 AND &ORG NE 12)).C
         MNOTE 8,'DGEN150 - LCHILD STATEMENT FOR ROOT SEGMENT '
         MNOTE *,'          OMITTED OR INVALID. '
&GB1(16) SETB  1
.C       AIF  (&DNBR EQ 0 OR &ORG EQ 16 OR (&ORG GT 2 AND &ORG LT 6)).D
         AIF   (&RTKEY NE 0).D
         MNOTE 8,'DGEN160 - ROOT SEGMENT HAS NO SEQUENCE FIELD. '
&GB1(16) SETB  1
.D       AIF   (NOT &GB1(13)).D1
         MNOTE 4,'DGEN270 - ESDS DATA SET SPECIFICATION IS IGNORED. '
         MNOTE *,'          (INDEX,VSAM) DATA SET CONTAINS SEGMENTS '
         MNOTE *,'          WITH UNIQUE KEYS ONLY.      '
&ORG     SETA  12
&OVF(1)  SETC  ''
.D1      AIF   (NOT &GB1(16)).E
         MNOTE 8,'DGEN110 - DBDGEN TERMINATED; ERRORS IN DBD. '
         MEXIT
.E       AIF   ((&ORG NE 7 AND &ORG NE 12) OR &S EQ 1).GEN
&A       SETA  2
.F       ANOP
&C       SETC  '&SN(&A+99).       '(1,8)
         PUNCH '  ALIAS &C '
&A       SETA  &A+1
         AIF   (&A LE &S).F
&C       SETC  '&DBN.       '(1,8)
.GEN     TITLE '                 IMS/VS DATABASE DEFINITION FOR  &C '
***********************************************************************
*                                                                     *
*                            VECTOR TABLE                             *
*                                                                     *
***********************************************************************
&DBN     CSECT
         DC    AL1(&GB1(3)*X'80'+&GB1(4)*X'40'+X'30'+&GB4(10)*X'08'),AL*
               3(PREFIX),A(SEGTAB)
*                                       DOSCOMPAT,PROTECT,LEVEL,PASSWRD
         DC    A(FLDTAB,LCHDTAB)
         DC    A(EXTDBD,SORTAB)
&CODE    SETC  'A'
         AIF   (&ORG NE 5).G
&CODE    SETC  'V'
.G       DC    &CODE.(RDMVTAB),A(INDXTAB)
***********************************************************************
*                                                                     *
*                            PREFIX                                   *
*                                                                     *
***********************************************************************
PREFIX   DC    CL8'&C'             DATABASE NAME
         AIF (&ORG NE 5 AND &ORG NE 6 AND &ORG NE 13 AND &ORG NE 14).G1
         AIF   (((&SF1(1)-&SF1(1)/65536*65536)/256-(&SF1(1)-&SF1(1)/655*
               36*65536)/256/64*64)/32 NE 1 OR  (&ORG NE 6 AND &ORG NE *
               14)).H              HIDAM AND TWIN BKW. NO FOUR @BA38223
.G1      ANOP                           NO ROOT ANCHOR POINTS  @BA38223
&RAPS    SETA  0                        ZERO ROOT ANCHOR POINT @BA28267
.H       DC    AL2(&LEV,&S)        NBR OF LEVELS,NBR OF SEGMENTS
         AIF   (&ORG EQ 11 OR &ORG EQ 12).I
         AIF   (&ORG NE 4 OR &S GT 1).H1 NOT SSAM              @BA36168
&ORG     SETA  3                         IS SSAM               @BA36168
.H1      ANOP
&ORG     SETA  &ORG+&GB1(2)*8
.I       DC    AL1(&ORG)              DATABASE ORGANIZATION    @BA20825
         DC    AL1(&DNBR)          NBR OF DATASET GROUPS
         DC    AL2(&EXTDB)         NBR OF EXTERNAL DBD ENTRIES
&C       SETC  '&RMN.       '(1,8)
         DC    CL8'&C'             RANDOMIZING MODULE NAME
         DC    H'&SLC'             NBR OF LOGICAL CHILDREN
         DC    H'&RAPS'            NBR OF ROOT ANCHOR POINTS
&C       SETC  '&HDB'
         AIF   (&HDB GT 0).J
&C       SETC  '-1'
.J       DC    F'&HDRBN,&C'        MAXIMUM RBN, MAXIMUM BYTES (HD)
         SPACE 2
         AIF   ((&ORG NE 12 AND &ORG NE 15) OR &S EQ 1).L
         AIF   (&LAST).K
&A       SETA  1
&LAST    SETB  1
.K       ANOP
&A       SETA  &A+1
&C       SETC  '&SN(&A+99).       '(1,8)
         AIF   (&A GT &S).L
         DC    CL8'&C'             SECONDARY INDEX NAME
         DC    AL2(&LEV,&S)        NBR OF LEVELS, NBR OF SEGMENTS
         AGO   .I
.L       ANOP
&A       SETA  0
&LAST    SETB  0
&CDNBR   SETA  0
         AIF   (&CDNBR EQ &DNBR).P
         SEGPTRS S                 FIND SMALLEST SEGMENT IN DATASET
&TRAPS   SETA  &RAPS                    SAVE OFF RAPS OF PRIMAR@BA22679
.M       ANOP                                                  @BA22679
         AIF   (&CDNBR EQ 0).M1         ONLY ONE DATASET CARD  @BA26083
         AIF   (&ORG NE 5 AND &ORG NE 6 AND &ORG NE 13 AND &ORG NE 14).*
               M1                                              @BA22679
&RAPS    SETA  0                        RESET RAPS FOR SECONDAR@BA22679
.M1      ANOP                                                  @BA22679
         SPACE
&CDNBR   SETA  &CDNBR+1
&C       SETC  '&DS1(&CDNBR).       '(1,8)
&DMN(&CDNBR) DC    CL8'&C'         PRIME DDNAME
         DC    CL4'&DEV(&CDNBR)'   DEVICE TYPE
         DC    AL1(&CDNBR)         DATASET IDENTIFICATION
         DC    AL1(&DSC(&CDNBR))   NBR OF SEGMENTS IN DATASET GROUP
         DC    H'&SCN(&CDNBR)'     NBR OF FREE SPACE SCAN CYLINDERS
&A       SETA  &DSL(&CDNBR)/65536
&B       SETA  &DSL(&CDNBR)-&DSL(&CDNBR)/65536*65536
.N       AIF   (&ORG NE 9 AND &ORG NE 12 AND &ORG NE 15  OR &GB1(4)).N1
*                                       HISAM INDEX DOSCOMPAT
&A       SETA  &A-1
&B       SETA  &B-1
.N1      AIF   (&ORG EQ 16 OR &ORG EQ 24).GSAMEND              @BI5ZZ
         DC    AL2(&A)             LENGTH OF LONGEST SEGMENT   @BI5ZZ
         DC    AL2(0)              RESERVED                    @BA35298
&E       SETA  &DFL(&CDNBR)/65536
&F       SETA  &DFL(&CDNBR)-&DFL(&CDNBR)/65536*65536
         DC    AL2(&E)             LENGTH OF LONGEST KEY
         DC    AL2(&F)             LENGTH OF SMALLEST KEY
         DBDLRECL &CDNBR
         AIF   (&GB1(16)).D
         DC    AL2(&FRS(&CDNBR))        FREE SPACE VALUE       @BI5ZZ
         DC    AL1(0)                                          @BM02997
         DC    AL1(0)                   RESERVED               @BI5ZZ
         AIF   (&DSC(&CDNBR) GT 0 OR &ORG EQ 16 OR &ORG EQ 24).O
         SPACE
         MNOTE 4,'DGEN140 - DATASET-&CDNBR &DMN(&CDNBR) CONTAINS NO SEG,
               MENTS. '
         SPACE
.O       AIF   (&CDNBR LT &DNBR).M
&RAPS    SETA  &TRAPS                   RESET PRIMARY RAPS VALU@BA22679
.P       DC    F'-1'               END OF PREFIX
         AGO   .DBGO                                           @BI5ZZ
.GSAMEND ANOP                                                      GSAM
         DC    AL2(&REC(&CDNBR))   MAX. RECORD LENGTH          @BI5ZZ
         DC    AL2(&REC(&CDNBR+1)) MIN. RECORD LENGTH          @BI5ZZ
         DC    AL2(0,0)            RESERVED                    @BI5ZZ
         DC    AL2(&REC(&CDNBR),&BLK(&CDNBR)) PRIME LRECL AND B@BI5ZZ
         DC    AL2(0,0)                                        @BI5ZZ
&C       SETC  '&DS2(&CDNBR).        '(1,8)                    @BI5ZZ
         DC    CL8'&C'             SECONDARY DD NAME           @BI5ZZ
         DC    AL2(0)              RESERVED                    @BI5ZZ
&A       SETA  &GB4(1)*1+&GB4(2)*2+&GB4(3)*3+&GB4(4)*5+&GB4(5)*6   GSAM
         AIF   (&ORG NE 24).GSAMEXT                            @BI5ZZ
&A       SETA  &A+8                                            @BI5ZZ
.GSAMEXT DC    AL1(&A)             GSAM RECFM FLAG             @BI5ZZ
         DC    AL1(0)              RESERVED                    @BI5ZZ
         DC    F'-1'               END OF PREFIX               @BI5ZZ
SEGTAB   EQU   0                                               @BI5ZZ
FLDTAB   EQU   0                                               @BI5ZZ
LCHDTAB  EQU   0                                               @BI5ZZ
EXTDBD   EQU   0                                               @BI5ZZ
SORTAB   EQU   0                                               @BI5ZZ
RDMVTAB  EQU   0                                               @BI5ZZ
INDXTAB  EQU   0                                               @BI5ZZ
         MEXIT                                                     GSAM
.DBGO    ANOP                                                      GSAM
         EJECT
***********************************************************************
*                                                                     *
*                            SEGMENT TABLE                            *
*                                                                     *
***********************************************************************
&D       SETA  0
         SPACE
SEGTAB   EQU   *
         AGO   .R
.Q       SPACE
.R       ANOP
&D       SETA  &D+1
&G       SETA  (&SLDP(&D)-&SLDP(&D)/4096*4096)/32
&B       SETA  &SB(&D)-&SB(&D)/65536*65536
&E       SETA  &SLDP(&D)/1048576
&F       SETA  &SLDP(&D)-&SLDP(&D)/32*32
         DC    AL1(&G,&D)          DATASET ID, SEGMENT CODE
         DC    AL1(&E,&F)          PARENTS SEGMENT CODE, LEVEL
&A       SETA  &SFLD(&D)-&SFLD(&D)/65536*65536
&E       SETA  &SLCS(&D)-&SLCS(&D)/65536*65536
&F       SETA  &SLCS(&D)/65536
         AIF   (&ORG NE 9 OR &B EQ 0).Q1  HISAM/VSAM           @BA33273
&B       SETA &B-1            MINUS ONE FOR DELIMITER          @BA27160
.Q1      ANOP
         DC    AL1(&E)             NBR OF LOGICAL CHILDREN
         DC    AL1(&A)             NBR OF FIELDS
         DC    AL2(&B)             SEGMENT LENGTH
&B       SETA  &SFLD(&D)/65536
         DC    F'&SFQ(&D)'         SEGMENT FREQUENCY X 100
&C       SETC  '&SN(&D).       '(1,8)
         DC    CL8'&C'             SEGMENT NAME
         CONVERT VALUE=&SF1(&D),TO=EBCDIC,DIGITS=4
&C       SETC  '&EBCDIC'
         CONVERT VALUE=&SF2(&D),TO=EBCDIC,DIGITS=4
         DC    X'&C&EBCDIC'        SEGMENT FLAGS
         AIF   (&E NE 0).S
         DC    F'-1'               OFFSET TO FIRST LOGICAL CHILD ENTRY
         AGO   .T
.S       ANOP
&F       SETA  (&F-1)*24
         DC    A(&F)               OFFSET TO FIRST LOGICAL CHILD ENTRY
.T       AIF   (&G NE 0 OR &ORG EQ 16).U                       @BI5ZZ
         SOURSEG &D,FIND      GENERATE OFFSET TO SOURCE SEGMENT ENTRY
         AGO   .V
.U       DC    AL2(0)              NBR OF SOURCE SEGMENTS
         DC    AL2(65535)          OFFSET TO FIRST SOURCE SEGMENT ENTRY
         AIF   (&ORG NE 16).V           SET &B FOR GSAM        @BI5ZZ
&B       SETA  1                        SET &B TO 1 FOR GSAM   @BI5ZZ
.V       ANOP
&B       SETA  (&B-1)*24
         AIF   (&A NE 0).W
         DC    F'-1'               OFFSET TO FIRST FIELD TABLE ENTRY
         AGO   .X
.W       DC    A(&B)               OFFSET TO FIRST FIELD TABLE ENTRY
.X       ANOP
&B       SETA  &SB(&D)/65536
&G       SETA  &SB1(&D)-&SB1(&D)/65536*65536
&SB1(&D) SETA  &SB1(&D)/65536
         DC    AL2(&B)             LENGTH OF SEGMENT PREFIX    @BA48216
         DC    AL2(&G)             MINIMUM SEGMENT LENGTH
         DC    A(0)                RESERVED
&G       SETA  &SB1(&D)/256
&B       SETA  &SB1(&D)-&G*256
&CODE    SETC  '0'
&C       SETC  'A'
         AIF   (&B EQ 0).Y
&CODE    SETC  '&SN(&D)'
&C       SETC  'V'
.Y       DC    AL1(&G)             COMPACTION ROUTINE FLAGS
         DC    &C.L3(&CODE)        COMPACTION ROUTINE CSECT NAME
.Z       AIF   (&D LT &S).Q
         EJECT
***********************************************************************
*                                                                     *
*                            FIELD TABLE                              *
*                                                                     *
***********************************************************************
&D       SETA  0
         SPACE
FLDTAB   EQU   *
         AGO   .F2
.F1      SPACE
.F2      AIF   (&F1 EQ 0).AA
&D       SETA  &D+1
&B       SETA  0
         AIF   (&D EQ &F1).F3
&B       SETA  &FT1(&D+1)-&FT1(&D+1)/65536*65536
.F3      AIF   (NOT &FK1(&D)).F4
         MNOTE *,'****** SEQUENCE FIELD ****** '
.F4      DC    CL8'&FN1(&D)'       FIELD NAME
&A       SETA  &FT1(&D)/65536
         AIF   ((&A/256-&A/256/32*32)/16 NE 1).F6
&H(1)    SETA  &H(1)+1
.F5      ANOP
&H(2)    SETA  &H(2)+1
         AIF   (&SD(&H(2))/65536-16384 NE &D).F5
&A       SETA  &A/256
         AIF   (&H(2) EQ 1).F5B
&H(4)    SETA  &H(2)
&H(5)    SETA  0
.F5A     AIF   (&H(4) EQ 2).F5B
&H(4)    SETA  &H(4)-1
         AIF   (&SD(&H(4))/65536-16384 NE &SD(&H(4)-1)/65536-16384).F5A
         AIF   ((&SD(&H(4))-&SD(&H(4))/65536*65536)/256 NE (&SD(&H(4)-1,
               )-&SD(&H(4)-1)/65536*65536)/256).F5A
&H(5)    SETA  &H(5)+1
         AGO   .F5A
.F5B     ANOP
&H(3)    SETA  (&H(1)-1)*16+(&H(2)-&H(5)-1)*16+4
.F6      ANOP
&E       SETA  &FT1(&D)-&FT1(&D)/65536*65536
&LAST    SETB  (&E NE &B OR &D EQ &F1)    LAST FIELD FOR SEGMENT ?
&F       SETA  (&LAST*128)+(&FK1(&D)*64)+&A
&E       SETA  (&E-1)*48
         AIF   ((&A-&A/32*32)/16 EQ 1).F8
&A       SETA  &FB1(&D)/65536*65536-&FB1(&D)
&B       SETA  &FB1(&D)/65536
&C       SETC  '&FN1(&D).       '(1,8)
         AIF   ('&C'(1,3) NE '/CK' AND '&C'(1,3) NE '/SX').F7
&F       SETA  (&LAST*8+1)*4096
         DC    AL2(&B)             FIELD START
         DC    AL2(&F+&A-1)        FLAGS AND FIELD LENGTH MINUS 1
         AGO   .F9
.F7      DC    AL2(&B)             FIELD START
         DC    AL2(&F*256+&A-1)    FLAGS AND FIELD LENGTH MINUS 1
         AGO   .F9
.F8      DC    AL2(&H(3))          OFFSET TO INDEX TABLE ENTRY
         DC    AL1(&F,0)           FLAGS, RESERVED
.F9      ANOP
&C       SETC  '&FSF1(&D).       '(1,8)
         DC    CL8'&C'             SOURCE FIELD NAME
         DC    A(&E)               OFFSET TO SEGMENT TABLE ENTRY
         AIF   (&D LT &F1).F1
.AA      EJECT
***********************************************************************
*                                                                     *
*              EXTERNAL CSECT AND DBD REFERENCE TABLES                *
*                                                                     *
***********************************************************************
         SPACE
&D       SETA  0
.AB      ANOP
&D       SETA  &D+1
         AIF   (&SB1(&D)/2048 EQ 0).AD
&A       SETA  &SB1(&D)/256
&B       SETA  &SB1(&D)-&A*256
&A       SETA  &A-8
         SPACE
&SN(&D)  CSECT
         DC    CL8'&SN(&D)'        SEGMENT NAME
         DC    CL8'&DBNAME(&B)'    COMPACTION ROUTINE NAME
         DC    A(0)                COMPACTION ROUTINE ENTRY POINT
&E       SETA  &SFLD(&D)/65536
         AIF   (&SFLD(&D)-&SFLD(&D)/65536*65536 EQ 0).AC
&A       SETA  &A+&FK1(&E)*8
.AC      ANOP
&F       SETA  &FB1(&E)/65536
&G       SETA  &FB1(&E)-&F*65536
&B       SETA  &SB(&D)-&SB(&D)/65536*65536
         AIF   (&ORG NE 9).AC1 HISAM/VSAM                      @BA33273
&B       SETA  &B-1                     MINUS ONE FOR DELIMETER@BA30636
.AC1     ANOP                           NOT HISAM/VSAM         @BA30636
         DC    AL2(&A*256+&G)      FLAGS
         DC    AL2(&F)             FIELD LENGTH
         DC    AL2(&B)             SEGMENT LENGTH
         DC    AL2(32)             COMPACTION ROUTINE CSECT SIZE
         DC    A(0)                RESERVED
.AD      AIF   (&D LT &S).AB
         SPACE
&DBN     CSECT
EXTDBD   EQU   *
&D       SETA  0
.AE      AIF   (&D EQ &EXTDB).AF
&D       SETA  &D+1
         DC    CL8'&DBNAME(&D)',F'0' EXTERNAL DATABASE ENTRY
         AGO   .AE
.AF      DC    F'-1'               END OF EXTERNAL DATABASE TABLE
         SPACE 3
LCHDTAB  EQU   *
&D       SETA  0
         AIF   (&D EQ &SLC).AM
.AG      ANOP
&D       SETA  &D+1
         DC    CL8'&SLCN(&D)'      LCHILD SEGMENT NAME
&A       SETA  0
         AIF   (&SLCF(&D)/1048576 EQ 0).AH
&A       SETA  (&SLCF(&D)/1048576-1)*12
         AGO   .AI
.AH      EXTDBD &SLCN(&D),SEGM     FIND SEGMENT OFFSET
         AIF   (&GB1(16)).D
&A       SETA  &EXTDBN
.AI      ANOP
&B       SETA  &A/65536/256*128+&IB(&D)/256
&A       SETA  &A-&A/65536/256*256*65536
         DC    AL1(&B)             CODE BYTE
         DC    AL3(&A)             EXTERNAL DATABASE TABLE ENTRY OFFSET
&A       SETA  (&SLCF(&D)-(&SLCF(&D)/1048576*1048576))/256
&B       SETA  0
&LAST    SETB  (&D EQ &SLC)
         AIF   (&LAST).AJ
&B       SETA  (&SLCF(&D+1)-(&SLCF(&D+1)/1048576*1048576))/256
&LAST    SETB  (&A NE &B)
.AJ      ANOP
&F       SETA  (&SLCF(&D)-(&SLCF(&D)/256*256))+(&LAST*128)
&E       SETA  65535
&IB(&D)  SETA  &IB(&D)-&IB(&D)/256*256    CLEAR HIGH-ORDER BYTE
         AIF   ('&SLCP(&D)' EQ '').AL
         EXTDBD &SLCP(&D),SEGM
         AIF   (&GB1(16)).D
&E       SETA  1*65536*256
&E       SETA  &EXTDBN-&E          CLEAR HIGH-ORDER BYTE
&G       SETA  &E/48+1             PAIRED SEGMENT NUMBER
         AIF   ((&SLDP(&G)-&SLDP(&G)/4096*4096)/32 NE 0).AK   LOGICAL ?
         AIF   (&SF2(&G)-&SF2(&G)/256*256 EQ 0).AL       ANY CHILDREN ?
         MNOTE 8,'DGEN180 - REFERENCED PAIRED SEGMENT CANNOT HAVE '
         MNOTE *,'          ANY CHILDREN; SEGMENT NAME=&SN(&G)..'
&GB1(16) SETB  1
         AGO   .D
.AK      AIF   (&PRD(&G)).AL
         MNOTE 8,'DGEN130 - REFERENCED PAIRED SEGMENT DOES NOT SPECIFY'
         MNOTE *,'          ''PAIRED'' IN ITS POINTER OR PTR OPERAND.'
         MNOTE *,'          PAIRED SEGMENT IS &SN(&G)..   '
&GB1(16) SETB  1
         AGO   .D
.AL      DC    AL1(&F,&IB(&D))     FLAGS
         DC    AL2(&E)             PAIRED SEGMENT OFFSET
&C       SETC  '&SI(&D).       '(1,8)
         DC    CL8'&C'             INDEX FIELD NAME
         SPACE
         AIF   (&D LT &SLC).AG
.AM      DC    F'-1'
         SOURSEG ,LIST             GENERATE SOURCE SEGMENT TABLE
         AIF   (&ORG NE 5 AND &ORG NE 13).AO
         SPACE 6
RDMVTAB  CSECT
&C       SETC  '&RMN.       '(1,8)
VTABLE   DC    CL8'&C'             RANDOMIZING MODULE NAME
         DC    AL1(&RTKEY-1),AL3(0)
         DC    AL2(VTABEND-VTABLE) LENGTH OF TABLE
         DC    H'&RAPS'            NBR OF ROOT ANCHOR POINTS
         DC    F'&HDRBN'           MAXIMUM RELATIVE BLOCK NUMBER
&C       SETC  '&HDB'
         AIF   (&HDB GT 0).AN
&C       SETC  '-1'
.AN      DC    F'&C'               MAXIMUM NUMBER OF BYTES
         DC    2F'0'
VTABEND  EQU   *
         MEXIT
.AO      ANOP
RDMVTAB  DS    0D
         MEXIT
***********************************************************************
***********************************************************************
**                                                                   **
**                  EXTERNAL CSECT AND DBD REFERENCE TABLES          **
**                                                                   **
***********************************************************************
***********************************************************************
         SPACE 1
SEGTAB   EQU   *              START OF SEGMENT TABLE           @BI5ZZ
FLDTAB   EQU   *              START OF FIELD TABLE             @BI5ZZ
&DBN     CSECT
EXTDBD   EQU   *              START OF EXTERNAL DB TABLE       @BI5ZZ
         DC    F'-1'          END OF EXTERNAL DATABASE TABLE   @BI5ZZ
LCHDTAB  EQU   *                                               @BI5ZZ
         DC    F'-1'                                           @BI5ZZ
SORTAB   DS    0F                                              @BI5ZZ
         DC    F'-1'                                           @BI5ZZ
INDXTAB  DS    0F                                              @BI5ZZ
         DC    A($NDX0-INDXTAB-4)                              @BI5ZZ
$NDX0    DC    F'-1'                                           @BI5ZZ
RDMVTAB  DS    0D                                              @BI5ZZ
         MEND
