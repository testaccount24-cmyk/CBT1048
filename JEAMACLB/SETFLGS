         MACRO
         SETFLGS &PN,&RULES=
         GBLA  &S,&ORG,&PNBR,&CDNBR
         GBLA  &SB(255),&SF1(255),&SF2(255)
         GBLA  &SLCF(255),&SLCS(255),&SLDP(255)
         GBLB  &GB1(16),&ERROR
         GBLB  &PRD(255),&HIER(255),&PARNT(255)
         GBLC  &SN(500),&PLIST(100)
         GBLC  &SLCN(255),&SLCP(255)
         LCLA  &N,&Y
         LCLB  &NT,&PZ,&B(8)
.*
.*
.*
&PZ      SETB  (&ORG LE 4 OR &ORG GE 7)
         AIF   (&ORG EQ 7 OR &ORG EQ 12).M
&N       SETA  1              INITIALIZE COUNTER               @BA24119
&NT      SETB  ('&PLIST(&N)' EQ 'NOTWIN' OR '&PLIST(&N)' EQ 'NT')
         AIF   ((&NT OR '&PLIST(1)' EQ 'NONE') AND &PNBR EQ 1).M
.A       AIF   (&N EQ &PNBR).B
&N       SETA  &N+1
         AIF   ('&PLIST(&N)' NE 'NOTWIN' AND '&PLIST(&N)' NE 'NT').A
         AIF   (&NT).L
&NT      SETB  1
.B       AIF   (&N LT &PNBR).A
&N       SETA  0
.C       AIF   (&N EQ &PNBR).M
&N       SETA  &N+1
         AIF   ('&PLIST(&N)' EQ '' OR  '&PLIST(&N)' EQ 'NOTWIN' OR     *
               '&PLIST(&N)' EQ 'NT').C                         @BA24119
         AIF   (&NT OR &B(2) OR &B(3) OR &B(8)).G
         AIF   ('&PLIST(&N)' NE 'HIER' AND '&PLIST(&N)' NE 'H').D
         AIF   (&ORG LE 2).C
&B(2)    SETB  1                   INDICATES TWIN POINTER
&B(8)    SETB  1                   INDICATES HIERARCHIC USE
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF POINTER
         AGO   .C
.D       AIF   ('&PLIST(&N)' NE 'HIERBWD' AND '&PLIST(&N)' NE 'HB').E
         AIF   (&ORG LE 2).C
&B(2)    SETB  1                   INDICATES TWIN POINTER
&B(3)    SETB  1                   INDICATES TWINBWD POINTER
&B(8)    SETB  1                   INDICATES HIERARCHIC USE
&SB(&S)  SETA  &SB(&S)+8*65536     ADD SIZE OF POINTERS
         AGO   .C
.E       AIF   ('&PLIST(&N)' NE 'TWIN' AND '&PLIST(&N)' NE 'T').F
         AIF   (&ORG LE 2).C
&B(2)    SETB  1                   INDICATES TWIN POINTER
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF POINTER
         AGO   .C
.F       AIF   ('&PLIST(&N)' NE 'TWINBWD' AND '&PLIST(&N)' NE 'TB').G
         AIF   (&ORG LE 2).C
&B(2)    SETB  1                   INDICATES TWIN POINTER
&B(3)    SETB  1                   INDICATES TWINBWD POINTER
&SB(&S)  SETA  &SB(&S)+8*65536     ADD SIZE OF POINTERS
         AGO   .C
.G       AIF   (&B(5) OR &B(6)).I
         AIF   ('&PLIST(&N)' NE 'LTWIN' AND '&PLIST(&N)' NE 'LT').H
         AIF   (&ORG LE 2).C
&B(5)    SETB  1                   INDICATES LTWIN POINTER
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF POINTER
         AIF   (&PRD(&S)).L
         AGO   .C
.H       AIF   ('&PLIST(&N)' NE 'LTWINBWD' AND '&PLIST(&N)' NE 'LTB').I
         AIF   (&ORG LE 2).C
&B(5)    SETB  1                   INDICATES LTWIN POINTER
&B(6)    SETB  1                   INDICATES LTWINBWD POINTER
&SB(&S)  SETA  &SB(&S)+8*65536     ADD SIZE OF POINTER
         AIF   (&PRD(&S)).L
         AGO   .C
.I       AIF   (&B(7)).J
         AIF   ('&PLIST(&N)' NE 'LPARNT' AND '&PLIST(&N)' NE 'LP').J
&B(7)    SETB  1                   INDICATES LOGICAL PARENT POINTER
&GB1(11) SETB  0                   INDICATES LP PTR DEFAULT NOT REQ'D
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF POINTER
         AGO   .C
.J       AIF   (&PRD(&S) OR '&PLIST(&N)' NE 'PAIRED').K
&PRD(&S) SETB  1                   INDICATES PAIRED
         AIF   (&B(5)).L
         AGO   .C
.K    AIF  (&B(1) OR ('&PLIST(&N)' NE 'CTR' AND '&PLIST(&N)' NE 'C')).L
&B(1)    SETB  1                   INDICATES COUNTER
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF COUNTER
         AGO   .C
.L       MNOTE 8,'SEGM270 - POINTER OR PTR OPERAND CONTAINS INVALID '
         MNOTE *,'          OR CONFLICTING KEYWORD VALUES. '
&GB1(16) SETB  1
.M       AIF   (NOT &GB1(11)).N    LP PTR SPECIFIED, IF NEEDED @BA22679
&B(7)    SETB  1                   SET LP PTR BIT
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF POINTER
.N       AIF   (NOT &B(8) OR &S NE 1).O  IS ROOT HIERARCHIC ?
&SF2(1)  SETA  1                   NBR OF PHYSICAL CHILDREN FOR ROOT
&SB(1)   SETA  &SB(1)+4*65536      ADD SIZE OF PHYSICAL CHILD POINTER
.O       ANOP
&GB1(7)  SETB  (&B(2)+&PZ EQ 0 AND &S NE 1) SEQ FIELDS INVALID
&PARNT(&S) SETB (&GB1(6) OR &B(5)) PARENT PTR REQ, IF LOG RELATIONSHIP
         AIF   (NOT &PRD(&S)).S    SEGMENT PAIRED ?
         AIF   (&S LE 1 OR &PN EQ 0).R
&N       SETA  &SLCS(&PN)/65536    FIRST LOGICAL CHILD OF PARENT
&Y       SETA  &SLCS(&PN)-&N*65536+&N-1 LAST LOGICAL CHILD OF PARENT
         AIF   (&Y LT &N).R        NO LOGICAL CHILDREN ?
.P       AIF   ('&SLCP(&N)' NE '&SN(&S)').Q
         AIF   (&SLCF(&N)-&SLCF(&N)/16*16 EQ 0).S
         MNOTE 8,'SEGM420 - LCHILD STMT - &SLCN(&N) - MUST NOT SPECIFY'
         MNOTE *,'        POINTERS WHEN PAIRED WITH A LOGICAL SEGMENT.'
&GB1(16) SETB  1
         AGO   .S
.Q       ANOP
&N       SETA  &N+1
         AIF   (&N LE &Y).P
.R       MNOTE 8,'SEGM410 - INVALID OR INCOMPLETE SEGMENT PAIRING. '
&GB1(16) SETB  1
.S       AIF   (NOT &PARNT(&S)).U  PARENT POINTER REQUIRED ?
         AIF   (&PZ OR &S EQ 1).U  HS ORG OR ROOT SEGMENT ?
&B(4)    SETB  1
&SB(&S)  SETA  &SB(&S)+4*65536     ADD SIZE OF PARENT POINTER
&N       SETA  &PN
.T       AIF   (&N LE 1).U         NEXT PARENT = ROOT SEGMENT ?
         AIF   (&PARNT(&N)).U      PARENT PTR ?
&PARNT(&N) SETB 1
&SB(&N)  SETA  &SB(&N)+4*65536     ADD POINTER SIZE
&SF1(&N) SETA  &SF1(&N)+4096       SET PARENT PTR FLAG
         SEGPTRS &N                CHECK SEGMENT SIZE
&N       SETA  &SLDP(&N)/1048576   NEXT HIGHER SEGMENT
         AGO   .T
.U       ANOP
&HIER(&S) SETB (&B(8))             HIERARCHIC POINTERS
&SF1(&S) SETA  ((&B(1)*128)+(&B(2)*64)+(&B(3)*32)+(&B(4)*16)+(&B(5)*8)+,
               (&B(6)*4)+(&B(7)*2)+&B(8))*256
&N       SETA  1
&Y       SETA  1
         AIF   ('&RULES(1)' NE '').V
&SF1(&S) SETA  &SF1(&S)+207        DEFAULT IS X'CF' OR 11--1111
&Y       SETA  7
         AGO   .Z
.V       AIF   (K'&RULES(1) NE 3 OR N'&RULES GT 2).AA
.W       ANOP
&B(&Y)   SETB  ('&RULES(1)'(&N,1) EQ 'P' OR '&RULES(1)'(&N,1) EQ 'L')
&B(&Y+1) SETB  ('&RULES(1)'(&N,1) EQ 'V' OR '&RULES(1)'(&N,1) EQ 'L')
         AIF   (&N NE 2).X
         AIF   ('&RULES(1)'(2,1) EQ 'B').Y
.X       AIF   (NOT &B(&Y) AND NOT &B(&Y+1)).AA
.Y       ANOP
&N       SETA  &N+1
&Y       SETA  &Y+2
         AIF   (&N LT 4).W
.Z       ANOP
&B(&Y)   SETB  ('&RULES(2)' EQ 'FIRST' OR '&RULES(2)' EQ 'F' OR '&RULES,
               (2)' EQ 'HERE' OR '&RULES(2)' EQ 'H')
&B(&Y+1) SETB  ('&RULES(2)' EQ 'LAST' OR '&RULES(2)' EQ 'L' OR '&RULES(,
               2)' EQ 'HERE' OR '&RULES(2)' EQ 'H')
         AIF   (&B(&Y) OR &B(&Y+1)).AB
&B(&Y+1) SETB  (N'&RULES EQ 1 OR '&RULES' EQ '')
         AIF   (&B(&Y+1)).AB
.AA      MNOTE 8,'SEGM280 - RULES OPERAND IS INVALID. '
&GB1(16) SETB  1
.AB      AIF   ('&RULES(1)' EQ '').AC
&SF1(&S) SETA  &SF1(&S)+(&B(1)*128)+(&B(2)*64)+(&B(5)*8)+(&B(6)*4)+(&B(,
               3)*2)+&B(4)
.AC      ANOP
&SF1(&S) SETA  &SF1(&S)+(&B(8)*32)+(&B(7)*16)
         MEXIT
         MEND
