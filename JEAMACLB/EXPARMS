         MACRO
         EXPARMS &PARM=,&NBR=
         GBLA  &PNBR               NBR PARAMETERS PROCESSED.
         GBLB  &ERROR              ERROR FLAG.
         GBLC  &PLIST(100)         PARAMETER LIST MATRIX.
         LCLA  &XX,&P,&V,&L,&S,&N                                  6953
.*
.*       PROSE FOLLOWS MACRO.
.*
&ERROR   SETB  0
&PNBR    SETA  0
&P       SETA  1              CURR SUBSTRING PARAMETER.
&S       SETA  1              START CHAR OF CURR SUBSTRING PARM.
&V       SETA  1              CURR VALUE OF SUB PARM MATRIX.
.LOOP    AIF   ('&PARM(&P)' EQ '').NULL         IS PARAMETER NULL?
         AIF   ('&PARM(&P)'(&S,1) EQ '(').FNDS  IS PARAMETER A SUBLIST?
         AIF   (K'&PARM(&P) GT 8).ERR1
&PLIST(&V)  SETC  '&PARM(&P)'
&V       SETA  &V+1           NEXT VALUE MATRIX PTR.
&N       SETA  &N+1           NBR SUB PARM OPERANDS PROCESSED.
&XX      SETA  &XX+1+K'&PARM(&P)                                   7449
         AGO   .SETNULL
.FNDS    ANOP
&S       SETA  &S+1                               FIND 1ST CHARACTER OF
         AIF   ('&PARM(&P)'(&S,1) EQ '(').FNDS    SUBPARAMETER OPERAND.
.FNDL    AIF   (&N EQ &NBR).ERR2
&L       SETA  &L+1           FIND LAST CHAR OF CURR SUB PARM.
   AIF   ('&PARM(&P)'(&L,1) NE ',' AND '&PARM(&P)'(&L,1) NE ')').FNDL
         AIF   (&L GT &S).SET      VALUE NOT NULL?
&PLIST(&V)  SETC  ''
         AGO   .INCR
.SET     AIF   ((&L-&S) GT 8).ERR1
&PLIST(&V)  SETC  '&PARM(&P)'(&S,(&L-&S))
.INCR    ANOP
&V       SETA  &V+1           NEXT VALUE MATRIX PTR.
&S       SETA  &L+1           NEXT START CHAR.
&N       SETA  &N+1           NBR SUB PARM OPERANDS PROCESSED.
         AIF   ('&PARM(&P)'(&L,1) NE ')').FNDL
&XX      SETA  &XX+&S+1                                            6953
         AIF   (&XX GT K'&PARM).SETNULL                            6953
         AIF   ('&PARM'(&XX,1) NE ',' AND '&PARM'(&XX,1) NE ')').ERR3
.SETNULL AIF (&N EQ &NBR).NXTP          IF FEWER THAN THE MAXIMUM
.NULL    ANOP
&PLIST(&V)  SETC  ''                    NBR OF SUBLIST PARAMETERS
&V       SETA  &V+1                     ARE SPECIFIED, THE REMAINING
&N       SETA  &N+1                     OPERAND POSITIONS ARE SET TO
         AIF   (&N LT &NBR).NULL        NULL VALUES.
.NXTP    ANOP
&N       SETA  0              RESET SUB PARM OPERAND CTR.
&L       SETA  0
&S       SETA  1
&P       SETA  &P+1           NEXT PARMAMETER.
&PNBR    SETA  &PNBR+1
         AIF   (&P LE N'&PARM).LOOP
         MEXIT
.ERR1    MNOTE 8,'  SUBSTRING PARAMETER EXCEEDS 8 CHARACTERS.'
         MNOTE *,'     &PARM(&P)  '
&ERROR   SETB  1
         MEXIT
.ERR2    MNOTE 8,'  SUBSTRING PARAMETER CONTAINS EXCESSIVE OPERANDS.'
         MNOTE *,'  NUMBER OF OPERANDS CANNOT EXCEED &NBR.. '
         MNOTE *,'      &PARM(&P)   '
&ERROR   SETB  1
         MEXIT
.ERR3    MNOTE 8,'  SUBSTRING PARAMETER IS INVALID. '              6953
         MNOTE *,'  &PARM  '                                       6953
&ERROR   SETB  1                                                   6953
         MEXIT                                                     6953
.*
.*
.*
.**********************************************************************
.*                                                                    *
.*     THIS MACRO EXTRACTS PARAMETERS FROM A SUBLIST AND STORES THEM  *
.*  IN A GLOBAL MATRIX(&PLIST).  NULL VALUES IN THE ENTERED PARAMETER *
.*  LIST ARE STORED AS NULL VALUES IN THE &PLIST MATRIX.              *
.*                                                                    *
.*     INPUTS:                                                        *
.*                                                                    *
.*       PARM=  SPECIFIES THE INPUT PARAMETER LIST VALUES.            *
.*                                                                    *
.*       NBR=   SPECIFIES THE MAXIMUM NUMBER OF OPERAND VALUES TO     *
.*              BE ALLOWED IN EACH SUBPARAMETER.                      *
.*                                                                    *
.*     OUTPUTS:                                                       *
.*                                                                    *
.*       &PNBR  CONTAINS THE NUMBER OF SUBPARAMETERS IN THE ENTERED   *
.*              PARAMETER LIST.                                       *
.*                                                                    *
.*       &PLIST CONTAINS THE ENTERED PARAMETER LIST.  &PLIST IS A 100 *
.*              POSITION CHARACTER GLOBAL MATRIX.                     *
.*                                                                    *
.*       &ERROR A BINARY SWITCH, SET IF A INPUT ERROR IS ENCOUNTERED. *
.*              AN MNOTE MESSAGE IS PRODUCED WHENEVER &ERROR IS SET.  *
.*                                                                    *
.**********************************************************************
         MEND
