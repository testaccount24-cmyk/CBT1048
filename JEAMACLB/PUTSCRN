         MACRO
&NAME    PUTSCRN &ENTRY,&OPRNDS,&MF=I,&ADCON=,&WRITE=YES,&CURSOR=
.**********************************************************************
.*
.*       THE OPERAND 'ADCON=' ALLOWS SEVERAL WAYS TO ADDRESS THE
.*       SCREEN DEFINITION. IF  THE OPERAND IS OMITTED, THEN ACCESS
.*       IS BY 'LA 1,SCREEN-NAME'. IF AN ADCON OR VCON IS REQUIRED,
.*       (IF THE DEFINITION IS IN ANOTHER CSECT FOR EXAMPLE), THEN
.*       ADCON CAN BE CODED AS 'A' OR 'V' ACCORDINGLY.
.*
.*       THE OPERAND '&WRITE=' IS CODED AS 'WRITE=NO' IS A SCREEN
.*       HAS ALREADY BEEN WRITTEN AND IS STILL FORMATTED. IN THIS CASE
.*       IT IS NOT NECCESSARY TO RE-WRITE IT AND DATA CAN SIMPLY BE
.*       READ FROM THE SCREEN.
.*
.*       THE '&CURSOR=' OPERAND ALLOWS THE ISSUER TO SPECIFY
.*       DYNAMICALLY THE POSITION OF A CURSOR. THE OPERAND IS CODED
.*       AS EITHER THE ADDRESS OF A FULLWORD OR A REGISTER CONTAINING
.*       THE HALFWORD LINE AND COLUMN NUMBERS TO PLACE THE CURSOR.
.*       THE SCREEN DEFINITION MUST HAVE BEEN DEFINED WITH THE CURSOR
.*       OPTION IN THE HEADER MACRO.
.*
.**********************************************************************
         GBLB  &IHBSWA,&IHBSWB
         GBLC  &IHBNO
         LCLC  &GNAME
&IHBNO   SETC  '309'
&GNAME   SETC  'IHB'.'&SYSNDX'
&IHBSWA  SETB  1
&IHBSWB  SETB  0
         AIF   ('&MF' EQ 'L' AND '&ENTRY' NE '').ERROR1
         AIF   ('&MF' NE 'L' AND '&ENTRY' EQ '').ERROR3
         AIF   (T'&OPRNDS EQ 'O').NOOPS NO PASS LIST IS REQUIRED
         IHBOPLST &ENTRY,&OPRNDS,&NAME,MF=&MF
         AIF   ('&MF' EQ 'L').EXIT
         LR    0,1                               LOAD PASS LIST ADDRESS
.NOOPS   ANOP
         AIF   (T'&ADCON EQ 'O').SCLA  ADCON/VCON NOT NEEDED
         CNOP  0,4                 FULLWORD ALIGNMENT
         BAL   1,*+8               BRANCH AND LOAD PARAMETER REG
         DC    &ADCON.(&ENTRY)     ADDRESS OF SCREEN DEFINITION
         L     1,0(0,1)            LOAD ADDRESS OF DEFINITION
         AGO   .ADLODED            ADDRESS IS LOADED IN REG 1
.SCLA    ANOP  ,                   LOAD ADDRESS IS REQUIRED
         LA    1,&ENTRY                          LOAD 1 WITH DEFN ADR
.ADLODED ANOP
         AIF   (T'&CURSOR EQ 'O').NOCURS NO DYNAMIC CURSOR
         L     15,0(0,1)           LOAD ADDRESS OF SCREEN CNTL AREA
         AIF   ('&CURSOR'(1,1) EQ '(').REGCUR CURSOR ADDRESS IN REG
         MVC   4(4,15),&CURSOR     MOVE IN CURSOR DYNAMIC LOCATION
         AGO   .CURDONE            AND CONTINUE
.REGCUR  ANOP
&WORKA   SETA  K'&CURSOR-2         GET LENGTH OF REGISTER NO.
&WORKC   SETC  '&CURSOR'(2,&WORKA) GET REGISTER NUMBER
         ST    &WORKC,4(REG15)      STORE DYNAMIC CURSOR LOCATION
.CURDONE ANOP
.NOCURS  ANOP
         AIF   ('&WRITE' EQ 'YES').DOWRITE SUPPRESS WRITE NOT NEEDED
         LNR   1,1                 SIGNAL NO RE-WRITE NEEDED
.DOWRITE ANOP
         LINK  EP=SCREEN
.EXIT    MEXIT
.CONTCC  ANOP
&NAME    DS    0H
         AGO   .CONTC
.ERROR1  IHBERMAC 73,&IHBNO,&ENTRY          ENTRY  W/ MF=L
         MEXIT
.ERROR2  IHBERMAC 74,&IHBNO,&ID             ID W/ MF=L
         MEXIT
.ERROR3  IHBERMAC 26,&IHBNO                 ENTRY SYMBOL MISSING
         MEND
