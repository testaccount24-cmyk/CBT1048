         MACRO
         PSBGEN &LANG=,&PSBNAME=,&SSASIZE=,&IOASIZE=,&IOEROPN=,        *
               &MAXQ=,&CMPAT=
         GBLA  &P
         GBLA  &TPPCBS
         GBLA  &TPFLG1(255)       TP PCB FLAG1                 @BM02079
         GBLA  &GSAM                                           @BI5ZZ
         GBLA  &S
         GBLA  &PK(255)
         GBLA  &PSS(255)
         GBLA  &EXTDB
         GBLA  &DVSIZE
         GBLA  &SSE(500)
         GBLA  &SG
         GBLA  &SPC(500)
         GBLA  &SP(500)
         GBLA  &SS(255)
         GBLA  &SECNT
         GBLA  &SECN(500)
         GBLB  &E
         GBLB  &DB
         GBLB  &GSW                                            @BI5ZZ
         GBLB  &TP(255)
         GBLB  &PM(255)
         GBLB  &PEX(255)
         GBLB  &PFB(255)
         GBLB  &PS(255)
         GBLB  &SM(255)                                        @BI5ZZ
         GBLC  &PN(255)
         GBLC  &PO(255)
         GBLC  &SN(500)
         GBLC  &SPO(500)
         GBLC  &SSG(1000)
         GBLC  &EBCDIC
         GBLC  &SEC(1000)
         GBLC  &PSEQ(255)
         GBLC  &DBNAME(255)
         LCLA  &K,&L,&ERRCDE,&J,&N,&CT                         @BI591
         LCLA  &M,&X,&Y,&Z                                     @BI5ZZ
         LCLA  &SZ(2),&MQ
         LCLB  &COB,&PLI,&CMP,&WTR                             @BI5ZZ
         LCLC  &C                     WTOR SPECIFIED           @BI5ZZ
.*
.*
         AIF   ('&PSBNAME' NE '' AND K'&PSBNAME LE 8).PP
         MNOTE 8,'PGEN100 - PSBNAME OPERAND IS OMITTED OR INVALID. '
&E       SETB  1
.PP      ANOP
&COB     SETB  1
         AIF   ('&LANG' EQ 'COBOL' OR '&LANG' EQ 'ASSEM' OR '&LANG' EQ ,
               'FORTRAN').LL
&PLI     SETB  1
&COB     SETB  0
         AIF   ('&LANG' EQ 'PLI' OR '&LANG' EQ 'PL1' OR '&LANG' EQ 'PL/,
               I' OR '&LANG' EQ 'PL/1').LL
         MNOTE 8,'PGEN110 - LANG OPERAND IS OMITTED OR INVALID. '
&E       SETB  1
.LL      AIF   ('&SSASIZE' EQ '').LL03
         AIF   (T'&SSASIZE NE 'N' OR N'&SSASIZE NE 1).LL01
         AIF   (&SSASIZE GE 0 AND &SSASIZE LE 256000).LL02
.LL01    MNOTE 8,'PGEN150 - SSASIZE OPERAND IS INVALID. '
&E       SETB  1
         AGO   .LL03
.LL02    ANOP
&SZ(2)   SETA  &SSASIZE
.LL03    AIF   ('&IOASIZE' EQ '').LL06
         AIF   (T'&IOASIZE NE 'N' OR N'&IOASIZE NE 1).LL04
         AIF   (&IOASIZE GE 0 AND &IOASIZE LE 256000).LL05
.LL04    MNOTE 8,'PGEN160 - IOASIZE OPERAND IS INVALID. '
&E       SETB  1
         AGO   .LL06
.LL05    ANOP
&SZ(1)   SETA  &IOASIZE
.LL06    AIF   ('&MAXQ' EQ '').LL09
         AIF   (T'&MAXQ NE 'N' OR N'&MAXQ NE 1).LL07
         AIF   (&MAXQ GE 0 AND &MAXQ LE 32768).LL08
.LL07    MNOTE 8,'PGEN170 - MAXQ OPERAND IS INVALID. '
&E       SETB  1
         AGO   .LL09
.LL08    ANOP
&MQ      SETA  &MAXQ
.LL09    AIF   ('&CMPAT' EQ '').LL10
&CMP     SETB  ('&CMPAT' EQ 'YES')
         AIF   (&CMP OR '&CMPAT' EQ 'NO').LL10
         MNOTE 8,'PGEN180 - CMPAT OPERAND IS INVLAID. '
&E       SETB  1
.LL10    AIF   ('&IOEROPN' EQ '').LL12  OMITTED DEFAULT TO ONE @BI5ZZ
&J       SETA  0                        SET INDEX TO 0         @BI5ZZ
&CT      SETA  K'&IOEROPN(1)                                   @BI5ZZ
         AIF   (N'&IOEROPN GT 2).LL113  TOO MANY PARMS         @BI5ZZ
         AIF   (N'&IOEROPN EQ 1).LL101  ONE PARM               @BI5ZZ
         AIF   ('&IOEROPN(2)'(1,4) NE 'WTOR').LL113            @BI5ZZ
&WTR     SETB  1                        SET WRITE TO OP BIT    @BI5ZZ
.LL101   AIF   (&J GT K'&IOEROPN(1)).LL113 NO DELIMETERS       @BM02392
&J       SETA  &J+1                     BUMP COUNT             @BI5ZZ
         AIF   ('&IOEROPN(1)'(&J,1) EQ '(').LL101              @BI5ZZ
&N       SETA  &J                       START OF VALUE         @BI5ZZ
.LL111   ANOP                           CHECK LOOP FOR NUMERICS@BI5ZZ
         AIF   (&J+1 GT K'&IOEROPN(1)).LL104                   @BI591
&J       SETA  &J+1                                            @BI591
         AIF   ('&IOEROPN(1)'(&J,1) GE '0' AND '&IOEROPN(1)'(&J,1) LE  *
               '9').LL111
.LL113   MNOTE 8,'  IOEROPN SPECIFIED INCORRECTLY'
&E       SETB  1                                               @BI5ZZ
         AGO   .LL13                                           @BI5ZZ
.LL104   ANOP
&J       SETA  &J+1                                            @BI591
         AIF   (&J-&N GT 4).LL113                              @BI591
&C       SETC  '&IOEROPN(1)'(&N,&J-&N)  SET NUMBER IN BOX      @BI5ZZ
&ERRCDE  SETA  &C                       SET IN ABEND CODE      @BI5ZZ
         AIF   (&ERRCDE LT 0 OR &ERRCDE GT 4095).LL113         @BM02392
         AGO   .LL13                                           @BI5ZZ
.LL12    ANOP                           DEFAULT VALUE OF 0     @BM03365
&ERRCDE  SETA  0                                               @BM03365
.LL13    AIF   (NOT &E).GEN             EXPAND THE PSB         @BI5ZZ
         MNOTE 8,'PGEN120 - PSBGEN TERMINATED; ERRORS IN PSB. '
         MEXIT
.GEN     TITLE '                              IMS/VS PROGRAM SPECIFICAT*
               ION BLOCK GENERATION FOR &PSBNAME . '           @BA24684
         SPACE 3
***********************************************************************
*                                                                     *
*                             PREFIX                                  *
*                                                                     *
***********************************************************************
&PSBNAME CSECT
         DC    X'30',AL3(SEGTAB)   SENSITIVE SEGMENT TABLE ADDRESS.
         DC    A(SORTAB)      SOURCE SEGMENT TABLE ADDRESS.
         DC    A(DBREFTAB)    DATABASE REFERENCE TABLE ADDRESS.
PSBTOP   EQU   *
&N       SETA  (&COB*32)+(&PLI*16)
         DC    A(ENDTPPCB-PSBTOP)  PST ADDRESS.
         DC    3A(0)  PSBNDXWK, PSBXIOWK, PSBSEGWK.
         DC    A(&SZ(1),&SZ(2))  PSBIOAWK, PSBSSAWK.
         AIF   (NOT &GSW).SKP1                                 @BI5ZZ
         DC    3F'0' PSBPRMWK,  PSBXPCB,  RESERVED(4F)         @BM02241
         DC    H'0'  RETURN CODE TO O/S                        @BM02241
         DC    H'&ERRCDE'       USER RETURN CODE OPTION        @BM02241
         DC    F'0'             RESERVED                       @BM02241
         DC    AL1(128)   GSAM PCB EXISTS INDICATOR            @BI5ZZ
         DC    AL1(&GSAM) GSAM PCB COUNT                       @BI5ZZ
         DC    AL2((&P-&GSAM+1)*4) FIRST GSAM PCB OFFSET       @BI5ZZ
         AGO   .SKP2                                           @BI5ZZ
.SKP1    ANOP                                                      GSAM
         DC    3F'0'  PSBPRMWK, PSBXPCB, PSBCQCNT.             @BI5ZZ
         DC    H'0'                     RETURN CODE TO O/S     @BI591
         DC    H'&ERRCDE'               USER RETURN CODE OPTION@BI591
         DC    F'0'                     RESERVED               @BI591
         DC    F'0'                     GSAM RESERVED WORD     @BM02241
.SKP2    ANOP                                                      GSAM
         DC    AL1(&WTR*X'80'+&CMP*X'01') PSBFLAGS             @BI591
         DC    AL1(0),AL2(&MQ)           RESERVED, PSBMQCNT.   @BI5ZZ
         DC    AL1(0),AL1(&N),AL2(ENDTPPCB-PSBTOP)  INDEX, CODE, SIZE.
&N       SETA  &TPPCBS*4
&X       SETA  65535
         AIF   (NOT &DB).PD
&X       SETA  &N+4
.PD      DC    AL2(&N,&X)     LAST TP PCB AND FIRST DB PCB OFFSETS.
         DC    F'0'           I/O PCB ADDRESS.
         AIF   (&DB OR &TPPCBS NE 0 OR &GSW).LIST              @BI5ZZ
ENDTPPCB EQU   *
SEGTAB   EQU   *
SORTAB   EQU   *
DBREFTAB EQU   *
         MEXIT
.LIST    AIF   (&TPPCBS EQ 0).DBLIST
&Z       SETA  &Z+1
         AIF   (&Z LT &TPPCBS).TPL                                 4542
&Y       SETA  128-&DB*128                                         4542
.TPL     DC    AL1(&Y),AL3(PCB&Z-PSBTOP)  TP PCB OFFSET            4542
         AIF   (&Z LT &TPPCBS).LIST
.DBLIST  ANOP
&Z       SETA  &Z+1
&K       SETA  &P-&GSAM                                        @BI5ZZ
         AIF   (&Z GT &K).ENDBL                                @BI5ZZ
         AIF   (&Z LT &K).DBL                                  @BI5ZZ
&Y       SETA  128
.DBL     DC    AL1(&Y),AL3(PCB&Z)       DB PCB ADDRESS.
         AGO   .DBLIST
.ENDBL   ANOP                                                      GSAM
         AIF   (NOT &GSW).ENDLIST                              @BI5ZZ
&Y       SETA  0                                               @BI5ZZ
.GSAML1  ANOP                                                      GSAM
&L       SETA  &L+1                                            @BI5ZZ
         AIF   (&L GT &GSAM).ENDLIST                           @BI5ZZ
         AIF   (&L LT &GSAM).GSAML2                            @BI5ZZ
&Y       SETA  128                                             @BI5ZZ
.GSAML2  DC    AL1(&Y),AL3(PCB&Z)       GSAM PCB ADDRESS.      @BI5ZZ
&Z       SETA  &Z+1                                            @BI5ZZ
         AGO   .GSAML1                                         @BI5ZZ
.ENDLIST AIF   (&TPPCBS EQ 0).LTP
         EJECT
***********************************************************************
*                                                                     *
*                           TP PCB'S                                  *
*                                                                     *
***********************************************************************
&N       SETA  1
.TP      ANOP
PCB&N    EQU   *
         AIF   (NOT &PLI).SKV
         PCBPDV TV,&N
.SKV     ANOP
TNAME&N  DC    CL8'&PN(&N)'   DESTINATION NAME.
&Y       SETA  &PEX(&N)*64+&PM(&N)
CNT&N    DC    HL1'&Y,&TPFLG1(&N)' TPFLAG1 BYTE                @BM02079
STAT&N   DC    CL2' '         STATUS CODE.
PREF1&N  DC    F'0'           PREFIX - DATA 00YYDDDC.
PREF2&N  DC    F'0'           PREFIX - TIME HHMMSSTC.
PREF3&N  DC    F'0'           PREFIX - MESSAGE NUMBER.
OPTL&N   DC    F'0'           LAST TTR.
OPTN&N   DC    F'0'           NEXT TTR.
SMBPT&N  DC    F'0'           CNT/SMB POINTER.
.LTP     ANOP
&N       SETA  &N+1
         AIF   (&N GT &TPPCBS).STDB
         SPACE 3
         AGO   .TP
.STDB    ANOP
ENDTPPCB EQU   *
         AIF   (&N GT &P).SEG
         EJECT
***********************************************************************
*                                                                     *
*                           DB PCB'S                                  *
*                                                                     *
***********************************************************************
.DB      AIF   (&N GT &P).SEG
PCB&N    DS    0F
         AIF   (NOT &PLI).SKVA
         PCBPDV DV,&N,&PK(&N),&SM(&N)                          @BI5ZZ
.SKVA    ANOP
DNAME&N  DC    CL8'&PN(&N)'   DBD NAME.
LEVFD&N  DC    H'0'           LEVEL FEEDBACK.
STACD&N  DC    CL2' '         STATUS CODE.
PROCO&N  DC    CL4'&PO(&N)'   PROCESSING OPTION.
         AIF   (NOT &SM(&N)).SKP3                              @BI5ZZ
JCBAD&N  DC    AL1(2) GSAM PCB INDICATOR                       @BI5ZZ
         DC    AL3(0) RESERVED FOR GB ADDR                     @BI5ZZ
SEGFD&N  DC    CL8' '                                          @BI5ZZ
KEYLN&N  DC    AL1(0),AL3(12) POSITION, LENGTH OF KEY FB.      @BI5ZZ
NOSS&N   DC    AL2(0),AL2(65535) NSS, OFFSET TO 1ST SENSEG     @BI5ZZ
KEYFD&N  DC    XL12'00' RRA AND URL FEEDBACK AREA              @BI5ZZ
         AGO   .TDB                                            @BI5ZZ
.SKP3    ANOP                                                      GSAM
JCBAD&N  DC    F'0'           JCB ADDRESS.
SEGFD&N  DC    CL8' '         SEGMENT NAME FEEDBACK.
KEYLN&N  DC    AL1(&PS(&N)),AL3(&PK(&N)) POSITION, LENGTH OF KEY FB.
&X       SETA  ((&PSS(&N)/65536)-1)*20
NOSS&N   DC    H'&SS(&N)',H'&X'  NSS, OFFSET TO 1ST SENSEG.
KEYFD&N  DC    &PK(&N).CL1' '   KEY FEEDBACK AREA.
         AIF   (&SS(&N) NE 0).TDB
         SPACE
         MNOTE 4,'PGEN130 - PCB&N HAS NO SENSITIVE SEGMENTS. '
.TDB     ANOP
&N       SETA  &N+1
         AIF   (&N GT &P).SEG
         SPACE 3
         AGO   .DB
.SEG     AIF   (&S NE 0).STSEG
SEGTAB   DS    0F
         AGO   .EXTDB
.STSEG   EJECT
***********************************************************************
*                                                                     *
*                      SENSITIVE SEGMENTS                             *
*                                                                     *
***********************************************************************
SEGTAB   DS    0F
&N       SETA  0
.NSEG    ANOP
&X       SETA  0
&N       SETA  &N+1
         AIF   (&N GT &S).EXTDB
         DC    CL8'&SN(&N)'   SENSITIVE SEGMENT NAME.
         DC    CL4'&SPO(&N)'     PROCESSING OPTION.
         AIF   (&SPC(&N) EQ &SPC(&N+1)).NS
&X       SETA  128
.NS      DC    AL1(&X),AL3(PCB&SPC(&N))  FLAG, PCB ADDRESS.
&X       SETA  &SSE(&N)/65536/3*12
         AIF   ((&SSE(&N)/65536) NE 0).NSA
&X       SETA  65535
.NSA     ANOP
&Y       SETA  65535
         AIF   (&SP(&N) EQ 0).NSB
&Y       SETA  (&SP(&N)-1)*20
.NSB     DC    AL2(&Y),AL2(&X)  PARENT OFFSET, SOURCE SEGMENT OFFSET.
         SPACE 3
         AGO   .NSEG
.EXTDB   AIF   (&SG NE 0 OR &EXTDB NE 0 OR &SECNT NE 0).XDBS
         SPACE 2
SORTAB   EQU   *
DBREFTAB EQU   *
&N       SETA  1
.MINCK   AIF   ('&PSEQ(&N)' NE '').MIN1
&N       SETA  &N+1
         AIF   (&N LE &P).MINCK
         MEXIT
.XDBS    EJECT
***********************************************************************
*                                                                     *
*              SOURCE SEGMENT AND DATABASE REFERENCE TABLES           *
*                                                                     *
***********************************************************************
&N       SETA  1
&M       SETA  1
SORTAB   EQU   *
         AIF   (&SG EQ 0).RDB
.FSSE    AIF   (&SSE(&M) NE 0).SSE
&M       SETA  &M+1
         AIF   (&M LE &S).FSSE
         SPACE
         MNOTE 12,'PGEN140 - SYSTEM ERROR; PSBGEN TERMINATED. '
         MEXIT
.SSE     ANOP
&Y       SETA  &SSE(&M)-(&SSE(&M)/65536*65536)
&Y       SETA  &SSE(&M)-(&SSE(&M)/65536*65536)
.SOR     AIF   ((&N+2) GT &SG).RDB
         DC    CL8'&SSG(&N)'  SOURCE SEGMENT NAME.
&X       SETA  1
         AIF   ('&SSG(&N+1)' EQ 'DATA').SORA
&X       SETA  2
         AIF   ('&SSG(&N+1)' EQ 'PATH').SORA
&X       SETA  4
.SORA    ANOP
&Z       SETA  (&SSG(&N+2)-1)*12
         AIF   ((&N+2) EQ &SG).SORB
         AIF   (&Y NE (&N+2)).SDC
.SORB    ANOP
&X       SETA  &X+128
&M       SETA  &M+1
.NSSE    AIF   (&SSE(&M) NE 0).SETSSE
&M       SETA  &M+1
         AIF   (&M LT &S).NSSE
         AGO   .SDC
.SETSSE  ANOP
&Y       SETA  &SSE(&M)-(&SSE(&M)/65536*65536)
.SDC     DC    AL1(&X),AL3(&Z)  FLAG, SOURCE SEGMENT DATABASE ENTRY.
         SPACE 2
&N       SETA  &N+3
         AGO   .SOR
.RDB     SPACE 3
DBREFTAB EQU   *
&N       SETA  1
.NDB     AIF   (&N GT &EXTDB).DONE
         DC    CL8'&DBNAME(&N)'  REFERENCED DATABASE NAME.
         DC    F'0'           RESERVED.
&N       SETA  &N+1
         AGO   .NDB
.DONE    ANOP
&N       SETA  1
.MIN1    AIF   ('&PSEQ(&N)' EQ '').MIN2
         DC    CL8'&PSEQ(&N)' PROCESSING SEQUENCE INDEX DBD NAME
         DC    AL1(64),AL3(PCB&N) CODE BYTE, OFFSET TO PCB
.MIN2    ANOP
&N       SETA  &N+1
         AIF   (&N LE &P).MIN1
         AIF   (&SECNT EQ 0).MINEND
&X       SETA  1
&N       SETA  1
.MIN3    AIF   (&SECN(&N)/65536/256/32 EQ 1).MIN4
&N       SETA  &N+1
         AIF   (&N LE &S).MIN3
.MINEND  DC    F'-1'
         MEXIT
.MIN4    ANOP
&Y       SETA  &SECN(&N)/65536-&SECN(&N)/65536/256*256
&Z       SETA  &SECN(&N)-&SECN(&N)/65536*65536
         DC    CL8'&SEC(&X)'  SENSEG SECONDARY INDEX NAME
         DC    AL1(32),AL1(&Y),AL2(&Z)  CODE, # OF INDICES, SENSEG OFF.
&Y       SETA  &X+(&Y-1)
.MIN5    ANOP
&X       SETA  &X+1
         AIF   (&X GT &Y).MIN6
         DC    CL8'&SEC(&X)' SENSEG SECONDARY INDEX NAME
         DC    AL1(32,0),AL2(&Z)   CODE BYTE, OFFSET TO SENSEG
         AGO   .MIN5
.MIN6    ANOP
&N       SETA  &N+1
         AIF   (&N LE &S).MIN3
         AGO   .MINEND
         MEXIT
         MEND
