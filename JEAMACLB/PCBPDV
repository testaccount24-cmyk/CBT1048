         MACRO
&LAB     PCBPDV &TY,&V1,&V2,&V3,&V4,&V5,&MASK=
         GBLA  &DVSIZE
         LCLC  &R,&RETRN
*        MODIFIED  8-10-73 FOR MVM                                 MVM
         AIF   ('&TY' NE 'DV').TRYTA
.**********************************************************************
.*                                                                    *
.*             GENERATE DB PCB DOPE VECTORS.                          *
.*                                                                    *
.*                 &V1 = ADDRESS REFERENCE SUFFIX VALUE.              *
.*                 &V2 = MAX CONCATENATED KEY LENGTH.                 *
.*                                                                    *
.**********************************************************************
         SPACE
***********************************************************************
*                                                                     *
*                          PL/I DOPE VECTORS                          *
*                                                                     *
***********************************************************************
         DC    A(DNAME&V1-*),H'8',H'8'  CHAR(8)  DB NAME DOPE VECTOR.
         DC    A(LEVFD&V1-*),H'2',H'2'  CHAR(2)  LEVEL FEEDBACK DV.
         DC    A(STACD&V1-*),H'2',H'2'  CHAR(2)  STATUS CODE DV.
         DC    A(PROCO&V1-*),H'4',H'4'  CHAR(4)  PROCESSING OPTIONS DV.
         DC    A(JCBAD&V1-*)  FIXED BIN(31)  JCB ADDRESS DOPE VECTOR.
         DC    A(SEGFD&V1-*),H'8',H'8'  CHAR(8)  SEG FEEDBACK DV.
         DC    A(KEYLN&V1-*)  FIXED BIN(31) LEVEL FEEDBACK DOPE VECTOR.
         DC    A(NOSS&V1-*)  FIXED BIN(31) NO SEN SEG DOPE VECTOR.
         AIF   (NOT &V3).DBPCBDV                               @BI5ZZ
         DC    A(KEYFD&V1-*)   FIXED BIN(31) GSAM RBA DOPE VECT@BI5ZZ
         DC    A(KEYFD&V1-*+4) FIXED BIN(15) GSAM VOL SEQ DV.  @BI5ZZ
         DC    A(KEYFD&V1-*+6) FIXED BIN(15) GSAM DISPLACEMENT @BI5ZZ
         DC    A(KEYFD&V1-*+8) FIXED BIN(31) GSAM URL DOPE VECT@BI5ZZ
&DVSIZE  SETA  8+8+8+8+4+8+4+4+4+4+4+4                         @BI5ZZ
         SPACE 1
         MEXIT
.DBPCBDV ANOP
         DC    A(KEYFD&V1-*),H'&V2',H'&V2' CHAR(N) N=MAX CONCATENATED K,
               EY
&DVSIZE  SETA  8+8+8+8+4+8+4+4+8        SIZE OF DB PCB DOPE VECTOR.
.OUT     SPACE
         MEXIT
.TRYTA   AIF   ('&TY' EQ 'TA').TA
.TRYTV   AIF   ('&TY' NE 'TV').TRYDR
.**********************************************************************
.*                                                                    *
.*             GENERATE TP PCB DOPE VECTORS (TV AND TA TYPE CALLS)    *
.*                                                                    *
.*                 &V1 = ADDRESS REFERENCE SUFFIX VALUE.              *
.*                                                                    *
.**********************************************************************
&R       SETC  '-*'
.TA      SPACE
***********************************************************************
*                                                                     *
*                          PL/I DOPE VECTORS                          *
*                                                                     *
***********************************************************************
DVTNM&V1 DC    A(TNAME&V1&R),H'8',H'8'  CHAR(8)  TERMINAL NAME DV.
DVCNT&V1 DC    A(CNT&V1&R),H'16',H'16'  BIT(16)  RESERVED
DVSTA&V1 DC    A(STAT&V1&R),H'2',H'2' CHAR(2) STATUS CODE DOPE VECTOR.
DVPR1&V1 DC    A(PREF1&V1&R) DEC(5) PREFIX - DATE DOPE VECTOR
DVPR2&V1 DC    A(PREF2&V1&R) DEC(7) PREFIX - TIME DOPE VECTOR
DVPR3&V1 DC    A(PREF3&V1&R) FIXED BIN(31) PREFIX - MSG NBR DOPE VECTOR
&DVSIZE  SETA  8+8+8+4+4+4              SIZE OF TP PCB DOPE VECTOR.
         AIF   ('&TY' EQ 'TV').OUT
DVMOD&V1 DC    A(MODN&V1&R),H'8',H'8'  CHAR(8)  MODULE NAME DV.
.*
.*             GENERATE TP PCB FOR TA CALL.
.*
         DC    F'0'           RESERVED
TNAME&V1 DC    CL8' '         PSTSYSMB0
CNT&V1   DC    X'1000'        PSTCODE,PSTRFFU
STAT&V1  DC    CL2' '         PSTSTAT
PREF1&V1 DC    F'0'           PSTPRE1
PREF2&V1 DC    F'0'           PSTPRE2
PREF3&V1 DC    F'0'           PSTPRE3
MODN&V1  DC    CL8' '         PSTMODN
         DC    3F'0'          PSTOPQCR,PSTOBUFF,PSTUSER
         MEXIT
.TRYDR AIF ('&TY' NE 'DR' AND '&TY' NE 'TR').BADTYPE
.**********************************************************************
.*                                                                    *
.*       GENERATE CODE TO RELOCATE TP AND DB PCB DOPE VECTORS.        *
.*                                                                    *
.*             &V1 = PCB BASE REG.                                    *
.*             &V2 = MASK REG.                                        *
.*             &V3 = WORK REG.                                        *
.*             &V4 = RETURN ADDRESS.                                  *
.*             &V5 = RETURN REG.                                      *
.*                                                                    *
.*          IF BOTH &V4 AND &V5 ARE SPECIFIED, &V5 WILL BE USED.      *
.*                                                                    *
.**********************************************************************
         AIF   ('&V4' EQ '' AND '&V5' EQ '').ERR
&RETRN   SETC  '&V4'
         AIF   ('&V5' EQ '').CON
&R       SETC  'R'
&RETRN   SETC  '&V5'
.CON     SPACE
&LAB     LA    &V2,&TY.V&SYSNDX.M       LOAD ADDR OF MASK.
&TY.V&SYSNDX.L CLI   0(&V2),X'80'       TEST FOR END OF MASK.
         BC&R   8,&RETRN                LOOP EXIT.
         SR    &V3,&V3                  CLEAR WORK REG.
         IC    &V3,0(,&V2)              INSERT MASK BYTE.
         AR    &V1,&V3                  ADD MASK TO BASE.
         L     &V3,0(,&V1)              LOAD DOPE VECTOR.
         AR    &V3,&V1                  ADD BASE TO DOPE VECTOR.
         ST    &V3,0(,&V1)              STORE RELOCATED DOPE VECTOR.
         BCT   &V2,&TY.V&SYSNDX.L       LOOP CONTROL.
         AIF   ('&TY' EQ 'DR').DBMASK
         DC    X'80040408080800'        TP PCB DOPE VECTOR MASK.
         AGO   .END
.DBMASK  DC    X'80040408040808080800'  DB PCB DOPE VECTOR MASK.
.END     ANOP
&TY.V&SYSNDX.M EQU   *-1
         AIF   ('&MASK' EQ '').FIN
&MASK    EQU   &TY.V&SYSNDX.M
.FIN     DS    0H
         SPACE
         MEXIT
.BADTYPE MNOTE 12,' OPERATION TYPE OPERAND IS INVALID - &TY '
         MEXIT
.ERR     MNOTE 12,' NO RETURN ADDR OR REG SPECIFIED.'
         MEND
