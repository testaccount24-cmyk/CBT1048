         MACRO
&NAME    PCB   &DBDNAME=,          NAME OF DBD REFERENCED BY PCB       X
               &ACCESS=INPUT,      OPTIONS ARE: OUTPUT AND UPDATE      X
               &SENSEGS=0,         NUMBER OF SENSITIVE SEGMENTS        X
               &INDD=SYSUT1,       NAME OF INPUT DD CARD FOR FILE      X
               &OUTDD=SYSUT2       NAME OF OUTPUT DD CARD FOR FILE
         LCLA  &FLAG               USED TO SET BITS IN FLAG FIELD
         LCLA  &WORK               WORK ARITHMETIC
.*
.*
         DS    0D                  ALIGNMENT FIELD
&WORK    SETA  &SENSEGS*24
         DC    A(IHB&SYSNDX-*+&WORK) LENGTH OF THIS PCB
         DC    CL8'&NAME'          NAME OF THIS PCB
         DC    CL8'&DBDNAME'       NAME OF DBD
         DC    A(0)                ADDRESS OF DBD FOR THIS PCB
         DC    A(IHB&SYSNDX)       ADDRESS OF SENSITIVE SEGMENT TABLE
         DC    F'&SENSEGS'         NUMBER OF SENSITIVE SEGMENTS
.*
.*       SET THE FLAG BIT FOR CORRECT PROCESSING OPTION
.*
         AIF   ('&ACCESS' NE 'INPUT').XSTEST2 IS THIS INPUT ?
&FLAG    SETA  &FLAG+128           SET ON 'INPUT' BIT
         OPEN  (IHB&SYSNDX.I,INPUT),MF=L CREATE OPEN PARAMETER LIST
         DC    F'0'                ZERO ADDRESS FOR OUTPUT DCB
         CLOSE (IHB&SYSNDX.I),MF=L CREATE CLOSE PARAMETER LIST
         DC    F'0'                ZERO ADDRESS FOR OUTPUT DCB
         AGO   .DEFFLG             GO DEFINE THE FLAG FIELD
.XSTEST2 AIF   ('&ACCESS' NE 'OUTPUT').XSTEST3 IS THIS FOR OUTPUT ?
&FLAG    SETA  &FLAG+64            SET OUTPUT BIT ON
         OPEN  (IHB&SYSNDX.I,INPUT,IHB&SYSNDX.O,OUTPUT),MF=L
         CLOSE (IHB&SYSNDX.I,,IHB&SYSNDX.O),MF=L
         AGO   .DEFFLG             GO DEFINE THE FLAG
.XSTEST3 AIF   ('&ACCESS' NE 'UPDATE').DEFFLG IS THIS UPDATE MODE ?
&FLAG    SETA  &FLAG+32            SET ON UPDATE BIT
         OPEN  (IHB&SYSNDX.I,UPDAT),MF=L
         DC    F'0'                ZERO ADDRESS FOR OUTPUT DCB
         CLOSE (IHB&SYSNDX.I),MF=L
         DC    F'0'                ZERO ADDRESS FOR OUTPUT DCB
.DEFFLG  ANOP
         DC    AL1(&FLAG)          PROCESSING FLAG
.*
.*
         DC    XL3'00'             FLAG FIELDS
         DC    10F'0'              WORK AREA FOR PCB
IHB&SYSNDX.I DS 0F                 START OF INPUT DCB
         DCB   DDNAME=&INDD,DSORG=PS,MACRF=(GL,PL),RECFM=VB
         AIF   ('&ACCESS' NE 'OUTPUT').NOEXDCB
IHB&SYSNDX.O DS 0F                 START OF OUTPUT DCB
         DCB   DDNAME=&OUTDD,DSORG=PS,MACRF=PM,RECFM=VB
.NOEXDCB ANOP
IHB&SYSNDX DS 0D                   START OF SENSEG TABLE
         MEND
