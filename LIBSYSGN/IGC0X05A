   TITLE ' FORMAT DUMP CONTROL BLOCKS PART 3'
IGC0X05A   CSECT
R0       EQU       0
R1       EQU       1
R2       EQU       2
R3       EQU       3
R4       EQU       4
R5       EQU       5
R6       EQU       6
R7       EQU       7
R8       EQU       8
R9       EQU       9
RB       EQU       11
RA       EQU       10
RC       EQU       12
RD       EQU       13
RE       EQU       14
RF       EQU       15
PARREG   EQU       14
CVT      EQU       16
TCBTIOT   EQU      12
TCBDEB   EQU       8
DEBDCB   EQU       24
DEBDEB   EQU       4
DCBDEB   EQU       44
DCBTIOT  EQU      40
         BALR  RC,0
         USING *,RC               R12 = BASE
         USING ABDAREA,RD
         USING PARDSECT,PARREG    R 14=PTR TO PARMLIST
         L     R9,CVT             GET CVT ADDRESS
         L     R9,164(R9)         GET HIGH MACHINE ADDRESS
         LA    R9,000(R9)         CLEAR HI BYTE.
         LA    RA,256             DECREMENT FOR POSSIBLE
         SR    R9,RA              HIGH ADDR WHEN WRITING
         ST    R9,LIMIT           SAVE HI ADDRESS
         MVI       ABDLINE,C'*'
         MVC       ABDLINE+1(119),ABDLINE
         BAL        R5,ABDOUT   PRINT THE ASTRIC
*
* INITIALIZE DEB AND SET UP ROUTINE
*
         L     R9,TCBDEB(R2)      GET DEB POINTER
TESTEND  LA    R9,0(R9)           CLEAR HI BYTE
         LTR   R9,R9              TEST FOR ADDR OF DEB
         BZ    EXIT               BRANCH IF END OF DEB CHAIN
         L     RA,DEBDCB(R9)      GET POINTER TO DCB
         LA    RA,0(RA)           CLEAR HIGH BYTE
         TM    DCBDEB+3(RA),X'03' IS IT VALID ADDRESS
         BC    7,BADCORE          ERROR MSG IF SO
         L     RB,DCBDEB(RA)      GET DEB ADDR FROM DCB
         LA    RB,0(RB)           CLEAR HIGH BYTE
         CR   R9,RB
         BNE   BADCORE            BRANCH IF NOT TO PRINT CORE MSG
         TM    DCBTIOT+1(RA),X'03' IS IT VALID ADDRESS
         BC    7,BADCORE          ERROR MSG IF SO
         L     RB,DCBTIOT(RA)     TIOT DISPLACEMENT
         L     R6,TCBTIOT(R2)     GET TIOT ADDR
         SRL   RB,16              ADJ OFFSET
         LA    RB,0(R6,RB)        POINTER TO TIOT ENTRY
         C     RB,LIMIT           IS ENTRY VALID ADDRESS
         BH    BADADDR            BRANCH IF BAD ADDRESS
         ST    RB,ABDWORK2        SAVE TIOT ENTRY POINTER
         SR    RA,RA              CLEAR REG 10
         IC    RA,0(RB)           LENGTH OF DD ENTRY IN TIOT
         SH    RA,SIXTEEN         LENGTH OF ENTRY -16 AND DIVIDED
         SRL   RA,2               BY 4 = NUMBER OF UCB'S
         CLC   SYSAB(8),4(RB)     IS IT SYSABEND DD CARD
         BE    NEXTDEB            IF YES, BR TO GET NEXT DEB ADDR
         CLC   SYSU(8),4(RB)      IS IT SYSUDUMP DD CARD
         BE    NEXTDEB            IF YES, BR TO GET NEXT DEB ADDR
         MVC       ABDLINE(8),4(RB)  GET DD NAME
*
* R6=TIOT,R9=DEB,RA=#OFUCB'S,RB=TIOTENTRY,RC=BASE,RD=ABDAREA,R2=TCB
*
DEBPRT   EQU   *
*
* DEB PRINT ROUTINE
*
* DCB PRINT ROUTINE.
* LENGTH OF DCB MUST BE FOUND IN ORDER TO PRINT THE DCB FOR ALL
* ACCESS METHODS.
*
         L     RB,DEBDCB(R9)      GET DCB ADDRESS FOR PRINT
         LA        RB,0(RB)   CLEAR HIGH ORDER BYTE
         ST        RB,DCBADD  SAVE DCB ADDRESS
*
* IOB PRINT ROUTINE
* IOB'S WILL BE PRINTED ONLY IF THEY ARE BSAM, BPAM, OR QSAM
* RB=DCB ADDRESS, R7=IOB ADDRESS, R8=WORK REGISTER
*
*
* UCB PRINT ROUTINE
*
NOIOB EQU *
NEXTCB   L     RB,ABDWORK2        RESTORE TIOT ENTRY ADDRESS
         LA    RB,16(RB)          SHIFT RB TO UCB ENTRY
AGAIN  EQU  *
         MVC   ABDLINE+13(3),UCB  SET UP HEADER LINE
         L     R4,0(RB)           POINT TO UCB FOR PRINT RTN
         LA    R4,0(R4)           CLEAR HIGH BYTE
         C     R4,LIMIT           IS IT WITHIN THE BOX
         BH    UCBBAD             PRINT ERROR MSG IF NOT
         TM    2(R4),X'FF'        IS THIS A UCB
         BO    UCBOK              BRANCH IF OK
         CLC   18(2,R4),DEV2321   IS IT A 2321
         BNE   UCBBAD             BRANCH TO PRINT ERROR MSG
UCBOK   EQU   *
         MVC       ABDLINE+17(L'UCBMSG),UCBMSG
         MVC        ABDLINE+59(6),28(R4)   MOVE IN VOL SER
         MVC       ABDLINE+22(3),13(R4)     GET  UNIT
         UNPK      ABDLINE+38(13),22(7,R4)  GET  SENSE BYTE
         TR        ABDLINE+38(12),TBL-240
         MVI       ABDLINE+50,C' '
         TM        6(R4),X'80'
         BNO       ITISNOTE
         MVC       ABDLINE+93(4),=C'BUSY'
         B         XX5
ITISNOTE TM        6(R4),X'40'
         BNO       ITISNOTF
         MVC       ABDLINE+93(9),=C'NOT READY'
         B         XX5
ITISNOTF MVI       ABDLINE+81,C' '
         MVC       ABDLINE+82(24),ABDLINE+81
XX5      UNPK      ABDLINE+75(7),20(3,R4)   GET   LAST  REQ
         TR        ABDLINE+75(6),TBL-240
         MVI       ABDLINE+81,C' '
         BAL       R5,ABDOUT
         TM        18(R4),X'80'  IS IT A TAPE
         BNO       ITISNOTG
         MVC       ABDLINE+13(12),=C'BLOCK COUNT='
         L         R5,DCBADD  GET  DCB ADDRESS
         L         R5,12(R5)  GET  BLOCK COUNT
         CVD       R5,ABDWORK1  MAKE IT DECMIAL
         UNPK      ABDLINE+25(5),ABDWORK1+5(3)
        OI         ABDLINE+29,X'F0'  MAKE LAST CHAR PRINTABLE
         L         R5,DCBADD
         IC        R5,20(R5)       GET BUFFER NUMBER NUMBER
         N         R5,=X'000000FF'
         CVD       R5,ABDWORK1
         UNPK      ABDLINE+49(3),ABDWORK1+6(2)
         OI        ABDLINE+51,X'F0'
         MVC       ABDLINE+31(L'BUFMSG),BUFMSG
         L         R5,DCBADD
         TM        36(R5),X'48'         IS IT VARB SPANN%D
         BNO       ITISNOTZ
         MVC       ABDLINE+53(19),=C'RECORDS ARE SPANNED'
ITISNOTZ  EQU  *
         TM        17(R4),X'04'    IS IT CHANNEL TO CHANNEL
         BNO       ITISNOTY
         MVC  ABDLINE+73(26),=C'UNIT IS CHANNEL TO CHANNEL'
ITISNOTY EQU       *
         TM     17(R4),X'80'        IS IT 7 TRK TAPE
         BNO       ITISNOTH
         MVC       ABDLINE+100(13),=C'UNIT IS 7 TRK'
ITISNOTH TM     17(R4),X'20'    IS IT DUAL DENSITY
         BNO       ITISNOTI
         MVC       ABDLINE+100(16),=C'UNIT IS DUAL DEN'
ITISNOTI EQU       *
         BAL       R5,ABDOUT  PRINT ITITITITIT
ITISNOTG   EQU   *
         LA    RB,4(RB)           POINT TO THE NEXT ENTRY IN TIOT
         BCT   RA,AGAIN           BRANCH BACK ON = OF UCB'S
NEXTDEB  TM    DEBDEB+3(R9),X'03' IS IT VALID ADDRESS
         BC    7,BADADDR          ERROR MSG IF SO
         L     R9,DEBDEB(R9)      GET NEXT DEB ADDRESS
         B     TESTEND            SEE IF WE'RE THROUGH
UCBBAD   EQU  *
SETCC    EQU  *
BADCORE  EQU  *
         B     NEXTDEB            TRY TO PROCCESS NEXT DEB
BADADDR    EQU  *
EXIT   EQU  *
         MVC       ABDLINE(36),=C'DISPATCHING PRIORITY OF THIS TASK IS'
         SR        R5,R5
         IC        R5,35(R2)  GET DISPATCHING PRIORITY
         CVD       R5,ABDWORK1
         UNPK      ABDLINE+37(3),ABDWORK1+6(2)
         OI        ABDLINE+39,X'F0'
         BAL       R5,ABDOUT  PRINT ITITITITITIT
NOSMF    EQU       *
         LA        R9,4
LOOPIT   MVI       ABDLINE,C'*'
         MVC       ABDLINE+1(119),ABDLINE
         BAL       R5,ABDOUT
       BCT         R9,LOOPIT
         LA        RA,LOAD5
         LA    RF,ABDWORK3
         XCTL  EPLOC=(10),SF=(E,(15))
LOAD5    DC    CL8'IGC0W05A'
SYSU     DC    CL8'SYSUDUMP'
SYSAB    DC    CL8'SYSABEND'
SIXTEEN  DC    H'16'
DCBADD   DC        F'0'  SAVE DCB ADDRESS HERE
DEV2321  DC    X'2005'            MASK FOR 2321
UCB      DC    C'UCB'
TBL      DC    C'0123456789ABCDEF'
   EJECT
UCBMSG DC C'UNIT=XXX,SENSE BYTES=XXXXXXXXXXXX VOL SER=XXXXXX LAST RQE XX
               XXXXX,DEVICE WAS'
BUFMSG   DC       C'NUMBER OF BUFFERS'
       LTORG
ABDAREA  DSECT
ABDSAVE  DS    18F                SAVE AREA
ABDECB   DS    1F                 DECB-ECB
ABOTYPE  DS    1H                 TYPE FIELD
ABDLEN   DS    1H                 LENGTH FILD
ABDDCB   DS    1F                 DCB
ABDPTR   DS    1F                 AREA ADDRESS
ABDSTAT  DS    1F                 POINTER TO STATUS INDICATORS
ABDCTR1  DS    1H                 LINE CTR
ABDCTR2  DS    1H                 PAGE CTR
ABDOUT   DS        55F
ABDPARA  DS    1F                 PTR TO PARAMETER LIST
         DS    1F
ABDWORK2 DS    1C
ABDWORK4 DS    11C
ABDWORK1 DS    1D
ABDWORK3 DS    1F
ABDPTRS  DS    3F                 BUFFER POINTERS USED BY OUTPUT
ABDPHY   DS    2F                 AREA FOR PRINT ROUTINE
ABDCC    DS    1C                 CARRIAGE CONTROL CHARACTER
ABDLINE  DS    CL32               120 CHAR PRINT LINE
         DS    CL32
         DS    CL32
         DS    CL24
         DS    1C                 FOR UNPACKING INTO LINE
ABDINCPL DS      1H
         DS      2C
LIMIT    DS    1F                 HI ADDRESS SAVE AREA
PARDSECT DSECT
PID      DS    1C
PWTG     DS    1C
PFLAGS   DS    2C                 OPTION FLAGS
PDCBPTR  DS    1F .               DCB ADDRESS
PTCBPTR  DS    1F                 TCB ADDRESS
PSNAPPTR DS    1F                 ADDRESS OF SNAPSHOT LIST
         END
