000010 ID DIVISION.
000020 PROGRAM-ID. C9CA3X.
000030 AUTHOR. EARL RYAN.
000040 DATE-WRITTEN. JUNE 1973.
000050 DATE-COMPILED.
000060*REMARKS.       THIS PROGRAM PRODUCES INDEX LISTINGS OF
000070*               MODULES ON THE LOG LIBRARIAN DISK MASTER
000080*               FILE FOR EACH PROGRAMMER SORTED BY JOB NO.
000090
000100*               SUBROUTINES USED - ADR FAIR ROUTINES
000110*                   MASTOPN - MASTMOD - MASTCLS.
000120
000130 ENVIRONMENT DIVISION.
000140 CONFIGURATION SECTION.
000150 SOURCE-COMPUTER. IBM-370.
000160 OBJECT-COMPUTER. IBM-370.
000170 SPECIAL-NAMES. C01 IS TO-TOP-OF-PAGE.
000180 INPUT-OUTPUT SECTION.
000190 FILE-CONTROL.
000200     SELECT INDEX-LISTING ASSIGN TO UT-S-O9CA3X1.
000210     SELECT SORTFIL ASSIGN TO UT-S-O9CA3X2.
000220 DATA DIVISION.
000230 FILE SECTION.
000240 FD  INDEX-LISTING BLOCK CONTAINS 0 RECORDS LABEL RECORD IS
000250       STANDARD RECORD CONTAINS 133 CHARACTERS RECORDING MODE IS
000260       F DATA RECORD IS PRINT-LINE.
000270 01  PRINT-LINE.
000280     05 FILLER          PIC XX.
000290     05 P-MODULE-NAME   PIC X(8).
000300     05 FILLER          PIC XXX.
000310     05 P-PASSWORD      PIC X(4).
000320     05 FILLER          PIC X(4).
000330     05 P-DESCRIPTION   PIC X(30).
000340     05 FILLER          PIC X.
000350     05 P-DATE-ADDED    PIC X(8).
000360     05 FILLER          PIC X(4).
000370     05 P-LAST-UPDATE   PIC X(13).
000380     05 FILLER          PIC XXX.
000390     05 P-RECORDS       PIC Z(5).
000400     05 FILLER          PIC X(4).
000410     05 P-PROGRAMMER    PIC X(10).
000420     05 FILLER          PIC XX.
000430     05 P-PROC          PIC X(8).
000440     05 FILLER          PIC X(4).
000450     05 P-SEQ-PARAM     PIC X(15).
000460     05 FILLER          PIC X(5).
000470 01  REPORT-HDR-LINE.
000480     05 FILLER          PIC X.
000490     05 HDR-ID          PIC X(12).
000500     05 FILLER          PIC X(120).
000510 SD  SORTFIL RECORDING MODE IS F DATA RECORD IS SORTREC RECORD
000520       CONTAINS 108 CHARACTERS.
000530 01  SORTREC.
000540     05 MODULE-NAME     PIC X(8).
000550     05 MODULE-INPUT-AREA.
000560       10 PASSWORD      PIC X(4).
000570       10 DATE-ADDED.
000580         15 MONTH-ADDED PIC XX.
000590         15 DAY-ADDED   PIC XX.
000600         15 YEAR-ADDED  PIC XX.
000610       10 LAST-UPDATED.
000620         15 MONTH-UPDATED PIC XX.
000630         15 DAY-UPDATED PIC XX.
000640         15 YEAR-UPDATED PIC XX.
000650         15 TIME-UPDATED PIC X(4).
000660       10 NUMBER-OF-CARDS PIC S9(5) USAGE COMP-3.
000670       10 PROGRAMMER.
000680         15 PGMR        PIC XX.
000690         15 FILLER      PIC X(8).
000700       10 FILLER        PIC X(5).
000710       10 PROC-PARM     PIC X(8).
000720       10 DESCRIPTION   PIC X(30).
000730       10 SEQ-COL-X     PIC X.
000740       10 SEQ-LENGTH-X  PIC X.
000750       10 SEQ-INCREMENT PIC 9(4) USAGE COMPUTATIONAL.
000760       10 SEQ-START-VAL PIC 9(4) USAGE COMPUTATIONAL.
000770       10 FILLER        PIC X(18).
000780 WORKING-STORAGE SECTION.
000790 01  FILLER             PIC X(27) VALUE
000800       'WORKING STORAGE BEGINS HERE'.
000810 01  TPHDR              PIC X(16).
000820 01  RETCD              PIC 9.
000830 01  MESSAGE1.
000840     05 FILLER          PIC X(29) VALUE
000850         'OPEN FAILED - RETURN CODE IS '.
000860     05 OPENCD          PIC X VALUE ' '.
000870     05 FILLER          PIC X(15) VALUE ' - MODULE NAME '.
000880     05 NAME1           PIC X(8).
000890 01  MESSAGE2.
000900     05 FILLER          PIC X(28) VALUE
000910         'GET FAILED - RETURN CODE IS '.
000920     05 RETCD-G         PIC X VALUE SPACE.
000930     05 FILLER          PIC X(15) VALUE ' - MODULE NAME '.
000940     05 NAME2           PIC X(8).
000950 01  HEADER1.
000960     05 FILLER          PIC X(7) VALUE '  DATE '.
000970     05 HDR-DATE        PIC X(8).
000980     05 FILLER          PIC X(11) VALUE '      TIME '.
000990     05 HDR-TIME        PIC X(4).
001000     05 FILLER          PIC X(12) VALUE SPACES.
001010     05 FILLER          PIC X(15) VALUE 'MASTER LOG.LIBR'.
001020     05 FILLER          PIC X(5) VALUE SPACES.
001030     05 FILLER          PIC X(13) VALUE 'INDEX LISTING'.
001040     05 FILLER          PIC X(16) VALUE ' FOR PROGRAMMER '.
001050     05 PGMRID          PIC XX VALUE SPACES.
001060     05 FILLER          PIC X(31) VALUE SPACES.
001070     05 FILLER          PIC X(5) VALUE 'PAGE '.
001080     05 PAGEID          PIC XXX VALUE SPACES.
001090     05 FILLER          PIC X VALUE SPACE.
001100 01  HEADER2.
001110     05 FILLER          PIC X(25) VALUE IS
001120         '  MODULE   PASSWORD    MO'.
001130     05 FILLER          PIC X(25) VALUE IS
001140         'DULE DESCRIPTION         '.
001150     05 FILLER          PIC X(25) VALUE IS
001160         '    ADDED      LAST UPDAT'.
001170     05 FILLER          PIC X(25) VALUE IS
001180         'E   RECORDS   PROGRAMMER '.
001190     05 FILLER          PIC X(25) VALUE IS
001200         '  PROC       SEQUENCE PAR'.
001210     05 FILLER          PIC X(8) VALUE IS 'AM      '.
001220 01  TRAILER.
001230     05 FILLER          PIC X(26) VALUE
001240         '  TOTAL NUMBER OF MODULES '.
001250     05 TRL-NO          PIC ZZZ.
001260     05 FILLER          PIC X(104) VALUE SPACES.
001270 01  HOLD-LINE          PIC X(133).
001280 01  LAST-PGMR          PIC XX VALUE SPACES.
001290 01  LINE-CTR           PIC S999 USAGE COMP-3 VALUE +0.
001300 01  PAGE-CTR           PIC S9 USAGE COMP-3 VALUE +0.
001310 01  MODULE-COUNT       PIC S999 USAGE COMP-3 VALUE +0.
001320 01  BUILD-P-DATE.
001330     05 BUILD-DATE.
001340       10 MONTH         PIC XX.
001350       10 FILLER        PIC X VALUE '/'.
001360       10 D-A-Y         PIC XX.
001370       10 FILLER        PIC X VALUE '/'.
001380       10 YEAR          PIC XX.
001390     05 FILLER          PIC X VALUE SPACE.
001400     05 BUILD-TIME      PIC X(4).
001410 01  BUILD-SEQ-PARAM.
001420     05 B-SEQ-START     PIC XX.
001430     05 FILLER          PIC X VALUE IS '/'.
001440     05 B-SEQ-LENGTH    PIC XX.
001450     05 FILLER          PIC X VALUE IS '/'.
001460     05 B-SEQ-INCREMENT PIC X(4).
001470     05 FILLER          PIC X VALUE IS '/'.
001480     05 B-SEQ-START-VAL PIC X(4).
001490 01  SHORT-TIME.
001500     05 B-TIME          PIC X(4).
001510     05 FILLER          PIC XX.
001520 01  SEQ-B              PIC 99 USAGE COMPUTATIONAL.
001530 01  SEQ-FIELD          REDEFINES SEQ-B.
001540     05 FILLER          PIC X.
001550     05 SEQ-X           PIC X.
001560 PROCEDURE DIVISION.
001561     MOVE +40000 TO SORT-CORE-SIZE.
001570 0001-SORT-PROC.
001580     SORT SORTFIL ON ASCENDING KEY PGMR
001590                  ON ASCENDING KEY MODULE-NAME
001591     INPUT PROCEDURE 0002-BUILD-SORTREC
001610     OUTPUT PROCEDURE 0011-LIST-INDICIES.
001620     STOP RUN.
001630 0002-BUILD-SORTREC SECTION.
001640 0003-OPEN-LIST-FILE.
001650     OPEN OUTPUT INDEX-LISTING.
001660     MOVE SPACES TO PRINT-LINE.
001670     MOVE 'DRN 9CA3X-01' TO HDR-ID.
001680     WRITE REPORT-HDR-LINE AFTER ADVANCING TO-TOP-OF-PAGE.
001690     MOVE CURRENT-DATE TO HDR-DATE.
001700     MOVE TIME-OF-DAY TO SHORT-TIME.
001710     MOVE B-TIME TO HDR-TIME.
001720     MOVE 1 TO PAGEID.
001730     WRITE PRINT-LINE FROM HEADER1 AFTER ADVANCING 2 LINES.
001740     MOVE 000 TO LINE-CTR.
001750     WRITE PRINT-LINE FROM HEADER2 AFTER ADVANCING 2 LINES.
001760     MOVE SPACES TO PRINT-LINE.
001770     MOVE 0 TO SEQ-B.
001780 0004-OPEN-MASTER.
001790     CALL 'MASTOPN' USING TPHDR, RETCD.
001800 0005-CHECK-OPEN.
001810     IF RETCD EQUAL TO ZERO GO TO 0007-GET-A-MODULE.
001820     MOVE RETCD TO OPENCD.
001830     MOVE MODULE-NAME TO NAME1.
001840     MOVE SPACES TO PRINT-LINE.
001850     MOVE MESSAGE1 TO PRINT-LINE.
001860     WRITE PRINT-LINE AFTER ADVANCING TO-TOP-OF-PAGE.
001870 0006-SET-RETURN-CODE.
001880     MOVE 20 TO RETURN-CODE.
001890     GO TO 0009-CLOSE-MASTER.
001900 0007-GET-A-MODULE.
001910     MOVE SPACES TO MODULE-NAME.
001920     CALL 'MASTMOD' USING MODULE-NAME, RETCD, MODULE-INPUT-AREA.
001930     IF RETCD EQUAL TO ZERO GO TO 0008-RELEASE-PROC.
001940     IF RETCD EQUAL TO 1 GO TO 0009-CLOSE-MASTER.
001950     MOVE RETCD TO RETCD-G MOVE MODULE-NAME TO NAME2.
001960     MOVE SPACES TO PRINT-LINE.
001970     MOVE MESSAGE2 TO PRINT-LINE.
001980     WRITE PRINT-LINE AFTER ADVANCING TO-TOP-OF-PAGE.
001990     GO TO 0006-SET-RETURN-CODE.
002000 0008-RELEASE-PROC.
002010     RELEASE SORTREC.
002020     GO TO 0007-GET-A-MODULE.
002030 0009-CLOSE-MASTER.
002040     CALL 'MASTCLS'.
002050 0010-EXIT-CLOSE.
002060     EXIT.
002070 0011-LIST-INDICIES SECTION.
002080 0012-CHECK-SORT-CODE.
002090     IF SORT-RETURN = 16 MOVE 16 TO RETURN-CODE GO TO 0022-EOJ.
002100 0013-RETURN-A-RECORD.
002110     RETURN SORTFIL AT END GO TO 0021-SET-GOOD-RETURN.
002120 0014-CHECK-PGMR-ID.
002130     IF PGMR EQUAL TO LAST-PGMR GO TO 0015-BUILD-PRINT-LINE.
002140     PERFORM 0020-PUT-TRAILER.
002150     MOVE SPACES TO PRINT-LINE MOVE PGMR TO LAST-PGMR.
002160     MOVE 1 TO PAGE-CTR.
002170     PERFORM 0019-PUT-HEADERS.
002180 0015-BUILD-PRINT-LINE.
002190     MOVE MODULE-NAME TO P-MODULE-NAME.
002200     MOVE PASSWORD TO P-PASSWORD.
002210     MOVE DESCRIPTION TO P-DESCRIPTION.
002220     MOVE MONTH-ADDED TO MONTH.
002230     MOVE DAY-ADDED TO D-A-Y.
002240     MOVE YEAR-ADDED TO YEAR.
002250     MOVE BUILD-DATE TO P-DATE-ADDED.
002260     MOVE MONTH-UPDATED TO MONTH.
002270     MOVE DAY-UPDATED TO D-A-Y.
002280     MOVE YEAR-UPDATED TO YEAR.
002290     IF BUILD-DATE = '00/00/00' GO TO 0016-BUILD-CONSTANT.
002300     MOVE TIME-UPDATED TO BUILD-TIME.
002310     MOVE BUILD-P-DATE TO P-LAST-UPDATE.
002320     GO TO 0017-RESUME-BUILD.
002330 0016-BUILD-CONSTANT.
002340     MOVE ' NOT UPDATED ' TO P-LAST-UPDATE.
002350 0017-RESUME-BUILD.
002360     MOVE NUMBER-OF-CARDS TO P-RECORDS.
002370     MOVE PROGRAMMER TO P-PROGRAMMER.
002380     MOVE PROC-PARM TO P-PROC.
002390     MOVE SEQ-COL-X TO SEQ-X.
002400     MOVE SEQ-B TO B-SEQ-START.
002410     MOVE SEQ-LENGTH-X TO SEQ-X.
002420     MOVE SEQ-B TO B-SEQ-LENGTH.
002430     MOVE SEQ-INCREMENT TO B-SEQ-INCREMENT.
002440     MOVE SEQ-START-VAL TO B-SEQ-START-VAL.
002450     MOVE BUILD-SEQ-PARAM TO P-SEQ-PARAM.
002460 0018-WRITE-PRINT-LINE.
002470     IF LINE-CTR = 25 ADD 1 TO PAGE-CTR,
002480       MOVE PRINT-LINE TO HOLD-LINE, PERFORM 0019-PUT-HEADERS
002490       MOVE HOLD-LINE TO PRINT-LINE.
002500     WRITE PRINT-LINE AFTER ADVANCING 2 LINES.
002510     ADD 1 TO LINE-CTR.
002520     ADD 1 TO MODULE-COUNT.
002530     GO TO 0013-RETURN-A-RECORD.
002540 0019-PUT-HEADERS.
002550     MOVE PGMR TO PGMRID.
002560     MOVE PAGE-CTR TO PAGEID.
002570     WRITE PRINT-LINE FROM HEADER1 AFTER ADVANCING
002580       TO-TOP-OF-PAGE.
002590     MOVE 000 TO LINE-CTR.
002600     WRITE PRINT-LINE FROM HEADER2 AFTER ADVANCING 2 LINES.
002610     MOVE SPACES TO PRINT-LINE.
002620 0020-PUT-TRAILER.
002630     MOVE MODULE-COUNT TO TRL-NO.
002640     MOVE 0 TO MODULE-COUNT.
002650     WRITE PRINT-LINE FROM TRAILER AFTER ADVANCING 2 LINES.
002660 0021-SET-GOOD-RETURN.
002670     MOVE 0 TO RETURN-CODE.
002680 0022-EOJ.
002690     PERFORM 0020-PUT-TRAILER.
002700     CLOSE INDEX-LISTING.
002710 0023-EXIT-EOJ.
002720     EXIT.
