//STAGE2       JOB  (1306,5,,,104),'CREATE STAGE2 JOBFIL',CLASS=N
//STEP00   EXEC     PGM=IEHPROGM
//SYSPRINT   DD   SYSOUT=A
//DSK312    DD   UNIT=3330,VOL=SER=DSK312,DISP=OLD
     SCRATCH DSNAME=SYS2.STAGE2.JOBS,VOL=3330=DSK312
     UNCATLG DSNAME=SYS2.STAGE2.JOBS
/*
//STEP02   EXEC     ASMFCG
//ASM.SYSIN   DD  *
STAGE2   CSECT
         PRINT ON,NOGEN,NODATA
         SPACE 1
         USING *,15
         SAVE  (14,12)
         BAL   12,SAVEAREA+72
SAVEAREA DS    18F
         ST    12,8(13)
         ST    13,4(12)
         LR    13,12
         DROP  15
         USING SAVEAREA,12
         SPACE 1
         CALL  NIBSIO,(CCA),VL
READ     CALL  NIBSIO,(SYSUT1),VL
         CLI   EOF,C'X'
         BE    FINISH
         CLC   SYSGEN,CARDIN
         BE    READ
         CLC   CARDIN(9),LOGREC
         BNE   ASRTST
         MVC   CARDIN,LOGREC
         B     WRITE
ASRTST   CLC   CARDIN(9),ASRLIB
         BNE   FORTLKED
         MVC   CARDIN,ASRLIB
         B     WRITE
FORTLKED  EQU *
         SCAN  CARDIN,=C'SIZE=(' SEARCH FOR LKED SIZE
         B     STEP                JUMP TO STEP IF NOT FOUND
         CLI   13(R1),C')'         IS SIZE RIGHT LENGTH?
         BNE   STEP                NO. JUMP
         MVC   0(15,R1),SIZE2      MOVE IN DEFAULT LKED SIZE
STEP     CLC   CARDIN(4),SG
         BNE   WRITE
         XC    SW1,SW1             CLEAR SWITCHES
         SCAN  CARDIN,=C'PGM=ASMBLR' ASSEMBLY STEP?
         B     NOTASM              NO. BRANCH
         OI    SW1,X'80'           TURN ON ASSEMBLY SWITCH
NOTASM   EQU   *
         SCAN  CARDIN,=C'PGM=IEHLIST' LIST STEP?
         B     NOTLIST             NO. BRANCH
         OI    SW1,X'40'           YES. TURN PN LIST SWITCH
NOTLIST  EQU   *
         SCAN  CARDIN,=C'PGM=IEHPROGM' PROGM STEP?
         B     NOTPROGM            NO. BRANCH
         OI    SW1,X'20'           YES. TURN ON PROGM SWITCH
         SPACE
         B     READ                DELETE ALL PROG CARDS
NOTPROGM EQU   *
         CLC   CARDIN+4(2),=C'1 '
         BE    JOB
         MVC   CARDOUT,IEFBR14
         MVC   CARDOUT+36(3),CARDIN+4
         CLI   CARDOUT+38,C'E'
         BNE   *+8
         MVI   CARDOUT+38,C' '
         BAL   4,INCREMNT
         BAL   4,PAGESKIP
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
         MVI   CARDPRT,C' '
         MVC   CARDPRT+1(79),CARDPRT
         CALL  NIBSIO,(SYSOUT),VL
         AP    LINECNT,ONE
JOB      MVC   CARDOUT,JOBCARD
         MVC   CARDOUT+6(3),CARDIN+4
         CLI   CARDOUT+8,C'E'
         BNE   *+8
         MVI   CARDOUT+8,C' '
         BAL   4,INCREMNT
         BAL   4,PAGESKIP
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
         MVC   CARDOUT,JOBCARD2
         BAL   4,INCREMNT
         BAL   4,PAGESKIP
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
         MVC   CARDOUT,ROUTE
         BAL   4,INCREMNT
         BAL   4,PAGESKIP
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
***************  REMOVE FOLLOWING INSTN. IF GERATING SYSTEM NOT SAME
***************  RELEASE AS SYSTEM BEING GENNED.
         B     NOJOBLIB
         TM    SW1,X'40'           IEHLIST?
         BO    NOJOBLIB            YES. DROP JOBLIB CARD
         MVC   CARDOUT,JOBLIB
         BAL   4,INCREMNT
         BAL   4,PAGESKIP
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
NOJOBLIB EQU   *
         TM    SW1,X'80'           ASSEMBLY STEP?
         BNO   WRITE               NO. BRANCH
         MVC   CARDOUT,SGENASM
         BAL   4,INCREMNT
         BAL   4,PAGESKIP
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
         B     READ
         SPACE
WRITE    EQU   *
         TM    SW1,X'20'           PROGM STEP?
         BO    READ                DELETE ALL PROGM CARDS
         CLC   CARDIN(2),=C'//'    JCL CARD?
         BNE   WRITEOUT            NO. BRANCH
         TM    SW1,X'80'           ASM STEP?
         BNO   WRITE1A
         CLC   CARDIN+2(8),=C'SYSPUNCH'
         BE    WRITE1A            DELETE ALL ASM JCL BUT SYSPUNCH
         B     READ
         SPACE
WRITE1A  EQU   *
         SCAN  CARDIN,=C'SYS1'          CHANGE ALL SYS1 DS TO R217
         B     *+10
         MVC   0(4,1),=C'R217'
         SCAN  CARDIN,=C'PSG1'
         B     WRITEOUT
         MVC   0(4,1),=C'SYS1'
WRITEOUT EQU   *
         MVC   CARDOUT,CARDIN
         BAL   4,INCREMNT
         CALL  NIBSIO,(SYSUT2),VL
         CLI   CARDOUT,C'/'
         BNE   READ
         BAL   4,PAGESKIP
         CALL  NIBSIO,(SYSOUT),VL
         B     READ
         EJECT
INCREMNT EQU   *
         AP    CARDNO,TEN
         MVC   WORKAREA,EDIT
         ED    WORKAREA,CARDNO
         MVC   CARDOUT+72(8),WORKAREA+2
         BR    4
         SPACE 5
PAGESKIP EQU   *
         MVI   CONTROL,X'09'
         MVC   CARDPRT,CARDOUT
         AP    LINECNT,ONE
         CP    LINECNT,SIXTY
         BNH   0(4)
         MVI   CONTROL,X'89'
         MVC   LINECNT,ZERO
         BR    4
         SPACE 5
FINISH   EQU   *
         MVC   CARDOUT,STEP99
         BAL   4,INCREMNT
         BAL    4,PAGESKIP
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
         CALL  NIBSIO,(SYSOUT,SYSUT2),VL
         MVI   REQUEST,C'B'
         CALL  NIBSIO,(CCA),VL
         MVI   REQUEST,C'D'
         CALL  NIBSIO,(CCA),VL
         SR    15,15
         L     13,4(13)
        RETURN (14,12),RC=(15)
          EJECT
CCA      EQU   *
USERID   DC    CL7'STAGE2 '
REQUEST  DC    C'A'
ERROR    DC    C'0'
EOF      DC    C'0'
         SPACE 1
SYSUT1   EQU   *
         DC    C'SYSUT1 '
         DC    C'Z'
CARDIN   DC    CL80' '
         SPACE 1
SYSUT2   EQU   *
         DC    C'SYSUT2 '
         DC    C'Y'
CARDOUT  DC    CL80' '
         SPACE 1
SYSOUT   EQU   *
         DC    C'SYSOUT '
         DC    C'Y'
CONTROL  DC    X'09'
CARDPRT  DC    CL80' '
         SPACE 1
IEFBR14  DS    0CL80
         DC    C'//NEXTSTEP EXEC     RELEASE,JOB=STEPXX '
         DC    CL41' '
JOBCARD  DC    CL80'//STEP    JOB (1306,5,,,104,,,,30),''OF STAGE2 R21.C
               7'','
JOBCARD2 DC    CL80'//             TYPRUN=HOLD,COND=(4,LT)'
ROUTE    DC    CL80'/*ROUTE  PRINT REMOTE14'
JOBLIB   DS    0CL80
         DC    C'//JOBLIB       DD   DSN=SYS1.LINKLIB,DISP=SHR,'
         DC    C'UNIT=3330,VOL=SER=DSK312'
         DC    CL10' '
LOGREC   DS    0CL80
         DC    C'//SERERDS      DD   DSN=SYS1.LOGREC,DISP=OLD,'
         DC    CL35' '
ASRLIB   DS    0CL80
         DC    C'//ASRFRSH      DD   DSN=SYS1.ASRLIB,DISP=OLD,'
         DC    CL35' '
SGENASM  DC    CL80'//SGENASM  EXEC     SGENASM'
SIZE1    DC    CL15'SIZE=(250K,50K)'
SIZE2    DC    CL15'SIZE=(98K,12K)' DEFAULT LKED SIZE
STEP99   DC    CL80'//STEP99   EXEC     MAPDISK,DD1=DSK401'
SYSGEN   DC    C'//SYSGEN'
SG       DC    CL4'//SG'
         SPACE 1
LINECNT  DC    PL2'0'
ONE      DC    PL2'1'
ZERO     DC    PL2'0'
SIXTY    DC    PL2'60'
CARDNO   DC    PL5'0'
TEN      DC    PL5'10'
SW1      DC    X'00'
R1       EQU   1
         SPACE 1
WORKAREA DS    CL10
EDIT     DC    X'40202120202020202020'
         SPACE 1
         LTORG
         END
//GO.SYSOUT       DD   SYSOUT=A,DCB=(RECFM=FM,BLKSIZE=81)
//NIBCNTRL     DD   SYSOUT=A,DCB=(RECFM=FBA,LRECL=133,BLKSIZE=133)
//SYSUT1       DD   DSN=SYS2.STAGE2.NOVR76$2,DISP=OLD
//SYSUT2       DD   DSN=SYS2.STAGE2.JOBS,DISP=(,CATLG),
//             UNIT=3330,VOL=SER=DSK312,SPACE=(CYL,(5,2),RLSE),
//             DCB=DSCB
//NEXT   EXEC     RELEASE,JOB=SG2ALLOC
//SG2ALLOC     JOB  (1306,5),'STAGE2 DATASETS',CLASS=T
/*SETUP
//STEP00   EXEC   PGM=UT434
//SYSPRINT     DD   SYSOUT=A
//DSK312       DD   UNIT=3330,VOL=SER=DSK312,DISP=OLD
//STOOL1       DD   UNIT=3330,VOL=SER=STOOL1,DISP=OLD
//DSK301       DD   UNIT=3330,VOL=SER=DSK301,DISP=OLD
//SYSIN        DD   *
     REMOVE   DSN=R217.UT1,PURGE
     REMOVE   DSN=R217.UT2,PURGE
     REMOVE   DSN=R217.UT3,PURGE
     REMOVE   DSN=R217.UT4,PURGE
     REMOVE   DSN=R217.OBJ1$2,PURGE
/*
//STEP02   EXEC   PGM=IEFBR14
//UT1          DD   DSN=R217.UT1,DISP=(,CATLG),
//             UNIT=3330,VOL=SER=STOOL1,SPACE=(CYL,(5,2))
//UT2          DD   DSN=R217.UT2,DISP=(,CATLG),
//             VOL=REF=DSK301,SPACE=(CYL,(5,2))
//UT3          DD   DSN=R217.UT3,DISP=(,CATLG),
//             VOL=REF=DSK312,SPACE=(CYL,(10,5))
//UT4          DD   DSN=R217.UT4,DISP=(,CATLG),
//             UNIT=3330,VOL=SER=DSK312,SPACE=(CYL,(1,1))
//OBJPDS       DD   DSN=R217.OBJ1$2,DISP=(,CATLG),
//             UNIT=3330,VOL=SER=DSK312,SPACE=(CYL,(3,1,17)),
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=400)
//NEXT   EXEC     RELEASE,JOB=EXTRACT1
