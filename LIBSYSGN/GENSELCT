//GENSELCT     JOB  (1306,5),'STEPS FROM SYSGEN',CLASS=T
//STEP01   EXEC     ASMFCL
//ASM.SYSIN    DD   *
         TITLE  'EXTRACT SELECTED DATA GROUPS FROM TAPE'
          PRINT     ON,NOGEN,NODATA
SELECT    CSECT
          SAVE (14,12),,*
          BALR 12,0
          USING *,12
          ST   13,SAVEREGS+4
          LR   11,13
          LA   13,SAVEREGS
          ST   13,8(11)
*
*                  END HOUSEKEEPING
*
*                  OPEN UP AND START PROCESSING
*
          OPEN  SYSIN
          LA   3,TABLE
READSIN   EQU   *
          GET   SYSIN
          CLC  5(6,1),=C'SELECT'
          BNE  READSIN
          MVC  0(8,3),15(1)
          LA   3,8(3)
          B    READSIN
ENDSYSIN  EQU   *
          CLOSE  SYSIN
          OPEN (MASTIN,,SYSPUNCH,(OUTPUT))
READMAST  EQU   *
          GET MASTIN
         CLC   9(5,1),=C' JOB '
          BNE  PUNCHIT
          MVI  PUNCHSW,C' '
          LA   3,TABLE
COMPARE   EQU   *
          CLC  0(8,3),=C'        '
          BE   READMAST
         CLC   6(3,1),0(3)         IS THIS GROUP REQUIRED
          LA   3,8(3)
          BNE  COMPARE
          MVI  PUNCHSW,C'*'
PUNCHIT   EQU   *
          CLI  PUNCHSW,C'*'
          BNE  READMAST
          LR   0,1
          PUT  SYSPUNCH,(0)
          B    READMAST
ENDMAST   EQU   *
          CLOSE (MASTIN,,SYSPUNCH)
          L    13,SAVEREGS+4
          RETURN (14,12),RC=0
*
*                  SAVEAREAS CONSTANTS DCBS AND WORKAREAS
*
SAVEREGS  DS   18F
MASTIN    DCB  DDNAME=MASTIN,DSORG=PS,MACRF=GL,                        C
               EODAD=ENDMAST,LRECL=80,RECFM=FB
SYSIN     DCB  DDNAME=SYSIN,DSORG=PS,MACRF=GL,                         C
               EODAD=ENDSYSIN,LRECL=80,RECFM=FB,BLKSIZE=80
SYSPUNCH  DCB  DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM,                      C
               RECFM=FB,LRECL=80,BLKSIZE=80
TABLE     DC   25CL8'        '
PUNCHSW   DC   CL1' '
          END  SELECT
/*
//LKED.SYSLMOD  DD   DSN=TESTLIB(GENSELCT),DISP=SHR
