./ ADD NAME=INITIAL,LIST=ALL
     MACRO
&N       INITIAL  &BASE=3,&SAVE=OSSAVE
         GBLC   &SV
         LCLA   &CT,&A
         LCLC   &W,&X,&Y,&Z
&A       SETA   K'&BASE-2
&N     CSECT
 AIF ('&BASE'(1,1) EQ '(').A
         USING *,&BASE
         SAVE  (14,12)
         LR    &BASE,15
   AGO   .B
.A       AIF  (&A LT 9).W
         AIF  (&A LT 17).X
         AIF  (&A LT 25).Y
&Z       SETC   '&BASE'(26,&A-24)
&A       SETA  24
.Y       ANOP
&Y       SETC   '&BASE'(18,&A-16)
&A       SETA  16
.X       ANOP
&X       SETC   '&BASE'(10,&A-8)
&A       SETA  8
.W       ANOP
&W       SETC  '&BASE'(2,&A)
       USING *,&W&X&Y&Z
         SAVE    (14,12)
         LR    &BASE(1),15
         AIF   (N'&BASE LE 1).B
         LA    &BASE(N'&BASE),2048
.C   ANOP
&CT    SETA   &CT+1
         LA    &BASE(&CT+1),2048(&BASE(&CT),&BASE(N'&BASE))
         AIF (&CT+1 LT N'&BASE).C
.B     AIF  ('&SV' NE '').D
&SV    SETC   '&SAVE'
         AIF   ('&SV' NE 'NO').E
         MEXIT
.E       B     OSAV&SYSNDX
&SV      DC    18F'0'
OSAV&SYSNDX   EQU   *
.D       ST    13,&SV+4
         LA    14,&SV
         ST    14,8(13)
         LR    13,14
         MEND
./ ADD NAME=CLRM,LIST=ALL
         MACRO
&NAME    CLRM  &A
*** M/I BANK *** CLRM MACRO TO CLEAR MULTIPLE FIELDS  CL 18.1 ** ***
         LCLC  &F,&L
         LCLA  &Z
&L       SETC  'L'''
&NAME    DS   0H
.A       ANOP
&Z       SETA  &Z+1
         AIF   (&Z LE N'&A).B
         MEXIT
.B       ANOP
&F       SETC  T'&A(&Z)
         AIF   ('&F' NE 'C').C
.I       ANOP
         MVI   &A(&Z),X'40'
         AIF   (L'&A(&Z) LT 2).A
         MVC   &A(&Z)+1(&L&A(&Z)-1),&A(&Z)
         AGO   .A
.C       AIF   ('&F' NE 'P').D
         ZAP   &A(&Z),=X'0C'
         AGO   .A
.D       AIF   ('&F' NE 'Z').E
         UNPK  &A(&Z),=X'0C'
         AGO   .A
.E       AIF   ('&F' NE 'N').F
.H       SR    &A(&Z),&A(&Z)
         AGO   .A
.F       AIF   ('&A(&Z)'(1,1) EQ '(').H
         AIF   ('&F' EQ 'U').I
         AIF  ('&F' EQ 'I' OR '&F' EQ 'J' OR '&F' EQ 'M').INV
         AIF   ('&F' EQ 'T' OR '&F' EQ 'W' OR '&F' EQ 'O').INV
         XC    &A(&Z),&A(&Z)
         AGO   .A
.INV     MNOTE 'INVALID OPERAND &A(&Z)'
         AGO   .A
         MEND
./ ADD NAME=OSEOJ,LIST=ALL
         MACRO
&N       OSEOJ &RC=0
         GBLC  &SV
         AIF   ('&SV' NE 'NO').A
&N       RETURN (14,12),RC=&RC
         MEXIT
.A       ANOP
&N       L     13,&SV+4
         RETURN (14,12),RC=&RC
         MEND
