   TITLE ' FORMAT DUMP CONTROL BLOCKS PART 4'
IGC0W05A   CSECT
     PRINT  ON,GEN,DATA
R0       EQU       0
R1       EQU       1
R2       EQU       2
R3       EQU       3
R4       EQU       4
R5       EQU       5
R6       EQU       6
R7       EQU       7
R8       EQU       8
R9       EQU       9
RB       EQU       11
RA       EQU       10
RC       EQU       12
RD       EQU       13
RE       EQU       14
RF       EQU       15
PARREG   EQU       14
R10      EQU       10
R11      EQU       11
BLANK    EQU       X'40'
         BALR  RC,0
         USING *,RC               R12 = BASE
         USING ABDAREA,RD
         USING PARDSECT,PARREG    R 14=PTR TO PARMLIST
*
         L         R5,164(R2)  GET TIME CONT TABLE
         LTR       R5,R5
         BZ        NNSMF
         MVC       ABDLINE(L'RDRMSG),RDRMSG
         L         R5,28(R5)      GET ADDRESS OF JOB MANAGEMENT RECD
         LA        R8,8(R5)       GET ENTRY TIME OF THIS JOB
         L         RB,0(R8)
         SR        RA,RA
         D         RA,=F'6000' GET MIN RB NOW HAS MIN RA SEC/60
         CVD       RA,ABDWORK1
         UNPK      ABDLINE+40(3),ABDWORK1+6(2)
         OI        ABDLINE+42,X'F0'
         MVI     ABDLINE+40,C'.'
         SR        RA,RA
         D         RA,=F'60'   GET HOURS
         CVD       RA,ABDWORK1
         UNPK      ABDLINE+37(3),ABDWORK1+6(2)
         OI        ABDLINE+39,X'F0'
         MVI       ABDLINE+37,C'.'
         CVD       RB,ABDWORK1
         UNPK      ABDLINE+34(3),ABDWORK1+6(2)
         OI        ABDLINE+36,X'F0'
         MVI       ABDLINE+34,C' '
         UNPK       ABDLINE+51(3),6(2,R8)  GET DAY
         UNPK      ABDLINE+62(3),5(2,R8)  GET YEAR
         MVI       ABDLINE+64,C' '
         BAL       R5,ABDOUT  PRINT ITITITITITITITITI
NNSMF  EQU  *
          L        R5,16   GET CVT ADD AGAIN
         L         R5,196(R5)   GET SMF
         LTR       R5,R5
         BZ        NNNSMF
         TM        1(R5),X'C0'  ARE WE RECORDING
         BNZ       RECORD
         MVC        ABDLINE+50(2),=C'NO'
RECORD MVC  ABDLINE+54(39),=C'SMF RECORDING HAS OCCURRED FOR THIS JOB'
NNNSMF   EQU       *
         BAL       R5,ABDOUT
         L         R5,16  CVT
         S         R5,=F'4'       GO BACK 1 WORK
         MVC       ABDLINE+20(4),0(R5)   MOVE IN RELEASE
         MVC       ABDLINE(L'REL),REL
         BAL       R5,ABDOUT
         MVC       ABDLINE(106),NOTE
         BAL       R5,ABDOUT      PRINT NOTE FOR NEW DEVICES
         MVC       ABDLINE(L'NOTE-106),NOTE+106
         BAL       R5,ABDOUT   PRINTIT
* R6= START ADDRESS OF THE CONTROL BLOCK
* R7= 1/4 NUMBER OF WORDS =NUMBER LINES
         LR        R6,R2
         LA        R7,15     COUNT OF LINES TO PRINT
         MVC       ABDLINE(L'TCBMSG),TCBMSG PREPARE TO PRINT TCB
         BAL       R9,PRINT  GO PRINT TCB
         LA        R7,18     COUNT OF LINES TO PRINT
         L         R6,16
         MVC       ABDLINE(L'CVTMSG),CVTMSG PREPARE TO PRINT CVT
         BAL       R9,PRINT  GO PRINT CVT
EXIT     LA    RA,LOAD5           LOAD LOAD5 PARAMETER
         LA    RF,ABDWORK3
         XCTL  EPLOC=(10),SF=(E,(15))
PRINT    EQU       *
         ST        R6,TEMP   SAVE START ADDRESS
         LA        R11,0     SET      COUNT TO 0
         UNPK      ABDLINE+11(7),TEMP(5)  PUT IN START LOCATION
         TR        ABDLINE+11(6),TBL-240   MAKE IT PRINT RIGHT
         MVI       ABDLINE+17,BLANK
BIGLOOP  LA        R5,4       COUNTER FOR WORDS ON LINE
         LA        R10,ABDLINE+20      START OF +
LOOPIT   MVC       0(L'PAREN,R10),PAREN  MOVE IN +XXX(XXX)
         UNPK      TEMP(4),COUNT(3)     PREPARE TO MAKE COUNT PRINTABLE
         OI        TEMP+3,X'F0'
         MVC       1(3,R10),TEMP+1      MOVE IN PRINTABLE COUNT
         ST        R11,TEMP   PREPARE TO MAKE HEX COUNT PRINTABLE
         UNPK      5(4,R10),TEMP+2(3)      MAKE IT  FX
         TR        5(4,R10),TBL-240         MAKE IT PRINTABLE
         MVI       8(R10),C')'      MOVE IN ) KILLED BY MOVE
         UNPK      10(9,R10),0(5,R6)        GET THE WORDS IN TABLE
         TR       10(9,R10),TBL-240         MAKE IT PRINTABLE
         MVI       18(R10),BLANK     BLANK LAST JUNK BYTE
         AP        COUNT,=PL2'4'
         LA        R10,20(R10)     UP LINE POINTER BY 20
         LA        R11,4(R11)  UP HEX POINTER BY4
         LA        R6,4(R6)   UP BYTE COUNTER BY 4
         BCT       R5,LOOPIT
         BAL       R5,ABDOUT      PRINT LINE
         BCT       R7,BIGLOOP
         MVC       COUNT(3),=PL3'0'    RESTORE COUNT DECIMAL
         BR        R9        RETURN
TEMP     DC        F'0'
COUNT    DC        PL3'0'       BCD COUNT
PAREN    DC        C'+XXX(XXX)'
CVTMSG   DC        C'CVT IS AT'
TCBMSG   DC        C'TCB IS AT'
NOTE     DC        C' NOTE FOR DEVICES WITH MORE THAN 6 SENSE BYTES, BYX
               TES 1-2 ARE UNUSED,      BYTE 3 NUMBER OF EXPANDED SENSEX
                 BYTES, BYTES 4-6 ADDRESS OF EXPANDED SENSE BYTES'
REL   DC   C'THIS IS OS RELEASE'
RDRMSG DC C'READER STARTED READING THIS JOB AT XX.YY.ZZ ON DAY XXX YEARC
                19XX **** FOR  HASP THIS IS IPL TIME'
LOAD5    DC    CL8'IGC0Y05A'
TBL      DC    C'0123456789ABCDEF'
   EJECT
ABDAREA  DSECT
ABDSAVE  DS    18F                SAVE AREA
ABDECB   DS    1F                 DECB-ECB
ABOTYPE  DS    1H                 TYPE FIELD
ABDLEN   DS    1H                 LENGTH FILD
ABDDCB   DS    1F                 DCB
ABDPTR   DS    1F                 AREA ADDRESS
ABDSTAT  DS    1F                 POINTER TO STATUS INDICATORS
ABDCTR1  DS    1H                 LINE CTR
ABDCTR2  DS    1H                 PAGE CTR
ABDOUT   DS        55F
ABDPARA  DS    1F                 PTR TO PARAMETER LIST
         DS    1F
ABDWORK2 DS    1C
ABDWORK4 DS    11C
ABDWORK1 DS    1D
ABDWORK3 DS    1F
ABDPTRS  DS    3F                 BUFFER POINTERS USED BY OUTPUT
ABDPHY   DS    2F                 AREA FOR PRINT ROUTINE
ABDCC    DS    1C                 CARRIAGE CONTROL CHARACTER
ABDLINE  DS    CL32               120 CHAR PRINT LINE
         DS    CL32
         DS    CL32
         DS    CL24
         DS    1C                 FOR UNPACKING INTO LINE
ABDINCPL DS      1H
         DS      2C
LIMIT    DS    1F                 HI ADDRESS SAVE AREA
PARDSECT DSECT
PID      DS    1C
PWTG     DS    1C
PFLAGS   DS    2C                 OPTION FLAGS
PDCBPTR  DS    1F .               DCB ADDRESS
PTCBPTR  DS    1F                 TCB ADDRESS
PSNAPPTR DS    1F                 ADDRESS OF SNAPSHOT LIST
         END
   TITLE ' FORMAT DUMP CONTROL BLOCKS PART 5'
IGC0Y05A   CSECT
     PRINT  ON,GEN,DATA
R0       EQU       0
R1       EQU       1
R2       EQU       2
R3       EQU       3
R4       EQU       4
R5       EQU       5
R6       EQU       6
R7       EQU       7
R8       EQU       8
R9       EQU       9
RB       EQU       11
RA       EQU       10
RC       EQU       12
RD       EQU       13
RE       EQU       14
RF       EQU       15
PARREG   EQU       14
R10      EQU       10
R11      EQU       11
BLANK    EQU       X'40'
         BALR  RC,0
         USING *,RC               R12 = BASE
         USING ABDAREA,RD
         USING PARDSECT,PARREG    R 14=PTR TO PARMLIST
*
* R6= START ADDRESS OF THE CONTROL BLOCK
* R7= 1/4 NUMBER OF WORDS =NUMBER LINES
         L         R6,164(R2) GET TIMEING CONTROL TABLE IF ANY
         LTR       R6,R6
         BZ        NOSMF      BRANCH IF NOT SMF IN SYSTEM
         MVC       ABDLINE(L'TCTMSG),TCTMSG
         BAL       R5,ABDOUT    PRINTIT IT
         LA        R7,6      COUNT OF LINES TO PRINT
         BAL       R9,PRINT PRINT IT
         MVC       ABDLINE(L'JMRMSG),JMRMSG    GET JOB MANAGEMENT MSG
         BAL       R5,ABDOUT
         L         R6,164(R2)
         L         R6,28(R6)
         LA        R7,5
         BAL       R9,PRINT
NOSMF    EQU       *
         MVC       ABDLINE(L'JSCBMSG),JSCBMSG
         BAL       R5,ABDOUT
         L         R6,180(R2)  GET JOB STEP CONTROL BLOCK
         LA        R6,0(R6)  CLEAR HI ORDER BYTE
         LA        R7,18     COUNT OF LINES TO PRINT
         BAL       R9,PRINT
         L         R6,16
         L         R6,196(R6) GET SYSTEM MANAGEMENT CONTROL AREA
         LTR       R6,R6
         BZ        SKIP       NO SMF
         MVC       ABDLINE(L'SMCAMSG),SMCAMSG
         BAL       R5,ABDOUT
         LA        R7,8      COUNT OF LINES TO PRINT
         BAL       R9,PRINT
SKIP     EQU       *
* INSERT MORE FUNCTIONS HERE IF NEEDED
* INSERT MORE FUNCTIONS HERE IF NEEDED
* INSERT MORE FUNCTIONS HERE IF NEEDED
* INSERT MORE FUNCTIONS HERE IF NEEDED
EXIT     LA    RA,LOAD5           LOAD LOAD5 PARAMETER
         LA    RF,ABDWORK3
         XCTL  EPLOC=(10),SF=(E,(15))
PRINT    EQU       *
         ST        R6,TEMP   SAVE START ADDRESS
         LA        R11,0     SET      COUNT TO 0
         UNPK      ABDLINE+11(7),TEMP(5)  PUT IN START LOCATION
         TR        ABDLINE+11(6),TBL-240   MAKE IT PRINT RIGHT
         MVI       ABDLINE+17,BLANK
BIGLOOP  LA        R5,4       COUNTER FOR WORDS ON LINE
         LA        R10,ABDLINE+20      START OF +
LOOPIT   MVC       0(L'PAREN,R10),PAREN  MOVE IN +XXX(XXX)
         UNPK      TEMP(4),COUNT(3)     PREPARE TO MAKE COUNT PRINTABLE
         OI        TEMP+3,X'F0'
         MVC       1(3,R10),TEMP+1      MOVE IN PRINTABLE COUNT
         ST        R11,TEMP   PREPARE TO MAKE HEX COUNT PRINTABLE
         UNPK      5(4,R10),TEMP+2(3)      MAKE IT  FX
         TR        5(4,R10),TBL-240         MAKE IT PRINTABLE
         MVI       8(R10),C')'      MOVE IN ) KILLED BY MOVE
         UNPK      10(9,R10),0(5,R6)        GET THE WORDS IN TABLE
         TR       10(9,R10),TBL-240         MAKE IT PRINTABLE
         MVI       18(R10),BLANK     BLANK LAST JUNK BYTE
         AP        COUNT,=PL2'4'
         LA        R10,20(R10)     UP LINE POINTER BY 20
         LA        R11,4(R11)  UP HEX POINTER BY4
         LA        R6,4(R6)   UP BYTE COUNTER BY 4
         BCT       R5,LOOPIT
         BAL       R5,ABDOUT      PRINT LINE
         BCT       R7,BIGLOOP
         MVC       COUNT(3),=PL3'0'    RESTORE COUNT DECIMAL
         BR        R9        RETURN
LOAD5    DC    CL8'IGC0305A'
TBL      DC    C'0123456789ABCDEF'
TEMP     DC        F'0'
COUNT    DC        PL3'0'       BCD COUNT
PAREN    DC        C'+XXX(XXX)'
TCTMSG   DC        C'TIMING CONTROL TABLE IS AT'
JMRMSG   DC        C'JOB MANAGEMENT RECORD IS AT'
JSCBMSG  DC        C'JOB STEP CONTROL BLOCK IS AT'
SMCAMSG  DC        C'SYSTEM MANAGEMENT CONTROL AREA IS AT'
ABDAREA  DSECT
ABDSAVE  DS    18F                SAVE AREA
ABDECB   DS    1F                 DECB-ECB
ABOTYPE  DS    1H                 TYPE FIELD
ABDLEN   DS    1H                 LENGTH FILD
ABDDCB   DS    1F                 DCB
ABDPTR   DS    1F                 AREA ADDRESS
ABDSTAT  DS    1F                 POINTER TO STATUS INDICATORS
ABDCTR1  DS    1H                 LINE CTR
ABDCTR2  DS    1H                 PAGE CTR
ABDOUT   DS        55F
ABDPARA  DS    1F                 PTR TO PARAMETER LIST
         DS    1F
ABDWORK2 DS    1C
ABDWORK4 DS    11C
ABDWORK1 DS    1D
ABDWORK3 DS    1F
ABDPTRS  DS    3F                 BUFFER POINTERS USED BY OUTPUT
ABDPHY   DS    2F                 AREA FOR PRINT ROUTINE
ABDCC    DS    1C                 CARRIAGE CONTROL CHARACTER
ABDLINE  DS    CL32               120 CHAR PRINT LINE
         DS    CL32
         DS    CL32
         DS    CL24
         DS    1C                 FOR UNPACKING INTO LINE
ABDINCPL DS      1H
         DS      2C
LIMIT    DS    1F                 HI ADDRESS SAVE AREA
PARDSECT DSECT
PID      DS    1C
PWTG     DS    1C
PFLAGS   DS    2C                 OPTION FLAGS
PDCBPTR  DS    1F .               DCB ADDRESS
PTCBPTR  DS    1F                 TCB ADDRESS
PSNAPPTR DS    1F                 ADDRESS OF SNAPSHOT LIST
         END
