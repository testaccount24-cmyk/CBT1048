*   NAME            JULTOACT
*   LEVEL           1
*   DATE            SEPTEMBER 19,1966
*   DESCRIPTION     THIS ROUTINE, JULIAN-TO-ACTUAL, CONVERTS A FIVE
*                   POSITION JULIAN DATE, YYDDD (Y-INDICATING YEAR,
*                   D-DAY) TO A SIX POSITION ACTUAL DATE, MMDDYY
*                   (M-MONTH D-DAY Y-YEAR).
*   REGISTER USAGE  1 - PACKED JULIAN DATE SUPPLIED BY CALLER
*                   2 - PACKED ACTUAL DATE RETURNED TO CALLER
*                   9 - INTERNAL SHORT LOOPS COUNTER
*                   10- RETURN REGISTER FOR BAL
*
         EJECT
JULTOACT START 0                   JULIAN TO ACTUAL DATE ROUTINE
         SAVE  (14,12),T           SAVE REGS FOR CALLING PROGRAM
         BALR  12,0
         USING *,12                REG 12 IS THE BASE REG
         ST    13,SAVE+4           CALLING PROG SAVE AREA ADDRESS IN
*                                  THE CALLED PROG SAVE AREA
         LA    2,SAVE
         ST    2,8(13)             CALLED PROG SAVE AREA ADDRESS  IN
*                                  THE CALLING PROG SAVE AREA
         LR    13,2                CALLED PROG SAVE AREA ADD LOADED 13
ERRBEGIN STH   1,JULDATE           REG 1 CONTAINS THE JUL DATE YYDDD
         CP    =P'000',JULDATE     VALID JULIAN DATE ?  ABOVE 000
         BL    CONTIN1             VALID DATE,CONTINUE
         B     INVALID             INVALID, ERROR MESSAGE
CONTIN1  CP    =P'366',JULDATE     VALID JULIAN DATE ? BELOW 237
         BL    INVALID
         ST    1,JULYR
         MVO   JULYR,JULYR(2)
         DP    JULYR,=P'4'         SET FIELD FOR LEAP YEAR TEST
         MVC   MONTH,=P'01'
         BAL   10,RTNMON31         JAN
         CP    JULYR+3(1),=P'0'   TEST FOR LEAP YEAR
         BE    RTNMON29            LEAP YEAR FEB
         CP    JULDATE,=P'29'      REG YEAR FEB
         BL    OUT
         AP    MONTH,=P'01'
         SP    JULDATE,=P'28'
MARCH    BAL   10,RTNMON31        MARCH
         LA    9,2
APRIL    BAL   10,RTNMON30         APRIL JUNE
         BAL   10,RTNMON31         MAY   JULY
         BCT   9,APRIL
         LA    9,2                 RESET REG 9
AUGUST   BAL   10,RTNMON31         AUG  OCT
         BAL   10,RTNMON30         SEPT NOV
         BCT   9,AUGUST
         BAL   10,RTNMON31         DEC
         B     INVALID
OUT      ST    1,WRKAREA1          SET UP RESULTS IN REG 2
         MVO   WRKAREA1,WRKAREA1(2)        YEAR
         MVZ   WRKAREA1+2(1),JULDATE+1     DAY UNIT POSITION
         MVN   WRKAREA1+1(1),JULDATE       DAY TENS POSITION
         MVZ   WRKAREA1+1(1),MONTH+1       MONTH UNIT POSITION
         MVN   WRKAREA1(1),MONTH           MONTH TENS POSITION
         L     2,WRKAREA1
         L     13,SAVE+4           CALLING PROG SAVE AREA ADDRESS
         LM    14,0,12(13)        RESTORE  REG 14,15,0
         LM    3,12,32(13)         RESTORE  REG 3 TO 12
         BR    14                  RETURN TO CALLING PROGRAM
INVALID  ABEND 028,DUMP
RTNMON29 CP    JULDATE,=P'030'     ROUTINE FOR A 29 DAY MONTH
         BL    OUT
         AP    MONTH,=P'01'
         SP    JULDATE,=P'029'
         B     MARCH
RTNMON30 CP    JULDATE,=P'031'     ROUTINE FOR A 30 DAY MONTH
         BL    OUT
         AP    MONTH,=P'01'
         SP    JULDATE,=P'030'
         BR    10
RTNMON31 CP    JULDATE,=P'032'     ROUTINE FOR A 31 DAY MONTH
         BL    OUT
         AP    MONTH,=P'01'
         SP    JULDATE,=P'031'
         BR    10
         EJECT
*
*   CONSTANTS AND WORK AREAS
*
SAVE     DS    18F                 CALLED PROG (MY PROG) SAVE AREA ADD
JULDATE  DS    PL2                 JUL DATE WORKAREA
         DS    0F
JULYR    DS    PL4                 JUL YR WORKAREA
MONTH    DS    PL2
         DS    0F
WRKAREA1 DS    PL4
         END
