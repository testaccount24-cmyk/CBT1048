         MACRO
&NAME    UT458 &METHOD=,&OPCODE=,&DATE=,&LANGAGE=,&UTDS=
         LCLC  &OPC,&LANGA,&DS,&OP1,&OP2,&METH
         AIF   ('&NAME' EQ '').NONAME
&NAME    EQU   *
.NONAME  ANOP
&METH    SETC  'LINK'
         AIF   ('&METHOD' EQ '').A
&METH    SETC  '&METHOD'
         AIF   ('&METH' EQ 'LINK').A
         AIF   ('&METH' EQ 'LOAD').B
         AIF   ('&METH' EQ 'LADE').B
         AIF   ('&METH' EQ 'EXEC').C
         AIF   ('&METH' EQ 'DELETE').DELETE
         MNOTE 4,'INVALID METHOD OPERAND - LINK IS ASSUMED'
&METH    SETC  'LINK'
.A       AIF   ('&UTDS' EQ '').YES
&DS      SETC  '&UTDS'
         AIF   ('&DS' EQ 'YES').YES
         AIF   ('&DS' EQ 'NO').C
         MNOTE 4,'INVALID UTDS OPERAND - YES IS ASSUMED'
.YES     ANOP
.B       CNOP  0,4
         B     *+106          BRANCH AROUND PARAMLIST PROBLEM PROGRAM
UTPARMAD DC    A(UTOPCODE)              ADDRESS PARAMLIST PROBLEM PROG.
UTEPADD  DC    A(0)                     ADDRESS OF UT458
UTEPSYMB DC    CL8'UT458'               ENTRY POINT SYMBOL
UTOPCODE DS    C  *****************************************************
UTMMDDYY DS    CL6  *                                                 *
         ORG   *-6  *    YY = YEAR NUMBER                             *
UTMM     DS    CL2  *    MM = MONTH NUMBER                            *
UTDD     DS    CL2  *    DD = DAY NUMBER OF THE MONTH                 *
UTYY     DS    CL2  *    WW = WEEK NUMBER                             *
UTDDMMYY DS    CL6  *    DDD = DAY NUMBER OF THE YEAR                 *
UTYYMMDD DS    CL6  *    D = DAY NUMBER OF THE WEEK  ( 0 = SUNDAY,    *
UTDWWYY  DS    CL5  *                                  1 = MONDAY,    *
UTYYWWD  DS    CL5  *                                  2 = TUESDAY,   *
         ORG   *-3  *                                  3 = WEDNESDAY, *
UTWW     DS    CL2  *                                  4 = THURSDAY,  *
UTD      DS    CL1  *                                  5 = FRIDAY,    *
UTYYDDD  DS    CL5  *                                  6 = SATURDAY ) *
         ORG   *-3  *                                                 *
UTDDD    DS    CL3  *                                                 *
UTREPORT DS    CL9  *    <--- DATE FOR REPORT (DD MMM YY)             *
UTLASTM  DS    CL9  *    <--- NAME OF LAST MONTH                      *
UTTHISM  DS    CL9  *    <--- NAME OF THIS MONTH                      *
UTNEXTM  DS    CL9  *    <--- NAME OF NEXT MONTH                      *
UTDAYNAM DS    CL9  *    <--- NAME OF THE DAY                         *
UTHHMMSS DS    CL6  *    <-- HH = HOURS, MM = MINUTES, SS = SECONDS.  *
UTRTCODE DS    C  *****************************************************
         AIF   ('&METH' EQ 'LINK').C
         LA    0,UTEPSYMB               LOAD ADDRESS OF EP SYMBOL
         SR    1,1                      SHOW NO DCB PRESENT
         SVC   8                        ISSUE LOAD SVC
         ST    0,UTEPADD                SAVE ADDRESS OF UT458
         AIF   ('&METH' EQ 'LOAD').END
.C       ANOP
         AIF   ('&OPCODE' EQ '').DOPC
&OPC     SETC  '&OPCODE'
         AIF   ('&OPC' EQ '0').MO
         AIF   ('&OPC' EQ '1').OPC1
         AIF   ('&OPC' EQ '2').OPC2
         AIF   ('&OPC' EQ '3').OPC3
         AIF   ('&OPC' EQ '4').OPC4
         AIF   ('&OPC' EQ '5').OPC5
         AIF   ('&OPC' EQ '6').OPC6
         MNOTE 4,'INVALID OPCODE OPERAND - 0 IS ASSUMED'
.DOPC    ANOP
&OPC     SETC  '0'
         AGO   .MO
.OPC1    ANOP
&OP1     SETC  'UTMMDDYY'
         AGO   .TESTDAT
.OPC2    ANOP
&OP1     SETC  'UTDDMMYY'
         AGO   .TESTDAT
.OPC3    ANOP
&OP1     SETC  'UTYYMMDD'
         AGO   .TESTDAT
.OPC4    ANOP
&OP1     SETC  'UTDWWYY'
         AGO   .TESTDAT
.OPC5    ANOP
&OP1     SETC  'UTYYWWD'
         AGO   .TESTDAT
.OPC6    ANOP
&OP1     SETC  'UTYYDDD'
.TESTDAT ANOP
&OP2     SETC  '&DATE'
         AIF   ('&DATE' NE '').MO
         MNOTE 16,'DATE OPERAND MISSING'
         MEXIT
.MO      MVI   UTOPCODE,C'&OPC'         MOVE OPERATION CODE FOR UT458
         AIF   ('&OPC' EQ '0').LANG
         MVC   &OP1,&OP2
.LANG    ANOP
         AIF   ('&LANGAGE' EQ '').ENGLISH
&LANGA   SETC  '&LANGAGE'
         AIF   ('&LANGA' EQ 'ENG').ENGLISH
         AIF   ('&LANGA' EQ 'FRE').FRENCH
         AIF   ('&LANGA' EQ 'FLE').FLEMISH
         AIF   ('&LANGA' EQ 'GER').GERMAN
         MNOTE 4,'INVALID LANGUAGE OPERAND - ENGLISH IS ASSUMED'
.ENGLISH MVC   UTREPORT(7),=C'ENGLISH'  MOVE LANGUAGE REQUESTED
         AGO   .LEND
.FRENCH  MVC   UTREPORT(7),=C'FRENCH '  MOVE LANGUAGE REQUESTED
         AGO   .LEND
.FLEMISH MVC   UTREPORT(7),=C'FLEMISH'  MOVE LANGUAGE REQUESTED
         AGO   .LEND
.GERMAN  MVC   UTREPORT(7),=C'GERMAN '  MOVE LANGUAGE REQUESTED
.LEND    ANOP
         LA    1,UTPARMAD               LOAD ADDRESS PARAMLIST ADDRESS
         AIF   ('&METH' EQ 'LINK').D
         L     15,UTEPADD               LOAD ADDRESS OF EP UT458
         BALR  14,15                    PERFORM UT458
         AGO   .END
.D       CNOP  0,4
         BAL   15,*+12                  LOAD SUP. PARAMLIST ADDRESS
         DC    A(UTEPSYMB)              ADDRESS OF EP SYMBOL
         DC    A(0)                     ADDRESS DCB
         SVC   6                        ISSUE LINK SVC
         AGO   .END
.DELETE  LA    0,UTEPSYMB               LOAD ADDRESS EP SYMBOL
         SVC   9                        ISSUE DELETE SVC
.END     MEND
