         MACRO
         REMOTE &RMT,&ACCESS
.*
.*       THIS MACRO IS USED WITH THE MONSANTO OPEN MOD FOR SECURITY
.*       CHECKING ON ACCESS TO FILES. IT'S PURPOSE IS TO DEFINE
.*       THE REMOTES WHOSE ACCESS TO FILES IS TO BE CHECKED AND IT
.*       IS USED IN CONJUNCTION WITH THE 'DSN' MACRO TO BUILD A TABLE
.*       OF FILE NAMES FOR EACH REMOTE INVOLVED IN THE SECURITY
.*       CHECK. THE LIST OF NAMES CAN BE INCLUSIVE (REMOTE CAN ACCESS
.*       ONLY THE FILES ON THE LIST), OR EXCLUSIVE (THE REMOTE CAN
.*       ACCESS ANY FILES EXCEPT THOSE ON THE LIST). MORE THAN ONE
.*       REMOTE CAN BE COVERED BY ONE DSN LIST. THE FORMAT OF THE
.*       PARAMETERS IS:
.*
.*       REMOTE (NN,NN,..),<ONLY|EXCEPT>
.*
.*       'NN' IS A REMOTE NUMBER AND 'ONLY' OR 'EXCEPT' GIVE
.*             INCLUSIVE OR EXCLUSIVE PROTECTION RESPECTIVELY
.*
.*
.*
.*
         GBLB  &START              FIRST TIME ENTRY SWITCH
         GBLB  &REMOTE             REMOTE MACRO HAS BEEN ISSUED
         LCLB  &COVER              USED TO SIGNAL WETHER THIS IS AN
.*                                 INCLUSIVE OR EXCLUSIVE LIST
         LCLA  &WORKA,&WORKA2,&WORKA3 WORK COUNTERS AND LOOP CONTROL
         LCLC  &WORKC              USED TO MAKE LABEL DEFINITIONS TIDY
         AIF   (&REMOTE).ERR4      MISSING ENDLIST MACRO
&REMOTE  SETB  1                   SIGNAL THAT MACRO ISSUED
&COVER   SETB  0                   ASSUME DEFAULT OF 'ONLY'
         AIF   ('&ACCESS' NE 'ONLY' AND '&ACCESS' NE 'EXCEPT').ERR1
         AIF   ('&ACCESS' EQ 'ONLY').ACCESS ACCESS TO FILE IS INCL.
&COVER   SETB  1                   ACCESS IS EXCLUSIVE
.ACCESS  ANOP
.**********************************************************************
.*       TEST FOR FIRST TIME THROUGH AND INITIALISE IF YES
.**********************************************************************
         AIF   (&START).STDONE     TABLE IS INITIALISED.
FILELIST CSECT ,                   CSECT ID
         DC    A(TSUSER)           START POINT ADDRESS OF TSO USERS TAB
         DC    A(REMOTE)           START POINT ADDRESS OF REMOTES TABLE
TSUSER   DC    150F'0'             ALLOW FOR 50 USER ID'S
REMOTE   DC    50F'0'              ALLOW FOR 50 REMOTE ID'S
&START   SETB  1                   SET ON INITIALISATION FLAG
.STDONE  ANOP
.**********************************************************************
.*       LOOP THROUGH ALL THE SUPPLIED REMOTE ID'S AND PROCESS
.**********************************************************************
&WORKA   SETA  0                   INITIALISE LOOP CONTROL
.RMTLOOP ANOP
         AIF   (&WORKA EQ N'&RMT).ALLDONE FINISH IF ALL OPERANDS DONE
&WORKA   SETA  &WORKA+1            INCREMENT LOOP COUNTER
         AIF   (T'&RMT(&WORKA) NE 'N').ERR2 REMOTE ID IS NOT NUMERIC
&WORKA2  SETA  &RMT(&WORKA)        PICK UP THE REMOTE NUMBER
         AIF   (&WORKA2 GT 50).ERR3 REMOTE NO > 50
&WORKA2  SETA  &WORKA2*4           MULTIPLY BY FOUR
         ORG   REMOTE+&WORKA2      LOCATE ENTRY FOR THIS REMOTE
&WORKA3  SETA  0                   INITIALISE WORK FLAG
         AIF   (&COVER EQ 0).NOSET LEAVE FLAG AT ZERO
&WORKA3  SETA  1                   SET ON FLAG FOR EXCLUSIVE
.NOSET   ANOP
         DC    AL1(&WORKA3)        PROCESSING FLAG
         DC    AL3(IHB&SYSNDX)     ADDRESS OF START OF DSN LIST
         AGO   .RMTLOOP            AND PROCESS ALL OPERANDS
.ALLDONE ANOP
.**********************************************************************
.*       ORG TO END OF TABLE AND DEFINE LABEL FOR DSN LIST
.**********************************************************************
         ORG   ,                   RESET LOCATION COUNTER
&WORKC   SETC  'IHB&SYSNDX'        BUILD A TIDY LABEL
&WORKC   EQU   *                   START OF DSN LIST
         MEXIT
.ERR4    ANOP
         MNOTE 16,'MISSING ''ENDLIST'' MACRO. - DEFINITION ABORTED'
         MEXIT
.ERR3    ANOP
         MNOTE 16,'INVALID REMOTE NO - &RMT(&WORKA) - GREATER THAN 50 -X
                DEFINITION ABORTED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'REMOTE ID IS NOT A VALID NUMBER - &RMT(&WORKA)'
         MEXIT
.ERR1    ANOP
         MNOTE 16,'INVALID ACCESS TYPE - &ACCESS. DEFINITION ABORTED'
         MEND
