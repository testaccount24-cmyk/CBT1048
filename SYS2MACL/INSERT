         MACRO
&LABEL   INSERT &DATA,&LENGTH
         LCLC  &WORKC,&WORKC2
&WORKC2  SETC  'L'''
&LABEL   DS    0H
         AIF   ('&DATA'(1,1) NE '''').DATALD
&WORKC   SETC  'IHB&SYSNDX'
         BAL   0,&WORKC            JUMP AROUND ARGUMENT
&WORKC.D DC    C&DATA
&WORKC   DS    0H
         AGO   .LOADLEN
.DATALD  AIF   ('&DATA'(1,1) EQ '(').DATAREG
         LA    0,&DATA             LOAD ADDRESS OF ARGUMENT
         AGO   .LOADLEN
.DATAREG LA    0,0&DATA            LOAD ADDRESS OF ARGUMENT
         AGO   .LOADLEN
.LOADLEN AIF   (T'&LENGTH NE 'O').LENHERE
         AIF   ('&DATA'(1,1) NE '(').LATTRIB
         MNOTE 0,'NO LENGTH AVAILABLE FOR DATA. ''1'' ASSUMED'
         LA    15,1                ASSUME DATA LENGTH OF 1
         AGO   .CALL
.LATTRIB AIF   ('&DATA'(1,1) NE '''').NOTLIT
         LA    15,&WORKC2&WORKC.D  LOAD LENGTH OF SCAN LITERAL
         AGO   .CALL
.NOTLIT  LA    15,&WORKC2&DATA     LOAD LENGTH OF SCAN DATA
         AGO   .CALL
.LENHERE AIF   ('&LENGTH'(1,1) EQ '(').LENREG
         LA    15,&LENGTH.(0,0)    LOAD LENGTH OF DATA
         AGO   .CALL
.LENREG  LA    15,0&LENGTH         LOAD LENGTH OF DATA
         AGO   .CALL
.CALL    L     11,JCLINS           LOAD ADDRESS OF ROUTINE
         BALR  11,11               CALL THE INSERT ROUTINE
         MEND
