         MACRO
&LABEL   ZSNAP &ID=0,&SDATA=,&PDATA=,&STORAGE=,&LIST=,&CLOSE=YES
.****                                                              ****
.**            ZSNAP WILL DUMP THE CONTENTS OF DATA AREAS            **
.**            LISTED. ALL OPERANDS USED BY SNAP TO DUMP             **
.**            AREAS RELATING TO THE ACTIVE TASK CAN BE              **

.**            USED BY ZSNAP PROVIDED AT LEAST ONE DATA              **
.**            AREA NAME IS SPECIFIED. IF MORE THAN ONE              **
.**            ZSNAP IS USED, CODE 'CLOSE=NO' ON ALL BUT THE         **
.**            LAST ZSNAP. ZSNAP CANNOT BE USED IN MORE THAN         **
.**            ONE CSECT IN THE SAME ASSEMBLY.                       **
.****                                                              ****
         GBLB  &SNAPON                 PREVIOUS ZSNAP INDICATOR
         LCLA  &TEMP
         LCLA  &N                      SYSLIST POINTER
         LCLC  &LABEL2
         LCLC  &LISTADD
&LISTADD SETC  '&LIST'
&LABEL2  SETC  'TS&SYSNDX.A'
         AIF   ('&LABEL' EQ '').LABEL
&LABEL2  SETC  '&LABEL'
.LABEL   ANOP
         B     &LABEL2
         AIF   (&SNAPON EQ 1).NODCB
SNAPDCB  DCB   DDNAME=DDSNAP,DSORG=PS,RECFM=VBA,MACRF=W,BLKSIZE=882,   X
               LRECL=125
.NODCB   ANOP
         AIF   ('&STORAGE' NE '').SNAP
         AIF   ('&LIST' NE '').SNAP
TS&SYSNDX DS   0F
.LOOP    ANOP
&N       SETA  &N+1
         DC    A(&SYSLIST(&N))
&TEMP    SETA  L'&SYSLIST(&N)
         DC    A(&TEMP-1)
         AIF   (&N LT N'&SYSLIST).LOOP
&LABEL2  EQU   *
         OI    *-8,X'80'
         LA    1,&LABEL2
         LA    14,TS&SYSNDX
TS&SYSNDX.C EQU *
         L     15,0(14)
         A     15,4(14)
         N     15,=F'-4'
         ST    15,4(14)
         LA    14,8(14)
         CR    14,1
         BL    TS&SYSNDX.C
&LISTADD SETC  'TS&SYSNDX'
.SNAP    ANOP
         AIF   (&SNAPON EQ 1).NOOPEN
         OPEN  (SNAPDCB,OUTPUT)
&SNAPON  SETB  1
.NOOPEN  ANOP
         SNAP  DCB=SNAPDCB,ID=&ID,SDATA=&SDATA,PDATA=&PDATA,           X
               STORAGE=&STORAGE,LIST=&LISTADD
         AIF   ('&CLOSE' NE 'YES').NOCLOSE
         CLOSE SNAPDCB
.NOCLOSE ANOP
         MEND
