         MACRO
         TSUSER &USERID,&ACCESS
.*
.*       THIS MACRO IS USED WITH THE MONSANTO OPEN MOD FOR SECURITY
.*       CHECKING ON ACCESS TO FILES. IT'S PURPOSE IS TO DEFINE
.*       THE TSO USERS WHOSE ACCESS TO FILES IS TO BE CHECKED AND IT
.*       IS USED IN CONJUNCTION WITH THE 'INDEX', 'DSN' AND 'VOL'
.*       MACROS TO BUILD A TABLE OF FILE/DISK NAMES FOR EACH USER
.*       INVOLVED IN THE SECURITY CHECK.
.*       THE LIST OF NAMES CAN BE INCLUSIVE (USER CAN ACCESS
.*       ONLY THE FILES ON THE LIST), OR EXCLUSIVE (THE USER CAN
.*       ACCESS ANY FILES EXCEPT THOSE ON THE LIST). MORE THAN ONE
.*       USER CAN BE COVERED BY ONE INDEX, DSN AND VOL LIST. THE FORMAT
.*       OF PARAMETERS IS :
.*
.*       TSUSER (USERID,USERID,..),<ONLY|EXCEPT>
.*
.*          'ONLY' OR 'EXCEPT' GIVE
.*           INCLUSIVE OR EXCLUSIVE PROTECTION RESPECTIVELY
.*
.*
.*
.*
         GBLB  &START              FIRST TIME ENTRY SWITCH
         GBLB  &REMOTE             REMOTE MACRO HAS BEEN ISSUED
         GBLB  &TSUSER             TSUSER MACRO HAS BEEN ISSUED
         GBLA  &TABINX             TSO USER TABLE INDEX
         LCLB  &COVER              USED TO SIGNAL WETHER THIS IS AN
.*                                 INCLUSIVE OR EXCLUSIVE LIST
         LCLA  &WORKA,&WORKA2,&WORKA3 WORK COUNTERS AND LOOP CONTROL
         LCLC  &WORKC              USED TO MAKE LABEL DEFINITIONS TIDY
         AIF   (&TSUSER).ERR4      MISSING ENDLIST MACRO
&TSUSER  SETB  1                   SIGNAL THAT MACRO ISSUED
&COVER   SETB  0                   ASSUME DEFAULT OF 'ONLY'
         AIF   ('&ACCESS' NE 'ONLY' AND '&ACCESS' NE 'EXCEPT').ERR1
         AIF   ('&ACCESS' EQ 'ONLY').ACCESS ACCESS TO FILE IS INCL.
&COVER   SETB  1                   ACCESS IS EXCLUSIVE
.ACCESS  ANOP
.**********************************************************************
.*       TEST FOR FIRST TIME THROUGH AND INITIALISE IF YES
.**********************************************************************
         AIF   (&START).STDONE     TABLE IS INITIALISED.
FILELIST CSECT ,                   CSECT ID
         DC    A(TSUSER)           START POINT ADDRESS OF TSO USERS TAB
         DC    A(REMOTE)           START POINT ADDRESS OF REMOTES TABLE
TSUSER   DC    150F'0'             ALLOW FOR 50 USER ID'S
REMOTE   DC    50F'0'              ALLOW FOR 50 REMOTE ID'S
&START   SETB  1                   SET ON INITIALISATION FLAG
.STDONE  ANOP
.**********************************************************************
.*       LOOP THROUGH ALL THE SUPPLIED USER ID'S AND PROCESS
.**********************************************************************
&WORKA   SETA  0                   INITIALISE LOOP CONTROL
.USRLOOP ANOP
         AIF   (&WORKA EQ N'&USERID).ALLDONE FINISH IF ALL OPRS DONE
&WORKA   SETA  &WORKA+1            INCREMENT LOOP COUNTER
         AIF   (K'&USERID(&WORKA) GT 8).ERR2 LENGTH OF 'USERID' > 8
         AIF   ('&USERID(&WORKA)' EQ '$DEFAULT').DEFAULT
&TABINX  SETA  &TABINX+1           INCREMENT TABLE INDEX
         AIF   (&TABINX GT 49).ERR3 TABLE OVERFLOW
&WORKA2  SETA  &TABINX*12          MULTIPLY BY TWELVE
&WORKA2  SETA  &WORKA2-12          SUBTRACT TWELVE FOR TABLE OFFSET
         ORG   TSUSER+&WORKA2      LOCATE ENTRY FOR THIS USER
         AGO   .NODEF
.DEFAULT ANOP
&WORKA2  SETA  49*12               $DEFAULT MUST BE THE LAST ENTRY
         ORG   TSUSER+&WORKA2      LOCATE ENTRY FOR THIS USER
.NODEF   ANOP
&WORKA3  SETA  0                   INITIALISE WORK FLAG
         AIF   (&COVER EQ 0).NOSET LEAVE FLAG AT ZERO
&WORKA3  SETA  1                   SET ON FLAG FOR EXCLUSIVE
.NOSET   ANOP
         DC    AL1(&WORKA3)        PROCESSING FLAG
         DC    AL3(IHB&SYSNDX)     ADDRESS OF START OF DSN LIST
         DC    CL8'&USERID(&WORKA)' USER ID
         AGO   .USRLOOP            AND PROCESS ALL OPERANDS
.ALLDONE ANOP
.**********************************************************************
.*       ORG TO END OF TABLE AND DEFINE LABEL FOR DSN LIST
.**********************************************************************
         ORG   ,                   RESET LOCATION COUNTER
&WORKC   SETC  'IHB&SYSNDX'        BUILD A TIDY LABEL
&WORKC   EQU   *                   START OF DSN LIST
         MEXIT
.ERR4    ANOP
         MNOTE 16,'MISSING ''ENDLIST'' MACRO. - DEFINITION ABORTED'
         MEXIT
.ERR3    ANOP
         MNOTE 16,'MORE THAN 50 TSO USERS, TABLE OVERFLOW - DEFINITION X
               ABORTED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'LENGTH OF ''&USERID(&WORKA)''   IS > 8 POS. - DEFINIX
               TION ABORTED'
         MEXIT
.ERR1    ANOP
         MNOTE 16,'INVALID ACCESS TYPE - &ACCESS. DEFINITION ABORTED'
         MEND
