         MACRO
         PARMPROC
         LCLA  &NUM,&DIGIT
         LCLB  &SWITCH
         LCLC  &NOPEXT
         L     1,0(1)
         LH    3,0(1)
         LA    4,2(1)
         IF  (LTR,REG3,REG3,NZ)
         DO    WHILE=(CLI,@EOPARM,NE,X'FF'),                           X
               UNTIL=(CLI,@VALPARM,EQ,X'FF')
.LOOP    ANOP
&NUM     SETA  &NUM+1
         AIF   ('&SYSLIST(&NUM)' EQ '').LOOPEND
         AIF   (N'&SYSLIST(&NUM) EQ 2).NEXT
         MNOTE 4,'INVALID OPERAND FORMAT'
         MEXIT
.NEXT    AIF   ('&SYSLIST(&NUM,1)' NE '').CONTINU
&SWITCH  SETB  1
&NOPEXT  SETC  '&SYSLIST(&NUM,2)'
         AGO   .LOOP
.CONTINU ANOP
         IF    (CLC,@A&NUM,EQ,0(4))
             INVOKE  &SYSLIST(&NUM,2)
             MVI   @PARAM,X'FF'
         ENDIF
         AGO   .LOOP
.LOOPEND ANOP
         BCTR  3,0
         IF    (EX,3,@TRT,Z)
             MVI   @EOPARM,X'FF'
         ELSE
             SR    1,4
             SR    3,1
             LA    4,1(1,4)
         ENDIF
         IF    (CLI,@PARAM,NE,X'FF')
             MVI   @VALPARM,X'FF'
         ELSE
             MVI   @PARAM,X'00'
         ENDIF
         ENDDO
         AIF   (&SWITCH NE 1).NOELSE
         ELSE
         INVOKE &NOPEXT
.NOELSE  ENDIF
         B     @ENDPARM
@TRT     TRT   0(1,4),@TABLE
@TABLE   DC    256X'00'
         ORG   @TABLE+C','
         DC    C','
         ORG   @TABLE+256
@EOPARM  DC    X'00'
@VALPARM DC    X'00'
@PARAM   DC    X'00'
.LOOP2   ANOP
&DIGIT   SETA  &DIGIT+1
         AIF   (&DIGIT EQ &NUM).END
         AIF   ('&SYSLIST(&DIGIT,1)' EQ '').LOOP2
@A&DIGIT DC    C'&SYSLIST(&DIGIT,1)'
         AGO   .LOOP2
.END     ANOP
         DS    0H
@ENDPARM EQU   *
         IF    (CLI,@VALPARM,EQ,X'FF')
             LA    15,16
             WTO   'INVALID PARM FOR &SYSECT',ROUTCDE=11
         ELSE
             XR    15,15
         ENDIF
         MEND
