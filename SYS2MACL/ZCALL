         MACRO
&LABEL   ZCALL &MOD,&PARMS,&VL,&MF=
.****
         GBLC  &ARRAY(50)  CONTAINS EP NAMES OF PREVIOUS WEXTRNS
.****
         LCLA  &INDX       INDEX FOR ABOVE ARRAYS
.****
         AIF   ('&LABEL' EQ '').NOLABEL TEST FOR LABEL
&LABEL   EQU   *
.NOLABEL ANOP
         AIF   ('&PARMS' EQ '').EXEC   TEST FOR PARAMETERS
         AIF   ('&MF(1)' EQ 'E').EXEC  TEST FOR EXECUTE FORM
         AIF   ('&MF' EQ 'L').LIST     TEST FOR LIST FORM
         B     IHB&SYSNDX.D BRANCH ROUND PARMS
.LIST    ANOP
IHB&SYSNDX.A CALL ,&PARMS,&VL,MF=L
         AIF   ('&MF' EQ 'L').END      TEST FOR LIST FORM
IHB&SYSNDX.D LA 1,IHB&SYSNDX.A LOAD PARM LIST ADDRESS
.EXEC    ANOP
         AIF   ('&MF(2)' EQ '').EX     TEST FOR PARM ADDRESS
         LA    1,&MF(2)       LOAD REG1 WITH PARM ADDRESS
.EX      ANOP
         AIF   ('&MOD' EQ '').MNOTE    CHECK EP SPECIFIED
&INDX    SETA  0
.LOOP    ANOP
&INDX    SETA  &INDX+1
         AIF   ('&ARRAY(&INDX)' EQ '').NOTFOND
         AIF   ('&ARRAY(&INDX)' NE '&MOD').LOOP
         AGO   .CALL
.NOTFOND ANOP
&ARRAY(&INDX) SETC '&MOD'
         WXTRN &MOD
.CALL    ANOP
         CNOP  0,4
         BAL   15,*+8  BRANCH AROUND ADCON
         DC    A(&MOD) EP ADDRESS
         L     15,0(15) LOAD 15 WITH EP ADDRESS

         LTR   15,15 WAS WEXTRN SATISFIED?
         BZ    IHB&SYSNDX.B NO - BRANCH TO LINK
         BALR  14,15 BRANCH TO MODULE
         B     IHB&SYSNDX.C
IHB&SYSNDX.B EQU *
         LINK  EP=&MOD
IHB&SYSNDX.C EQU *
         MEXIT
.MNOTE   MNOTE 8,'**** ENTRY POINT NOT SPECIFIED'
.END     ANOP
         MEND
