         MACRO
&LABEL   ZTIME &CC=0,&TYPE=REAL,&TIME=,&MINS=3,&DUMP=YES,&GEN=YES
         LCLC  &TEMP
         AIF   ('&GEN' EQ 'NO').NODOC
***********************************************************************
*                                                                     *
*                               ZTIME                                 *
*                                                                     *
*        GEN=  YES OR NO FOR THIS DOCUMENTATION    DEFAULT YES        *
*        CC=   JOB COMPLETION CODE AT END OF TIME  DEFAULT 0          *
*        TYPE= REAL OR TASK AS IN STIMER           DEFAULT REAL       *
*        MINS= NUMBER OF MINUTES TO BE SET,        DEFAULT 3          *
*              IGNORED IF TIME CODED                                  *
*        TIME= EIGHT DIGIT TIME OF FORMAT HHMMSSTH                    *
*        DUMP= YES IF DUMP REQUIRED,NO IF NOT      DEFAULT YES        *
*                                                                     *
***********************************************************************
.NODOC   ANOP
&LABEL   STIMER &TYPE,TS&SYSNDX.A,DINTVL=TS&SYSNDX
         B     TS&SYSNDX.B
         DS    0D
         AIF   ('&TIME' EQ '').MINS
         AIF   (K'&TIME EQ 8).SET
         MNOTE 8,'****  TIME OPERAND NOT 8 DIGITS'
TS&SYSNDX EQU  *
TS&SYSNDX.A EQU *
TS&SYSNDX.B EQU *
         MEXIT
.SET     ANOP
TS&SYSNDX DC   C'&TIME'
         AGO   .ABEND
.MINS    AIF   (&MINS GT 9).TEST
TS&SYSNDX DC   C'000&MINS.0000'
         AGO   .ABEND
.TEST    AIF   (&MINS LE 60).SETUP
         MNOTE 8,'****  MINS OPERAND OUT OF RANGE'
TS&SYSNDX EQU  *
TS&SYSNDX.A EQU *
TS&SYSNDX.B EQU *
         MEXIT
.SETUP   ANOP
TS&SYSNDX DC   C'00&MINS.0000'
.ABEND   ANOP
TS&SYSNDX.A EQU *
         AIF   ('&DUMP' EQ 'NO').NODUMP
&TEMP    SETC  'DUMP'
.NODUMP  ANOP
         USING *,15
         ABEND &CC,&TEMP
         DROP  15
TS&SYSNDX.B EQU *
         MEND
ID       MVS
