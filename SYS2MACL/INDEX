         MACRO
         INDEX
.*
.*       THIS MACRO IS USED WITH THE MONSANTO OPEN MOD FOR SECURITY
.*       CHECKING ON ACCESS TO FILES. IT'S PURPOSE IS TO DEFINE
.*       THE NAMES OF DATA SETS WHOSE ACCESS BY A PARTICULAR
.*       REMOTE IS TO BE CONTROLLED. EACH MACRO MAY DEFINE A LIST OF
.*       INDEXES OR JUST ONE. THE DEFINITIONS MUST ALWAYS BE PRECEEDED
.*       BY A 'REMOTE' DEFINITION.
.*
.*       THE PERMISSIBLE OPERANDS OF THE MACRO ARE:
.*
.*       INDEX (DSNAME1,<READ|WRITE>),(DSNAME2,<READ|WRITE>),DSNAME3,..
.*
.*       ANY NUMBER OF INDEXES MAY BE CODED ON EACH MACRO.
.*       EACH INDEX MAY BE A NAME BY ITSELF OR A NAME AND AN ACCESS
.*       CODE IN BRACKETS.
.*       THE VALID ACCESS CODES ARE 'READ' FOR READ ONLY AND 'WRITE'
.*       FOR WRITE ONLY ACCESS. THE DEFAULT IS NO ACCESS AT ALL OR
.*       ALL ACCESS DEPENDING ON WETHER THE REMOTE IS DEFINED AS
.*       AN 'ONLY', (INCLUSIVE), OR 'EXCLUDE', (EXCLUSIVE) TYPE OF
.*       DEVICE.
.*
         GBLB  &REMOTE             SET BY REMOTE MACRO
         GBLB  &TSUSER             SET BY TSUSER MACRO
         LCLA  &WORKA,&WORKA2      WORK COUNTERS AND LOOP CONTROL
         LCLA  &FLAG               WORK AREA TO BUILD FLAG VALUE
.**********************************************************************
.*       CHECK THAT A 'TSUSER' OR 'REMOTE' MACRO HAS BEEN ENTERED
.**********************************************************************
         AIF   (&TSUSER EQ 0 AND &REMOTE EQ 0).ERR1 NO PREVIOUS
.**********************************************************************
.*       LOOP THROUGH ALL THE SUPPLIED DSNAMES AND PROCESS
.**********************************************************************
&WORKA   SETA  0                   INITIALISE LOOP CONTROL
.DSNLOOP ANOP
         AIF   (&WORKA EQ N'&SYSLIST).ALLDONE FINISH IF ALL OPNDS DONE
&WORKA   SETA  &WORKA+1            INCREMENT LOOP COUNTER
&FLAG    SETA  64                  SET FLAG FOR INDEX TYPE ENTRY
.*********************************************************************
.*       TEST FOR AN ACCESS OPERAND
.*********************************************************************
         AIF   (N'&SYSLIST(&WORKA) EQ 1).NOACC NO ACCESS CODE ENTERED
         AIF   ('&SYSLIST(&WORKA,2)' NE 'READ' AND '&SYSLIST(&WORKA,2)'X
                NE 'WRITE').ERR2 ACCESS CODE IS NOT VALID
         AIF   ('&SYSLIST(&WORKA,2)' NE 'READ').NOTREAD ACCESS NOT READ
&FLAG    SETA  &FLAG+32            SET FLAG FOR READ ACCESS
.NOTREAD ANOP
         AIF   ('&SYSLIST(&WORKA,2)' NE 'WRITE').NOWRITE NOR WRITE
&FLAG    SETA  &FLAG+16            SET FLAG FOR WRITE ACCESS
.NOWRITE ANOP
&WORKA2  SETA  K'&SYSLIST(&WORKA,1) GET LENGTH OF THE DSN
         DC    AL1(&FLAG)          THIS IS AN INDEX ENTRY
         DC    AL2(0)              SPARE
         DC    AL1(&WORKA2)        LENGTH OF THE INDEX
         DC    C'&SYSLIST(&WORKA,1)' INDEX NAME
         AGO   .DSNLOOP            AND PROCESS ALL OPERANDS
.NOACC   ANOP
&WORKA2  SETA  K'&SYSLIST(&WORKA)  GET LENGTH OF THE DSN
         DC    AL1(&FLAG)          THIS IS AN INDEX ENTRY
         DC    AL2(0)              SPARE
         DC    AL1(&WORKA2)        LENGTH OF THE INDEX
         DC    C'&SYSLIST(&WORKA)' INDEX NAME
         AGO   .DSNLOOP            AND PROCESS ALL OPERANDS
.ALLDONE ANOP
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID ACCESS CODE FOR DSN=&SYSLIST(&WORKA,1),..&SYX
               SLIST(&WORKA,2). DEFINITION ABORTED'
         MEXIT
.ERR1    ANOP
         MNOTE 16,'''TSUSER'' OR ''REMOTE'' MACRO IS MISSING - DEFINITIX
               ON ABORTED'
         MEND
