         MACRO
         VOL
.*
.*       THIS MACRO IS USED WITH THE MONSANTO OPEN MOD FOR SECURITY
.*       CHECKING ON ACCESS TO DISKS. IT'S PURPOSE IS TO DEFINE
.*       THE NAMES OF DISKS WHOSE ACCESS BY A PARTICULAR TSO USER AD/OR
.*       REMOTE IS TO BE CONTROLLED. EACH MACRO MAY DEFINE A LIST OF
.*       DISKS OR JUST ONE. THE DEFINITIONS MUST ALWAYS BE PRECEEDED
.*       BY A 'TSUSER' OR 'REMOTE' DEFINITION.
.*
.*       THE PERMISSIBLE OPERANDS OF THE MACRO ARE:
.*
.*       VOL  (DISK1,<READ|WRITE>),(DISK2,<READ|WRITE>),DISK3,..
.*
.*       ANY NUMBER OF DISKS MAY BE CODED ON EACH MACRO.
.*       EACH DISK MAY BE A NAME BY ITSELF OR A NAME AND AN ACCESS
.*       CODE IN BRACKETS.
.*       THE VALID ACCESS CODES ARE 'READ' FOR READ ONLY AND 'WRITE'
.*       FOR WRITE ONLY ACCESS. THE DEFAULT IS NO ACCESS AT ALL OR
.*       ALL ACCESS DEPENDING ON WETHER THE REMOTE IS DEFINED AS
.*       AN 'ONLY', (INCLUSIVE), OR 'EXCLUDE', (EXCLUSIVE) TYPE OF
.*       DEVICE.
.*
         GBLB  &REMOTE             SET BY REMOTE MACRO
         GBLB  &TSUSER             SET BY TSUSER MACRO
         LCLA  &WORKA,&WORKA2      WORK COUNTERS AND LOOP CONTROL
         LCLA  &FLAG               WORK AREA TO BUILD FLAG VALUE
.**********************************************************************
.*       CHECK THAT A 'TSUSER' OR 'REMOTE' MACRO HAS BEEN ENTERED
.**********************************************************************
         AIF   (&TSUSER EQ 0 AND &REMOTE EQ 0).ERR1 NO PREVIOUS
.**********************************************************************
.*       LOOP THROUGH ALL THE SUPPLIED DSNAMES AND PROCESS
.**********************************************************************
&WORKA   SETA  0                   INITIALISE LOOP CONTROL
.DSKLOOP ANOP
         AIF   (&WORKA EQ N'&SYSLIST).ALLDONE FINISH IF ALL OPNDS DONE
&WORKA   SETA  &WORKA+1            INCREMENT LOOP COUNTER
&FLAG    SETA  128+64              SET FLAG FOR VOL TYPE ENTRY
.*********************************************************************
.*       TEST FOR AN ACCESS OPERAND
.*********************************************************************
         AIF   (N'&SYSLIST(&WORKA) EQ 1).NOACC NO ACCESS CODE ENTERED
         AIF   ('&SYSLIST(&WORKA,2)' NE 'READ' AND '&SYSLIST(&WORKA,2)'X
                NE 'WRITE').ERR2 ACCESS CODE IS NOT VALID
         AIF   ('&SYSLIST(&WORKA,2)' NE 'READ').NOTREAD ACCESS NOT READ
&FLAG    SETA  &FLAG+32            SET FLAG FOR READ ACCESS
.NOTREAD ANOP
         AIF   ('&SYSLIST(&WORKA,2)' NE 'WRITE').NOWRITE NOR WRITE
&FLAG    SETA  &FLAG+16            SET FLAG FOR WRITE ACCESS
.NOWRITE ANOP
         AIF   (K'&SYSLIST(&WORKA,1) NE 6).ERR3 LENGTH NOT 6
         DC    AL1(&FLAG)          THIS IS A VOLUME ENTRY
         DC    AL2(0)              SPARE
         DC    AL1(6)              LENGTH OF VOL SER NO
         DC    CL6'&SYSLIST(&WORKA,1)' DISK VOLUME SERIAL NUMBER
         AGO   .DSKLOOP            AND PROCESS ALL OPERANDS
.NOACC   ANOP
         AIF   (K'&SYSLIST(&WORKA) NE 6).ERR3 LENGTH NOT 6
         DC    AL1(&FLAG)          THIS IS A VOLUME ENTRY
         DC    AL2(0)              SPARE
         DC    AL1(6)              LENGTH OF VOL SER NO
         DC    CL6'&SYSLIST(&WORKA)' DISK VOLUME SERIAL NUMBER
         AGO   .DSKLOOP            AND PROCESS ALL OPERANDS
.ALLDONE ANOP
         MEXIT
.ERR3    ANOP
         MNOTE 16,'INVALID VOL SER NO, LENGTH NOT = TO 6 - DEFINITION AX
               BORTED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID ACCESS CODE FOR VOL=&SYSLIST(&WORKA,1),..&SYX
               SLIST(&WORKA,2). DEFINITION ABORTED'
         MEXIT
.ERR1    ANOP
         MNOTE 16,'''TSUSER'' OR ''REMOTE'' MACRO IS MISSING - DEFINITIX
               ON ABORTED'
         MEND
