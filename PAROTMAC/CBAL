         MACRO                                                            031160
&NAME    CBAL  &R,&RTN,&LOOP=YES,&NESTED=NO                               031170
         GBLC  &CBAL                                                      031180
         LCLC  &T                                                         031190
.*                                                                        031200
.*       ESTABLISH LINKAGE TO SUB-ROUTINE                                 031210
.*                                                                        031220
.*       &R    - RETURN REGISTER ( MAY BE OMITTED )                       031230
.*       &RTN  - ROUTINE NAME                                             031240
.*       &LOOP=YES - SET GLOBAL SYMOLIC '&CBAL' TO 'Y' TO TRIGGER         031250
.*                   DETECTION OF SUBROUTINES WITH POTENTIAL LOOPS        031260
.*       &LOOP=NO  - DO NOT SET GLOBAL SYMBOL '&CBAL'                     031270
.*       &NESTED=NO - USE NORMAL CBAL REGISTER PRESERVATION TECHNIQUES    031280
.*       &NESTED=YES - PRESERVE BASE REGISTER WITHIN CODE                 031290
.*                                                                        031300
.*       CBAL TECHNOLOGY PRECLUDES ISSUING A CBAL TO A ROUTINE THAT IN    031310
.*        TURN CBALS TO THE ORIGINAL ROUTINE, IF THE ROUTINES ARE IN      031320
.*        DIFFERENT CSECTS, BECAUSE WHEN THE ORIGINAL ROUTINE RETURNS     031330
.*        CONTROL TO THE ROUTINE THAT INVOKED IT, THE WROND BASE          031340
.*        REGISTER WILL BE RESTORED.  SUCH A SEQUENCE IS UNUSUAL - IT     031350
.*        HAS TAKEN ME THREE YEARS TO ENCOUNTER IT - BUT IT CAN HAPPEN.   031360
.*        (VIZ 'WRITEP', A WRITE ROUTINE THAT CAN INVOKE A HEADING RTN    031370
.*        WHICH CAN INVOKE A PROCESSING ROUTINE WHICH CAN IN TUNR         031380
.*        INVOKE WRITEP).  TO CATER FOR THIS EVENTUALITY, 'NESTED=YES'    031390
.*        CAUSES THE BASE REGISTER TO BE PRESERVED WITHIN THE MACRO       031400
.*        EXPANSION, AND THUS ENSURING THE CORRECT VALUE IS RESTORED.     031410
.*                                                                        031420
         AIF   ('&R' EQ '').X00                                           031430
         AIF   ('&LOOP' EQ 'NO').X00                                      031440
&CBAL    SETC  'Y'                                                        031450
.X00     ANOP                                                             031460
&T       SETC  T'&RTN                                                     031470
         AIF   (T'&RTN EQ 'U').A01 BR IF ROUTINE UNDEFINED                031480
         AIF   (T'&RTN EQ 'T').A01 BRANCH IF ROUTINE EXTRN                031490
         AIF   (T'&RTN EQ '$').A01 BRANCH IF ROUTINE WXTRN                031500
.*                                                                        031510
.*       ASSUME THAT THE ROUTINE EXISTS IN THIS CSECT                     031520
.*                                                                        031530
         AIF   ('&R' NE '').A00                                           031540
&NAME    B     &RTN                GO TO REQUESTED ROUTINE                031550
         MEXIT                                                            031560
.A00     ANOP                                                             031570
&NAME    BAL   &R,&RTN             PERFORM REQUESTED ROUTINE              031580
         MEXIT                                                            031590
.A01     ANOP                                                             031600
.*                                                                        031610
.*       ROUTINE IS IN ANOTHER CSECT SO ESTABLISH LINKAGE VIA CCB         031620
.*                                                                        031630
&NAME    LA    R9,$&RTN            POINT AT RTN DEFINITION IN CCB         031640
         AIF   ('&R' NE '').A03                                           031650
         B     $CBALINK            BR TO LINKAGE ROUTINE                  031660
         AGO   .A04                                                       031670
.A03     ANOP                                                             031680
         AIF   ('&NESTED' NE 'YES').N01                                   031690
         CNOP  0,4                 ENSURE ALIGNMENT                       031700
         ST    R12,*+16            PRESERVE BASE REGISTER IN S/A          031710
.N01     ANOP                                                             031720
         BAL   R4,$CBALINK         BR TO LINKAGE ROUTINE                  031730
.A04     ANOP                                                             031740
         AIF   ('&SYSECT' EQ 'PARROT').A02                                031750
         AIF   ('&SYSECT' EQ 'BUDGIE3').A02                               031760
         AIF   ('&NESTED' NE 'YES').N02                                   031770
         L     R12,8(R4)           RESTORE BASE REGISTER FROM S/A         031780
         B     *+8                 BRANCH OVER REGISTER SAVE AREA         031790
         DS    F                   SAVE AREA FOR BASE REGISTER            031800
         AGO   .A02                                                       031810
.N02     ANOP                                                             031820
         L     R12,$&RTN.+4        RESTORE SUB-CSECT BASE REGISTER        031830
.A02     ANOP                                                             031840
         MEND                                                             031850
