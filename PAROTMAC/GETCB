         MACRO                                                            031870
&NAME    GETCB &CB,&BACK,&START,&CBLABL=,&GR13=NO                         031880
.*                                                                        031890
.*       ACQUIRE CORE FOR AND CHAIN A NEW C/B                             031900
.*       ACQUIRE AND INITIALISE CONTROL BLOCK PREFIX                      031910
.*       REGISTER 2 MUST CONTAIN THE REQUIRED LENGTH                      031920
.*       C/B ADDRESSABILITY MUST BE ESTABLISHED VIA R8                    031930
.*       &BACK=BACK - REQUESTS THAT BACK CHAINING BE ESTABLISHED          031940
.*       &BACK=NOCHAIN - NO CHAINING OTHER THAN C/B PREFIX                031950
.*       &START - IDENTIFES THE DUMMY FIRST ELEMENT AT THE START OF CHN   031960
.*       &CBLABL - NAME OF FIELD CONTAINING LABEL TO GO IN 'CBPRLABL'     031970
.*       &GR13=NO|YES - FLAG PREFIX FOR GR13 RESOLUTION                   031980
.*                                                                        031990
&NAME    LA    R2,CBPRLGTH(,R2)    INCREMENT BY LENGTH OF C/B PREFIX      032000
         CBAL  R4,GETMAIN          ACQUIRE CORE FOR CONTROL BLOCK         032010
         USING CBPREFIX,R1         ESTABLISH PREFIX ADDRESSABILITY        032020
         MVC   CBPRNAME,=CL4'&CB'  SET C/B IDENTIFIER IN PREFIX           032030
         STH   R2,CBPRLEN          PRESERVE C/B LENGTH IN PREFIX          032040
         ZAP   CBPRCCNO,$CCSEQNO   SET C/C SEQ NO IN PREFIX               032050
         L     R15,$CBPLAST        GET ADDRESS OF PREVIOUS C/B PREFIX     032060
         ST    R1,CBPRCBPR-CBPREFIX(R15) POINT PREV PREFIX AT THIS ONE    032070
         ST    R1,$CBPLAST         SAVE THIS PREFIX ADDRESS AS THE LAST   032080
         MVI   CBPRSW,0            INITIALISE SWITCH BYTE                 032090
         AIF   ('&CBLABL' EQ '').A02                                      032100
         MVC   CBPRLABL,&CBLABL    SET LABEL IN PREFIX FOR LATER          032110
.A02     AIF   ('&GR13' EQ 'NO').A03                                      032120
         OI    CBPRSW,MCBPGR13     FLAG THAT PCB REFERENCE IS REQUIRED    032130
.A03     ANOP                                                             032140
         DROP  R1                  DROP PREFIX ADDRESSABILITY             032150
         LA    R1,CBPRLGTH(,R1)    POINT R1 PAST PREFIX AT C/B            032160
         LR    R8,R1               POINT BASE REGISTER AT C/B             032170
         AIF   ('&BACK' EQ 'NOCHAIN').A01                                 032180
         L     R1,$PTR&CB          RETRIEVE CURRENT C/B POINTER           032190
         ST    R8,$PTR&CB          ESTABLISH NEW C/B AS CURRENT           032200
         MVC   &CB&CB-&CB.NTRY+1(3,R1),$PTR&CB+1 POINT LAST AT THIS ONE   032210
         MVI   &CB.ID,FLAG&CB      IDENTIFY CURRENT C/B                   032220
.*       ZAP   &CB.CCNO,SYSIN+IOPSRCNT(4) ADD C/C SEQ NO TO C/B           032230
         AIF   ('&BACK' NE 'BACK').A01                                    032240
         ST    R1,&CB.L&CB         POINT NEW C/B AT LAST                  032250
         ST    R8,$LPTR&CB         PRESERVE ADDRESS OF LAST IN CHAIN      032260
         AIF   ('&START' EQ '').A01                                       032270
         CLM   R8,7,&START.+1      IS THIS C/B THE 1ST IN THE CHAIN       032280
         BNE   *+10                BR IF NOT 1ST IN CHAIN                 032290
         XC    &CB.L&CB.(4),&CB.L&CB ZEROISE BACK POINTER IF 1ST C/B      032300
.A01     ANOP                                                             032310
         MEND                                                             032320
