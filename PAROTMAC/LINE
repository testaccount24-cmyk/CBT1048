         MACRO                                                            032880
&NAME    LINE  &MSG,&DISP,&SKIPCHR,&PRINT=,&S=,&ELEVEL=NO,&LA=NO          032890
.*                                                                        032900
.*       SET UP MESSAGE FOR SYSPRINT AND PRINT IT                         032910
.*                                                                        032920
.*       &MSG  - NAME OF MESSAGE                                          032930
.*       &DISP - DISPLACEMENT IN LINE: DEFAULT=5                          032940
.*       &SKIPCHR - CARRIAGE CONTROL CHARACTER: DEFAULT-NOT ALTERED       032950
.*       &PRINT - NAME OF PRINT ROUTINE: DEFAULT=PRINT                    032960
.*       &S=   - NO. OF LINES TO SPACE: DEFAULT-NOT ALTERED               032970
.*       &ELEVEL= - 'YES' = E-LEVEL ERROR: DEFAULT=NO                     032980
.*       &LA=  - 'YES' = MESSAGE NOT DIRECTLY ADDRESSABLE SO 'LA'         032990
.*                                                                        033000
         LCLC  &D,&L                                                      033010
         LCLA  &LEN,&DD                                                   033020
&L       SETC  'L'''                                                      033030
&D       SETC  '5'                                                        033040
         AIF   ('&DISP' EQ '').A01                                        033050
&D       SETC  '&DISP'                                                    033060
.A01     ANOP                                                             033070
&DD      SETA  5                                                          033080
         AIF   ('&DISP' EQ '').A05                                        033090
&DD      SETA  &DISP                                                      033100
.A05     ANOP                                                             033110
&LEN     SETA  L'&MSG                                                     033120
&LEN     SETA  &LEN+&DD                                                   033130
         AIF   (&LEN LE 133).A06                                          033140
         MNOTE 4,'WARNING - MESSAGE IS TOO LONG FOR THE LINE'             033150
.A06     ANOP                                                             033160
         AIF   ('&LA' EQ 'NO').A01A                                       033170
         L     R1,=A(&MSG)         GET ADDRESS OF MESSAGE                 033180
         MVC   &D.(&L&MSG,R5),0(R1) MOVE MESSAGE TO LINE                  033190
         AGO   .A01B                                                      033200
.A01A    ANOP                                                             033210
&NAME    MVC   &D.(&L&MSG,R5),&MSG MOVE MESSAGE TO LINE                   033220
.A01B    AIF   ('&SKIPCHR' EQ '').A02                                     033230
         MVI   0(R5),&SKIPCHR      ESTABLISH SKIP CHARCETER               033240
.A02     AIF   ('&S' EQ '').A02A                                          033250
         MVI   0(R5),SPCE&S.AFT    ESTABLISH SKIP CHARACTER               033260
.A02A    AIF   ('&PRINT' EQ 'NO').A03                                     033270
         CBAL  R4,PRINT            PRINT LINE                             033280
.A03     ANOP                                                             033290
         AIF   ('&ELEVEL' NE 'YES').A04                                   033300
         OI    SWERROR,MERRCCER    FLAG THAT 'E' LEVEL ERROR OCCURRED     033310
.A04     ANOP                                                             033320
         MEND                                                             033330
