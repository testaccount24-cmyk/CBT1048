         MACRO                                                            024630
&NAME    IOM   &SUBTYPE,&PARM1,&PARM2,&PARM3,&PARM4,                   *  024640
               &TYPE=,&LOOP=,&RTN=,&MSGID=,&AUTOPEN=                      024650
.*                                                                        024660
.*       EQUATES,MAXDD                                                    024670
.*                                                                        024680
.*       DCBSW,DDNAME,DCBNAME,INPUT ,MANDAT                               024690
.*       DCBSW,DDNAME,DCBNAME,OUTPUT,MANDAT                               024700
.*       DCBSW,DDNAME,DCBNAME,PRINT,MANDAT                                024710
.*                                                                        024720
.*       PUT,DCBSW,(REG),PRINT,LNECNT,RTN=HEADING                         024730
.*                                                                        024740
.*       PUTM,DCBSW,(REG)                                                 024750
.*                                                                        024760
.*       GET,DCBSW,(REG)                                                  024770
.*                                                                        024780
.*       GETM,DCBSW,(REG)                                                 024790
.*                                                                        024800
.*       OPEN,TYPE=DEF                                                    024810
.*       CLOSE,TYPE=DEF                                                   024820
.*       TIOTSCAN,TYPE=DEF                                                024830
.*                                                                        024840
.*       OPEN,DCBSW,NOREOPEN,LOOP=1,RTN=OPEN                              024850
.*       OPEN,DCBSW,NOREOPEN,LOOP=N,RTN=NAME                              024860
.*                                                                        024870
.*       CLOSE,DCBSW,LOOP=1,RTN=CLOSE                                     024880
.*       CLOSE,DCBSW,LOOP=N,RTN=NAME                                      024890
.*                                                                        024900
.*       TIOTSCAN,DCBSW,LOOP=1,RTN=TIOTSCAN                               024910
.*       TIOTSCAN,DCBSW,LOOP=N,RTN=NAME                                   024920
.*                                                                        024930
.*       SUMMARY,DCBSW,(REG),PRESENT,RTN=NAME,LOOP=N                      024940
.*                                                                        024950
.*       AUDIT,DCBSW,(REG),ENDJOB,RTN=NAME,LOOP=N                         024960
.*                                                                        024970
.*       EOF,DCBSW,BUFF-ADDR                                              024980
.*                                                                        024990
.*       BLKSZRTN,TYPE=DEF                                                025000
.*                                                                        025010
.*       IOERROR,(REG),PRINTRTN,EOJRTN,TYPE=DEF                           025020
.*                                                                        025030
.*       ISRANDOM,DCBSW-ADDR,KEY-ADDR                                     025040
.*                                                                        025050
         GBLC  &OPEN                                                      025060
         LCLA  &MAXDD                                                     025070
         LCLC  &BIT0,&BIT1,&DCB                                           025080
         LCLC  &BIT8                                                      025090
         LCLC  &B,&D                                                      025100
         LCLA  &BA,&DA                                                    025110
         LCLC  &LABEL,&RTNE,&REG                                          025120
         LCLC  &EOJRTNE                                                   025130
         LCLC  &LNECNT                                                    025140
.*                                                                        025150
.*                                                                        025160
.*       DETERMINE IF AUTOPEN WAS SPECIFIED                               025170
.*                                                                        025180
         AIF   ('&AUTOPEN' EQ '').A00                                     025190
&OPEN    SETC  'Y'                                                        025200
         AIF   ('&AUTOPEN' EQ 'YES').A00                                  025210
&OPEN    SETC  'N'                                                        025220
         AIF   ('&AUTOPEN' EQ 'NO').A00                                   025230
         MNOTE 1,'UNKNOWN ''AUTOPEN'' SPECIFICATION - TREATED AS NO'      025240
.A00     ANOP                                                             025250
         AIF   ('&SUBTYPE' NE 'EQUATES').B00                              025260
IOSWMAND EQU   128 0               DDNAME MANDATORY                       025270
IOSWINPT EQU   64 0                INPUT DATA SET (DEFAULT)               025280
IOSWOUTP EQU   191 0               OUTPUT DATA SET            (NI ONLY)   025290
IOSWPRES EQU   32 0                DDNAME PRESENT                         025300
IOSWOPEN EQU   16 0                DCB NOW OPEN                           025310
IOSWCLOS EQU   239 0               CLOSE DCB                  (NI ONLY)   025320
IOSWEOF  EQU   8 0                 EOF DETECTED                           025330
IOSWIOER EQU   4 0                 I/O ERROR DETECTED                     025340
IOSWOPNH EQU   2 0                 DCB HAS BEEN OPENED                    025350
IOSWEOP  EQU   1 0                 PRINT DATA SET WITH EOP ROUTINE        025360
IOPSSW   EQU   0                   DISPLACEMENT OF SWITCH BYTE            025370
IOPSEOD  EQU   1                   DISPLACEMENT OF EOD INDICATOR          025380
IOPSISI  EQU   2                   DISPLACEMENT OF IS STATUS FLAG         025390
IOPSDCB  EQU   4                   DISPLACEMENT OF DCB ADDRESS            025400
IOPSDDN  EQU   8                   DISPLACEMENT OF DDNAME                 025410
IOPSRECA EQU   16                  DISPLACEMENT OF RECORD ADDRESS         025420
IOPSRCNT EQU   20                  DISPLACEMENT OF RECORD COUNT           025430
IOPSR4SA EQU   24                  DISPLACEMENT OF R4 SAVE AREA           025440
IOPSLGTH EQU   28                  LENGTH OF DCBSW C/B                    025450
         AIF   ('&PARM1' EQ '').A02                                       025460
         AIF   (T'&PARM1 NE 'N').A01                                      025470
&MAXDD   SETA  &PARM1                                                     025480
IOMMAXDD EQU   &MAXDD 0            MAX NO. OF DD STATEMENTS               025490
         AGO   .A02                                                       025500
.A01     MNOTE 1,'MAX NO OF DD STATEMENTS IS NON-NUMERIC'                 025510
.A02     AIF   ('&PARM2' NE '').A03                                       025520
         AIF   ('&PARM3' NE '').A03                                       025530
         AIF   ('&PARM4' NE '').A03                                       025540
         AIF   ('&TYPE' NE '').A03                                        025550
         AIF   ('&LOOP' NE '').A03                                        025560
         AIF   ('&RTN' NE '').A03                                         025570
         AGO   .A04                                                       025580
.A03     MNOTE *,'THE SUPERFLUOUS OPERANDS CODED WERE IGNORED'            025590
.A04     MEXIT                                                            025600
.B00     AIF   ('&SUBTYPE' NE 'DCBSW').C00                                025610
         AIF   ('&PARM1' NE '').B01                                       025620
         MNOTE 16,'NO DDNAME SUPPLIED FOR DCBSW OPERATION'                025630
         MEXIT                                                            025640
.B01     AIF   ('&PARM2' NE '').B02                                       025650
&DCB     SETC  '&PARM1'                                                   025660
         MNOTE *,'NO DCB NAME SUPPLIED - DDNAME ASSUMED'                  025670
         AGO   .B03                                                       025680
.B02     ANOP                                                             025690
&DCB     SETC  '&PARM2'                                                   025700
.B03     ANOP                                                             025710
&BIT0    SETC  '0'                                                        025720
&BIT1    SETC  '1'                                                        025730
&BIT8    SETC  '0'                                                        025740
         AIF   ('&PARM3' EQ '').B04                                       025750
         AIF   ('&PARM3' EQ 'INPUT').B05                                  025760
         AIF   ('&PARM3' EQ 'PRINT').B20                                  025770
         AIF   ('&PARM3' NE 'OUTPUT').B10                                 025780
&BIT1    SETC  '0'                                                        025790
         AGO   .B05                                                       025800
.B20     ANOP                                                             025810
&BIT8    SETC  '1'                                                        025820
&BIT1    SETC  '0'                                                        025830
         AGO   .B05                                                       025840
.B04     MNOTE *,'I/O TYPE NOT SPECIFIED - INPUT ASSUMED'                 025850
.B05     AIF   ('&PARM4' EQ '').B06                                       025860
         AIF   ('&PARM4' NE 'MANDAT').B10                                 025870
&BIT0    SETC  '1'                                                        025880
         AGO   .B07                                                       025890
.B06     MNOTE *,'DD STATEMENT IS OPTIONAL'                               025900
.B07     ANOP                                                             025910
&B       SETC  '00000'                                                    025920
         CNOP  0,4 0               ENSURE CORRECT ALIGNMENT               025930
&B       SETC  '&BIT0&BIT1.00000&BIT8'                                    025940
&NAME    DC    B'&B',C'X  ',V(&DCB),CL8'&PARM1',A(0),PL4'0',A(0)          025950
         AIF   ('&TYPE' NE '').A03                                        025960
         AIF   ('&LOOP' NE '').A03                                        025970
         AIF   ('&RTN' NE '').A03                                         025980
         MEXIT                                                            025990
.B10     MNOTE 16,'UNKNOWN POSITIONAL PARAMETER'                          026000
         MEXIT                                                            026010
.*                                                                        026020
.*                                                                        026030
.*       IOM   PUT,DCBSW (,(REG) (,PRINT) (,LNECNT) (,RTN=HEADING)        026040
.*                                                                        026050
.*       IOM   GET,DCBSW (,(REG))                                         026060
.*                                                                        026070
.C00     AIF   ('&SUBTYPE' EQ 'PUT').C01                                  026080
         AIF   ('&SUBTYPE' NE 'GET').D00                                  026090
.C01     AIF   ('&PARM1' NE '').C021                                      026100
.C011    MNOTE 16,'MANDATORY DCBSW ADDRESS NOT SUPPLIED'                  026110
         MEXIT                                                            026120
.C021    ANOP                                                             026130
&NAME    TM    &PARM1.+IOPSSW,IOSWPRES 0IS DD STATEMENT PRESENT           026140
         BZ    IOMC&SYSNDX 0       BYPASS I/O IF NO DD STATEMENT          026150
         AIF   ('&OPEN' EQ 'N').C100                                      026160
         TM    &PARM1.+IOPSSW,IOSWOPEN 0 TEST IF DCB OPEN                 026170
         BO    IOME&SYSNDX 0       BRANCH IF OPEN                         026180
         ST    R4,&PARM1.+IOPSR4SA 0 PRESERVE R4 RETUR  ADDRESS           026190
         LA    R2,&PARM1           POINT R2 AT DCBSW                      026200
         CBAL  R4,OPEN,LOOP=NO     OPEN DATA SET                          026210
         L     R4,&PARM1.+IOPSR4SA 0 RESTORE SAVED R4 VALUE               026220
IOME&SYSNDX EQU * 0                BYPASS OPEN PROCESSING                 026230
.C100    ANOP                                                             026240
         AIF   ('&SUBTYPE' NE 'GET').C023                                 026250
         TM    &PARM1.+IOPSSW,IOSWEOF 0TEST FOR END OF FILE ALREADY       026260
         BO    IOMC&SYSNDX 0       BR IF EOF ALREADY REACHED              026270
.C023    ANOP                                                             026280
         AIF   ('&SUBTYPE' NE 'PUTM').C024                                026290
         LR    R0,&REG             POINT R0 AT DATA                       026300
         L     R1,&PARM1.+IOPSDCB  POINT R1 AT DCB ADDRESS                026310
         L     R15,48(0,R1)        LOAD PUT ROUTINE ADDRESS               026320
         BALR  R14,R15             LINK TO PUT ROUTINE                    026330
         AGO   .C05                                                       026340
.C024    ANOP                                                             026350
         AIF   ('&SUBTYPE' NE 'GETM').C025                                026360
         L     R1,&PARM1.+IOPSDCB  POINT R1 AT DCB ADDRESS                026370
         LR    R0,&REG             POINT R0 AT DESTINATION                026380
         L     R15,48(0,R1)        LOAD GET ROUTINE ADDRESS               026390
         BALR  R14,R15             LINK TO GET ROUTINE                    026400
         AGO   .C05                                                       026410
.C025    ANOP                                                             026420
         AIF   ('&PARM3' NE 'PRINT' OR '&RTN' NE'').C022                  026430
IOMD&SYSNDX EQU * 0                LOOP BACK FOR EOP POOCESSING           026440
.C022    ANOP                                                             026450
         L     R1,&PARM1.+IOPSDCB  POINT R1 AT DCB ADDRESS                026460
         L     R15,48(,R1) 0       GET I/O RTN ADDRESS                    026470
         BALR  R14,R15             LINK TO I/O ROUTINE                    026480
         ST    R1,&PARM1.+IOPSRECA STORE BUFFER ADDRESS                   026490
         AIF   ('&PARM2' EQ '').C05                                       026500
         AIF   ('&PARM2'(1,1) EQ '(').C04                                 026510
.C031    MNOTE 16,'INVALID REGISTER SPECIFICATION FOR &SUBTYPE'           026520
         MEXIT                                                            026530
.C04     LR    &PARM2(1),R1 0      SET BUFFER ADDRESS IN WORK REGISTER    026540
.C05     AP    &PARM1.+IOPSRCNT(4),=P'1' 0 INCREMENT RECORD COUNT         026550
         AIF   ('&PARM3' NE 'PRINT').C9                                   026560
&LNECNT  SETC  '&PARM4'                                                   026570
         AIF   ('&PARM4' NE '').C6                                        026580
         MNOTE *,'NO LINE COUNT SUPPLIED - 60 ASSUMED'                    026590
         CP    &PARM1.+IOPSRCNT(4),=P'60' TEST FOR END OF PAGE            026600
         AGO   .C63                                                       026610
.C6      ANOP                                                             026620
         AIF   (T'&PARM4 EQ 'N').C62                                      026630
         CP    &PARM1.+IOPSRCNT(4),&PARM4 TEST FOR END OF PAGE            026640
         AGO   .C63                                                       026650
.C62     CP    &PARM1.+IOPSRCNT(4),=P'&LNECNT' 0TEST FOR END OF PAGE      026660
.C63     BL    IOMC&SYSNDX 0       BRANCH IF NOT                          026670
         ZAP   &PARM1.+IOPSRCNT(4),=P'0' 0 ZEROISE LINE COUNT             026680
         AIF   ('&RTN' NE '').C7                                          026690
         MNOTE *,'NO EOP ROUTINE SUPPLIED - PAGE SKIP FORCED'             026700
         MVI   0(R1),PAGESKIP 0    ESTABLISH PAGE SKIP                    026710
         B     IOMD&SYSNDX 0       SKIP TO NEW PAGE                       026720
IOMC&SYSNDX EQU * 0                END OF ROUTINE                         026730
.C8      AIF   ('&TYPE' NE '').A03                                        026740
         AIF   ('&LOOP' NE '').A03                                        026750
         MEXIT                                                            026760
.C7      ST    R4,IOMD&SYSNDX 0    PRESERVE RETURN ADDRESS                026770
         CBAL  R4,&RTN,LOOP=NO     PERFORM HEADING ROUTINE                026780
         L     R4,IOMD&SYSNDX 0    RESTORE RETURN ADDRESS                 026790
         B     IOMC&SYSNDX 0       BRANCH OVER SAVE AREA                  026800
IOMD&SYSNDX DS F 0                 SAVE AREA FOR R4                       026810
IOMC&SYSNDX EQU * 0                END OF ROUTINE                         026820
         AGO   .C8                                                        026830
.C9      ANOP                                                             026840
IOMC&SYSNDX EQU * 0                 END OF ROUTINE                        026850
         AIF   ('&PARM3' NE '').A03                                       026860
         AIF   ('&PARM4' NE '').A03                                       026870
         AIF   ('&TYPE' NE '').A03                                        026880
         AIF   ('&LOOP' NE '').A03                                        026890
         AIF   ('&RTN' NE '').A03                                         026900
         MEXIT                                                            026910
.D00     AIF   ('&SUBTYPE' NE 'OPEN').E00                                 026920
         AIF   ('&TYPE' EQ '').D04                                        026930
         AIF   ('&TYPE' EQ 'DEF').D02                                     026940
.D01     MNOTE 16,'UNKNOWN TYPE OPERAND'                                  026950
         MEXIT                                                            026960
.*                                                                        026970
.*                                                                        026980
.*       IOM   OPEN,DCBSW-ADDR, (,NOREOPEN) (,LOOP=N) (,RTN=AAA)          026990
.*                                                                        027000
.D02     ANOP                                                             027010
&LABEL   SETC  'OPEN'                                                     027020
         AIF   ('&NAME' EQ '').D03                                        027030
&LABEL   SETC  '&NAME'                                                    027040
.D03     ANOP                                                             027050
&LABEL   DS    0H 0                ENTRY TO OPEN SUBROUTINE               027060
         L     R1,IOPSDCB(,R2) 0   GET DCB ADDRESS                        027070
         TM    IOPSSW(R2),IOSWPRES TEST IF DD STATEMENT PRESENT           027080
         BNO   0(R4) 0             RETURN IF NOT                          027090
         TM    IOPSSW(R2),IOSWOPEN 0 TEST IF DATA SET OPEN                027100
         BO    0(R4) 0             RETURN IF DATA STE OPEN                027110
         CNOP  0,4 0               ENSURE CORRECT ALIGNMENT               027120
         ST    R1,*+8 0            STORE DCB ADDRESS                      027130
         BAL   R1,*+8              LOAD REG 1 WITH LIST ADDRESS           027140
         DC    A(0) 0              DCB ADDRESS                            027150
         TM    IOPSSW(R2),IOSWINPT 0 TEST IF INPUT                        027160
         BO    IOMD&SYSNDX 0       BRANCH IF YES                          027170
         MVI   0(R1),143 0         SET OPTION BYTE FOR INPUT              027180
         B     *+8 0               GO TO SVC                              027190
IOMD&SYSNDX MVI 0(R1),128          SET OPTION BYTE                        027200
         SVC   19 0                ISSUE OPEN SVC                         027210
         OI IOPSSW(R2),IOSWOPEN+IOSWOPNH INDICATE DATA SET OPEN           027220
         MVI   IOPSEOD(R2),C'X'    RESET EOD INDICATOR IF NECESSARY       027230
         BR    R4 0                RETURN TO MAIN-LINE                    027240
.D031    AIF   ('&PARM1' NE '').A03                                       027250
         AIF   ('&PARM2' NE '').A03                                       027260
         AIF   ('&PARM3' NE '').A03                                       027270
         AIF   ('&PARM4' NE '').A03                                       027280
         AIF   ('&LOOP' NE '').A03                                        027290
         AIF   ('&RTN' NE '').A03                                         027300
         MEXIT                                                            027310
.D04     AIF   ('&PARM1' EQ '').C011                                      027320
         AIF   ('&LOOP' EQ '').D05                                        027330
&NAME    LA    R10,&LOOP 0         ESTABLISH LOOP                         027340
.D05     ANOP                                                             027350
&LABEL   SETC  '&NAME'                                                    027360
         AIF   ('&LOOP' EQ '').D06                                        027370
&LABEL   SETC  ' '                                                        027380
.D06     ANOP                                                             027390
&LABEL   LA    R2,&PARM1 0         POINT AT DCBSW                         027400
         AIF   ('&RTN' EQ '').D07                                         027410
&RTNE    SETC  '&RTN'                                                     027420
         AGO   .D09                                                       027430
.D07     AIF   ('&SUBTYPE' EQ 'CLOSE').D08                                027440
&RTNE    SETC  'OPEN'                                                     027450
         AIF   ('&SUBTYPE' EQ 'OPEN').D09                                 027460
&RTNE    SETC  'TIOTSCAN'                                                 027470
         AGO   .D09                                                       027480
.D08     ANOP                                                             027490
&RTNE    SETC  'CLOSE'                                                    027500
.D09     ANOP                                                             027510
         AIF   ('&PARM2' NE 'NOREOPEN').D081                              027520
         TM    IOPSSW(R2),IOSWOPNH TEST IF OPENED ALREADY                 027530
         BO    IOME&SYSNDX         BR IF ALREADY OPENED ONCE              027540
.D081    ANOP                                                             027550
IOMD&SYSNDX CBAL R4,&RTNE 0        PERFORM SUB-ROUTINE                    027560
IOME&SYSNDX    EQU *               CONTINUE PROCESSING                    027570
         AIF   ('&LOOP' EQ '').D10                                        027580
         LA    R2,IOPSLGTH(,R2) 0  INCREMENT TO NEXT DCBSW                027590
         BCT   R10,IOMD&SYSNDX 0   LOOP BACK TO PROCESS NEXT              027600
.D10     AIF   ('&PARM2' NE '').A03                                       027610
         AIF   ('&PARM3' NE '').A03                                       027620
         AIF   ('&PARM4' NE '').A03                                       027630
         MEXIT                                                            027640
.E00     AIF   ('&SUBTYPE' NE 'CLOSE').F00                                027650
         AIF   ('&TYPE' EQ 'DEF').E01                                     027660
         AIF   ('&TYPE' EQ '').D04                                        027670
         AGO   .D01                                                       027680
.E01     ANOP                                                             027690
         AIF   ('&SUBTYPE' EQ 'OPEN').D09                                 027700
&RTNE    SETC  'TIOTSCAN'                                                 027710
&LABEL   SETC  'CLOSE'                                                    027720
         AIF   ('&NAME' EQ '').E02                                        027730
&LABEL   SETC  '&NAME'                                                    027740
.E02     ANOP                                                             027750
&LABEL   DS    0H 0                ENTRY TO CLOSE SUBROUTINE              027760
         TM    IOPSSW(R2),IOSWOPEN TEST IF DATA SET OPEN                  027770
         BZ    0(R4) 0             RETURN IF NOT                          027780
         L     R1,IOPSDCB(R2) 0    GET DCB ADDRESS                        027790
         CNOP  0,4 0               ENSURE CORRECT ALIGNMENT               027800
         ST    R1,*+8 0            STORE DCB ADDRESS                      027810
         BAL   R1,*+8              POINT R1 AT PARM LIST                  027820
         DC    A(0) 0              DCB ADDRESS                            027830
         MVI   0(R1),128 0         SET OPTION BYTE                        027840
         SVC   20 0                ISSUE CLOSE SVC                        027850
         L     R1,IOPSDCB(R2)      GET DCB ADDRESS                        027860
       FREEPOOL (R1)               FREE ACQUIRED BUFFER POOL              027870
         NI    IOPSSW(R2),IOSWCLOS INDICATE CLOSED                        027880
         BR    R4 0                RETURN TO MAIN-LINE                    027890
         AIF   ('&PARM1' NE '').A03                                       027900
         AIF   ('&PARM2' NE '').A03                                       027910
         AIF   ('&PARM3' NE '').A03                                       027920
         AIF   ('&PARM4' NE '').A03                                       027930
         AIF   ('&LOOP' NE '').A03                                        027940
         AIF   ('&RTN' NE '').A03                                         027950
         MEXIT                                                            027960
.*                                                                        027970
.*                                                                        027980
.*       IOM   EOF,DCBSW-NAME (,DUMMY BUFFER ADDRESS)                     027990
.*                                                                        028000
.F00     AIF   ('&SUBTYPE' NE 'EOF').G00                                  028010
         AIF   ('&PARM1' EQ '').C011                                      028020
&NAME    OI    &PARM1.+IOPSSW,IOSWEOF INDICATE EOF                        028030
         MVI   &PARM1.+IOPSEOD,C'Y' SET CHARACTER FOR EOF INDICATION      028040
         AIF   ('&PARM2' EQ '').F01                                       028050
         L     R1,&PARM2           POINT R1 AT DUMMY BUFFER               028060
         ST    R1,&PARM1.+IOPSRECA 0PRESERVE DUMMY BUFFER ADDRESS         028070
         AGO   .F02                                                       028080
.F01     XC    &PARM1.+IOPSRECA(4),&PARM1.+IOPSRECA NULLIFY RECORD ADD    028090
.F02     ANOP                                                             028100
         AGO   .A02                                                       028110
.G00     AIF   ('&SUBTYPE' NE 'TIOTSCAN').H00                             028120
         AIF   ('&TYPE' EQ 'DEF').G01                                     028130
         AIF   ('&TYPE' NE '').D01                                        028140
         AGO   .D04                                                       028150
.G01     ANOP                                                             028160
&LABEL   SETC  'TIOTSCAN'                                                 028170
         AIF   ('&NAME' EQ '').G02                                        028180
&LABEL   SETC  '&NAME'                                                    028190
.G02     ANOP                                                             028200
&LABEL   DS    0H 0                ENTRY TO TIOT SCAN ROUTINE             028210
*                                                                         028220
*        EXTRACT TIOT ADDRESS EVERY TIME LEST TIOT ALTERED BY             028230
*         DYNAMICALLY ALLOCATED DD STATEMENTS.                            028240
*                                                                         028250
         EXTRACT IOMATIOT,'S',FIELDS=TIOT  OBTAIN ADDRESS OF TOOT         028260
         B     IOMG&SYSNDX         BRANCH AROUND SAVE AREAS               028270
IOMATIOT DS    F 0                 ADDRESS OF TIOT                        028280
IOMTIOT1 DS    F 0                 SAVE AREA FOR R3                       028290
IOMTIOT2 DS    F 0                 SAVE AREA FOR R10                      028300
IOMG&SYSNDX EQU * 0                SUBSEQUENT PROCESSING                  028310
         ST    R3,IOMTIOT1 0       PRESERVE CONTENTS                      028320
         ST    R10,IOMTIOT2 0       OF R3 AND R10                         028330
         L     R3,IOMATIOT 0       GET ADDRESS OF TIOT                    028340
         LA    R3,24(,R3) 0        POINT AT FIRST DD ENTRY                028350
IOMH&SYSNDX CLC 0(4,R3),=F'0' 0     TEST FOR END OF TIOT                  028360
         BE    IOMI&SYSNDX.+4      RETURN IF YES                          028370
         CLC   4(8,R3),IOPSDDN(R2) TEST FOR DDNAME MATCH                  028380
         BE    IOMI&SYSNDX 0       BRANCH IF YES                          028390
         SR    R10,R10 0           ZEROISE WORK REGISTER                  028400
         IC    R10,0(R3) 0         INSERT ENTRY LENGTH                    028410
         AR    R3,R10 0            INCREMENT TO NEXT ENTRY                028420
         B     IOMH&SYSNDX 0       LOOP TO PROCESS NEXT ENTRY             028430
IOMI&SYSNDX OI IOPSSW(R2),IOSWPRES INDICATE DD STATEMENT PRESENT          028440
         L     R3,IOMTIOT1 0       RETRIEVE CONTENTS                      028450
         L     R10,IOMTIOT2 0       OF R3 AND R10                         028460
         BR    R4 0                RETURN TO MAIN LINE                    028470
         AGO   .D031                                                      028480
.H00     AIF   ('&SUBTYPE' NE 'BLKSZRTN').I00                             028490
         AIF   ('&TYPE' EQ 'DEF').H02                                     028500
         AIF   ('&TYPE' NE '').D01                                        028510
         MNOTE 16,'INCOMPLETE BLKSIZE REQUEST'                            028520
         MEXIT                                                            028530
.H02     ANOP                                                             028540
&LABEL   SETC  'BLKSZRTN'                                                 028550
         AIF   ('&NAME' EQ '').H01                                        028560
&LABEL   SETC  '&NAME'                                                    028570
.H01     ANOP                                                             028580
&LABEL   DS    0H 0                SUPPLY DEFAULT IF NO BLOCK SIZE        028590
         USING IHADCB,R1 0         ESTABLISH DCB ADDRESSABILITY           028600
         CLC   DCBBLKSI,=F'0' 0    TEST IF BLKSIZE SUPPLIED               028610
         BNE   0(R14) 0            RETURN TO OPEN IF YES                  028620
         MVC   DCBBLKSI,DCBLRECL 0 MOVE LRECL TO BLKSIZE                  028630
         BR    R14 0               RETURN TO OS OPEN ROUTINE              028640
         DROP  R1 0                DROP DCB ADDRESSABILITY                028650
         AGO   .D031                                                      028660
.*                                                                        028670
.*                                                                        028680
.*       IOM   SUMMARY,DCBSW-ADDR,(REG),RTN= (,MSGID=)                    028690
.*                                                                        028700
.I00     AIF   ('&SUBTYPE' NE 'SUMMARY').J00                              028710
         AIF   ('&PARM1' EQ '').C011                                      028720
         AIF   ('&PARM2' NE '').I01                                       028730
.I001    MNOTE 16,'MANDATORY REGISTER SPECIFICATION IS MISSING'           028740
         MEXIT                                                            028750
.I01     AIF   ('&PARM2'(1,1) NE '(').C031                                028760
         AIF   ('&RTN' NE '').I02                                         028770
.I011    MNOTE 16,'MANDATORY RTN NAME IS MISSING'                         028780
         MEXIT                                                            028790
.I02     ANOP                                                             028800
&NAME    DS    0H 0                PRINT SUMMARY OF D/S ACTIVITY          028810
         LA    R2,&PARM1 0         GET DCBSW ADDRESS                      028820
         AIF   ('&LOOP' EQ '').I03                                        028830
         LA    R10,&LOOP 0         ESTABLISH LOOP                         028840
IOMI&SYSNDX EQU * 0                RETURN POINT FOR LOOP                  028850
.I03     AIF   ('&PARM3' EQ '').I05                                       028860
         AIF   ('&PARM3' NE 'PRESENT').B10                                028870
         TM    IOPSSW(R2),IOSWPRES TEST IF DD STATEMENT PRESENT           028880
         BZ    IOMJ&SYSNDX 0       BYPASS PROCESSING IF NOT               028890
.I05     ANOP                                                             028900
&REG     SETC  '&PARM2(1)'                                                028910
         TM    IOPSSW(R2),IOSWEOP  TEST IF PRINT DATA SET                 028920
         BO    IOMJ&SYSNDX         BYPASS PROCESSING IF YES               028930
&BA      SETA  0                                                          028940
         AIF   ('&MSGID' EQ '').I10                                       028950
&BA      SETA  K'&MSGID                                                   028960
         AIF   ('&MSGID'(1,1) NE '''').I30                                028970
&BA      SETA  &BA-2                                                      028980
.I30     ANOP                                                             028990
         AIF   ('&MSGID'(1,1) NE '''').I20                                029000
         MVC   5(&BA,&REG),=C&MSGID MOVE MESSAGE ID TO LINE               029010
         AGO   .I21                                                       029020
.I20     ANOP                                                             029030
         MVC   5(&BA,&REG),=C'&MSGID' ADD MESSAGE ID TO LINE              029040
.I21     ANOP                                                             029050
&BA      SETA  &BA+2                                                      029060
.I10     ANOP                                                             029070
&DA      SETA  &BA+5                                                      029080
         MVC   &DA.(18,&REG),=C'NUMBER OF RECORDS ' ADD MESSSAGE TO BUF   029090
&DA      SETA  &BA+23                                                     029100
         MVC   &DA.(11,&REG),=C'READ FROM  ' ADD FUTHER MESSAGE           029110
         TM    IOPSSW(R2),IOSWINPT TEST IF INPUT DATA SET                 029120
         BO    *+10                BR IF INPUT                            029130
         MVC   &DA.(11,&REG),=C'WRITTEN TO ' SHOW WHITTEN TO NOT READ     029140
&DA      SETA  &BA+34                                                     029150
         MVC   &DA.(8,&REG),IOPSDDN(R2) ADD DNAME TO MESSAGE              029160
&DA      SETA  &BA+42                                                     029170
         MVC   &DA.(2,&REG),=C' =' ADD EQUALS SIGN TO MESSGGE             029180
&DA      SETA  &BA+44                                                     029190
         MVC   &DA.(10,&REG),=X'40206B2020206B202120' EDIT MASK TO MSG    029200
         ED    &DA.(10,&REG),IOPSRCNT(R2) EDIT THE RECORD COUNT           029210
         CBAL  R4,&RTN 0           PERFORM PRINT ROUTINE                  029220
IOMJ&SYSNDX EQU * 0                BYPASS BRANCH ADDRESS                  029230
.I06     AIF   ('&LOOP' EQ '').I07                                        029240
         LA    R2,IOPSLGTH(,R2) 0  INCREMENT TO NEXT DCBSW                029250
         BCT   R10,IOMI&SYSNDX 0   LOOP TO PROCESS NEXT                   029260
.I07     AIF   ('&PARM4' NE '').A03                                       029270
         AIF   ('&TYPE' NE '').A03                                        029280
         MEXIT                                                            029290
.*                                                                        029300
.*                                                                        029310
.*       IOM   AUDIT,DCBSW-ADDR,(REG),EOJRTN,RTN= (,MSGID=)               029320
.*                                                                        029330
.J00     AIF   ('&SUBTYPE' NE 'AUDIT').K00                                029340
         AIF   ('&PARM1' EQ '').C011                                      029350
         AIF   ('&PARM3' NE '').J001                                      029360
         MNOTE *,'NO END-OF-JOB ROUTINE ADDRESS SUPPLIED'                 029370
         MNOTE *,'PROGRAM WILL ABEND WITH 0C1 IF MANDATORY STATEMENT MI$  029380
               SSING'                                                     029390
.J001    ANOP                                                             029400
         AIF   ('&PARM2' EQ '').I001                                      029410
         AIF   ('&PARM2'(1,1) NE '(').C031                                029420
         AIF   ('&RTN' EQ '').I011                                        029430
&NAME    DS    0H 0                REPORT ON PRESENCE OF DD STATEMENTS    029440
         LA    R2,&PARM1           GET DCBSW ADDRESS                      029450
         AIF   ('&LOOP' EQ '').J01                                        029460
         LA    R10,&LOOP 0         ESTABLISH LOOP                         029470
IOMJ&SYSNDX EQU * 0                RETURN POINT FOR LOOP                  029480
.J01     ANOP                                                             029490
&REG     SETC  '&PARM2(1)'                                                029500
&BA      SETA  0                                                          029510
         AIF   ('&MSGID' EQ '').J10                                       029520
&BA      SETA  K'&MSGID                                                   029530
         AIF   ('&MSGID'(1,1) NE '''').J30                                029540
&BA      SETA  &BA-2                                                      029550
.J30     ANOP                                                             029560
         AIF   ('&MSGID'(1,1) NE '''').J20                                029570
         MVC   5(&BA,&REG),=C&MSGID MOVE MESSAGE ID TO LINE               029580
         AGO   .J21                                                       029590
.J20     ANOP                                                             029600
         MVC   5(&BA,&REG),=C'&MSGID' ADD MESSAGE ID TO LINE              029610
.J21     ANOP                                                             029620
&BA      SETA  &BA+2                                                      029630
.J10     ANOP                                                             029640
&DA      SETA  &BA+5                                                      029650
         MVC   &DA.(35,&REG),=C'MANDATORY DATA SET          PRESENT'      029660
&DA      SETA  &BA+24                                                     029670
         MVC   &DA.(8,&REG),IOPSDDN(R2) ADD DDNAME TO MESSAGE             029680
         TM    IOPSSW(R2),IOSWMAND TEST IF MANDATORY                      029690
         BO    *+10 0              BRANCH IF YES                          029700
&DA      SETA  &BA+5                                                      029710
         MVC   &DA.(9,&REG),=C' OPTIONAL' SHOW DATA SET IS OPTIONAL       029720
         TM    IOPSSW(R2),IOSWPRES TEST IF PRESENT                        029730
         BO    *+10 0              BRANCH IF YES                          029740
&DA      SETA  &BA+33                                                     029750
         MVC   &DA.(7,&REG),=C'MISSING' SHOW DATA SET IS MISSING          029760
         CBAL  R4,&RTN 0           PERFORM PRINT ROUTINE                  029770
         TM    IOPSSW(R2),IOSWMAND TEST IF MANDATORY                      029780
         BZ    IOMK&SYSNDX 0       BRANCH IF NOT                          029790
         TM    IOPSSW(R2),IOSWPRES TEST IF PRESENT                        029800
         BO    IOMK&SYSNDX 0       BRANCH IF YES                          029810
         OI    IOML&SYSNDX.+1,X'F0' 0    SET TO BRANCH AT END OF JOB      029820
         MVC   IOMM&SYSNDX.+27(8),IOPSDDN(R2) ADD DD NAME TO WRO          029830
IOMM&SYSNDX WTO 'MANDATORY DATA SET ******** MISSING'                     029840
IOMK&SYSNDX EQU * 0                                                       029850
         AIF   ('&LOOP' EQ '').J02                                        029860
         LA    R2,IOPSLGTH(,R2) 0  POINT AT NEXT DCBSW                    029870
         BCT   R10,IOMJ&SYSNDX 0   LOOP BACK TO PROCESS                   029880
.J02     ANOP                                                             029890
&RTNE    SETC  '*+2'                                                      029900
         AIF   ('&PARM3' NE '').J03                                       029910
         MNOTE *,'NO END-OF-JOB ADDRESS SUPPLIED'                         029920
         AGO   .J04                                                       029930
.J03     ANOP                                                             029940
&RTNE    SETC  '&PARM3'                                                   029950
.J04     ANOP                                                             029960
IOML&SYSNDX BC 0,&RTNE 0           GO TO END-OF-JOB IF MAND MISSING       029970
         AGO   .I07                                                       029980
.*                                                                        029990
.*                                                                        030000
.*       IOM   IOERROR,(REG),PRINTRTN,EOJRTN,MSGID=TEXT' (,TYPE=DEF)      030010
.*                                                                        030020
.K00     AIF   ('&SUBTYPE' NE 'IOERROR').L00                              030030
         AIF   ('&TYPE' EQ 'DEF').K01                                     030040
         AIF   ('&TYPE' NE '').D01                                        030050
         MNOTE 16,'INVALID I/O ERROR REQUEST'                             030060
         MEXIT                                                            030070
.K01     ANOP                                                             030080
&RTNE    SETC  'PRINT'                                                    030090
&EOJRTNE SETC  'PRINTCDE'                                                 030100
&REG     SETC  'R5'                                                       030110
&LABEL   SETC  'IOERROR'                                                  030120
         AIF   ('&NAME' EQ '').K02                                        030130
&LABEL   SETC  '&NAME'                                                    030140
.K02     AIF   ('&PARM3' NE '').K03                                       030150
         MNOTE *,'NO END-OF-JOB ADDRESS SUPPLIED - ''PRINTCDE'' ASSUMD'   030160
         AGO   .K04                                                       030170
.K03     ANOP                                                             030180
&EOJRTNE SETC  '&PARM3'                                                   030190
.K04     AIF   ('&PARM2' NE '').K05                                       030200
         MNOTE *,'NO PRINT ROUTINE ADDRESS - ''PRINT'' ASSUMED'           030210
         AGO   .K06                                                       030220
.K05     ANOP                                                             030230
&RTNE    SETC  '&PARM2'                                                   030240
.K06     AIF   ('&PARM1' EQ '').K07                                       030250
         AIF   ('&PARM1'(1,1) NE '(').C031                                030260
&REG     SETC  '&PARM1(1)'                                                030270
         AGO   .K08                                                       030280
.K07     MNOTE *,'NO REGISTER SPECIFIED - R5 ASSUMED'                     030290
.K08     ANOP                                                             030300
&LABEL   DS    0H 0                ENSURE ALIGNMENT                       030310
        SYNADAF ACSMETH=QSAM 0     CALL ANALYSIS ROUTINE                  030320
         MVC   IOMM&SYSNDX,50(R1) ADD RESULT OF ANALYSIS                  030330
        SYNADRLS , 0               RELEASE OS BUFFER                      030340
         WTO   MF=(E,IOML&SYSNDX) 0TELL OPERATOR OF ERROR                 030350
         MVI   0(&REG),SPCE2AFT 0  ESTABLISH DOUBLE BLANK LINES           030360
         CBAL  R4,&RTNE,LOOP=NO     AND WRITE THEM                        030370
         MVC   5(98,&REG),IOMM&SYSNDX 0 MOVE IN ERROR MESSAGE             030380
         CBAL  R4,&RTNE,LOOP=NO    WRITE ERROR MESSAGE                    030390
        SUBOUT RETURN=&EOJRTNE 0   TERMINATE JOB                          030400
         SPACE 1                                                          030410
IOML&SYSNDX DS 0F                                                         030420
         DC    AL2(IOMN&SYSNDX.-*),AL2(0)                                 030430
         AIF   ('&MSGID' EQ '').K09                                       030440
         DC    C&MSGID             MESSAGE IDENTIFIER                     030450
.K09     DC    C'I/O ERROR - '                                            030460
IOMM&SYSNDX DC CL86' '             TEXT OF ERROR ANALYSIS                 030470
IOMN&SYSNDX EQU *                  END OF I/O ERROR MESSAGE               030480
         MEXIT                                                            030490
.L00     AIF   ('&SUBTYPE' NE 'PUTM').M00                                 030500
.L01     ANOP                                                             030510
         AIF   ('&PARM1' EQ '').C011                                      030520
&REG     SETC  'R1'                                                       030530
         AIF   ('&PARM2' EQ '').C021                                      030540
         AIF   ('&PARM2'(1,1) NE '(').C031                                030550
&REG     SETC  '&PARM2(1)'                                                030560
         AGO   .C021                                                      030570
.M00     AIF   ('&SUBTYPE' EQ 'GETM').L01                                 030580
.N00     AIF   ('&SUBTYPE' NE 'ISRANDOM').O00                             030590
.*                                                                        030600
.*                                                                        030610
.*       IOM   ISRANDOM,DCBSW-ADDR,KEY-ADDR                               030620
.*                                                                        030630
&NAME    LA    R2,&PARM1           POINT R2 AT DCBSW                      030640
         MVI   IOPSISI(R2),C'Y'    ASSUME RECORD NOT FOUND                030650
         TM    IOPSSW(R2),IOSWPRES TEST IF DD STATEMNT PRESENT            030660
         BZ    IOMN&SYSNDX         BYPASS I/O IF DD STAT MISSING          030670
         AIF   ('&OPEN' EQ 'N').N01                                       030680
         TM    IOPSSW(R2),IOSWOPEN TEST IF DD STATEMENT OPEN              030690
         BO    IOON&SYSNDX         BR IF OPEN                             030700
         CBAL  R4,ISOPEN           OPEN DCB                               030710
.N01     ANOP                                                             030720
IOON&SYSNDX LA R1,IOPSDCB(R2)      GET DCB ADDRESS                        030730
         LA    R15,&PARM2          GET KEY ADDRESS                        030740
         READ  DECB&SYSNDX,K,(R1),'S','S',(R15) READ IS RANDOMLY          030750
         CHECK DECB&SYSNDX,DSORG=IS WAIT FOR COMPLETION OF I/O            030760
.*       WAIT  ECB=DECB&SYSNDX     WAIT FR COMPLETION OF I/O              030770
         TM    DECB&SYSNDX.+24,DCBEXNKY TEST IF RECORD NOT FOUND          030780
         BO    IOMN&SYSNDX         EXIT TEST IF RECORD NOT FOUND          030790
.*       MVI   IOPSISI(R2),C'W'    ASSUME SOME SORT OF ERROR OCCURRED     030800
.*       CLC   DECB&SYSNDX.+24(2),=X'0000' TEST IF ERROR CONDITION        030810
.*       BNE   IOMN&SYSNDX         EXIT IF ANY ERROR CONDITION            030820
         MVI   IOPSISI(R2),C'Y'    ASSUME RECORD IS DELETED               030830
         L     R1,IOPSDCB(R2)      GET ADDRESS OF DCB                     030840
         TM    DCBOPTCD-IHADCB(R1),DCBOPTL DOES DELETE FLAG EXIST         030850
         BZ    *+16                BR IF NO DELETE FLAG                   030860
         L     R1,DECB&SYSNDX.+16  GET RECORD ADDRESS                     030870
         CLI   0(R1),X'FF'         TEST IF RECORD IS DELETED              030880
         BE    IOMN&SYSNDX         EXIT IF RECORD DELETED                 030890
         MVI   IOPSISI(R2),C'X'    SHOW RECORD FOUND                      030900
         L     R1,DECB&SYSNDX.+16  GET RECORD ADDRESS                     030910
IOMN&SYSNDX EQU *                  CONTINUE PROCESSING                    030920
         MEXIT                                                            030930
.O00     ANOP                                                             030940
         MNOTE 16,'UNKNOWN MACRO SUBTYPE'                                 030950
         MEND                                                             030960
