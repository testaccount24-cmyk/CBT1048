         MACRO                                                            014270
&NAME    PARROTT &DSECT=YES,&CSECT=NO                                     014280
         AIF   ('&DSECT' EQ 'YES' AND '&CSECT' EQ 'YES').ERR01            014290
         AIF   ('&DSECT' EQ 'NO' AND '&CSECT' EQ 'NO').ERR01              014300
         AIF   ('&CSECT' NE 'YES').A01                                    014310
         SCSECT T                  GENERATE CSECT IDENTIFIER              014320
         DROP  R12                 DROP STANDARD ADDRESSABILITY           014330
         AGO   .A02                                                       014340
.A01     ANOP                                                             014350
PARROTT  DSECT                                                            014360
         VERSID T                  GENERATE CSECT IDENTIFIER              014370
.A02     ANOP                                                             014380
         USING PARROTT,R9          ESTABLISH ADDRESSABILITY               014390
         USING CCBNTRY,R13         ESTABLISH CCB ADDRESSABILITY           014400
         TITLE 'PARROTT - CONVERSION TABLE FOR TEST TYPES'                014410
*        THIS TABLE IS USED TO CONVERT RELATIONSHIP TESTS SPECIFIED       014420
*         IN SELECTION CRITERIA BUCKETS ( IN GR02 AND GR04 CARDS )        014430
*         INTO THE EQUIVALENT INTERNAL NOTATION ( WHICH IS A VALUE        014440
*         USED FOR TRIGGERING INDEXED BRANCHES ).                         014450
         SPACE 5                                                          014460
TESTTAB  EQU   *                   TABLE OF EBCDIC AND FLAG TEST TYPES    014470
         DC    CL2'EQ',AL1(EQ)     EQ - EQUAL TO                          014480
         DC    CL2'NE',AL1(NE)     NE - NOT EQUAL TO                      014490
         DC    CL2'LT',AL1(LT)     LT - LESS THAN                         014500
         DC    CL2'LE',AL1(LE)     LE - LESS THAN OR EQUAL TO             014510
         DC    CL2'GT',AL1(GT)     GT - GREATER THAN                      014520
         DC    CL2'GE',AL1(GE)     GE - GREATER THAN OR EQUAL TO          014530
         DC    CL2'ON',AL1(ON)     ON - SPECIFIED INDICATORS ON           014540
         DC    CL2'OF',AL1(OFF)    OF - SPECIFIED INDICATORS OFF          014550
         DC    X'FFFFFF'           END OF TABLE                           014560
TESTTABL EQU   3                   LENGTH OF EACH TABLE ELEMENT           014570
TESTTABN EQU   (*-TESTTAB)/TESTTABL NO OF ELEMENTS                        014580
         TITLE 'PARROTT - CONVERSION TABLE OF FIELD TYPES'                014590
*        THIS TABLE IS USED TO CONVERT THE FIELD TYPE SPECIFICATION       014600
*         ( IN GR02, GR03, GR04 CONTROL CARDS ) INTO THE EQUIVALENT       014610
*         INTERNAL VALUE ( WHICH IS A VALUE USED TO TRIGGER INDEXED       014620
*         BRANCHES ).                                                     014630
         SPACE 5                                                          014640
TYPETAB  EQU   *                   CONVERSION TABLE OF FIELD TYPES        014650
         DC    CL1'CHAR',AL1(CHAR) CHAR - CHARACTER                       014660
         DC    CL1'   ',AL1(CHAR)  BL   - DEFAULT TO CHARACTER            014670
         DC    CL1'NUM',AL1(NUM)   NUM  - DISPLAY NUMERIC                 014680
         DC    CL1'PD',AL1(PD)     PD   - PACKED DECIMAL                  014690
         DC    CL1'XHEX',AL1(HEX)  HEX  - HEXADECIMAL STRING              014700
         DC    CL1'BIT',AL1(BIT)   BIT  - BIT STRING                      014710
         DC    CL1'MASK',AL1(MASK) MASK - PARTIAL MASK                    014720
         DC    CL1'1-C',AL1(BIN)   BIN  - BINARY VALUE ( COMP-1 )         014730
         DC    CL1'FLOAT',AL1(FLOAT) FLOAT - FLOATING POINT VALUE         014740
         DC    CL1'STR',AL1(STRING) STR - CHAR STRING TO OCCUR IN FLD     014750
         DC    CL1'IND',AL1(IND)        - INDICATOR                       014760
         DC    CL1'GUESS',AL1(FREEFORM) - FREE FORMAT DISPLAY NUMERIC     014770
         DC    CL1'UNUSED',AL1(12)      - RESERVED FOR FUTURE USE         014780
         DC    X'FFFF'             END OF TABLE                           014790
TYPETABL EQU   2                   LENGTH OF TABLE ELEMENTS               014800
TYPETABN EQU   (*-TYPETAB)/TYPETABL NO OF ELEMENTS IN TABLE               014810
         TITLE 'PARROTT - TRANSLATE TABLES FOR FIELD VALIDATION'          014820
*        THESE TABLES ARE USED BY VARIOUS ROUTINES TO DETERMINE THE       014830
*         VALIDITY OF, OR FORMAT OF, FIELDS.  THE MAIN TABLE MERELY       014840
*         TRANSLATES THE VARIOUS TYPES OF CHARACTERS INTO SPECIFIC        014850
*         CODES, WHILE THE SMALL TABLES ARE USED TO TRANSLATE AND TEST    014860
*         THE RESULT OF THE FIRST TRANSLATION TO DETERMINE THE TYPES      014870
*         OF CHARACTERS THAT THE SOURCE FIELD CONTAINED.  THUS, MANY      014880
*         TESTS CAN BE MADE WITH ONE 'TR' AND ONE 'TRT', OBVIATING        014890
*         THE NEED FOR ITERATIVE MULTIPLE TESTS.                          014900
         SPACE 1                                                          014910
*        NOTE THAT THIS CSECT IS UNIQUE, IN THAT ITS ADDRESSABILITY IS    014920
*         ESTABLISHED VIA R9 AND NOT R12.  THIS IS SO THAT OTHER          014930
*         CSECTS, MAY EXECUTE THE INSTRUCTIONS HEREIN WHILE               014940
*         MAINTAINING THEIR OWN ADDRESSABILTIY.                           014950
         EJECT                                                            014960
*        THE SPECIFIC TRANLATION CODES ASSIGNED ARE:-                     014970
*              X'01' : 0 - 9                                              014980
*              X'02' : A - R                                              014990
*              X'03' : S - Z                                              015000
*              X'04' : SPECIAL CHARACTERS                                 015010
*              X'05' : INVALID MULTI-PUNCHES                              015020
*              X'06' : 12-0 & 11-0 MULTI-PUNCHES                          015030
*              X'07' : BLANK                                              015040
         SPACE 1                                                          015050
TTABLE   DS    0CL256              TRANSLATION TABLE FOR TESTS            015060
         DC    64X'05'                                                    015070
         DC    X'07'               BLANK                                  015080
         DC    9X'05'                                                     015090
         DC    7X'04'              SPECIAL:- H.<(+|&                      015100
         DC    9X'05'                                                     015110
         DC    8X'04'              SPECIAL:- !$*);^-/                     015120
         DC    9X'05'                                                     015130
         DC    5X'04'              SPECIAL:- ,%_>?                        015140
         DC    10X'05'                                                    015150
         DC    6X'04'              SPECIAL:- :#@.="                       015160
         DC    64X'05'                                                    015170
         DC    X'06'               12-0 PUNCH                             015180
         DC    9X'02'              ALPHA:-   ABCDEFGHI                    015190
         DC    6X'05'                                                     015200
         DC    X'06'               11-0 PUNCH                             015210
         DC    9X'02'              ALPHA:-   JKLMNOPQR                    015220
         DC    8X'05'                                                     015230
         DC    8X'03'              ALPHA:-   STUVWXYZ                     015240
         DC    6X'05'                                                     015250
         DC    10X'01'             NUMERIC:- 0123456789                   015260
         DC    6X'05'                                                     015270
         SPACE 1                                                          015280
TTABLEL1 DC    X'0101000001010100' ALLOW A-Z, BLANK                       015290
TTABLEL2 DC    X'0100000001010100' ALLOW A-Z, 0-9, BLANK                  015300
TTABLEL3 DC    X'0100000000010100' ALLOW A-Z, 0-9, BLANK, SPECIAL         015310
TTABLENN DC    X'0100010101010101' ALLOW 0-9                              015320
TTABLEN  DC    X'0100010101010100' ALLOW 0-9, BLANK                       015330
TTABLES  DC    X'0100000101010000' ALLOW 0-9, A-R, BLANK, 12-0, 11-0      015340
TTABLEBL DC    X'0000000000000001' LOCATE FIRST BLANK                     015350
TTABLENO DC    X'0001000000000000' LOCATE FIRST 0-9                       015360
TTABLENS DC    X'0001010000000100' LOCATE FIRST 0-9, A-R, 11-0, 12-0      015370
         EJECT                                                            015380
***********************************************************************   015390
*        EXECUTABLE INSTRUCTIONS                                      *   015400
***********************************************************************   015410
         SPACE 1                                                          015420
TESTTR   TR    $TSTAREA(0),TTABLE  TRANSLATE AGAINST MAIN TABLE           015430
TESTTRL1 TRT   $TSTAREA(0),TTABLEL1 ACCEPT A-Z AND BLANK                  015440
TESTTRL2 TRT   $TSTAREA(0),TTABLEL2 ACCEPT A-Z, BLANK, 0-9                015450
TESTTRL3 TRT   $TSTAREA(0),TTABLEL3 ACCEPT A-Z,0-9, BLANK, SPECIAL CHA    015460
TESTTRNN TRT   $TSTAREA(0),TTABLENN ACCEPT 0-9                            015470
TESTTRN  TRT   $TSTAREA(0),TTABLEN  ACCEPT 0-9, BLANK                     015480
TESTTRS  TRT   $TSTAREA(0),TTABLES  ACCEPT 0-9, BLANK, A-R, 11-0, 12-0    015490
TESTTRNS TRT   $TSTAREA(0),TTABLENS LOCATE 1ST 0-9, A-R, 11-0, 12-0       015500
TESTTRNO TRT   $TSTAREA(0),TTABLENO LOCATE 1ST 0-9                        015510
TESTTRBL TRT   0(0,R14),TTABLEBL    LOCATE 1ST BLANK                      015520
         SPACE 5                                                          015530
***********************************************************************   015540
*        EXECUTABLE INSTRUCTIONS FOR PARTIAL KEY MATCHING ROUTINE     *   015550
***********************************************************************   015560
         SPACE 1                                                          015570
E1TRTEST TR    $TSTAREA+50(0),E1TABLE2 TRANSLATE PARTIAL KEY BL TO '00'   015580
E1NCMAST NC    $TSTAREA(0),$TSTAREA+50 TURN OFF UNTESTED BYTES            015590
E1NCTEST NC    $TSTAREA+50(0),0(R14) RESTORE PARTIAL KEY WITH BL='00'     015600
E1TRTAB  TR    $TSTAREA+50(0),E1TABLE1 TRANSLATE 'MBB' CHAR TO BLANK      015610
E1COMPAR CLC   $TSTAREA(0),$TSTAREA+50 COMPARE NON-BLANK PARTS OF KEYS    015620
E1MVCKEY MVC   $TSTAREA+50(0),0(R14) MOVE PARTIAL KEY TO WORK AREA        015630
E1MVCFLD MVC   $TSTAREA(0),0(R6)   MOVE FIELD UNDER TEST TO TEST AREA     015640
         SPACE 5                                                          015650
***********************************************************************   015660
*        EXECUTABLE INSTRUCTIONS FOR HEX STRING VALIDATION/CONVERSION *   015670
***********************************************************************   015680
         SPACE 1                                                          015690
HEXVALID TRT   0(0,R14),HEXTAB1    ENSURE ONLY VALID HEX CHARACTERS       015700
HEXCONV  TR    0(0,R14),HEXTAB2    CONVERT 'A' TO X'FA' ETC               015710
         TITLE 'PARROTT - TABLES FOR PARTIAL KEY MATCHING'                015720
*        THESE TABLES ARE USED BR THE PARTIAL KEY MATCHING ROUTINE.       015730
         SPACE 5                                                          015740
***********************************************************************   015750
*        ALTER 'MUST BE BLANK' CHARACTER TO BLANK                     *   015760
***********************************************************************   015770
         SPACE 1                                                          015780
E1TABLE1 DS    0CL256                                                     015790
         DC    X'000102030405060708090A0B0C0D0E0F'                        015800
         DC    X'101112131415161718191A1B1C1D1E1F'                        015810
         DC    X'202122232425262728292A2B2C2D2E2F'                        015820
         DC    X'303132333435363738393A3B3C3D3E3F'                        015830
         DC    X'404142434445464748494A4B4C4D4E4F'                        015840
         DC    X'505152535455565758595A5B5C5D5E5F'                        015850
         DC    X'606162636465666768696A6B6C6D6E6F'                        015860
         DC    X'707172737475767778797A7B7C7D7E7F'                        015870
         DC    X'808182838485868788898A8B8C8D8E8F'                        015880
         DC    X'909192939495969798999A9B9C9D9E9F'                        015890
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                        015900
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                        015910
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                        015920
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                        015930
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                        015940
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                        015950
         SPACE 5                                                          015960
***********************************************************************   015970
*        NULLIFY BLANKS                                               *   015980
***********************************************************************   015990
         SPACE 1                                                          016000
E1TABLE2 DS    0CL256                                                     016010
         DC    64X'FF'             EVERYTHING EXCEPT ...                  016020
         DC    X'00'                ... BLANK ...                         016030
         DC    191X'FF'             ... IS HIGH VALUES                    016040
         TITLE 'PARROTT - TRANSLATE TABLE FOR SCANNING FOR CHARACTERS'    016050
***********************************************************************   016060
*        TABLE FOR SCANNING A FIELD TO LOCATE A STRING                *   016070
***********************************************************************   016080
         SPACE 1                                                          016090
*        THIS TABLE IS USED BY A NUMBER OF ROUTINES TO SCAN FOR A         016100
*         FIELD FOR A SPECIFIC CHARACTER.  THE TABLE IS THUS SUBJECT TO   016110
*         MODIICATION, IN THAT EACH TIME IT IS USED, THE USER WILL :-     016120
*              . SET TABLE TO ALL LOW VALUES                              016130
*              . SET THE POSITION CORRESPONDING TO THE SEARCH CHARACTER   016140
*                TO A VALUE OTHER THAN X'00'                              016150
*        THE USER CAN THEN USE 'TRT' TO LOCATE THE DESIRED CHARACTER.     016160
         SPACE 5                                                          016170
STRTAB   DC    256X'00'            TABLE FOR 'TRT'                        016180
         TITLE 'PARROTT - TABLES FOR HEX STRING VALIDATION/CONVERSION'    016190
***********************************************************************   016200
*        TABLES FOR HEX STRING VALIDATION/CONVERSION                  *   016210
***********************************************************************   016220
         SPACE 1                                                          016230
*        THESE TABLES ARE USED FOR VALIDATING AND CONVERTING HEX          016240
*         STRING LITERALS.  THE FIRST TABLE IS USED TO ENSURE THAT THE    016250
*         LITERAL CONTAINS ONLY VALID ( CHARACTER REPRESENTATION OF )     016260
*         HEXADECIMAL CHARACTERS, WHILE THE SECOND TABLE CONVERTS         016270
*         'A'-'F' INTO X'FA'-X'FF' SO THAT A PACK OPERATION WILL ALTER    016280
*         THE STRING INTO A TRUE HEX LITERAL.                             016290
         SPACE 5                                                          016300
HEXTAB1  DC    256X'01'            VALIDATE HEX CHARACTERS                016310
         ORG   HEXTAB1+C'A'                                               016320
         DC    6X'00'              A - F                                  016330
         ORG   HEXTAB1+C'0'                                               016340
         DC    10X'00'             0 - 9                                  016350
         ORG                                                              016360
         SPACE 5                                                          016370
HEXTAB2  DC    256AL1(*-HEXTAB2)   CONVERT 'A' TO X'FA' ETC               016380
         ORG   HEXTAB2+C' '        BLANK                                  016390
         DC    X'00'               CONVERT PADDING BLANKS TO PADDING 00   016400
         ORG   HEXTAB2+C'A'                                               016410
         DC    X'FAFBFCFDFEFF'     A - F INTO X'FA' - X'FF'               016420
         ORG                                                              016430
         TITLE 'PARROTT - TABLE FOR LOCATING 1ST NON-BLANK CHARACTER'     016440
*        THIS TABLE CAN BE USED BY 'TRT' TO LOCATE THE 1ST NON-BLANK      016450
*         CHARACTER IN A FIELD.                                           016460
         SPACE 5                                                          016470
NONBLTAB DC    256AL1(*-NONBLTAB)  BASIC TABLE                            016480
         ORG   NONBLTAB+X'00'      POINT AT LOW-VALUES POSITION           016490
         DC    X'01'               ALTER SO AS TO FAIL 'TRT'              016500
         ORG   NONBLTAB+C' '       POINT AT BLANK                         016510
         DC    X'00'               ALTER TO PASS 'TRT'                    016520
         ORG                                                              016530
         TITLE 'PARROTT - TABLE TO TRANSLATE BLANKS TO ZEROES'            016540
         SPACE 5                                                          016550
BLTOZERO DC    256AL1(*-BLTOZERO)  BASIC TRANSLATE TABLE                  016560
         ORG   BLTOZERO+C' '       ORG TO BLANK                           016570
         DC    C'0'                CONVERT BLANK TO ZERO                  016580
         ORG                                                              016590
         TITLE 'PARROTT - TRANSLATE TABLE FOR MATHEMATICAL OPERATIONS'    016600
*        THIS TABLE IS USED BY THE MATHEMATICAL ROUTINES TO LOCATE        016610
*         THE 1ST NON-ZERO BYTE IN A PACKED DECIMAL FIELD.  IT IS ALSO    016620
*         USED TO VALIDATE PACKED DECIMAL FIELDS.                         016630
         SPACE 5                                                          016640
PDTABLE  EQU   *                                                          016650
         DC    X'00'               '00'                                   016660
         DC    9X'02',6X'03'       '01' - '09', '0A' - '0F'               016670
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016680
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016690
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016700
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016710
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016720
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016730
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016740
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016750
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'                016760
         DC    96X'04'             'A0' - 'FF'                            016770
         SPACE 5                                                          016780
*        THE SIGNIFICANCE OF THE FUNCTION BYTES IS:-                      016790
         SPACE 1                                                          016800
*        '00' - SOURCE BYTE CONTAINS LOW VALUES                           016810
*        '01' - 1ST HALF OF SOURCE BYTE CONTAINS DIGIT (NN)               016820
*        '02' - 2ND HALF OFSOURCE BYTE CONTAINS DIGIT (0N)                016830
*        '03' - SOURCE BYTE CONTAINS DIGIT AND SIGN                       016840
*        '04' - 1ST HALF OF SOURCE BYTE CONTAINS A - F (INVALID)          016850
         DROP  R9                  DROP ADDRESSABILITY                    016860
         MEXIT                                                            016870
.ERR01   MNOTE 16,'MUTUALLY EXCLUSIVE KEYWORD SPECIFICATION'              016880
         MEXIT                                                            016890
         MEND                                                             016900
