         MACRO                                                            037240
&NAME    CCNO  &LINE,&ELEVEL=NO,&NULLCB=NO,&REG=R8,&LA=NO,&LABEL=,     *  037250
               &CCNO=YES,&PREFIX=NO                                       037260
.*                                                                        037270
.*       INSERT CONTROL CARD SEQUENCE NUMBER INTO ERROR MESSAGE AND       037280
.*        ISSUE THE ERROR MESSAGE.  THE CONTROL CARD SEQUENCE NUMBER IS   037290
.*        EXTRACTED FROM THE PREFIX OF THE CONTROL BLOCK WHOSE ADDRESS    037300
.*        IS IN &REG.                                                     037310
.*                                                                        037320
.*       ELEVEL=NO|YES             SET FLAG THAT E-LEVEL ERROR FOUND      037330
.*       NULLCB=NO|YES             NULLIFY (NO-OP) CONTROL BLOCK          037340
.*       REG=R8|XX                 REGISTER CONTAINING C/B ADDRESS        037350
.*       LA=NO|YES                 SPECIFY LA=YES IF LINE NOT DIRECTLY    037360
.*                                  ADDRESSABLE ( I.E. BEYOND 1ST 4K )    037370
.*       LABEL=AAAAA               NAME OF FIELD CONTAINING LABEL TO BE   037380
.*                                  INSERTED INTO ERROR MESSAGE           037390
.*       CCNO=YES|NO               SUPPRESS INSERTION OF C/C SEQ NO.      037400
.*       PREFIX=NO|YES             REGISTER ALREADY CONTAINS PREFIX ADR   037410
.*                                                                        037420
         LCLC  &L                                                         037430
&L       SETC  'L'''                                                      037440
         AIF   ('&LA' NE 'YES').NOLA                                      037450
&NAME    L     R1,=A(&LINE)        GET ADDRESS OF MESSAGE                 037460
         MVC   5(&L&LINE,R5),0(R1) MOVE MESSAGE TO BUFFER                 037470
         AGO   .YESLA                                                     037480
.NOLA    ANOP                                                             037490
&NAME    MVC   5(&L&LINE,R5),&LINE MOVE MESSAGE TO BUFFER                 037500
.YESLA   AIF   ('&CCNO' EQ 'NO').NOCCNO                                   037510
         LR    R1,&REG             POINT R1 AT THE CONTROL BLOCK          037520
         AIF   ('&PREFIX' EQ 'YES').C03                                   037530
         S     R1,=A(CBPRLGTH)     POINT R1 AT THE CONTROL BLOCK PREFIX   037540
.C03     USING CBPREFIX,R1         ESTABLISH PREFIX ADDRESSABILITY        037550
         ED    5+&LINE.C-&LINE.(&L&LINE.C,R5),CBPRCCNO ADD C/C SEQ NO.    037560
         DROP  R1                  DROP PREFIX ADDRESSABILITY             037570
.NOCCNO  AIF   ('&LABEL' EQ '').C00                                       037580
         MVC   5+&LINE.L-&LINE.(&L&LABEL,R5),&LABEL SET LABEL IN LINE     037590
.C00     CBAL  R4,PRINT            PRINT THE ERROR MESSAGE                037600
         AIF   ('&ELEVEL' NE 'YES').C01                                   037610
         OI    SWERROR,MERRCCER    FLAG THAT E-LEVEL ERROR ENCOUNTERED    037620
.C01     AIF   ('&NULLCB' NE 'YES').C02                                   037630
         MVI   0(&REG),NULLCB      NULLIFY THE CONTROL BLOCK              037640
.C02     ANOP                                                             037650
         MEND                                                             037660
