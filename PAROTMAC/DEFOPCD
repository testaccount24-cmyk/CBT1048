         MACRO                                                            038030
&NAME    DEFOPCD &OPCD=,&MOD=N,&IND=N,&LIT=N,                          *  038040
               &DSECT=NO,&CARD=NONE,&DTYP=D,&ICODE=,&DFLDT=N,&DFLDL=8, *  038050
               &CBID=                                                     038060
.*                                                                        038070
.*       OPCD  - OPERATION CODE ( E.G. I, DW, C3 )                        038080
.*       ICODE - INTERNAL CODE EQUIVALENT OF OPERATION CODE               038090
.*       CBID  - ID OF ASSOCIATED CBXX CONTROL BLOCK                      038100
.*       MOD   - MODIFIER REQUIRED FLAG ( Y / N )                         038110
.*       IND   - INDICATOR SPECIFICATION REQUIRED FLAG ( Y / N )          038120
.*       LIT   - LITERAL REQUIRED ( Y / N )                               038130
.*                                                                        038140
.*       DSECT - REQUEST FOR DSECT GENERATION ( YES / NO )                038150
.*       CARD  - LABEL OF ASSOCIATED GRXX CONTROL CARD                    038160
.*       DTYP  - DEFAULT PROCESSING TYPE ( E.G. N, A, T )                 038170
.*       DFLDT - DEFAULT DESTINATION FIELD TYPE                           038180
.*       DFLDL - DEFAULT DESTINATION FIELD LENGTH                         038190
.*                                                                        038200
         LCLC  &FLAG1,&FLAG2,&ID                                          038210
         LCLC  &CURFLD                                                    038220
         LCLA  &NFLDS                                                     038230
         AIF   ('&DSECT' EQ 'YES').DSECT                                  038240
         AIF   ('&OPCD' EQ '').ERR01                                      038250
         AIF   ('&ICODE' EQ '').ERR04                                     038260
&ID      SETC  '0'                                                        038270
         AIF   ('&CBID' EQ '').B00                                        038280
&ID      SETC  'FLAG&CBID'                                                038290
.B00     ANOP                                                             038300
&FLAG1   SETC  '0'                                                        038310
&FLAG2   SETC  '0'                                                        038320
         AIF   ('&MOD' EQ 'N').A03                                        038330
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPMOD'                                     038340
.A03     AIF   ('&IND' EQ 'N').A04                                        038350
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPIND'                                     038360
.A04     AIF   ('&LIT' EQ 'N').A05                                        038370
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPLIT'                                     038380
.A05     AIF   ('&CARD' EQ 'NONE').A06                                    038390
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPCARD'                                    038400
.A06     ANOP                                                             038410
.*       INSERT TEST FOR BIT 6 WHEN IT IS ASSIGNED                        038420
.A07     ANOP                                                             038430
&NAME    DC    CL2'&OPCD',AL1(&ID),CL4'&CARD',AL1(&ICODE),CL1'&DTYP'      038440
         DC    CL1'&DFLDT',AL1(&DFLDL),AL1(&FLAG1),AL1(&FLAG2)            038450
         MEXIT                                                            038460
.DSECT   ANOP                                                             038470
***********************************************************************   038480
*        OPERATION/FUNCTION CODE DESCRIPTOR                           *   038490
***********************************************************************   038500
         SPACE 1                                                          038510
OPCDNTRY DSECT                                                            038520
OPCDOPCD DS    CL2                 OPERATION/FUNCTION CODE                038530
OPCDCBID DS    CL1                 ID OF ASSOCIATED CONTROL BLOCK         038540
OPCDCARD DS    CL4                 ID OF ASSOCIATED CONTROL CARD          038550
OPCDICDE DS    CL1                 INTERNAL REPRESENTATION OF FUNCTION    038560
OPCDDTYP DS    CL1                 DEFAULT PROCESSING TYPE ( $PROCTYP )   038570
OPCDFLDT DS    CL1                 DEFAULT DESTINATION FIELD TYPE         038580
OPCDFLDL DS    CL1                 DEFAULT FIELD LENGTH                   038590
OPCDFLG1 DS    CL1                 FLAG BYTE 1                            038600
OPCDFLG2 DS    CL1                 FLAG BYTE 2                            038610
OPCDLGTH EQU   *-OPCDNTRY          LENGTH OF DSECT                        038620
         SPACE 5                                                          038630
***********************************************************************   038640
*        EQUATES FOR FLAG BYTE 1                                      *   038650
***********************************************************************   038660
         SPACE 1                                                          038670
F1OPMOD  EQU   1                   OP-CODE MODIFIER REQUIRED              038680
F1OPIND  EQU   2                   INDICATOR SPECIFICATION REQUIRED       038690
F1OPLIT  EQU   4                   LITERAL VALUE REQUIRED                 038700
F1OPCARD EQU   8                   LABEL OF ASSOCIATED CARD REQUIRED      038710
         SPACE 5                                                          038720
***********************************************************************   038730
*        EQUATES FOR FLAG BYTE 2                                      *   038740
***********************************************************************   038750
         SPACE 1                                                          038760
         MEXIT                                                            038770
.ERRO1   MNOTE 16,'''OPCD='' KEYWORD IS MANDATORY'                        038780
         MEXIT                                                            038790
.ERR04   MNOTE 16,'''ICODE='' KEYWORD IS MANDATORY'                       038800
         MEXIT                                                            038810
         MEND                                                             038820
