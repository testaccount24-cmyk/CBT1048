         TITLE 'BUDGIE3J - INTERNAL MACROS'                             J 000010
         MACRO                                                          J 000020
         $CB   &PRINT=OFF                                               J 000030
         PUSH PRINT                PRESERVE CURRENT PRINT STATUS        J 000040
         PRINT &PRINT              ESTABLISH NEW PRINT STATUS           J 000050
               TITLE 'CB02 - CB02 CONTROL BLOCK'                        J 000060
         CB02  ,                   GENERATE DSECT                       J 000070
               TITLE 'CB13 - CB13 CONTROL BLOCK'                        J 000080
         CB13  ,                   GENERATE DSECT                       J 000090
               TITLE 'CB14 - CB14 CONTROL BLOCK'                        J 000100
         CB14  ,                   GENERATE DSECT                       J 000110
               TITLE 'CB40 - CB40 CONTROL BLOCK'                        J 000120
         CB40  ,                   GENERATE DSECT                       J 000130
               TITLE 'CCB  - CENTRAL CONTROL BLOCK'                     J 000140
         CCB   ,                   GENERATE DSECT                       J 000150
               TITLE '     - BATCH BALANCING REPORT LINES'              J 000160
      DETAILBB ,                   GENERATE DSECTS                      J 000170
               TITLE '     - GENERAL EQUATES'                           J 000180
       EQUATES ,                   GENERATE EQUATES                     J 000190
         POP   PRINT               RESTORE ORIGINAL PRINT STATUS        J 000200
         MEND                                                           J 000210
         SPACE 5                                                        J 000220
BUDGIE3J CSECT                                                          J 000230
         $CB   ,                   GENERATE DSECTS AND EQUATES          J 000240
         TITLE 'BUDGIE3J - TESTS AND OPERATIONS'                        J 000250
        SCSECT J                   DEFINE START OF CSECT BUDGIE3J       J 000260
         ENTRY SETINDS             SET INDICATORS ON                    J 000270
         ENTRY TESTIND             TEST STATUS OF DETAIL LEVEL INDICATR J 000280
         ENTRY TESTSL4             SELECT AND PERFORM RQUESTED TEST     J 000290
         USING DETAILBB,R7         ESTBLISH B/B REPORT ADDRESSABILITY   J 000300
         TITLE 'BUDGIE3J - SELECT APPROPRIATE ROUTINE'                  J 000310
*********************************************************************** J 000320
*        SELECT APPORPRIATE ROUTINE                                   * J 000330
*********************************************************************** J 000340
         SPACE 1                                                        J 000350
*        REGISTERS ARE NOW SET THUS :-                                  J 000360
*              R15 - END OF FIELD TO BE TESTED                          J 000370
*              R14 - START OF FIELD TO BE TESTED                        J 000380
*              R1  - EXECUTABLE LEBGTH                                  J 000390
*              R2  - ADDRESS OF ASSOC C/B ( WHERE RELEVANT )            J 000400
*              R4  - TEST CODE * 4 ( 2NDEX )                            J 000410
*              R6  - POINTS AT FIELD IN RECORD                          J 000420
*              R8  - ADDRESS OF CURRENT CB13                            J 000430
*              R10 - ADDRESS OF CURRENT CB02                            J 000440
         SPACE 1                                                        J 000450
TESTSL4  SUBIN ,                   EBER ROUTINE                         J 000460
         SH    R4,=H'300'          DECERMENT INDEX: 1ST 75 TESTS DONE   J 000470
         B     TESTBR(R4)          SELECT APPROPRIATE ROUTINE           J 000480
TESTBR   EQU   *                   BASE FOR INDEXED BRANCH              J 000490
         B     LOGICER             TEST TYPE UB - SUPPORTED IN BUDGIE3G J 000500
         B     TESTS               TEST TYPE S  - SUPPORTED             J 000510
         B     LOGICER             TEST TYPE NM - UNSUPPORTED           J 000520
         B     TESTSX              TEST TYPE SW - SUPPORTED             J 000530
         B     TESTIT              TEST TYPE IT - SUPPORTED             J 000540
         B     TESTON              TEST TYPE ON - SUPPORTED             J 000550
         B     TESTOF              TEST TYPE OF - SUPPORTED             J 000560
         B     TESTWR              TEST TYPE WR - SUPPORTED             J 000570
         B     LOGICER             TEST TYPE DM - SUPPORTED IN BUDGIE3G J 000580
         B     LOGICER             TEST TYPE ER - SUPPORTED IN BUDGIE3G J 000590
         B     LOGICER             TEST TYPE NR - UNSUPPORTED           J 000600
         B     LOGICER             TEST TYPE UR - UNSUPPORTED           J 000610
         B     LOGICER             TEST TYPE AB - SUPPORTED IN BUDGIE3G J 000620
         B     TESTWI              TEST TYPE WI - SUPPORTED             J 000630
         EJECT                                                          J 000640
*********************************************************************** J 000650
*        RETURN TO BUDGIE3G FOR TEST TERMINATION PROCESSING           * J 000660
*********************************************************************** J 000670
         SPACE 1                                                        J 000680
TESTEXIT CBAL  ,TESTRTN            PREPARE TO RETURN TO CALLER          J 000690
         SPACE 5                                                        J 000700
*********************************************************************** J 000710
*        ABEND IF GAIN CONTROL FOR A TEST PERFORMED IN BUDGIE3G       * J 000720
*********************************************************************** J 000730
         SPACE 1                                                        J 000740
LOGICER ABEND  80,FULL             SHOULD NEVER HAPPEN                  J 000750
         TITLE 'BUDGIE3J - CB13 ROUTINE PROCESSING'                     J 000760
*********************************************************************** J 000770
*        S  - LINK IN THE CB13 ROUTINE                                * J 000780
*********************************************************************** J 000790
         SPACE 1                                                        J 000800
         USING CB40NTRY,R2         ESTABLISH CB40 ADDRESSABILITY        J 000810
TESTS    SUBIN ,                   ENTER ROUTINE                        J 000820
         L     R14,CB40FRST        GET ADDR OF 1ST CB13 IN ROUTINE      J 000830
         L     R15,CB40LAST        GET ADDRESS OF LAST CB13 IN CHAIN    J 000840
         L     R1,$PTRCB13         GET ADDRESS OF THIS CB13             J 000850
         L     R2,CB13CB13-CB13NTRY(R1) GET ADDRESS OF NEXT SEQ CB13    J 000860
         ST    R2,CB13CB13-CB13NTRY(R15) POINT END OF RTN AT NEXT SEQ   J 000870
         ST    R14,CB13CB13-CB13NTRY(R1) POINT THIS CB13 AT STRT OF RTN J 000880
         ST    R1,CB13CBXX-CB13NTRY(R15) SAVE ADDR OF CURR CB13 IN LAST J 000890
*                                   (DUMMY) CB13 AT END OF CHAIN        J 000900
         OI    APSWITCH,APSWSRTN   FLAG THAT SUBROUTINE HAS CONTROL     J 000910
         L     R1,SRTNCNT          GET DEPTH OF SUBROUTINE NEST         J 000920
         LA    R1,1(,R1)           INCREMENT SUBROUTINE COUNT           J 000930
         ST    R1,SRTNCNT          RESTORE UPDATED DEPTH OF NEST        J 000940
        SUBOUT RETURN=TESTEXIT     PREPARE TO TETURN TO CALLER          J 000950
         DROP  R2                  DROP CB40 ADDRESSABULITY             J 000960
         EJECT                                                          J 000970
*********************************************************************** J 000980
*        SX - TERMINATE CB13 SUB-ROUTINE                              * J 000990
*********************************************************************** J 001000
         SPACE 1                                                        J 001010
*        THE CURRENT CB13 CONTAINS THE FOLLOWIND FIELDS:                J 001020
*         CB13CB13 - ADDR OF CB13 FOLLOWING ONE INVOKING SUBROUTINE     J 001030
*         CB13CBXX - ADDR OF CB13 THAT INVOKED SUBROUITNE               J 001040
         SPACE 1                                                        J 001050
*        SUBROUTINE MAY HAVE BEEN INVOKED BY :                          J 001060
*         S  - PERFOM SUB-ROUTINE ONCE                                  J 001070
*         DW - PERFROM SUBROUTINE WHILE CONDITIONS SATISFIED            J 001080
*        FOR 'S ' WE WISH TO RETURN TO THE NEXT SEQUENTIAL CB13, WHILE  J 001090
*         FOR 'DW' WE WISH TO RETURN TO THE SAME CB13.                  J 001100
         SPACE 1                                                        J 001110
TESTSX   SUBIN ,                   ENTER ROUTINE                        J 001120
*                                  NOTE THAT R2 CONTAINS THE ADDRESS OF J 001130
*                                   THE CB13 THAT INVOKED THE ROUTINE   J 001140
         L     R1,$PTRCB13         GET ADDRESS OF THIS (DUMMY) CB13     J 001150
         L     R1,CB13CB13-CB13NTRY(R1) GET ADDRESS OF NEXT CB13        J 001160
         ST    R1,CB13CB13-CB13NTRY(R2) RECHAIN THE CB13 THAT INVOKED   J 001170
*                                   THE SUBROUTINE TO THE SEQ CB13      J 001180
         USING CB13NTRY,R1         ESTABLISH CURRENT CB13 ADDRESSABILIT J 001190
         L     R1,$PTRCB13         GET ADDRESS OF THIS CB13             J 001200
         CLI   CB13TST0-CB13NTRY(R2),C'D' WAS RTN INVOKED BY 'DW'       J 001210
         BNE   *+10                BR IF NOT BY 'DW'                    J 001220
         MVC   CB13CB13,CB13CBXX   SET TO RETRUN TO 'DW' CB13           J 001230
         DROP  R1                  DROP CB13 ADDRESSABILITY             J 001240
         L     R1,SRTNCNT          GET COUNT OF DEPTH OF NEST           J 001250
         BCTR  R1,0                DECREMENT COUNT                      J 001260
         ST    R1,SRTNCNT          PRESERVE NEEW COUNT OF DEPTH OF NEST J 001270
         LTR   R1,R1               TEST IF EMERGED FROM SUBROUTINE NEST J 001280
         BNZ   *+8                 BR IF NOT                            J 001290
         NI    APSWITCH,FF-APSWSRTN INDICTA ENO LONGER WITHIN ROUTINE   J 001300
        SUBOUT RETURN=TESTEXIT     PREPARE TI RETURN TO CALLER          J 001310
         TITLE 'BUDGIE3J - INDOCATOR PROCESSING'                        J 001320
*********************************************************************** J 001330
*        IT - TEST INDICATOR STATUS                                   * J 001340
*********************************************************************** J 001350
         SPACE 1                                                        J 001360
*        NOTE THAT THIS TEST WORKS IN REVERSE.  IF ALL SPECIFIED        J 001370
*         INDICATORS HAVE THE SPECIFIED STATUS THEN TTHE CARD/BATCH     J 001380
*         IS CONSIDERED TO BE IN ERROR.  THUS, IF THE TESTS ARE       * J 001390
*         SATISFIED, 'TESTTSTX' IS SET TO 'Y' AND NOT 'X'.              J 001400
         SPACE 1                                                        J 001410
         USING CB14NTRY,R6         ESTABLISH CB14 ADDRESSABILITY        J 001420
TESTIT   SUBIN SAVE=(R3,R5,R9)     PRESERVE REGISTERS                   J 001430
         LR    R6,R2               POINT R6 AT CB14                     J 001440
        BBMESS 17                  ADD ERROR MESSAGETO LINE             J 001450
         LA    R9,9                ESTABLISH LOOP OF 9 BUCKTES          J 001460
         LA    R5,CB14IND          POINT AT 1ST BUCKET                  J 001470
TESTIT1  SR    R3,R3               ZEROISE WORK REGISTER ...            J 001480
         IC    R3,0(R5)             ... AND INSERT IND NUMBER           J 001490
         SR    R1,R1               ZEROISE WIRK REGISTER ...            J 001500
         IC    R1,1(,R5)            .. AND INSERT DESIRED IND STATUS    J 001510
         CLI   0(R5),FF            TEST IF IND NUMBER NULLIFIED         J 001520
         BE    TESTIT2             BR IF NULLIFIED TO GET NEXT          J 001530
         CBAL  R4,WHICHBIT         DETERMINE WHICH BIT WE WANT          J 001540
         TM    CB14SW,SW14BTCH     TEST IF BATCH LEVEL TEST             J 001550
         BO    *+12                BR IF BATCH LEVEL TEST               J 001560
         EX    R2,TESTITD          TEST STATE OF DETAIL LEVEL INDICATOR J 001570
         B     *+8                 GO DETERMINE RESULT OF TEST          J 001580
         EX    R2,TESTITB          TEST STATUS OF BATCH LEVEL INDICATOR J 001590
         CBAL  R4,COMPARE          DETERMINE RESULT OF TEST             J 001600
         BNE   TESTITX             EXIT IF STATUS NOT AS REQUIRED       J 001610
TESTIT2  LA    R5,2(,R5)           INCREMENT TO NEXT BUCKET             J 001620
         BCT   R9,TESTIT1          LOOP BACK IF ANY LEFT                J 001630
         MVI   $TSTTSTX,C'Y'       ALL STATUS CORRECT SO TEST FAILED    J 001640
TESTITX  SUBOUT RESTORE=(R3,R5,R9),RETURN=TESTEXIT PREPARE TO RETURN    J 001650
         DROP  R6                  DROP CB14 ADDRESSABILITY             J 001660
         EJECT                                                          J 001670
*********************************************************************** J 001680
*        TEST STATUS IF DETAIL LEVEL INDICATOR                        * J 001690
*********************************************************************** J 001700
         SPACE 1                                                        J 001710
TESTIND  SUBIN SAVE=(R3,R4)        PRESERVE REGISTERS                   J 001720
         CLI   0(R3),FF            TEST IF INDICATOR NUMBER NULLIFIED   J 001730
         BE    TESTNDDX            BYPASS ROUTINE IF YES                J 001740
         CBAL  R4,TESTINDS         COMPUTE BIT ADDRESS                  J 001750
         EX    R2,TESTITD          TEST DETAIL LEVEL BIT                J 001760
         CBAL  R4,COMPARE          DETERMINE RESULT OF TEST             J 001770
TESTNDDX SUBOUT RESTORE=(R3,R4)    RESTORE & RETURN TO CALLER           J 001780
         SPACE 4                                                        J 001790
*********************************************************************** J 001800
*        TEST STATUS OF BATCH LEVEL INDICATOR                         * J 001810
*********************************************************************** J 001820
         SPACE 1                                                        J 001830
TESTINDB SUBIN SAVE=(R3,R4)        PRESERVE REGISTERS                   J 001840
         CLI   0(R3),FF            TEST IN INDICATOR NUMBER NULLIFIED   J 001850
         BE    TESTNDBX            BYPASS ROUTINE IF YES                J 001860
         CBAL  R4,TESTINDS         COMPUET BIT ADDRESS                  J 001870
         EX    R2,TESTITB          TEST BATCH LEVEL INDICATOR STATUS    J 001880
         CBAL  R4,COMPARE          DETERMINE RESULT OF TEST             J 001890
TESTNDBX SUBOUT RESTORE=(R3,R4)    RESTORE & RETURN TO CALLER           J 001900
         EJECT                                                          J 001910
*********************************************************************** J 001920
*        PREPARE TO COMPUTE BIT ADDRESS                               * J 001930
*********************************************************************** J 001940
         SPACE 1                                                        J 001950
*        ON ENTRY, R3 POINTS AT TWO 1-BYTE BINARY FIELDS CONTAINING     J 001960
*         THE INDICATOR NUMBER AND THE INTERNAL REPRESENTATION OF THE   J 001970
*         DESIRED INDICATOR STATUS *                                    J 001980
         SPACE 1                                                        J 001990
TESTINDS SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              J 002000
         SR    R1,R1               ZEROISE WORK REGISTER                J 002010
         SR    R2,R2               ZEROISE WORK REGISTER                J 002020
         IC    R1,1(,R3)           PLACE STATUS IN R1                   J 002030
         IC    R2,0(R3)            GET IND NUMBER IF R2                 J 002040
         LR    R3,R2               SET IND NUMBER IN R3                 J 002050
         CBAL  R4,WHICHBIT         COMPUTE BIT ADDRESS                  J 002060
        SUBOUT RESTORE=R4          RESTOE & RETURN TO CALLER            J 002070
         EJECT                                                          J 002080
*********************************************************************** J 002090
*        ON - SET IN SPECIFIED INDICATOR                              * J 002100
*********************************************************************** J 002110
         SPACE 1                                                        J 002120
TESTON   SUBIN ,                   ENTER ROUTINE                        J 002130
         CBAL  R4,SETINDS          SET INDICATOR ON                     J 002140
         B     TESTEXIT            PREPARE TO RETURN TO CALLER          J 002150
         SPACE 5                                                        J 002160
*********************************************************************** J 002170
*        OF- SET OFF SPECIFIED INDICATOR                              * J 002180
*********************************************************************** J 002190
         SPACE 1                                                        J 002200
TESTOF   SUBIN ,                   ENTER ROUTINE                        J 002210
         USING CB13NTRY,R8         ESTABLISH CB13 ADDRESSABILITY        J 002220
         SR    R3,R3               ZEROISE WORK REGISTER                J 002230
         IC    R3,CB13RIND         OBTAIN INDICATOR NUMBER              J 002240
         CBAL  R4,WHICHBIT         CALCALATE BIT ADDRESS                J 002250
         EX    R2,SETMASKB         INSERT MASK INTO 'NI' FOR BATCH LEVL J 002260
         EX    R2,SETMASKD         INSERT MASK INTO 'NI' FOR DETAIL LVL J 002270
         XI    OFFINDB+1,FF        INVERT MASK FOR NI                   J 002280
         XI    OFFINDD+1,FF        INVERT MASK FOR NI                   J 002290
         EX    R0,OFFINDB          SET OFF INDICATOR AT BATCH LEVEL     J 002300
         EX    R0,OFFINDD          SET OFF INDICATOR AT DETAIL LEVEL    J 002310
         B     TESTEXIT            PREPARE TO RETURN TO CALLER          J 002320
         DROP  R8                  DROP CB13 ADDRESSABILITY             J 002330
         EJECT                                                          J 002340
*********************************************************************** J 002350
*        SET SPECIFIED INDICATOR ON                                   * J 002360
*********************************************************************** J 002370
         SPACE 1                                                        J 002380
*        ON ENTRY, R10 POINTS AT THE CB02 CONTAINING THE INDICATOR      J 002390
*         AREAS AND R8 POINTS AT THE CB13 CONTAINING THE INDICATOR NUMB J 002400
*         THIS ROUTINE ONLY RECEIVES CONTROL IF THE INDICATOR IS TO BE  J 002410
*         SET ON ( I.E. IF THE ASSOCIATED TEST HAS FAILED )             J 002420
         SPACE 1                                                        J 002430
         USING CB02NTRY,R10        ESTABLISH CB02 ADDEESSABILITY        J 002440
         USING CB13NTRY,R8         ESTABLIST CB13 ADDEESSABILITY        J 002450
SETINDS  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              J 002460
         SR    R3,R3               ZEROISE LINKAGE REGISTER             J 002470
         IC    R3,CB13RIND         INSERT INDICATOR NUMBER              J 002480
         CLI   CB13RIND,X'FF'      TEST IF INDICATOR SPECIDIED          J 002490
         BE    SETINDSX            BYPASS ROUTINE IF NO IND SPECIFIED   J 002500
         CBAL  R4,WHICHBIT         DETERMINE WHICH BIT IS REQUIRED      J 002510
         EX    R2,SETINDB          SET ON BETCH LEVEL INDICATOR         J 002520
         EX    R2,SETINDD          SET ON DETAIL LEVEL INDICATOR        J 002530
SETINDSX SUBOUT RESTORE=R4         RESTORE & RETURB TO CALLER           J 002540
         SPACE 1                                                        J 002550
SETINDB  OI    0(R14),X'00'        SET ON BATCH LEVEL INDICATOR         J 002560
SETINDD  OI    0(R15),X'00'        SET ON DETAIL LEVEL INDICATOR        J 002570
         DROP  R8,R10              DROP C/B ADDRESSABILITY              J 002580
         EJECT                                                          J 002590
*********************************************************************** J 002600
*        CONVERT INDICATOR NUMBER INTO A BIT ADDRESS                  * J 002610
*********************************************************************** J 002620
         SPACE 1                                                        J 002630
*        ON ENTRY, R10 CONTAINS THE ADDRESS OF THE CB02 CONTAINING THE  J 002640
*         INDICATOR AREAS, AND R3 CONTAINS THE INDICATOR NUMBER.  ON    J 002650
*         EXIT, R14 POINTS AT THE RELEVANT BATCH INDICATOR BYTE, R15    J 002660
*         POINTS AT THE RELEVANT DETEIL INDICATOR BYTE, AND R3 CONTAINS J 002670
*         THE NECESSARY BIT MASK TO ADDRESS THE CORRECT BIT.            J 002680
         SPACE 1                                                        J 002690
         USING CB02NTRY,R10        ESTABLISH CB02 ADDRESSABILITY        J 002700
WHICHBIT SUBIN ,                   ENTER SUB-ROUTINE                    J 002710
         SR    R2,R2               ZEROISE WORK REGISTER                J 002720
         D     R2,=F'8'            CONVERT BIT NO TO BYTES & BITS       J 002730
         LA    R14,CB02INDB        POINT R14 AT BATCH LEVEL SWITCHES    J 002740
         LA    R15,CB02INDD        PONT R15 AT THE DETAIL LEVEL INDICAT J 002750
         AR    R14,R3              POINT R14 AT CORRECT BATCH  LEVEL BY J 002760
         AR    R15,R3              POINT R15 AT CORRECT DETAIL LEVEL BY J 002770
         STC   R2,$WRKAREA         PRESERVE REMAINDER IN WORK AREA      J 002780
         TR    $WRKAREA(1),REMAINDR TRANSLATE REMIANDER INTO BIT        J 002790
         IC    R2,$WRKAREA         INSERT BIT MASK INTO REG FOR EXECUTE J 002800
        SUBOUT ,                   RETURN TO CALLER                     J 002810
         DROP  R10                 DROP CB02 ADDRESSABILITY             J 002820
         TITLE 'BUDGIE3J - WRITE ADDITIONAL OUTPUT RECORD'              J 002830
*********************************************************************** J 002840
*        WR - WRITE ADDITIONAL OUTPUT RECORD                          * J 002850
*********************************************************************** J 002860
         SPACE 1                                                        J 002870
         USING CB13NTRY,R8         ESTABLISH CB13 ADDRESSABILITY        J 002880
TESTWR   SUBIN ,                   ENTER ROUTINE                        J 002890
         TM    PSWITCH,PSWDLETE+PSWHARD HAS RECORD BEEN ACCEPTED        J 002900
         BNZ   TESTWRX             IGNORE WRITE IF NOT ACCEPTED         J 002910
         TM    MASTER,SWMHARD      HAS BATCH GOT HARD ERROR             J 002920
         BO    TESTWRX             BYPASS EXTRA WRITE IF YES            J 002930
         CLI   CB13PFLD,OO         WAS STARTING POSN SUPPLIED           J 002940
         BNE   *+8                 BRANCH IF STARTING POSN SUPPLIED     J 002950
         LA    R6,1(,R6)           POINT R6 AT START OF RECORD          J 002960
         TM    BRSWITCH,SWBRNOBC   IS IT PARM=NOBC                      J 002970
         BZ    TESTWR1             BR IF NOT TO WRITE TO WORK           J 002980
         CBAL  R4,PUTUT2           WRITE DIRECTLY TO SYSUT2             J 002990
         B     TESTWRX             GO TO EXIT                           J 003000
TESTWR1  CBAL  R4,WORKPUT          WRITE WORK FILE                      J 003010
TESTWRX SUBOUT RETURN=TESTEXIT     PREPARE TO RETURN TO VALLER          J 003020
         DROP  R8                  DROP CB13 ADDRESSABILITY             J 003030
         EJECT                                                          J 003040
*********************************************************************** J 003050
*        WI - WRITE TO INTERNAL READER                                * J 003060
*********************************************************************** J 003070
         SPACE 1                                                        J 003080
         USING CB13NTRY,R8         ESTABLISH CB13 ADDRESSABILITY        J 003090
TESTWI   SUBIN ,                   ENTER ROUTINE                        J 003100
         CLI   CB13PFLD,OO         WAS STARTING POSN SUPPLIED           J 003110
         BNE   *+8                 BR IF STARTING POSN SUPPLIED         J 003120
         LA    R6,1(,R6)           POINT R6 AT START OF RECORD          J 003130
         CBAL  R4,PUTINTR          WRITE TO INTERNAL READER             J 003140
        SUBOUT RETURN=TESTEXIT     PREPARE TO RETRUN TO CALLER          J 003150
         DROP  R8                  DROP CB13 ADDRESSABILITY             J 003160
         TITLE 'BUDGIE3J  - EXECUTED INSTRUCTIONS'                      J 003170
*********************************************************************** J 003180
*        EXECUTED INSTRUCTIONS                                        * J 003190
*********************************************************************** J 003200
         SPACE 1                                                        J 003210
OFFINDD  NI    0(R15),OO           SET OFF IND ( VIA INVERTED MASK )    J 003220
OFFINDB  NI    0(R14),OO           SET OFF IND ( VIA INVERTED MASK )    J 003230
SETMASKB MVI   OFFINDB+1,OO        INSERT INVERTED MASK INTO INSTRUCTN  J 003240
SETMASKD MVI   OFFINDD+1,OO        INSERT INVERTED MASK INTO INSTRUCTN  J 003250
TESTITB  TM    0(R14),OO           TEST INDICATOR STATUS                J 003260
TESTITD  TM    0(R15),OO           TEST INDICATOR STATUS                J 003270
         TITLE 'BUDGIE3J - MISCELLANEOUS CONSTANTS AND FIELDS'          J 003280
SRTNCNT  DC    F'0'                CURRENT DEPTH OF SUBROUTINES         J 003290
         SPACE 1                                                        J 003300
REMAINDR DC    X'8040201008040201' REMAINDER TO BIT CONVERSION TABLE    J 003310
         SPACE 1                                                        J 003320
BBMESS17 DC    C'FIELD COLS   -   FAILED TEST'                          J 003330
        SCSEND J                   TERMINATE CSECT BUDGIE3J             J 003340
         END                                                            J 003350
         SPACE 1                                                        J 003360
         REPRO                                                          J 003370
 IDENTIFY BUDGIE3J('TESTS AND OPERATIONS')                              J 003380
         END                                                            J 003390
