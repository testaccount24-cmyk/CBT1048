         TITLE 'PARROTC1 - CONTROL CARD PROCESSING - PART 1'            C1000100
         MACRO                                                          C1000200
         $CB   &PRINT=OFF          MACRO TO GENERATE DSECTS             C1000300
         PUSH  PRINT               PRESERVE PRINT OPTIONS               C1000400
         PRINT &PRINT              SET PRINT OPTIONS FOR DSECTS         C1000500
               EJECT                                                    C1000600
         CBPREFIX ,                CONTROL BLOCK PREFIX                 C1000700
               EJECT                                                    C1000800
         CCB   ,                   CENTRAL CONTROL BLOCK                C1000900
               EJECT                                                    C1001000
         CSECTID ,                 CSECT IDENTIFICATION PREFIX          C1001100
               EJECT                                                    C1001200
         EQUATES ,                 GENERATE STANDARD EQUATES            C1001300
               EJECT                                                    C1001400
         FDB   ,                   FIELD DEFINITION BLOCK               C1001500
               EJECT                                                    C1001600
         GR08  ,                   GR08 CONTROL CARD                    C1001700
               EJECT                                                    C1001800
         GR09  ,                   GR09 CONTROL CARD                    C1001900
               EJECT                                                    C1002000
         KBD   ,                   KEY BREAK DEFINITION                 C1002100
               EJECT                                                    C1002200
         LCD   ,                   LINE COMPONENT DESCRIPTOR            C1002300
               EJECT                                                    C1002400
         OCB   ,                   OUTPUT CONTROL BLOCK                 C1002500
               EJECT                                                    C1002600
         RAE   ,                   RANGE ANALYSIS ELEMENT               C1002700
               EJECT                                                    C1002800
         SCBUCKET ,                SELECTION CRITERION ' BUCKET '       C1002900
               EJECT                                                    C1003000
         SCE   ,                   SELECTION CRITERION ELEMENT          C1003100
               EJECT                                                    C1003200
         SPD   ,                   STORAGE POOL DEFINITION              C1003300
               EJECT                                                    C1003400
         TRD                       TWENTY80 REPORT DEFINITION           C1003500
         POP   PRINT               RESTORE ORIGINAL PRINT OPTIONS       C1003600
         MEND                                                           C1003700
         SPACE 4                                                        C1003800
PARROTC1  CSECT                                                         C1003900
         $CB   ,                   GENERATE DSECTS AND EQUATES          C1004000
         EJECT                                                          C1004100
     SCSECT C1                                                          C1004200
         USING PARROTC1,R12         ESTABLISH PARROTC1 ADDRESSABILITY   C1004300
         USING CCBNTRY,R13         ESTABLISH CCB ADDRESSABILITY         C1004400
         SPACE 1                                                        C1004500
         ENTRY CCGR08              PROCESS GR08 CONTROL CARDS           C1004600
         ENTRY CCGR09              PROCESS GR09 CONTROL CARDS           C1004700
         ENTRY PARENDC1            END IF CSECT                         C1004800
         EJECT                                                          C1004900
*********************************************************************** C1005000
*        PROCESS GR08 CONTROL CARDS                                   * C1005100
*********************************************************************** C1005200
         SPACE 1                                                        C1005300
         USING GR08CARD,R6         ESTABLISH C/C ADDRESSABILITY         C1005400
         USING LCDNTRY,R8          ESTABLISH C/B ADDRESSABILITY         C1005500
CCGR08   SUBIN SAVE=(R4,R6)        PRESERVE RETURN ADDRESS              C1005600
         CLC   GR08BSIC,$SPACES    IS BASIC REPORT SECTION BLANK        C1005700
         BE    CCGR08TW            GO PROCESS TWENTY80 SPEC IF BLANK    C1005800
         LA    R8,$LCDDETL         GET ADDRESS OF HEAD OF LCD CHAIN     C1005900
         LTA   R1,LCDLCD,*+10      GET ADDRESS OF NEXT LCD IN REG 1     C1006000
         LR    R8,R1               PRESERVE LCD ADDRESS IN REG 8        C1006100
         B     *-10                LOOP BACK TO GET NEXT LCD            C1006200
         ST    R8,$PTRLCD          SET ADDRESS OF END OF CHAIN          C1006300
         LA    R10,5               SET TO LOOP THROUGH 5 FIELDS         C1006400
CCGR0801 CBAL  R4,BLDLCD           CONSTRUCT LCD FOR NUMERIC FIELD      C1006500
         LA    R6,3(,R6)           INCREMENT C/C PTR FOR NEXT BUCKET    C1006600
         BCT   R10,CCGR0801        LOOP BACK IF ANY BUCKETS LEFT        C1006700
         SUBLR R6                  REFRESH CONTROL CARD ADDRESS         C1006800
         CBAL  R4,BLDLCDBS         CONSTRUCT LCD REQUESTING BASIC REPT  C1006900
CCGR08TW EQU   *                   PROCESS TWNETY80 SPECIFICATION       C1007000
         CBAL  R4,BLDTW80          PROCESS TWENTY80 SPECIFICATION       C1007100
         CLC   GR08UNUS,$SPACES    ARE UNUSED COLUMNS BLANK             C1007200
         BE    CCGR08X             BR IF UNUSED COLUMNS ARE BLANK       C1007300
         LINE  LINE01              TELL OF POTENTIALLY MISPLACED DATA   C1007400
CCGR08X SUBOUT RESTORE=(R4,R6)     RESTOE AND RETURN                    C1007500
         DROP R6,R8                DROP C/C & C/B ADDRESSABILITY        C1007600
         EJECT                                                          C1007700
*********************************************************************** C1007800
*        GR08 PROCESSING: CONSTRUCT LCD FOR NUMERIC OUTPUT FIELD      * C1007900
*********************************************************************** C1008000
         SPACE 1                                                        C1008100
         USING GR08CARD,R6         ESTABLISH GR08 ADDRESSABILITY        C1008200
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C1008300
BLDLCD   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1008400
         CLC   GR08PRT1,$SPACES    IS THE FIELD DEFINITION BLANK        C1008500
         BE    BLDLCD1             BR IF NO FLD DEFINITION              C1008600
         LA    R2,LCDLGTH          SET LCD LENGTH IN WORK REG           C1008700
         GETCB LCD                 ACQUIRE AND CHAIN LCD                C1008800
         MVC   LCDFLDP,OUTFLDP     SET DISPLACEMENT OF OUTPUT FIELD     C1008900
         MVI   LCDFLDL,BSICFLEN    SET LENGTH OF OUTPUT FIELD           C1009000
         MVI   LCDFLDF,BSICFLDF    SET FORMAT OF OUTPUT FIELD           C1009100
       NUMERIC GR08NDEC            ENSURE THAT NO. OF DECIMALS IS NUM   C1009200
         STC   R1,LCDNDEC          SET NO. OF OUTPUT DECIMALS IN LCD    C1009300
         MVC   LCDIOLB,$SPACES     SET OCB LABEL TO BLANK ('PRINT')     C1009400
         MVC   LCDLABL,GR08FID     SET FIELD LABEL IN LCD               C1009500
BLDLCD1  LH    R1,OUTFLDP          RETRIEVE POSN OF OUTPUT FIELD        C1009600
         LA    R1,BSICFLEN+1(,R1)  INCREMENT TO START OF NEXT FIELD     C1009700
         STH   R1,OUTFLDP          STORE UPDATES POSN FOR NEXT TIME     C1009800
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C1009900
         DROP  R8                  DROP LCD ADDRESSABILITY              C1010000
         DROP  R6                  DROP GR08 ADDRESSABILTIY             C1010100
         EJECT                                                          C1010200
*********************************************************************** C1010300
*        GR08 PROCESSING: CONSTRUCT LCD REQUESTING BASIC REPORT       * C1010400
*********************************************************************** C1010500
         SPACE 1                                                        C1010600
         USING GR08CARD,R6         ESTABLISH GR08 ADDRESSABILITY        C1010700
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C1010800
BLDLCDBS SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1010900
         LA    R2,LCDLGTH          SET LENGTH OF LCD IN WORK REG        C1011000
         GETCB LCD                 ACQUIRE AND CHAIN LCD                C1011100
         MVC   LCDIOLB,$SPACES     SET OCB LABEL TO BLANK ('PRINT')     C1011200
         OI    LCDSW,MLCDBSIC      SET FLAG FOR BASIC REPORT FORMAT     C1011300
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C1011400
         DROP  R6                  DROP LCD ADDRESSABILITY              C1011500
         DROP  R8                  DROP CONTROL CARD ADDRESSABILITY     C1011600
         EJECT                                                          C1011700
*********************************************************************** C1011800
*        GR08 PROCESSING: PROCESS TWENTY80 REPORT SPECIFICATION       * C1011900
*********************************************************************** C1012000
         SPACE 1                                                        C1012100
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         C1012200
         USING GR08CARD,R6         ESTABLISH GR06 ADDRESSABILITY        C1012300
BLDTW80  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1012400
         CLC   GR08TW80,$SPACES    IS TWENTY80 SPECIFICATION SUPPLIED   C1012500
         BE    BLDTW80X            EXIT IF NO SPECIFICATION             C1012600
         CBAL  R4,BLDTRD           CONSTRUCT TWENTY80 REPORT DEFINITION C1012700
         LTA   R8,$OCBOCB          GET ADDRESS OF 1ST OCB ('PRINT')     C1012800
         B     *+8                 BYPASS FORWARD CHAINING              C1012900
BLDTW801 LTA   R8,OCBOCB,BLDTW802  GET ADDRESS OF NEXT OCB              C1013000
         CLC   OCBLABL,=X'2080'    IS THIS THE DEFAULT TWENTY80 OCB     C1013100
         BE    BLDTW80X            BR IF YES: NOTHING TO DO             C1013200
         B     BLDTW801            LOOP BACK FOR NEXT OCB               C1013300
BLDTW802 CBAL  R4,OCBTW80          CONSTRUCT THE DEFAULT TWENTY80 OCB   C1013400
BLDTW80X SUBOUT RESTORE=R4         RESTORE AND RETURN TO CALLER         C1013500
         DROP  R6                  DROP GR08 ADDRESSABILITY             C1013600
         DROP  R8                  DROP OCB ADDRESSABILITY              C1013700
         EJECT                                                          C1013800
*********************************************************************** C1013900
*        GR08 PROCESSING: CONSTRUCT TRD                               * C1014000
*********************************************************************** C1014100
         SPACE 1                                                        C1014200
         USING GR08CARD,R6         ESTABLISH GR08 ADDRESSABILITY        C1014300
         USING TRDNTRY,R8          ESTABLISH TRD ADDRESSABILITY         C1014400
BLDTRD   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1014500
         LA    R2,TRDLGTH          SET LENGTH OF TRD IN REG 2           C1014600
         CBAL  R4,GETTRD           ACQUIRE AND CHAIN TRD CONTROL BLOCK  C1014700
         ZAP   TRDTNO,$ZERO        INITIALISE NO. OF ENTRIES IN FILE    C1014800
         ZAP   TRDKNO,$ZERO        INITIALISE NO. OF ENTRIES IN MAJOR K C1014900
         MVC   TRDDESC,GR08DESC    PRESERVE DESCRIPTION OF FIELD        C1015000
         MVC   TRDLAB,GR08TW80     PRESERVE LABEL OF FIELD TO BE STUDIE C1015100
         MVC   TRDLABA,GR08TW80    ASSUME SAME FIELD IS TO ACCUMULATED  C1015200
         MVC   TRDRID,=X'2080'     SET DEFAULT OCB LABEL                C1015300
         TM    SWPARM2,MPRMGR08    ARE ADDITIONAL GRO8 FIELDS SUPPLIED  C1015400
         BO    BLDTRD1             BR IF ADDITIONAL FIELDS SUPPLIED     C1015500
         CLC   GR08XTRA,$SPACES    ARE THE ADDITIONAL FIELDS NON-BLANK  C1015600
         BE    BLDTRD2             BR IF THE FIELDS ARE BLANK           C1015700
         LINE  LINE04              WARN THAT DATA WILL BE IGNORED       C1015800
         B     BLDTRD2             BR OVER EXTRA FIELD PROCESSING       C1015900
BLDTRD1  CLC   GR08ACUM,$SPACES    IS LABEL OF FIELD TO ACCUM SUPPLIED  C1016000
         BE    *+10                BR IF NOT                            C1016100
         MVC   TRDLABA,GR08ACUM    PRESERVE LABEL OF FIELD TO ACCUMULAT C1016200
         CLC   GR08RID,$SPACES     IS OCB LABEL SUPPLIED                C1016300
         BE    *+10                BR IF NOT                            C1016400
         MVC   TRDRID,GR08RID      PRESERVE OCB LABEL FOR LATER         C1016500
         CBAL  R4,BLDNOLO          PROCESS 'PRINT LOWER BOUND' SPEC'N   C1016600
BLDTRD2  EQU   *                   CONTINUE PROCESSING                  C1016700
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C1016800
         DROP  R8                  DROP TRD ADDRESSABILITY              C1016900
         DROP  R6                  DROP GR08 ADDRESSABILITY             C1017000
         EJECT                                                          C1017100
*********************************************************************** C1017200
*        GR08 PROCESSING: ACQUIRE AND CHAIN TRD CONTROL BLOCK         * C1017300
*********************************************************************** C1017400
         SPACE 1                                                        C1017500
         USING TRDNTRY,R8          ESTABLISH TRD ADDRESSABILITY         C1017600
GETTRD   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1017700
         GETCB TRD                 ACQUIRE AND CHAIN TRD                C1017800
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C1017900
         DROP  R8                  DROP TRD ADDRESSABILITY              C1018000
         EJECT                                                          C1018100
*********************************************************************** C1018200
*        GR08 PROCESSING: PROCESS 'PRINT LOWER BOUND' SPECIFICATION   * C1018300
*********************************************************************** C1018400
         SPACE 1                                                        C1018500
*        THE USER MAY SPECIFY THAT THE LOWER BOUND OF A RANGE IS NOT    C1018600
*         TO BE PRINTED ON THE TWENTY80 REPORT: IN SOME APPLICATIONS    C1018700
*         THE LOWER BOUND IS MEANINGLESS.  THIS ROUTINE PROCESSES THE   C1018800
*         RELEVANT GR08 SPECIFICATION.                                  C1018900
         SPACE 1                                                        C1019000
         USING TRDNTRY,R8          ESTABLISH TRD ADDRESSABILITY         C1019100
         USING GR08CARD,R6         ESTABLISH GR08 ADDRESSABILITY        C1019200
BLDNOLO  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1019300
         CLI   GR08NOLO,C' '       WAS SPECIFICATION BLANK              C1019400
         BE    BLDNOLOX            EXIT IF YES: NOTHING TO DO           C1019500
         CLI   GR08NOLO,C'Y'       IS LOWER BOUND TO BE PRINTED         C1019600
         BE    BLDNOLOX            EXIT IF YES: PRINTED BY DEFAULT      C1019700
         OI    TRDSW,MTRDNOLO      ASSUME THAT PRINTING IS TO BE SUPPRE C1019800
         CLI   GR08NOLO,C'N'       VERIFY THAT SPECIFICATION IS 'N'     C1019900
         BE    BLDNOLOX            EXIT IF YES: FLAGS SET AS NECESSARY  C1020000
         NI    TRDSW,FF-MTRDNOLO   SET OFF INCORRECTLY SET FLAG         C1020100
         LINE  LINE05              TELL OF UNKNOWN SPECIFICATION        C1020200
BLDNOLOX SUBOUT RESTORE=R4         RESTORE AND RETURN TO CALLER         C1020300
         DROP  R6                  DROP GR08 ADDRESSABILITY             C1020400
         DROP  R8                  DROP TRD ADDRESSABILITY              C1020500
         EJECT                                                          C1020600
*********************************************************************** C1020700
*        GR08 PROCESSING: CONSTRUCT DEFAULT TWENTY80 REPORT OCB       * C1020800
*********************************************************************** C1020900
         SPACE 1                                                        C1021000
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         C1021100
OCBTW80  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1021200
         LA    R2,OCBLGTH          SET LENGTH OF OCB IN REG 2           C1021300
         GETCB OCB                 ACQUIRE AND CHAIN OCB C/B            C1021400
         CHNCB OCB,HDG             SET POINTERS TO FACILITATE CHAINING  C1021500
         MVC   OCBLABL,=X'2080'    SET LABEL OF DEFAULT TWENTY80 OCB    C1021600
         LA    R1,TW80             GET ADDRESS OF ASSOCIATED DCBSW      C1021700
         ST    R1,OCBIOSW           ... AND PRESERVE IT IN THE OCB      C1021800
         ZAP   OCBPCNT,$ZERO       INITIALISE OCB PAGE COUNT            C1021900
         ZAP   OCBLCNT,$ZERO       ZEROISE LINE COUNTR STORE            C1022000
         MVC   OCBRID,$SPACES      BLANK OUT OPTIONAL REPORT ID         C1022100
         ZAP   OCBLPGE,=P'999'     INITIALISE OCB LINES/PAGE SPEC'N     C1022200
         MVI   OCBPCC,SPCE2AFT     SET TO DEFAULT DOUBLE SPACING        C1022300
         MVC   OCBLOGO,=CL8'PARROT' SET DEFAULT LOGO NAME               C1022400
         MVI   OCBHCNT+1,4         SET DEFAULT NO. OF HEADING LINES     C1022500
         OI    OCBSW,MOCBREPT+MOCB2080 FLAG THAT THIS IS A TW80 REPORT  C1022600
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C1022700
         DROP  R8                  DROP OCB ADDRESSABILITY              C1022800
         EJECT                                                          C1022900
*********************************************************************** C1023000
*        PROCESS GR09 CONTROL CARDS                                   * C1023100
*********************************************************************** C1023200
         SPACE 1                                                        C1023300
         USING GR09CARD,R6         ESTABLISH C/C ADDRESSABILITY         C1023400
         USING FDBNTRY,R8          ESTABLISH C/B ADDRESSABILITY         C1023500
CCGR09   SUBIN SAVE=R4             RESERVE RETURN ADDRESS               C1023600
         CLC   GR09UNUS,$SPACES    ENSURE THAT UNUSED COLUMNS ARE BLANK C1023700
         BE    CCGR0901            BR IF UNUSED COLUMNS ARE BLANK       C1023800
         LINE LINE02               TELL OF MISPLACED DATA               C1023900
CCGR0901 LA    R10,GR09VAL1        POINT AT FIRST BUCKET                C1024000
         LA    R9,7                SET TO LOOP THROUGH SEVEN BUCKETS    C1024100
         CBAL  R4,BLDRAE           GO CONSTRUCT RAE AS NECESSARY        C1024200
         LA    R10,L'GR09VAL1(,R10) INCREMENT POINTER TO NEXT BUCKET    C1024300
         BCT   R9,*-8              LOOP BACK IF ANY BUCKETS LEFT        C1024400
        SUBOUT RESTORE=R4          RESTORE AND REURN                    C1024500
         DROP  R6,R8               DROP ADDRESSABILITY                  C1024600
         EJECT                                                          C1024700
*********************************************************************** C1024800
*        PROCESS GR09 CARDS:       CONSTRUCT RAE                      * C1024900
*********************************************************************** C1025000
         SPACE 1                                                        C1025100
*        ON ENTRY, R10 CONTAINS THE ADDRESS OF THE CURRENT 'BUCKET'     C1025200
         SPACE 1                                                        C1025300
         USING RAENTRY,R8          ESTABLISH RAE ADDRESSABILITY         C1025400
BLDRAE   SUBIN SAVE=(R4,R9,R10)    PRESERVE CRITICAL REGISTERS          C1025500
         CLC   0(10,R10),$SPACES   IS THE BUCKET EMPTY                  C1025600
         BE    BLDRAEX             EXIT IF EMPTY                        C1025700
       NUMERIC 0(R10),10,SIGNED    VERIFY THAT THE DATA IS NUMERIC      C1025800
         BNE   BLDRAEX             EXIT IF VALUE NOT NUMERIC            C1025900
         ZAP   $FPDEC,$PVALUE      SET THE VALUE IN FLOATING POINT W/A  C1026000
         XC    $FPNDEC,$FPNDEC     SET NO. OF DECIMALS TO ZERO          C1026100
         CBAL  R4,CNVTOFP          CONVERT VALUE TO FLOATING POINT      C1026200
         LA    R2,RAELGTH          SET LENGTH OF BASIC RAE IN REG 2     C1026300
         LTA   R1,$KBDKBD,*+16     RETRIEVE ADDRESS OF KBD FOR MAJOR KE C1026400
         TM    KBDSW-KBDNTRY(R1),MKBD2080 IS MAJOR KEY SUBANALYSIS REQD C1026500
         BZ    *+8                 BR IF NO SUB-ANALYSIS IS REQUIRED    C1026600
         LA    R2,RAESLGTH         SET LENGTH OF RAE FOR SUBANALYSIS    C1026700
         CBAL  R4,GETRAE           ACQUIRE AND INITIALISE RAE           C1026800
         MVC   RAERVAL,$FPFP       PRESERVE UPPER RANGE LIMIT           C1026900
         CBAL  R4,CHAINRAE         CHAIN RAE INTO APPROPRIATE SLOT      C1027000
BLDRAEX SUBOUT RESTORE=(R4,R9,R10) RESTORE AND RETURN TO CALLER         C1027100
         DROP  R8                  DROP RAE ADDRESSABILITY              C1027200
         EJECT                                                          C1027300
*********************************************************************** C1027400
*        PROCESS GR09 CARDS:       ACQUIRE AND INITIALISE RAE         * C1027500
*********************************************************************** C1027600
         SPACE 1                                                        C1027700
         USING RAENTRY,R8          ESTABLISH RAE ADDRESSABILITY         C1027800
GETRAE   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C1027900
         GETCB RAE,NOCHAIN         ACQUIRE RAE BUT DO NOT CHAIN IT      C1028000
         ZAP   RAETNO,$ZERO        ZEROISE COUNT OF ENTRIES ON FILE     C1028100
         LTA   R1,$KBDKBD,*+18     GET ADDRESS OF 1ST KBD               C1028200
         TM    KBDSW-KBDNTRY(R1),MKBD2080 IS KEY SUBANAL REQUIRED       C1028300
         BZ    *+10                BR IF NO SUB-ANALYSIS REQUIRED       C1028400
         ZAP   RAEKNO,$ZERO        ZEROISE COUNT OF ENTRIES WITHIN KEY  C1028500
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C1028600
         DROP  R8                  DROP RAE ADDRESSABILITY              C1028700
         EJECT                                                          C1028800
*********************************************************************** C1028900
*        PROCESS GR09 CARDS:       INSERT RAE INTO CHAIN (BY VALUE)   * C1029000
*********************************************************************** C1029100
         SPACE 1                                                        C1029200
*        THE PURPOSE OF THIS ROUTINE IS TO INSERT THE NEW RAE INTO      C1029300
*         THE RAE CHAIN, QUEUING THE ENTRIES BY ASCENDING VALUE OF THE  C1029400
*         UPPER BOUND OF THE RANGE.                                     C1029500
         SPACE 1                                                        C1029600
*        REGISTER USAGE:-                                               C1029700
*         R1 - ADDRESS OF PREVIOUS RAE IN EXISTING CHAIN                C1029800
*         R2 - ADDRESS OF CURRENT RAE IN EXISTING CHAIN                 C1029900
*         R3 - ADDRESS OF NEXT ARE IN EXISTING CHAIN                    C1030000
*         R8 - ADDRESS OF THE NEW RAE TO BE PLACED IN THE CHAIN         C1030100
         SPACE 1                                                        C1030200
*        LOGIC:-                                                        C1030300
*              COMPARE NEW:CURRENT                                      C1030400
*              IF N > C THEN UPDATE POINTERS                            C1030500
*              IF N = C THEN TELL OF DUPLICATE VALUE                    C1030600
*              IF N < C THEN INSERT N BETWEEN PREVIOUS AND CURRENT      C1030700
         EJECT                                                          C1030800
*********************************************************************** C1030900
*        PROCESS GR09 CARDS:       INSERT RAE IN CHAIN ( CONT'D )     * C1031000
*********************************************************************** C1031100
         SPACE 1                                                        C1031200
         USING TRDNTRY,R7          ESTABLISH TRD ADDRESSABILITY         C1031300
         USING RAENTRY,R8          ESTABLISH RAE ADDRESSABILITY         C1031400
CHAINRAE SUBIN SAVE=(R4)           PRESERVE RETURN ADDRESS              C1031500
         LTA   R7,$PTRTRD          RETRIEVE ADDRESS OF CURRENT TRD      C1031600
         NC    TRDRAE+1(3),TRDRAE+1 TEST IF THIS IS THE 1ST RAE         C1031700
         BNZ   CHAINR1             BR IF THIS IS NOT THE 1ST RAE        C1031800
         ST    R8,TRDRAE           SET THIS RAE AS THE ONLY ON THIS TRD C1031900
         B     CHAINRX             GO TO EXIT                           C1032000
CHAINR1  LA    R1,TRDRAE           GET ADDRESS OF HEAD OF CHAIN (PREV)  C1032100
         L     R2,TRDRAE           GET ADDRESS OF 1ST RAE (CURRENT)     C1032200
         L     R3,RAERAE-RAENTRY(R2) GET ADDRESS OF 2ND RAE (NEXT)      C1032300
         MVC   $FPFP,RAERVAL       MOVE RAE UPPER BOUND TO FP W/A       C1032400
         LD    FPR0,$FPFP          SET UPPER BOUND IN FP REG 0          C1032500
CHAINR2  CD    FPR0,RAERVAL-RAENTRY(R2) COMPARE 'NEW' : 'CURRENT'       C1032600
         BL    CHAINRI             GO TO INSERT IF 'NEW' IS LOW         C1032700
         BE    CHAINRE             GO TELL OF DUPLICATE IF VALUES EQUAL C1032800
         NC    RAERAE-RAENTRY+1(3,R2),RAERAE-RAENTRY+1(R2) END OF CHN ? C1032900
         BNZ   CHAINR3             BR IF NOT END OF CHAIN               C1033000
         ST    R8,RAERAE-RAENTRY(R2) POINT 'CURRENT' AT 'THIS'          C1033100
         B     CHAINRX             GO TO EXIT                           C1033200
CHAINR3  LR    R1,R2               SET 'CURRENT' AS 'PREV'              C1033300
         LR    R2,R3               SET 'NEXT' AS 'CURRENT'              C1033400
         L     R3,RAERAE-RAENTRY(R2)  GET ADDRESS OF NEXT               C1033500
         B     CHAINR2             LOOP BACK TO TEST AGAIN              C1033600
CHAINRE  LINE  LINE03              TELL OF DUPLICATE VALUE              C1033700
         B     CHAINRX             GO TO EXIT                           C1033800
CHAINRI  ST    R8,RAERAE-RAENTRY(R1) POINT 'PREVIOUS' AT 'THIS'         C1033900
         ST    R2,RAERAE           POINT 'THIS' AT 'CURRENT'            C1034000
CHAINRX SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C1034100
         DROP  R7                  DROP TRD ADDRESSABILITY              C1034200
         EJECT                                                          C1034300
*********************************************************************** C1034400
*        CONVERT BINARY POSITION TO EBCDIC                            * C1034500
*********************************************************************** C1034600
         SPACE 1                                                        C1034700
CONVPOS  SUBIN ,                   ENTER ROUTINE                        C1034800
         MVC   $WRKSNGL,BLDMASK    MOVE EDIT MASK TO WORK AREA          C1034900
         CVD   R1,$WRKDUBL         CONVERT VALUETO DECIMAL              C1035000
         ED    $WRKSNGL,$WRKDUBL+6 RETURN TO CALLER                     C1035100
        SUBOUT ,                   RETIRN TO CALLER                     C1035200
         TITLE 'PARROTC1 - ERROR MESSAGES'                              C1035300
LINE01   DC    C'PTC1-001-W  COLUMNS 51-75 OF A GR08 CARD ARE UNASSIGNEDC1035400
               D AND SHOULD BE BLANK: POTENTIALLY MISPLACED DATA'       C1035500
LINE02   DC    C'PTC1-002-W  COLUMNS 5-10 OF A GR09 CARD ARE UNASSIGNED,C1035600
                AND SHOULD BE BLANK: POTENTIALLY MISPLACED DATA'        C1035700
LINE03   DC    C'PTC1-003-W  DUPLICATE RANGE VALUE ENCOUNTERED: VALUE IGC1035800
               GNORED'                                                  C1035900
LINE04   DC    C'PTC1-004-W  COLUMNS 77-80 OF THE PRECEDING GR08 CARD COC1036000
               ONTAIN DATA BUT PARM=GR08XTRA NOT SPECIFIED: DATA IGNOREDC1036100
               D'                                                       C1036200
LINE05   DC    C'PTC1-005-W  COLUMN 75 CONTAINS AN UNKNOWN SPECIFICATIO*C1036300
               N FOR INCLUSION/EXCLUSION OF LOWER BOUND IN REPORT: IGNO*C1036400
               RED'                                                     C1036500
         TITLE 'PARROTC1 - MISCELLANEOUS CONSTANTS AND FIELDS'          C1036600
*********************************************************************** C1036700
*        EQUATES AND FIELDS DEFINING THE BASIC REPORT FORMAT          * C1036800
*********************************************************************** C1036900
         SPACE 1                                                        C1037000
BSICFLDF EQU   FLCDNUMZ            FIELD FORMAT: UNEDITED NUMERIC       C1037100
BSICFLEN EQU   13                  EXECUTABLE LGTH OF OUTPUT PRINT FLD  C1037200
BSICSPOS EQU   62                  DISPLACEMENT OF 1ST NUMERIC FIELD    C1037300
         SPACE 1                                                        C1037400
         DS    0H                  ENSURE ALIGNMENT OF FOLLOWING FIELD  C1037500
OUTFLDP  DC    AL2(BSICSPOS)       DISPLACEMENT OF NEXT NUMERIC FIELD   C1037600
         SPACE 5                                                        C1037700
BLDMASK  DC    X'40212020'         MASK TO CONVERT POSN TO EBCDIC       C1037800
         SPACE 5                                                        C1037900
        SCSEND C1                  TERMINATE CSET PARROTC1              C1038000
         END                                                            C1038100
