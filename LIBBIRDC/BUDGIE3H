         TITLE 'BUDGIE3H - INTERNAL MACROS'                             H 000010
         MACRO                                                          H 000020
         $CB   &PRINT=OFF                                               H 000030
         PUSH PRINT                PRESERVE CURRENT PRINT STATUS        H 000040
         PRINT &PRINT              ESTABLISH NEW PRINT STATUS           H 000050
               TITLE 'CBPREFIX - CONTROL BLOCK PREFIX'                  H 000060
      CBPREFIX ,                   GENERATE DSECT                       H 000070
               TITLE 'CB02 - CB02 CONTROL BLOCK'                        H 000080
         CB02  ,                   GENERATE DSECT                       H 000090
               TITLE 'CB04 - CB04 CONTROL BLOCK'                        H 000100
         CB04  ,                   GENERATE DSECT                       H 000110
               TITLE 'CB05 - CB05 CONTROL BLOCK'                        H 000120
         CB05  ,                   GENERATE DSECT                       H 000130
               TITLE 'CB13 - CB13 CONTROL BLOCK'                        H 000140
         CB13  ,                   GENERATE DSECT                       H 000150
               TITLE 'CB32 - CB32 CONTROL BLOCK'                        H 000160
         CB32  ,                   GENERATE DSECT                       H 000170
               TITLE 'CCB  - CENTRAL CONTROL BLOCK'                     H 000180
         CCB   ,                   GENERATE DSECT                       H 000190
               TITLE '     - BATCH BALANCING REPORT LINES'              H 000200
      DETAILBB ,                   GENERATE DSECTS                      H 000210
               TITLE '     - GENERAL EQUATES'                           H 000220
       EQUATES ,                   GENERATE EQUATES                     H 000230
               TITLE 'SB11 - SECONDARY CB11 CONTROL BLOCK'              H 000240
         SB11  ,                   GENERATE DSECT                       H 000250
               TITLE '     - USER ROUTINE LINKAGE AREA'                 H 000260
      URTNPARM ,                   GENERATE DSECT                       H 000270
               TITLE '     - UT460 LINKAGE AREA'                        H 000280
         UT460 ,                   GENERATE DSECT                       H 000290
         POP   PRINT               RESTORE ORIGINAL PRINT STATUS        H 000300
         MEND                                                           H 000310
         SPACE 5                                                        H 000320
BUDGIE3H CSECT                                                          H 000330
         $CB   ,                   GENERATE DSECTS AND EQUATES          H 000340
         TITLE 'BUDGIE3H - TESTS AND OPERATIONS'                        H 000350
        SCSECT H                   DEFINE START OF CSECT BUDGIE3H       H 000360
         ENTRY CTESTL9             CURRENCY CONVERSION, ETC             H 000370
         ENTRY TESTSL2             SELECT AND PERFORM APPROPRIATE ROUTI H 000380
         ENTRY WEEK0               'WEEK0' STATUS FOR UT458 - DATE RTN  H 000390
         SPACE 1                                                        H 000400
         USING DETAILBB,R7         ESTABLISH B/B REPORT ADDRESSABILITY  H 000410
         TITLE 'BUDGIE3H - SELECT APPROPRIATE ROUTINE'                  H 000420
*********************************************************************** H 000430
*        SELECT APPROPRITAE ROUTINE                                   * H 000440
*********************************************************************** H 000450
         SPACE 1                                                        H 000460
*        REGISTERS ARE NOW SET THUS :-                                  H 000470
*              R15 - END OF FIELD TO BE TESTED                          H 000480
*              R14 - START OF FIELD TO BE TESTED                        H 000490
*              R1  - EXECUTABLE LEBGTH                                  H 000500
*              R2  - ADDRESS OF ASSOC C/B ( WHERE RELEVANT )            H 000510
*              R4  - TEST CODE * 4 ( INDEX )                            H 000520
*              R6  - POINTS AT FIELD IN RECORD                          H 000530
*              R8  - ADDRESS OF CURRENT CB13                            H 000540
*              R10 - ADDRESS OF CURRENT CB02                            H 000550
         SPACE 1                                                        H 000560
TESTSL2  SUBIN ,                   ENTER ROUTINE                        H 000570
         SH    R4,=H'100'          DECREMENT INDEX: 1ST 25 TESTS DONE   H 000580
         B     TESTBR(R4)          SELECT APPROPRIATE ROUTINE           H 000590
TESTBR   EQU   *                   BASE FOR INDEXED BRANCH              H 000600
         B     LOGICER             TEST TYPE C6 - SUPPORTED IN BUDGIE3G H 000610
         B     LOGICER             TEST TYPE C7 - SUPPORTED IN BUDGIE3G H 000620
         B     LOGICER             TEST TYPE C8 - SUPPORTED IN BUDGIE3G H 000630
         B     LOGICER             TEST TYPE R1 - SUPPORTED IN BUDGIE3G H 000640
         B     LOGICER             TEST TYPE R2 - SUPPORTED IN BUDGIE3G H 000650
         B     TESTR3              TEST TYPE R3 - SUPPORTED             H 000660
         B     TESTR4              TEST TYPE R4 - SUPPORTED             H 000670
         B     TESTR5              TEST TYPE R5 - SUPPORTED             H 000680
         B     TESTR6              TEST TYPE R6 - SUPPORTED             H 000690
         B     TESTR7              TEST TYPE R7 - SUPPORTED             H 000700
         B     TESTR8              TEST TYPE R8 - SUPPORTED             H 000710
         B     TESTR9              TEST TYPE R8 9 SUPPORTED             H 000720
         B     TESTD1              TEST TYPE D1 - SUPPORTED             H 000730
         B     TESTD2              TEST TYPE D2 - SUPPORTED             H 000740
         B     TESTD3              TEST TYPE D3 - SUPPORTED             H 000750
         B     TESTD4              TEST TYPE D4 - SUPPORTED             H 000760
         B     TESTD5              TEST TYPE D5 - SUPPORTED             H 000770
         B     TESTD6              TEST TYPE D6 - SUPPORTED             H 000780
         B     TESTD7              TEST TYPE D7 - SUPPORTED             H 000790
         B     LOGICER             TEST TYPE D8 - UNALLOCATED           H 000800
         B     LOGICER             TEST TYPE D9 - UNALLOCATED           H 000810
         B     LOGICER             TEST TYPE I  - SUPPORTED IN BUDGIE3G H 000820
         B     LOGICER             TEST TYPE JX - UNSUPPORTED           H 000830
         B     LOGICER             TEST TYPE C9 - UNSUPPORTED           H 000840
         B     LOGICER             TEST TYPE E1 - SUPPORTED IN BUDGIE3G H 000850
         EJECT                                                          H 000860
*********************************************************************** H 000870
*        RETURN TO BUDGIE3G FOR TEST TERMINATION PROCESSING           * H 000880
*********************************************************************** H 000890
         SPACE 1                                                        H 000900
TESTEXIT CBAL  ,TESTRTN            PREPARE TO RETURN TO CALLER          H 000910
         SPACE 5                                                        H 000920
*********************************************************************** H 000930
*        ABEND IF GAIN CONTROL FOR TEST PERFORMED IN BUDGIE3G         * H 000940
*********************************************************************** H 000950
         SPACE 1                                                        H 000960
LOGICER  ABEND 80,FULL             SHOULD NEVER HAPPEN                  H 000970
         TITLE 'BUDGIE3H - PROCESSING A BYTE OF SWITCHES'               H 000980
*********************************************************************** H 000990
*        R3 - EXPAND BYTE OF SWITCHES                                 * H 001000
*********************************************************************** H 001010
         SPACE 1                                                        H 001020
TESTR3   SUBIN ,                   ENTER ROUTINE                        H 001030
         MVC   1(8,R6),$SPACES      BLANK OUT RECEIBING AREA            H 001040
         TM    0(R6),X'01'         IS SWITCH ON                         H 001050
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001060
         MVI   8(R6),C'1'          SET CORRESPONDING BYET ON            H 001070
         TM    0(R6),X'02'         IS SWITCH ON                         H 001080
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001090
         MVI   7(R6),C'1'          SET CORRESPONDING BYET ON            H 001100
         TM    0(R6),X'04'         IS SWITCH ON                         H 001110
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001120
         MVI   6(R6),C'1'          SET CORRESPONDING BYET ON            H 001130
         TM    0(R6),X'08'         IS SWITCH ON                         H 001140
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001150
         MVI   5(R6),C'1'          SET CORRESPONDING BYET ON            H 001160
         TM    0(R6),X'10'         IS SWITCH ON                         H 001170
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001180
         MVI   4(R6),C'1'          SET CORRESPONDING BYET ON            H 001190
         TM    0(R6),X'20'         IS SWITCH ON                         H 001200
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001210
         MVI   3(R6),C'1'          SET CORRESPONDING BYET ON            H 001220
         TM    0(R6),X'40'         IS SWITCH ON                         H 001230
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001240
         MVI   2(R6),C'1'          SET CORRESPONDING BYET ON            H 001250
         TM    0(R6),X'80'         IS SWITCH ON                         H 001260
         BZ    *+8                 BR IF SWITCH IS OFF                  H 001270
         MVI   1(R6),C'1'          SET CORRESPONDING BYET ON            H 001280
        SUBOUT RETURN=TESTEXIT     PREPARE TO RETRUN TO CALLER          H 001290
         EJECT                                                          H 001300
*********************************************************************** H 001310
*        R4 - COMPRESS 8 FLAGS INTO ONE BYTE                          * H 001320
*********************************************************************** H 001330
         SPACE 1                                                        H 001340
TESTR4   SUBIN ,                   ENTER ROUTINE                        H 001350
         MVI   0(R6),X'00'         BLANK OUT RECEIVING AREA             H 001360
         CLI   1(R6),C' '          IS SWITCH/FLAG ON                    H 001370
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001380
         OI    0(R6),X'80'         SET CORRESPONDING BIT ON             H 001390
         CLI   2(R6),C' '          IS SWITCH/FLAG ON                    H 001400
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001410
         OI    0(R6),X'40'         SET CORRESPONDING BIT ON             H 001420
         CLI   3(R6),C' '          IS SWITCH/FLAG ON                    H 001430
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001440
         OI    0(R6),X'20'         SET CORRESPONDING BIT ON             H 001450
         CLI   4(R6),C' '          IS SWITCH/FLAG ON                    H 001460
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001470
         OI    0(R6),X'10'         SET CORRESPONDING BIT ON             H 001480
         CLI   5(R6),C' '          IS SWITCH/FLAG ON                    H 001490
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001500
         OI    0(R6),X'08'         SET CORRESPONDING BIT ON             H 001510
         CLI   6(R6),C' '          IS SWITCH/FLAG ON                    H 001520
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001530
         OI    0(R6),X'04'         SET CORRESPONDING BIT ON             H 001540
         CLI   7(R6),C' '          IS SWITCH/FLAG ON                    H 001550
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001560
         OI    0(R6),X'02'         SET CORRESPONDING BIT ON             H 001570
         CLI   8(R6),C' '          IS SWITCH/FLAG ON                    H 001580
         BE    *+8                 BR IF SWITCH/FLAG NOT ON             H 001590
         OI    0(R6),X'01'         SET CORRESPONDING BIT ON             H 001600
        SUBOUT RETURN=TESTEXIT     PREPARE TO RETURN TO CALLER          H 001610
         TITLE 'BUDGIE3H - CALL USER EXIT ( ITERATIVELY )'              H 001620
*********************************************************************** H 001630
*        R5 - CALL USER EXIT ( ITERATIVELY )                          * H 001640
*********************************************************************** H 001650
         SPACE 1                                                        H 001660
TESTR5   SUBIN ,                   ENTER ROUTINE                        H 001670
         TM    CB02SW-CB02NTRY(R10),SW02URTN DOES RTN EXIST             H 001680
         BZ    TESTR5X             BR IF NO RTN TO EXIT                 H 001690
         L     R1,$PTRPRMU         POINT R1 AT LINKAGE AREA             H 001700
         MVI   URTNCSW-URTNPARM(R1),C'5' SET CALL TYPE IN LINKAGE AREA  H 001710
         CBAL  R4,URTCALL          EXECUTE USER ROUTINE                 H 001720
TESTR5X SUBOUT RETURN=TESTEXIT     PREPARE TO RETURN TO CALLER          H 001730
         TITLE 'BUDGIE3H - CONDITIONAL B/C AND C/R ACCUMULATION'        H 001740
*********************************************************************** H 001750
*        R6-R9 - CONDITIONAL B/C AND C/R ACCUMULATION                 * H 001760
*********************************************************************** H 001770
         SPACE 1                                                        H 001780
         USING CB02NTRY,R10        ESTABLISH CB02 ADDRESSABILITY        H 001790
         USING SB11NTRY,R8         ESTABLISH SB11 ADDRESSABILITY        H 001800
TESTR6   EQU   *                   ENTER ROUTINE                        H 001810
TESTR7   EQU   *                   ENTER ROUTINE                        H 001820
TESTR8   EQU   *                   ENTER ROUTINE                        H 001830
TESTR9   EQU   *                   ENTER ROUTINE                        H 001840
TESTR6R9 SUBIN SAVE=R8,FLOWTHR=YES PRESERVE ADDRESS OF CB13 C/B         H 001850
        BBMESS 03                  SET UP 'NOT NUMERIC' MESSAGE         H 001860
         MVC   R6R9TMOD,CB13TMOD-CB13NTRY(R8)  PRESERVE TEST MODIFIER   H 001870
         LR    R8,R2               POINT R8 AT SB11 (FROM CB13CBXX)     H 001880
         TM    SB11SW,SW11INPD     IS THE INPUT PACKED                  H 001890
         BZ    TESTR601            BR IF FLD NOT PACKED                 H 001900
         L     R15,SB11CB05        POINT R15 AT ASSOC CB05 IF ANY       H 001910
         TM    SB11SW,SW11DBCN     IS THIS A B/C ACCUM                  H 001920
         B     *+24                BYPASS ACCUM IF NOT                  H 001930
         CLI   R6R9TMOD,C'T'       ARE WE TO ACCUM TOTAL                H 001940
         BNE   *+12                BR IF NOT TO ACCUM DETAIL            H 001950
         EX    R1,SB11AT05         ADD VALUE TO TOTAL ACCUM             H 001960
         B     *+8                 BYPASS ACCUMING INTO DETAIL          H 001970
         EX    R1,SB11AP05         ADD VLUE TO CB05 ACCUMULATOR         H 001980
         L     R15,SB11CB04        GET ADDR OF ASSOC CB04 IF ANY        H 001990
         TM    SB11SW,SW11DCRN     IS THIS A C/R ACCUM                  H 002000
         BZ    *+8                 BYPSS ACCUM IF NOT                   H 002010
         EX    R1,SB11AP04         ADD VALUE TO CB04 ACCUM              H 002020
         B     TESTR6X             PREPARE TO RETURN TO CALLER          H 002030
         EJECT                                                          H 002040
*********************************************************************** H 002050
*        RL-R. - CONDITIONAL B/C AND C/R ACCUMULATION ( CONT'D )      * H 002060
*********************************************************************** H 002070
         SPACE 1                                                        H 002080
TESTR601 EX    R1,TESTPACK         ASSUME NUMERIC AND PACK INTO W/A     H 002090
         CBAL  R4,TESTSN           TES IF FIELD IS NUMERIC              H 002100
         BH    TESTR6Y             GO INDICATE ERROR IF NOT NUMERIC     H 002110
         USING CB05NTRY,R1         ESTABLISH CB05 ADDRESSABULITY        H 002120
         L     R1,SB11CB05         POINT AT ASSOC CB05 IF ANY           H 002130
         TM    SB11SW,SW11DBCN     TEST IF THIS IS B/C ACCUM            H 002140
         BZ    *+28                BYPASS ACCUM IF NOT                  H 002150
         CLI   R6R9TMOD,C'T'       ARE WE TO ACCUMULATE 'TOTAL'         H 002160
         BNE   *+14                BR IF NOT TO ACCUM DETAIL            H 002170
         AP    CB05BVAL,$WRKPACK   ADD INTO 'TOTAL' FIELD               H 002180
         B     *+10                BYPASS ACCUMULATING INTO DETAIL      H 002190
         AP    CB05BACC,$WRKPACK   ADD TO B/C ACCUMULATOR               H 002200
         DROP  R1                  DROP CB05 ADDRESSABILITY             H 002210
         USING CB04NTRY,R1         ESTABLISH CB04 AFDRESSABILITY        H 002220
         L     R1,SB11CB04         POINT R1 AT ASSOC CB04 IF ANY        H 002230
         TM    SB11SW,SW11DCRN     TEST IF THIS IS C/R ACCUM            H 002240
         BZ    *+10                BYPASS ACCUM IF NOT                  H 002250
         AP    CB04BACC,$WRKPACK   ADD TO C/R ACCUM                     H 002260
         DROP  R1                  DROP CB04 ADDRESSABILITY             H 002270
         B     TESTR6X             BYPASS INDICTAING ARROR              H 002280
TESTR6Y  MVI   $TSTTSTX,C'Y'       SHOW FIELD NOT NUMERIC               H 002290
TESTR6X SUBOUT RESTORE=R8,RETURN=TESTEXIT RESORE AND PREPARE TO RETURN  H 002300
         DROP  R8                  DROP SB11 ADDRESSABILIY              H 002310
         TITLE 'BUDGIE3H - DATE VALIDATION ROUTINES'                    H 002320
*********************************************************************** H 002330
*        D1 - VALIDATE DATE IN FORM 'MMDDYY'                          * H 002340
*********************************************************************** H 002350
         SPACE 1                                                        H 002360
TESTD1   SUBIN ,                   ENTER ROUTINE                        H 002370
         MVI   UTOPCODE,C'1'       SET DATE TYPE                        H 002380
         MVC   UTMMDDYY,$TSTAREA   ADD DATE TO LINKAGE AREA             H 002390
         B     TESTDATE            GO VALIDATE DATE                     H 002400
         SPACE 5                                                        H 002410
*********************************************************************** H 002420
*        D2 - VALIDATE DATE IN FORM 'DDMMYY'                          * H 002430
*********************************************************************** H 002440
         SPACE 1                                                        H 002450
TESTD2   SUBIN ,                   ENTER ROUTINE                        H 002460
         MVI   UTOPCODE,C'2'       SET DATE TYPE                        H 002470
         MVC   UTDDMMYY,$TSTAREA   ADD DATE TO LINKAGE AREA             H 002480
         B     TESTDATE            GO VALIDATE DATE                     H 002490
         SPACE 5                                                        H 002500
*********************************************************************** H 002510
*        D3 - VALIDATE DATE IN FORM 'YYMMDD'                          * H 002520
*********************************************************************** H 002530
         SPACE 1                                                        H 002540
TESTD3   SUBIN ,                   ENTER ROUTINE                        H 002550
         MVI   UTOPCODE,C'3'       SET DATE TYPE                        H 002560
         MVC   UTYYMMDD,$TSTAREA   ADD DATE TO LINKAGE AREA             H 002570
         B     TESTDATE            GO VALIDATE DATE                     H 002580
         SPACE 5                                                        H 002590
*********************************************************************** H 002600
*        D4 - VALIDATE DATE IN FORM 'DWWYY'                           * H 002610
*********************************************************************** H 002620
         SPACE 1                                                        H 002630
TESTD4   SUBIN ,                   ENTER ROUTINE                        H 002640
         MVI   UTOPCODE,C'4'       SET DATE TYPE                        H 002650
         MVC   UTDWWYY,$TSTAREA    ADD DATE TO LINKAGE AREA             H 002660
         B     TESTDATE            GO VALIDATE DATE                     H 002670
         EJECT                                                          H 002680
*********************************************************************** H 002690
*        D5 - VALIDATE DATE IN FORM 'YYWWD'                           * H 002700
*********************************************************************** H 002710
         SPACE 1                                                        H 002720
TESTD5   SUBIN ,                   ENTER ROUTINE                        H 002730
         MVI   UTOPCODE,C'5'       SET DATE TYPE                        H 002740
         MVC   UTYYWWD,$TSTAREA    ADD DATE TO LINKAGE AREA             H 002750
         B     TESTDATE            GO VALIDATE DATE                     H 002760
         SPACE 5                                                        H 002770
*********************************************************************** H 002780
*        D6 - VALIDATE DATE IN FORM 'YYDDD'                           * H 002790
*********************************************************************** H 002800
         SPACE 1                                                        H 002810
TESTD6   SUBIN ,                   ENTER ROUTINE                        H 002820
         MVI   UTOPCODE,C'6'       SET DATE TYPE                        H 002830
         MVC   UTYYDDD,$TSTAREA    ADD ADTE TO LINKAGE AREA             H 002840
         B     TESTDATE            GO VALIDATE DATE                     H 002850
         SPACE 5                                                        H 002860
*********************************************************************** H 002870
*        PERFORM COMMON DATE VALIDATION                               * H 002880
*********************************************************************** H 002890
         SPACE 1                                                        H 002900
TESTDATE SUBIN ,                   ENTER ROUTINE                        H 002910
        BBMESS 08                  SET UP DEFAULT ERROR MESSAGE IN CASE H 002920
         LA    R1,UTPARMAD         POINT AT LINKAGE AREA                H 002930
         L     R15,$ADUT458        GET ADDRESS OF ROUTINE               H 002940
         MVC   UTHHMMSS,WEEK0      ESTABLISH WEEK0/NOWEEK0 STATUS       H 002950
         BALR  R14,R15             EXECUTE UT458 TI VALIDATE DATE       H 002960
         CLI   UTRTCODE,C'0'       TEST IF DATE VALID                   H 002970
         BE    *+8                 BR IFVALID                           H 002980
         MVI   $TSTTSTX,C'Y'       SHOW DATE INVALID                    H 002990
        SUBOUT RETURN=TESTEXIT     PREPARE TO RETURN TO CALLER          H 003000
         EJECT                                                          H 003010
*********************************************************************** H 003020
*        D7 - ENSURE THAT MONTH NUMBER EQUALS CURRENT                 * H 003030
*********************************************************************** H 003040
         SPACE 1                                                        H 003050
TESTD7   SUBIN ,                   ENTER ROUTINE                        H 003060
        BBMESS 18                  SET UP DEFAULT ERROR MESSAGE         H 003070
         CLC   $MMDDYY(2),$TSTAREA COMPARE WITH IPL MONTH NUMBER        H 003080
         BE    *+8                 BR IF SAME                           H 003090
         MVI   $TSTTSTX,C'Y'       SHOW TEST NOT SATISFIED              H 003100
        SUBOUT RETURN=TESTEXIT     PREPARE TO RETURN TO CALLER          H 003110
         EJECT                                                          H 003120
*********************************************************************** H 003130
*        UT458 ( DATE ROUTINE ) LINKAGE AREA                          * H 003140
*********************************************************************** H 003150
         SPACE 1                                                        H 003160
*        NOTE THAT THIS MACRO EXPANSION IS NEVER EXECUTED               H 003170
         SPACE 1                                                        H 003180
         UT458 ,                                                        H 003190
         TITLE 'BUDGIE3H - CURRENCY CONVERSION'                         H 003200
*********************************************************************** H 003210
*        L9 - CURRENCY CONVERSION, ETC                                * H 003220
*********************************************************************** H 003230
         SPACE 1                                                        H 003240
         USING CB32NTRY,R2         ESTABLISH CB32 ADDRESSABILITY        H 003250
         USING CB13NTRY,R8         ESTABLISH CB13 ADDRESSABILITY        H 003260
CTESTL9  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              H 003270
         TM    CB32SW1,SW321IVS    DOES INPUT COME FROM MS13            H 003280
         BZ    *+10                BR IF NOT                            H 003290
         MVC   CB32INP(2),CB13PFLD RETRIEVE PL FROM CB13                H 003300
         TM    CB32SW2,SW322OVS    DOES OUTPUT POSN COME FROM CB13      H 003310
         BZ    *+10                BR IF NOT                            H 003320
         MVC   CB32OUTP(2),CB13PFLD RETRIRVE PL FROM CB13               H 003330
         TM    CB32SW2,SW322RTS    DOES RATE COME FROM CB13             H 003340
         BZ    *+10                BR IF NOT                            H 003350
         MVC   CB32RTEP(2),CB13PFLD RETRIEVE PL FROM CB13               H 003360
         CBAL  R4,SETZERO          ZEROISE RESULT FIELDS LEST ERROR     H 003370
         CBAL  R4,GETRATE          DETERMINE CURRENCY CONVERSION RATE   H 003380
        BBMESS 19                  SET UP DEFAULT ERROR MEESSAGE        H 003390
         CBAL  R4,SETRATE          SET UP VERSION NUMBER, NAMES, RATE   H 003400
         CBAL  R4,CONVERT          PERFORM CURRENCY CONVERSION          H 003410
         B     *+8                 BYPASS FLAGGING AS ERROR             H 003420
TESTL9Y  MVI   $TSTTSTX,C'Y'       FLAG THAT CONVERSION FAILED          H 003430
TESTL9X SUBOUT RESTORE=R4          RESTORE AND RETURN                   H 003440
         DROP  R2                  DROP C/B ADDRESSABILITY              H 003450
         DROP  R8                  DROP C/B ADDRESSABILITY              H 003460
         EJECT                                                          H 003470
*********************************************************************** H 003480
*        SET L9 RESULT FIELDS TO ZERO LEST ERROR                      * H 003490
*********************************************************************** H 003500
         SPACE 1                                                        H 003510
         USING CB32NTRY,R2         ESTABLISH C/B ADDRESSABILITY         H 003520
SETZERO  SUBIN ,                   ZEROISE RESULT IF NESESSARY          H 003530
         TM    CB32SW1,SW321ZRO    ARE WE TO ZEROISE IF ERROR           H 003540
         BZ    SETZEROX            EXIT FROM ROUTINE IF NO              H 003550
         TM    CB32SW1,SW321NOC    IS THERE ANY CONVERSION TO BE DOCE   H 003560
         BO    SETZERO2            BYPASS ZEROISING ABSENT FIELD        H 003570
         PTR   CB32OUTP,CB32OUTL   GET ADDRESS & LENGTH OF OUTPUT       H 003580
         TM    CB32SW2,SW322OVP    IS OUTPUT VALUE PACKED               H 003590
         BO    SETZERO1            BR IF VALUE PACKED                   H 003600
         EX    R1,L9ZERO           MOVE ZEROES TO OUTPUT AMOUNT         H 003610
         B     SETZERO2            GO ZEROISE RATE IF ANY               H 003620
SETZERO1 SLL   R1,4                MOVE LENGTH 4 BITS TO THE LEFT       H 003630
         EX    R1,L9ZEROP          MOVE ZEROES TO OUTPUT AMOUNT         H 003640
SETZERO2 TM    CB32SW1,SW321RTE    IS A CONVERSION RATE REQUIRED        H 003650
         BZ    SETZEROX            BYPASS ZEROISING IT IF NOT           H 003660
         PTR   CB32RTEP,CB32RTEL   GET ADDESS & LGTH OF FIELF           H 003670
         TM    CB32SW2,SW322RTP    IS RATE TO BE PACKED                 H 003680
         BO    SETZERO3            BR IF RATE PACKED                    H 003690
         EX    R1,L9ZERO           ZEROISE EBCDEC CONVERSION RATE       H 003700
         B     SETZEROX            PREPARE TO LEAVE                     H 003710
SETZERO3 SLL   R1,4                MOVE LENGTH TO THE LEFT              H 003720
         EX    R1,L9ZEROP          MOVE ZEROES TO PACKED FIELD          H 003730
SETZEROX SUBOUT ,                  RETRUN TO CALLER                     H 003740
         DROP  R2                  DROP C/B ADDRESSABLIITY              H 003750
         EJECT                                                          H 003760
*********************************************************************** H 003770
*        OBTAIN CURRENCY CONVERSION RATE                              * H 003780
*********************************************************************** H 003790
         SPACE 1                                                        H 003800
         USING CB32NTRY,R2         ESTABLISH C/B ADDRESSABILITY         H 003810
         USING UT460LNK,R1         ESTABLISH L/A ADDRESSABILITY         H 003820
GETRATE  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              H 003830
        BBMESS 09                  SET UP DEFAULT EROR MESSAGE          H 003840
         L     R1,$PMUT460         GET ADDRESS OF LINKAGE AREA          H 003850
         MVC   UT460INV(4),=C'RATE' ESTABLISH AS REQUEST FOR RATE       H 003860
         MVC   UT460INC,CB32ICDE   ASSUME INPUT CODE IS A LITERAL       H 003870
         TM    CB32SW1,SW321ICL    TEST IF INPUT CODE IS A LITERAL      H 003880
         BO    GETRATE2            BR IF INPUT CODE IS A LITERAL        H 003890
         MVC   UT460INC,$TSTAREA   ASSUME INPUT CODE IS IN MS13 CARD    H 003900
         TM    CB32SW1,SW321ICS    TEST IF INPUT CODE IF MS13 CARD      H 003910
         BO    GETRATE2            BR IF INPUT CODE IN CB13             H 003920
         PTR   CB32ICP             GET ADDRESS OF INPUT CODE IN RECORD  H 003930
         CBAL  R4,CC19TO98         CONVERT CODE 19 TO 98 IF REQUESTED   H 003940
         MVC   UT460INC,0(R6)      GET INPUT CIRRENCY CODE FROM RECORD  H 003950
GETRATE2 MVC   UT460OTC,CB32OCDE   ASSUME OUTPUT CODEIS A LITERAL       H 003960
         TM    CB32SW1,SW321OCL    TEST IF OUTPUT CODE IS AL LITERAL    H 003970
         BO    GETRATE3            BR IF OUTPUT CODE  IS LITERAL        H 003980
         MVC   UT460OTC,$TSTAREA   ASSUME OUTPUT CODE FROM MS13 CARD    H 003990
         TM    CB32SW1,SW321OCS    TEST IF OUTPUT CODE IN MS13          H 004000
         BO    GETRATE3            BR IF OUTPUT CODE IN MS13            H 004010
         PTR   CB32OCP             GET ADDRESS OF OUTPUT CODE           H 004020
         CBAL  R4,CC19TO98         CONVERT CODE 19 TO 98 IF REQUESTED   H 004030
         MVC   UT460OTC,0(R6)      GET OUTPUT CURR CDE FROM RECORD      H 004040
         EJECT                                                          H 004050
*********************************************************************** H 004060
*        OBTAIN CURRENCY CONVERSION RATE ( CONT'D )                   * H 004070
*********************************************************************** H 004080
         SPACE 1                                                        H 004090
GETRATE3 L     R15,$ADUT460        POINT R15 AT UT460                   H 004100
         LA    R1,$PMUT460         RSTABLISH LINKAGE ADDRESS            H 004110
         BALR  R14,R15             USE UT460 TO DETREMINR RATE          H 004120
         L     R1,$PMUT460         RE-ESTABLISH L/A ADDRESSABILITY      H 004130
         TM    UT460OTV+7,X'03'    IS THE SIGN 'C'                      H 004140
         BNZ   *+8                 BR IF NOT                            H 004150
         OI    UT460OTV+7,X'03'    CHANGE IT TO 'F' FOR EBCDIC CLARITY  H 004160
         CLI   UT460RTC,C'1'       TEST IF RATE SUCCESSFULLY OBTAINED   H 004170
         BE    GETRATEX            EXIT IF YES                          H 004180
         MVI   $ULINEPL+1,X'02'     SET UNDERLINE LENGTH                H 004190
         MVC   $ULINEPL(1),CB32ICP  ASSUME INPUT CODE WRONG             H 004200
         CLI   UT460RTC,C'2'       TEST IF INPUT CODE WRONG             H 004210
         BE TESTL9Y                BR IF YES TO SHOW ERROR              H 004220
         MVC   $ULINEPL(1),CB32OCP  MUST BE BAD OYTPUT CODE             H 004230
         B     TESTL9Y             GO SHOW ERROR                        H 004240
GETRATEX SUBOUT RESTORE=R4         RESTORE & RETURN TO CALLER           H 004250
         DROP  R2,R1               DROP C/B, L/A ADDRESSABILITY         H 004260
         SPACE 5                                                        H 004270
*********************************************************************** H 004280
*        CONVERT CURRENCY CODE 19 TO 98 IN THE INPUT RECORD           * H 004290
*********************************************************************** H 004300
         SPACE 1                                                        H 004310
CC19TO98 SUBIN ,                   ENTER ROUTINE                        H 004320
         TM    YPSWITCH,YPSWCC19   IS CURR CODE 19 TO BE CONVERTED      H 004330
         BZ    *+20                BR IF NOT                            H 004340
         CLC   0(2,R6),CC19        IS THIS CURR CODE 19                 H 004350
         BNE   *+10                BR IF NOT                            H 004360
         MVC   0(2,R6),CC19+2      REPLACE 19 BY 98                     H 004370
        SUBOUT ,                   RETURN TO CALLER                     H 004380
         EJECT                                                          H 004390
*********************************************************************** H 004400
*        SET UP VERSION NUMBER, CURRENCY NAMES, CONV RATE, ETC        * H 004410
*********************************************************************** H 004420
         USING CB32NTRY,R2         ESTABLISH C/B ADDRESSABILITY         H 004430
         USING UT460LNK,R15        ESTABLISH L/A ADDRESSABILITY         H 004440
SETRATE  SUBIN ,                   SET UP OUTPUT FIELDS                 H 004450
         L     R15,$PMUT460        POINT AT L/A                         H 004460
         TM    CB32SW2,SW322VNO    IS VERSION NUMBER REQUIRED           H 004470
         BZ    SETRATE1            BR IF NOT REQUIRED                   H 004480
         PTR   CB32VNOP            GET POSN OF VERSION NUMBER           H 004490
         MVC   0(5,R6),$VERSNO      MOVE VERSION NUMBER TO OUTPUT       H 004500
SETRATE1 TM    CB32SW2,SW322INM    IS NAME OF INPUT CODE REQUIRED       H 004510
         BZ    SETRATE2            BR IF NAME NOT REQUIRED              H 004520
         PTR   CB32NMIP            POINT AT OUTPUT FIELD                H 004530
         MVC   0(4,R6),UT460DSI    MOVE DESC OF  INPUT CODE TO RECORD   H 004540
SETRATE2 TM    CB32SW2,SW322ONM    TEST IF NAME OF OUTPUT CODE REQUIRED H 004550
         BZ    SETRATE3            BR IF NAME NOT REQUIRED              H 004560
         PTR   CB32NMOP            POINT AT OUTPUT FIELD                H 004570
         MVC   0(4,R6),UT460DSO    MOVE DESC OF OUTPUT CDE TO RECORD    H 004580
SETRATE3 TM    CB32SW1,SW321RTE    IS CURRENCY CONVERSION RATE REQUIRED H 004590
         BZ    SETRATEX            BR IF NOT                            H 004600
         PTR   CB32RTEP,CB32RTEL   GET ADDRESS & LENGTH OF FIELD        H 004610
         SLL   R1,4                MOVE LENFTH 4 BITS TO THE LEFT       H 004620
         TM    CB32SW2,SW322RTP    IS RATE REQUIRED PCKED               H 004630
         BZ    SETRATE4            BR IF NOT PACKED                     H 004640
         EX    R1,L9RATEP          MOVE OUTPUT RATE TO FIELD            H 004650
         B     SETRATEX            PREPARE TO LEAVE                     H 004660
SETRATE4 EX    R1,L9RATE           UNPACK CONVERSION RATE INTO OUTPUT   H 004670
SETRATEX SUBOUT ,                  RETRUN TO CALLER                     H 004680
         DROP  R2,R15              DROP C/B, L/A ADDRESSABILITY         H 004690
         EJECT                                                          H 004700
*********************************************************************** H 004710
*        GET INPUT VALUE & CCONVERT                                   * H 004720
*********************************************************************** H 004730
         SPACE 1                                                        H 004740
         USING CB32NTRY,R2         ESTABLISH C/B ADDRESSABILITY         H 004750
         USING CB13NTRY,R8         ESTABLISH C/B ADDRESSABILITY         H 004760
         USING UT460LNK,R15        ESTABLISH L/A ADDRESSABILITY         H 004770
CONVERT  SUBIN SAVE=R4             PERFORM CONVERSION                   H 004780
        BBMESS 03                  ASSUME NOT NUMERIC                   H 004790
         MVC   $ULINEPL,CB32INP     SET TO UNDERLINE INPUT VAL IF ERROR H 004800
         TM    CB32SW1,SW321NOC    IS CURRENCY CONVERSION REQUIRED      H 004810
         BO    CONVERTX            BR IF NOT TO EXIT                    H 004820
         PTR   CB32INP,CB32INL     GET ADDRES & LENGTH OF INPUT         H 004830
         TM    CB32SW2,SW322IVP    TEST FI INPUT IS PACKED              H 004840
         BO    CONVERT1            BR IF INPUT IS PACKED                H 004850
         EX    R1,TESTMVC          MOVE FIELD TO WORK AREA              H 004860
         LA    R15,$TSTAREA        POINT R15 AT WORK AREA               H 004870
         AR    R15,R1              POINT R15 AT END OF FIELD            H 004880
         CBAL  R4,TESTSN           ENSURE INPUT IS NUMERIC              H 004890
         BH    TESTL9Y             GO FLAG AS ERROR IF NOT              H 004900
         BL    TESTL9X             EXIT IF INPUT VALUE IS ZERO          H 004910
         L     R2,CB13CBXX         RETRIEVE CB32 ADDR ( CORRUPTED TRT ) H 004920
         IC    R1,CB32INL          RETRIEVE LENGTH OF INPUT FIELD       H 004930
         BCTR  R1,0                DECREMENT FOR EXECITE                H 004940
         EX    R1,TESTPACK         PACK INPUT AMOUNT                    H 004950
         B     *+8                 B AROUND PACKED MOVE                 H 004960
         EJECT                                                          H 004970
*********************************************************************** H 004980
*        GET INPUT VALUE AND CONVERT ( CONT'D )                       * H 004990
*********************************************************************** H 005000
         SPACE 1                                                        H 005010
CONVERT1 EX    R1,TESTZAP          MOVE PACKED FIELD TO WORK AREA       H 005020
         L     R15,$PMUT460        GET ADDRESS OF LINKAGE AREA          H 005030
         ZAP   $WRKPCKL,$WRKPACK   MOVE AMOUNT TO LARGE WORK AREA       H 005040
         MP    $WRKPCKL,UT460OTV   MULTILPY BY CONVERSION FACTOR        H 005050
         MVN   $WRKPACK(1),$WRKPCKL+15 PRESERVE CURRENT SIGN            H 005060
         OI    $WRKPCKL+15,X'0F'   FORCE POSITIVE FOR ROUNDING          H 005070
         TM    CB32SW3,SW323RND    IS FINAL ROUNDING REQUIRED           H 005080
         BZ    CONVERT4            BR IF NOT                            H 005090
         TM    CB32SW3,SW323COM    IS ONLY COMMERCIAL ROUNDING REQUIRED H 005100
         BZ    CONVERT4+6          NO SO MUST ALWAYS ROUND              H 005110
         CLI   UT460RND,C'C'       DOES THIS CODE HAVE COMMERCIAL RNDG  H 005120
         BE    *+10                BYPASS INITIAL ROUNDING IF YES       H 005130
*        THE FOLLOWING INITIAL ROUNDING IS BYPASSED BY THE PRECEDING    H 005140
*         CODE IF THE RESULT IS SUBJECT  TO FINAL ROUNDING TO AVOID     H 005150
*         PROBLEMS DUE TO DOUNLE ROUNDING.                              H 005160
CONVERT4 AP    $WRKPCKL,FIVE0000   ADD INITIAL ROUNDING AMOUNT          H 005170
         MVN   $WRKPCKL+15(1),$WRKPACK RESTORE ORIGINAL SIGN            H 005180
         DP    $WRKPCKL,ONE00000   DISCARD SUPERFLUOUS DECIMALS         H 005190
         TM    CB32SW3,SW323RND    IS ROUNDING REQUIREDL                H 005200
         BZ    CONVERT2            BR IF NOT                            H 005210
         TM    CB32SW3,SW323COM    IS ONLY COMMERCIAL ROUNDING REQUIRED H 005220
         BNO   *+12                GO ROUND IF ALWAYS REQUIRED          H 005230
         CLI   UT460RND,C'C'       IS CODE SUBJECT TO COMMERCIAL RNFING H 005240
         BNE   CONVERT2            BYPASS ROUNDING IF NOT               H 005250
         SR    R1,R1               ZEROISE WORK REGISTER                H 005260
         IC    R1,CB32RNDN         GET NO OF DECIMALS TO BE ROUNDED     H 005270
         LCR   R1,R1               REVERSE FOR PURPOSE OF 'SRP'         H 005280
         SRP   $WRKPCKL(12),0(R1),5 SHIFT DIGITS AND ROUND              H 005290
         LCR   R1,R1               REVERSE SIGN OF NDEC AGAIN           H 005300
         SRP   $WRKPCKL(12),0(R1),5 SHIFT BACK TO ORIGINAL POSN & ZERO  H 005310
         EJECT                                                          H 005320
*********************************************************************** H 005330
*        GET INPUT VALUE AND CONVERT ( CONT'D )                       * H 005340
*********************************************************************** H 005350
         SPACE 1                                                        H 005360
CONVERT2 PTR   CB32OUTP,CB32OUTL   GET ADDRESS & LGTH OF OITPUT         H 005370
         ZAP   $WRKPACK,$WRKPCKL(12) MOVE TO SMALLER WORK AREA          H 005380
         SLL   R1,4                MOVE LENGH FOR EXECUTE               H 005390
         TM    $WRKPACK+7,X'03'    IS THE SIGN A 'C'                    H 005400
         BNZ   *+8                 BR IF NOT                            H 005410
         OI    $WRKPACK+7,X'03'    CHANGE SIGN TO 'F' FOR EBCDIC CLARIT H 005420
         TM    CB32SW2,SW322OVP    IS OUTPUT REQUIRED PACKED            H 005430
         BZ    CONVERT3            BR IF NOT REQUIRED PACKED            H 005440
         EX    R1,PACKOUT          MOVE CONVERTED VALUE TO OUTPUT       H 005450
         B     CONVERTX            PREPARE TO LEAVE                     H 005460
CONVERT3 EX    R1,UNPKOUT          UNPACK CONVERTED VALUE INTO OUTPUT   H 005470
CONVERTX SUBOUT RESTORE=R4         RETURN TO CALLER                     H 005480
         DROP  R2,R8,R15           DROP C/B, L/A ADDRESSABILITY         H 005490
         TITLE 'BUDGIE3H - EXECUTED INSTRUCTIONS'                       H 005500
*********************************************************************** H 005510
*        EXECUTED INSTRUCTIONS                                        * H 005520
*********************************************************************** H 005530
         SPACE 1                                                        H 005540
SB11AP04 AP    CB04BACC-CB04NTRY(L'CB04BACC,R15),0(0,R14)               H 005550
SB11AP05 AP    CB05BACC-CB05NTRY(L'CB05BACC,R15),0(0,R14)               H 005560
SB11AT05 AP    CB05BVAL-CB05NTRY(L'CB05BVAL,R15),0(0,R14)               H 005570
TESTPACK PACK  $WRKPACK,0(0,R6)    PACK FIELD INTO WORK AREA            H 005580
PACKOUT  ZAP   0(0,R6),$WRKPACK    MOVE PACKED FIELD TO OUTPUT          H 005590
UNPKOUT  UNPK  0(0,R6),$WRKPACK    UNPACK VALUE INTO OUTPUT             H 005600
TESTZAP  ZAP   $WRKPACK,0(0,R6)    MOVE PACKED INPUT TO WORJ AREA       H 005610
L9RATE   UNPK  0(0,R6),UT460OTV-UT460LNK(L'UT460OTV,R15)                H 005620
L9RATEP  ZAP   0(0,R6),UT460OTV-UT460LNK(L'UT460OTV,R15)                H 005630
TESTMVC  MVC   $TSTAREA(0),0(R6)   MOVE FIELD TO TEST AREA              H 005640
L9ZERO   MVC   0(0,R6),$ZEROES     MOVE EBCDIC ZEROES TO OUTPUT FIELD   H 005650
L9ZEROP  ZAP   0(0,R6),$ZERO       ZEROISE PACKED OUTPUT FIELD          H 005660
         TITLE 'BUDGIE3H - MISCELLANEOUS CONSTANTS AND FIELDS'          H 005670
BBMESS03 DC    C'FIELD COLS   -   NOT NUMERIC'                          H 005680
BBMESS08 DC    C'DATE (COLS   -   ) INVALID'                            H 005690
BBMESS09 DC    C'CURR CODE (  -  ) INVALID'                             H 005700
BBMESS18 DC    C'MONTH NO. (  -  ) NOT CURRENT'                         H 005710
BBMESS19 DC    C'FIELD COLS   -   NOT IN LIST'                          H 005720
         SPACE 1                                                        H 005730
WEEK0    DC    CL6'WEEK0'          SPECIFY 'WEEK0' STATUS TO UT458      H 005740
CC19     DC    C'19',C'98'         U.K. CURRENCY CODES                  H 005750
R6R9TMOD DS    CL1                 TEST MODIFIER FOR R6-R9 TESTS        H 005760
ONE00000 DC    P'100000'           USED TO ROUND CURR CONV              H 005770
FIVE0000 DC    P'50000'            USED TO ROUND CURR CONV              H 005780
         SPACE 1                                                        H 005790
        SCSEND H                   TERMINATE CSECT BUDGIE3H             H 005800
         END                                                            H 005810
         SPACE 1                                                        H 005820
         REPRO                                                          H 005830
 IDENTIFY BUDGIE3H('TESTS AND OPERATIONS')                              H 005840
         END                                                            H 005850
