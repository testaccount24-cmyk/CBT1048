         TITLE 'PARROTC2 - CONTROL CARD PROCESSING - PART 1'            C2000100
         MACRO                                                          C2000200
         $CB   &PRINT=OFF          MACRO TO GENERATE DSECTS             C2000300
         PUSH  PRINT               PRESERVE PRINT OPTIONS               C2000400
         PRINT &PRINT              SET PRINT OPTIONS FOR DSECTS         C2000500
               EJECT                                                    C2000600
         CBPREFIX ,                CONTROL BLOCK PREFIX                 C2000700
               EJECT                                                    C2000800
         CCB   ,                   CENTRAL CONTROL BLOCK                C2000900
               EJECT                                                    C2001000
         CSECTID ,                 CSECT IDENTIFICATION PREFIX          C2001100
               EJECT                                                    C2001200
         EQUATES ,                 GENERATE STANDARD EQUATES            C2001300
               EJECT                                                    C2001400
         FDB   ,                   FIELD DEFINITION BLOCK               C2001500
               EJECT                                                    C2001600
         GR10  ,                   GR10 CONTROL CARD                    C2001700
               EJECT                                                    C2001800
         KBD   ,                   KEY BREAK DEFINITION                 C2001900
               EJECT                                                    C2002000
         LCD   ,                   LINE COMPONENT DESCRIPTION           C2002100
               EJECT                                                    C2002200
         RST   ,                   RECORD SELECTION TEST DEF'N          C2002300
               EJECT                                                    C2002400
         SCBUCKET ,                SELECTION CRITERION ' BUCKET '       C2002500
               EJECT                                                    C2002600
         SCE   ,                   SELECTION CRITERION ELEMENT          C2002700
               EJECT                                                    C2002800
         SPD   ,                   STORAGE POOL DEFINITION              C2002900
         POP   PRINT               RESTORE ORIGINAL PRINT OPTIONS       C2003000
         MEND                                                           C2003100
         SPACE 4                                                        C2003200
PARROTC2  CSECT                                                         C2003300
         $CB   ,                   GENERATE DSECTS AND EQUATES          C2003400
         EJECT                                                          C2003500
     SCSECT C2                                                          C2003600
         USING PARROTC2,R12         ESTABLISH PARROTC2 ADDRESSABILITY   C2003700
         USING CCBNTRY,R13         ESTABLISH CCB ADDRESSABILITY         C2003800
         SPACE 1                                                        C2003900
         ENTRY CCGR10              PROCESS GR10 CONTROL CARDS           C2004000
         ENTRY CCGR11              PROCESS GR11 CONTROL CARDS           C2004100
         ENTRY CCGR12              PROCESS GR12 CONTROL CARDS           C2004200
         ENTRY PARENDC2            END IF CSECT                         C2004300
         EJECT                                                          C2004400
*********************************************************************** C2004500
*        PROCESS GR10 CONTROL CARDS                                   * C2004600
*********************************************************************** C2004700
         SPACE 1                                                        C2004800
         USING GR10CARD,R6         ESTABLISH C/C ADDRESSABILITY         C2004900
         USING LCDNTRY,R8          ESTABLISH C/B ADDRESSABILITY         C2005000
CCGR10   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2005100
         SR    R1,R1               ZEROISE WORK REGISTER                C2005200
         LA    R8,$LCDHEAD         GET ADDRESS OF FIRST LCD IN CHAIN    C2005300
CCGR1001 ICM   R1,7,LCDLCD+1       GET ADDRESS OF NEXT LCD              C2005400
         BZ    CCGR1002            BR IF END OF CHAIN                   C2005500
         LR    R8,R1               SET NEXT IN CHAIN IN CURRENT POINTER C2005600
         B     CCGR1001            LOOP BACK TO GET NEXT IN CHAIN       C2005700
CCGR1002 ST    R8,$PTRLCD          PRESERVE ADDRESS OF LAST LCD IN CHAI C2005800
CCGR1003 CBAL  R4,BLDLCD           PROCESS CONTROL CARD                 C2005900
        SUBOUT RESTORE=R4          RESTOE AND RETURN                    C2006000
         DROP R6,R8                DROP C/C & C/B ADDRESSABILITY        C2006100
         EJECT                                                          C2006200
*********************************************************************** C2006300
*        PROCESS GR11 CONTROL CARDS                                   * C2006400
*********************************************************************** C2006500
         SPACE 1                                                        C2006600
         USING GR10CARD,R6         ESTABLISH C/C ADDRESSABILITY         C2006700
         USING LCDNTRY,R8          ESTABLISH C/B ADDRESSABILITY         C2006800
CCGR11   SUBIN SAVE=R4             RESERVE RETURN ADDRESS               C2006900
         SR    R1,R1               ZEROISE WORK REGISTER                C2007000
         LA    R8,$LCDDETL         GET ADDRESS OF FIRST LCD IN CHAIN    C2007100
CCGR1101 ICM   R1,7,LCDLCD+1       GET ADDRESS OF NEXT LCD              C2007200
         BZ    CCGR1102            BR IF END OF CHAIN                   C2007300
         LR    R8,R1               SET NEXT IN CHAIN IN CURRENT POINTER C2007400
         B     CCGR1101            LOOP BACK TO GET NEXT IN CHAIN       C2007500
CCGR1102 ST    R8,$PTRLCD          PRESERVE ADDRESS OF LAST LCD IN CHAI C2007600
CCGR1103 CBAL  R4,BLDLCD           PROCESS CONTROL CARD                 C2007700
        SUBOUT RESTORE=R4          RESTORE AND REURN                    C2007800
         DROP  R6,R8               DROP ADDRESSABILITY                  C2007900
         EJECT                                                          C2008000
*********************************************************************** C2008100
*        PROCESS GR12 CONTROL CARDS                                   * C2008200
*********************************************************************** C2008300
         SPACE 1                                                        C2008400
         USING GR10CARD,R6         ESTABLISH C/C ADDRESSABILITY         C2008500
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2008600
CCGR12   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2008700
         SR    R1,R1               ZEROISE WORK REGISTER                C2008800
         LA    R8,$LCDTOTL         GET ADDRESS OF FIRST LCD IN CHAIN    C2008900
CCGR1201 ICM   R1,7,LCDLCD+1       GET ADDRESS OF NEXT LCD              C2009000
         BZ    CCGR1202            BR IF END OF CHAIN                   C2009100
         LR    R8,R1               SET NEXT IN CHAIN IN CURRENT POINTER C2009200
         B     CCGR1201            LOOP BACK TO GET NEXT IN CHAIN       C2009300
CCGR1202 ST    R8,$PTRLCD          PRESERVE ADDRESS OF LAST LCD IN CHAI C2009400
CCGR1203 CBAL  R4,BLDLCD           PROCESS CONTROL CARD                 C2009500
        SUBOUT RESTORE=R4          RESTORE AND REURN                    C2009600
         DROP  R8                  DROP LCD ADDRESSABILITY              C2009700
         DROP  R6                  DROP ADDRESSABILITY                  C2009800
         EJECT                                                          C2009900
*********************************************************************** C2010000
*        GR10/11/12 PROCESSING:    MAIN-LINE                          * C2010100
*********************************************************************** C2010200
         SPACE 1                                                        C2010300
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2010400
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2010500
BLDLCD   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2010600
         XC    BLDSTART(BLDEND-BLDSTART),BLDSTART NULLIFY TEMPORARY FLD C2010700
         MVI   BLDID,FLAGLCD       SET C/B FLAG IN TEMPORARY ID FIELD   C2010800
         CBAL  R4,BLCDKEYN         PROCESS KEY NUMBER ( GR10/12 ONLY )  C2010900
         CBAL  R4,BLCDPPPL         PROCESS START AND END POSNS          C2011000
         CBAL  R4,BLCDLABL         PROCESS NAME OF FIELD TO BE PRINTED  C2011100
         BNE   BLDLCDX             EXIT IF NO LCD IS REQUIRED           C2011200
         CBAL  R4,BLCDFMAT         PROCESS REQUIRED OUTPUT FORMAT       C2011300
         CBAL  R4,BLCDNDEC         PROCESS NO OF DECIMALS REQUIRED      C2011400
         CBAL  R4,BLCDPMOD         PROCESS PRINT MODIFIER               C2011500
         CBAL  R4,BLCDFRCZ         FORCE 'ZZ' SPECIFICATION IF NECESSAR C2011600
         CBAL  R4,BLCDDETL         PROCESS 'DETAIL VALUE ALWAYS' SPEC'N C2011700
         CBAL  R4,BLCDLCD          CONSTRUCT AND CHAIN LCD              C2011800
         EJECT                                                          C2011900
*********************************************************************** C2012000
*        GR10/11/12 PROCESSING:    MAIN-LINE ( CONT'D )               * C2012100
*********************************************************************** C2012200
         SPACE 1                                                        C2012300
         CLI   LCDID,NULLCB        HAS CONTROL BLOCK BEEN NULLIFIED     C2012400
         BNE   BLDLCD01            BR IF CONTROL BLOCK NOT NULLIFIED    C2012500
         LINE  LINE02,ELEVEL=YES   TELL THAT C/C IGNORED BECAUSE OF ERR C2012600
BLDLCD01 LA    R7,2                SET TO LOOP THROUGH TWO BUCKETS      C2012700
         LA    R10,GR10TST         POINT R10 AT 1ST SELECTION BUCKET    C2012800
BLDLCD02 CBAL  R4,BLDSCE           CONSTRUCT SCE IF NECESSARY           C2012900
         LA    R10,GR10TST2-GR10TST(,R10) INCREMENT TO NEXT BUCKET      C2013000
         BCT   R7,BLDLCD02         LOOP BACK IF BUCKET LEFT TO PROCESS  C2013100
BLDLCDX SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2013200
         DROP  R8                  DROP LCD ADDRESSABILITY              C2013300
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2013400
         EJECT                                                          C2013500
*********************************************************************** C2013600
*        GR10/11/12 PROCESSING:    PROCESS KEY NUMBER ( NOT GR11 )    * C2013700
*********************************************************************** C2013800
         SPACE 1                                                        C2013900
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2014000
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2014100
BLCDKEYN SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2014200
         CLC   GR10ID,=C'GR11'     IS IT A GR11 CONTROL CARD            C2014300
         BNE   BLCDKN01            BR IF IT IS A GR10/12 CARD           C2014400
         CLI   GR10KEYN,C' '       IS THE KEY LEVEL BLANK               C2014500
         BE    BLCDKNX             EXIT IF IT IS BLANK                  C2014600
         LINE  LINE01              TELL OF ILLEGALLY NON-BLANK FIELD    C2014700
         MVI   GR10KEYN,C' '       BLANK OUT FIELD TO PREVENT PROBLEMS  C2014800
         B     BLCDKNX             GO TO EXIT                           C2014900
BLCDKN01 CLI   GR10KEYN,C' '       IS THE KEY LEVEL BLANK               C2015000
         BE    BLCDKNX             EXIT IF IT IS BLANK                  C2015100
         CLC   GR10ID(5),=C'GR100' IS THIS GR10 KEY 0 SPECIFICATION     C2015200
         BNE   BLCDKN02            BR IF NOT                            C2015300
         LINE  LINE12              WARN INVALID (PL/1 COMPATIBILITY)    C2015400
         MVI   GR10KEYN,C' '       PLAY SAFE AND BLANK OUT THE SPEC'N   C2015500
         B     BLCDKNX             GO TO EXIT                           C2015600
BLCDKN02 CLC   GR10ID,=C'GR10'     IS THIS A GR10 CARD                  C2015700
         BNE   *+8                 BR IF NOT                            C2015800
         OI    $STATUS,STATCHD     FLAG THAT CONTROL HEADINGS REQUIRED  C2015900
         OI    BLDSW,MLCDKEYN      FLAG THAT LCD APPLIES TO KEY NO      C2016000
         CLC   GR10ID(5),=C'GR120' GRAND TOTAL LCD ?                      016100
         BNE   *+8                 NO. DO NOT SET THE FLAG                016200
         OI    $STATUS,STATKBDT    SIGNAL THAT GRAND TOTAL LCD'S EXIST    016300
         NUMERIC GR10KEYN          ENSURE THAT KEY NUMBER IS NUMERIC    C2016400
         STC   R1,BLDKEYN          PRESERVE KEY NUMBER   (ASSUME VALID) C2016500
         BNH   BLCDKNX             BR IF BLANK OR NUMERIC               C2016600
         MVI   BLDID,NULLCB        ALTER C/B FLAG TO NULLIFY C/B        C2016700
BLCDKNY  CLI   *,Y                 SET COND CODE TO SHOW ERROR          C2016800
         B     *+8                 BYPASS RESETTING COND CODE           C2016900
BLCDKNX  CLI   *,X                 SET COND CODE TO SHOW NO ERRORS      C2017000
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2017100
         DROP  R8                  DROP LCD ADDRESSABILITY              C2017200
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2017300
         EJECT                                                          C2017400
*********************************************************************** C2017500
*        GR10/11/12 PROCESSING:    PROCESS START AND END POSITIONS    * C2017600
*********************************************************************** C2017700
         SPACE 1                                                        C2017800
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2017900
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2018000
BLCDPPPL SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2018100
         CLC   GR10STRT(6),$ZEROES HAS HE SPECIFIED '000000'            C2018200
         BNE   *+10                BRANCH IF NOT                        C2018300
         MVC   GR10STRT(6),=C'001001' PLAY SAFE: '000000' CAN BE A PROB C2018400
        NUMERIC GR10STRT           ENSURE THAT START POSITION IS NUMERI C2018500
         BH    BLCDPLY             GO SHOW ERROR IF NOT NUMERIC         C2018600
         LR    R2,R1               PRESERVE START POSITION              C2018700
         BCTR  R1,0                DECREMENT FROM POSITION TO DISPLACEM C2018800
         STH   R1,BLDFLDP          PRESERVE DISPLACEMENT OF OUTPUT FLD  C2018900
        NUMERIC GR10END            ENSURE THAT END POSITION IS NUMERIC  C2019000
         BH    BLCDPLY             GO SHOW ERROR IF NOT NUMERIC         C2019100
         SR    R1,R2               DETERMINE EXECUTABLE LENGTH OF FIELD C2019200
         BNM   BLCDPL01            BR IF NOT NEGATIVE                   C2019300
         LINE  LINE07,ELEVEL=YES   WARN THAT END POSITION LESS THAN STA C2019400
         B     BLCDPLY             GO SHOW ERROR CONDITION              C2019500
BLCDPL01 STC   R1,BLDFLDL          PRESERVE EXECUTABLE LENGTH OF OUTPUT C2019600
         B     BLCDPLX             GO SHOW NO ERRORS FOUND              C2019700
         SPACE 1                                                        C2019800
BLCDPLY  MVI   BLDID,NULLCB        ALTER C/B FLAG TO NULLIFY C/B        C2019900
         CLI   *,Y                 SET COND CODE TO SHOW ERROR ENCOUNTE C2020000
         B     *+8                 BYPASS RESETTING COND CODE           C2020100
BLCDPLX  CLI   *,X                 SET COND CODE TO SHOW NO ERRORS      C2020200
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2020300
         DROP  R8                  DROP LCD ADDRESSABILITY              C2020400
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2020500
         EJECT                                                          C2020600
*********************************************************************** C2020700
*        GR10/11/12 PROCESSING:    PROCESS FIELD LABEL                * C2020800
*********************************************************************** C2020900
         SPACE 1                                                        C2021000
*        UPON RETURN THE COND CODE IS SET THUS :-                       C2021100
*         BE  - FIELD LABEL VALID: LCD REQUIRED                         C2021200
*         BNE - LCD NOT REQUIRED: LABEL INVALID OR 'DELETE' SPECIFIC'N  C2021300
         SPACE 1                                                        C2021400
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2021500
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2021600
BLCDLABL SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2021700
         CLC   GR10FLD,=C'LINE+'   IS IT A 'LINE+' REQUEST              C2021800
         BNE   *+12                BR IF NOT                            C2021900
         OI    BLDSW,MLCDPRNT      FLAG THAT LCD IS A 'LINE+' REQUEST   C2022000
         B     BLCDLBX             GO TO EXIT                           C2022100
         SPACE 1                                                        C2022200
         CLC   GR10FLD(3),=C'LIT'  DOES THE CONTROL CARD SPECIFY A LITE C2022300
         BNE   *+12                BR IF NOT A LITERAL                  C2022400
         OI    BLDSW,MLCDLIT       FLAG THAT LCD DEFINES A LITERAL      C2022500
         B     BLCDLBX             GO TO EXIT                           C2022600
         SPACE 1                                                        C2022700
         CLC   GR10ID,=C'GR12'     IS THIS A GR12 CONTROL CARD          C2022800
         BNE   *+10                BR IF NOT A GR12                     C2022900
         CLC   GR10FLD(4),=C'GR11' IS THIS A 'GR11' REQUEST             C2023000
         BNE   *+12                BR IF NOT                            C2023100
         OI    BLDSW,MLCDGR11      FLAG THAT THIS IS A GR11 SPECIFICATI C2023200
         B     BLCDLBX             GO TO EXIT                           C2023300
         SPACE 1                                                        C2023400
         CLC   GR10FLD,=C'DELET'   IS THIS A 'SUPPRESS GRANG TOTAL' REQ C2023500
         BNE   BLCDLB4             BR IF NOT                            C2023600
         CBAL  R4,BLCDELET         PROCESS 'DELETE' SPECIFICATION       C2023700
         CLC   GR10ID(5),=C'GR120' IS THIS FILE TOTAL SUPPRESS          C2023800
         BNE   BLCDLBX             GO TO EXIT IF NOT                    C2023900
         B     BLCDLBY             GO TO EXIT SHOWING LCD NOT REQUIRED  C2024000
         SPACE 1                                                        C2024100
BLCDLB4  CLC   GR10FLD(3),=C'KEY'  IS THIS A 'KEYN' SPECIFICATION       C2024200
         BNE   BLCDLB1             BR IF NO                             C2024300
         CBAL  R4,LABLKEY          CONSTRUCT INTERNAL REP'N OF 'KEYN'   C2024400
         B     BLCDLBX             GO TO EXIT                           C2024500
         SPACE 1                                                        C2024600
BLCDLB1  CLC   GR10FLD(4),=C'NAME' IS THIS A 'NAMEN' PSECIFICATION      C2024700
         BNE   BLCDLB2             BR IF NOT                            C2024800
         CBAL  R4,LABLNAME         CONSTRUCT INTERNAL REP'N OF 'NAMEN'  C2024900
         B     BLCDLBX             GO TO EXIT                           C2025000
         EJECT                                                          C2025100
*********************************************************************** C2025200
*        GR10/11/12 PROCESSING:    PROCESS FIELD LABEL ( CONT'D )     * C2025300
*********************************************************************** C2025400
         SPACE 1                                                        C2025500
BLCDLB2  CLC   GR10FLD(3),=C'CNT'  IS IT A 'CNTN' SPECIFICATION         C2025600
         BNE   BLCDLB3             BR IF NO                             C2025700
         CBAL  R4,LABLCNT          CONSRTUCT INTERNAL REP'N OF 'CNTN'   C2025800
         B     BLCDLBX             GO TO EXIT                           C2025900
         SPACE 1                                                        C2026000
BLCDLB3  MVC   BLDLABL,GR10FLD     SET FIELD LABEL IN WORK AREA         C2026100
         CLC   GR10FLD+2(3),$SPACES ENSURE THAT LABEL ONLY 2-CHARS      C2026200
         BE    BLCDLBX             GO TO EXIT IF YES                    C2026300
         LINE  LINE06,ELEVEL=YES   TELL OF ILLEGAL FIELD NAME           C2026400
         MVI   BLDID,NULLCB        SET FLAG TO NULLIFY C/B              C2026500
BLCDLBY  CLI   *,Y                 SET COND CODE TO SHOW ERROR          C2026600
         B     *+8                 BYPASS RESETTING COND CODE           C2026700
BLCDLBX  CLI   *,X                 SET COND CODE TO SHOW ALL IS WELL    C2026800
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2026900
         DROP  R8                  DROP LCD ADDRESSABILITY              C2027000
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2027100
         EJECT                                                          C2027200
*********************************************************************** C2027300
*        GR10/11/12 PROCESSING:    PROCESS 'DELETE' SPECIFICATION     * C2027400
*********************************************************************** C2027500
         SPACE 1                                                        C2027600
         USING GR10CARD,R6         ESTABLISH CONTROL CARD ADDRESSABILIT C2027700
BLCDELET SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2027800
         CLC   GR10ID(5),=C'GR12 ' IS IT REQUEST TO SUPPRESS FILE TOTAL C2027900
         BE    BLCDELT0            BR IF YES: SUPPRESS FILE TOTAL     C C2028000
         CLC   GR10ID(5),=C'GR120' IS IT REQUEST TO SUPPRESS FILE TOTAL C2028100
         BNE   BLCDELT1            BR IF NOT TO SUPPRESS FILE TOTAL     C2028200
BLCDELT0 OI    $STATUS,STATNGRT    FLAG THAT FILE TOTAL NOT REQUIRED    C2028300
         B     BLCDELTX            GO TO ROUTINE EXIT                   C2028400
BLCDELT1 OI    BLDSW2,MLCDELET     FLAG LCD FOR 'DELETE' PROCESSING     C2028500
BLCDELTX SUBOUT RESTORE=R4         RESTORE AND RETURN TO CALLER         C2028600
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2028700
         EJECT                                                          C2028800
*********************************************************************** C2028900
*        GR10/11/12 PROCESSING:    CONSTRUCT INTERNAL REP'N OF 'KEYN' * C2029000
*********************************************************************** C2029100
         SPACE 1                                                        C2029200
*        THESE ROUTINES CONSTRUCT THE INTERNAL REPRESENTATIONS OF       C2029300
*         'NAMEN', 'KEYN', AND 'CNTN'.  NOTE THAT THE ROUTINE 'NUMERIC' C2029400
*         SETS THE CONDITION CODE TO INDICATE IF THE KEY NUMBER IS      C2029500
*         NOT NUMERIC                                                   C2029600
         SPACE 1                                                        C2029700
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2029800
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2029900
LABLKEY  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2030000
         MVI   BLDLABL,KEY         SET INTERNAL REP'N OF 'KEY'          C2030100
       NUMERIC GR10FLD+3,1         ENSURE KEY NUMBER IS NUMERIC         C2030200
         STC   R1,BLDLABL+1        ASSUME NUMERIC AND PRESERVE KEY NO   C2030300
         BE    *+8                 BR IF KEY NUMBER WAS NUMERIC         C2030400
         MVI   BLDID,NULLCB        SET FLAG TO NULLIFY C/B              C2030500
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2030600
         DROP  R8                  DROP LCD ADDRESSABILITY              C2030700
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2030800
         EJECT                                                          C2030900
*********************************************************************** C2031000
*        GR10/11/12 PROCESSING:    CONSTRUCT INTERNAL REP'N OF 'NAMEN'* C2031100
*********************************************************************** C2031200
         SPACE 1                                                        C2031300
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2031400
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2031500
LABLNAME SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2031600
         MVI   BLDLABL,NAME        SET INTERNAL REP'N OF 'NAME'         C2031700
       NUMERIC GR10FLD+4,1         ENSURE KEY NUMBER IS NUMERIC         C2031800
         STC   R1,BLDLABL+1        ASSUME NUMERIC AND PRESERVE KEY NO   C2031900
         BE    *+8                 BR IF KEY NUMBER WAS NUMERIC         C2032000
         MVI   BLDID,NULLCB        SET FLAG TO NULLIFY C/B              C2032100
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2032200
         DROP  R8                  DROP LCD ADDRESSABILITY              C2032300
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2032400
         EJECT                                                          C2032500
*********************************************************************** C2032600
*        GR10/11/12 PROCESSING:    CONSTRUCT INTERNAL REP'N OF 'CNTN' * C2032700
*********************************************************************** C2032800
         SPACE 1                                                        C2032900
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2033000
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2033100
LABLCNT  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2033200
         MVI   BLDLABL,CNT         SET INTERNAL REP'N OF 'CNT'          C2033300
       NUMERIC GR10FLD+3,1         ENSURE KEY NUMBER IS NUMERIC         C2033400
         STC   R1,BLDLABL+1        ASSUME NUMERIC AND PRESERVE KEY NO   C2033500
         BE    *+8                 BR IF KEY NUMBER WAS NUMERIC         C2033600
         MVI   BLDID,NULLCB        SET FLAG TO NULLIFY C/B              C2033700
         OI    BLDSW,MLCDCNT       FLAG THAT LCD DEFINES A COUNTER      C2033800
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2033900
         DROP  R8                  DROP LCD ADDRESSABILITY              C2034000
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2034100
         EJECT                                                          C2034200
*********************************************************************** C2034300
*        GR10/11/12 PROCESSING:    PROCESS OUTPUT FORMAT              * C2034400
*********************************************************************** C2034500
         SPACE 1                                                        C2034600
*        THIS ROUTINE CONVERTS THE LINE SPACING REQUEST ( 'LINE+' )     C2034700
*         AND OUTPUT FORMAT SPECIFICATION INTO THE INTERNAL REPRESENT'N C2034800
         SPACE 1                                                        C2034900
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2035000
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2035100
BLCDFMAT SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2035200
         CLC   GR10FLD,=C'DELET'   IS IT REQUEST TO SUPPRESS GRAND TOTA C2035300
         BE    BLCDFMX             IGNORE FORMAT IF YES AND GO TO EXIT  C2035400
         CLC   GR10FLD(4),=C'GR11' IS IT 'GR11' REQUEST                 C2035500
         BE    BLCDFMX             IGNORE IT IF YES AND GO TO EXIT      C2035600
         CLC   GR10FLD,=C'LINE+'   IS IT A LINE SPACING REQUEST         C2035700
         BNE   BLCDFM1             BR IF NOT LINE SPACING REQUEST       C2035800
         OI    BLDSW,MLCDPRNT      SET FLAG THAT LCD DEFINES LINE SPACI C2035900
         LA    R14,LINETABL        POINT AT CONVERSION TABLE            C2036000
         LA    R15,LINETABN        SET R15 TO NO. OF TABLE ENTRIES      C2036100
BLCDFM0  MVC   BLDLINE,1(R14)      ASSUME MATCH AND SET INTERNAL REP'N  C2036200
         CLC   GR10FMAT,0(R14)     DO WE HAVE A MATCH                   C2036300
         BE    BLCDFMX             GO TO EXIT IF YES                    C2036400
         LA    R14,2(,R14)         INCREMENT TO NEXT TABLE ENTRY        C2036500
         BCT   R15,BLCDFM0         LOOP BACK IF ANY TABLE ELEMENTS LEFT C2036600
         LINE  LINE04              WARN OF UNKNOWN LINE SPACING REQUEST C2036700
         MVI   BLDLINE,SPCE1AFT    FORCE SINGLE SPACING                 C2036800
         B     BLCDFMX             GO TO EXIT                           C2036900
         EJECT                                                          C2037000
*********************************************************************** C2037100
*        GR10/11/12 PROCESSING:    PROCESS OUTPUT FORMAT ( CONT'D )   * C2037200
*********************************************************************** C2037300
         SPACE 1                                                        C2037400
*LCDFM1  CLC   GR10FLD(3),=C'LIT'  IS THIS A LITERAL SPECIFICATION      C2037500
*        BNE   *+16                BR IF NOT                            C2037600
BLCDFM1  CLI   GR10FMAT,C' '       IS AN OUTPUT FORMAT SUPPLIED         C2037700
         BNE   *+8                 BR IF YES                            C2037800
         MVI   GR10FMAT,C'X'       FORCE CHARACTER FORMAT               C2037900
         LA    R14,FMATABLE        POINT AT FORMAT CONVERSION TABLE     C2038000
         LA    R15,FMATABNO        SET R15 TO NO. OF TABLE ELEMENTS     C2038100
BLCDFM2  MVC   BLDFLDF,1(R14)      ASSUME MATCH AND SET INTERNAL REP'N  C2038200
         CLC   GR10FMAT,0(R14)     DO WE HAVE A MATCH                   C2038300
         BE    BLCDFMX             GO TO EXIT IF MATCH                  C2038400
         LA    R14,2(,R14)         INCREMENT TO NEXT TABLE ENTRY        C2038500
         BCT   R15,BLCDFM2         LOOP BACK IF ANY ENTRIES LEFT        C2038600
         LINE  LINE05,ELEVEL=YES   TELL OF UNKNOWN OUTPUT FORMAT        C2038700
         MVI   BLDID,NULLCB        SET FLAG TO NULLIFY C/B              C2038800
         CLI   *,Y                 SET COND CODE TO SHOW ERROR          C2038900
BLCDFMX SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2039000
         DROP  R8                  DROP LCD ADDRESSABILITY              C2039100
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2039200
         EJECT                                                          C2039300
*********************************************************************** C2039400
*        GR10/11/12 PROCESSING:    PROCESS NO. OF DECIMALS            * C2039500
*********************************************************************** C2039600
         SPACE 1                                                        C2039700
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2039800
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2039900
BLCDNDEC SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2040000
         CLI   GR10NDEC,C' '       IS NUMBER OF DECIMALS SUPPLIED       C2040100
         BE    BLCDNDX             EXIT IF NO NDEC                      C2040200
         CLC   GR10FLD(3),=C'LIT'  IS THIS A LITERAL SPECIFICATION      C2040300
         BNE   BLCDND1             BR IF NOT TO PROCESS NDEC SPECIFICAT C2040400
         LINE  LINE09              TELL OF INVALID NDEC SPECIFICATION   C2040500
         B     BLCDNDX             GO TO EXIT: IGNORE INVALID SPEC      C2040600
BLCDND1 NUMERIC GR10NDEC           ENSURE THAT NO. OF DECIMALS IS NUMER C2040700
         STC   R1,BLDNDEC          ASSUME NUMERIC AND PRESERVE VALUE    C2040800
         BNH   *+12                BR IF BLANK OR NUMERIC               C2040900
         MVI   BLDID,NULLCB        SET FLAG TO NULLIFY C/B              C2041000
         OI    SWERROR,MERRCCER    FLAG THAT 'E' LEVEL ERROR FOUND      C2041100
BLCDNDX SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2041200
         DROP  R8                  DROP LCD ADDRESSABILITY              C2041300
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2041400
         EJECT                                                          C2041500
*********************************************************************** C2041600
*        GR10/11/12 PROCESSING:    PROCESS PRINT MODIFIER             * C2041700
*********************************************************************** C2041800
         SPACE 1                                                        C2041900
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2042000
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2042100
BLCDPMOD SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2042200
         CLC   GR10PMOD,$SPACES    IS THE PRINT MODIFIER BLANK          C2042300
         BE    BLCDPMX             GO TO EXIT IF BLANK                  C2042400
         CLC   GR10PMOD,=C'ZZ'     IS IT REQUEST TO PRINT ZERO VALUES   C2042500
         BNE   *+12                BR IF NOT                            C2042600
         OI    BLDSW,MLCDZZ        SET FLAG TO PRINT ZERO VALUES        C2042700
         B     BLCDPMX             GO TO EXIT                           C2042800
         SPACE 1                                                        C2042900
         CLC   GR10PMOD,=C'BL'     IS IT REQUEST NOT TO PRINT IF SAME   C2043000
*                                  AS THE PREVIOUS LINE                 C2043100
         BNE   BLCDPM01            BR IF NOT                            C2043200
         OI    BLDSW,MLCDBL        SET FLAG TO SUPPRESS DUPLICATE FIELD C2043300
         OI    $PROCESS,PROCBL     FLAG THAT 'BL' PROCESSING REQUIRED   C2043400
         B     BLCDPMX             GO TO EXIT                           C2043500
         SPACE 1                                                        C2043600
BLCDPM01 LINE  LINE03              WARN THAT UNKNOWN PRINT MODIFIER FOU C2043700
         SPACE 1                                                        C2043800
BLCDPMX SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2043900
         DROP  R8                  DROP LCD ADDRESSABILITY              C2044000
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2044100
         EJECT                                                          C2044200
*********************************************************************** C2044300
*        GR10/11/12 PROCESSING:    FORCE 'ZZ' SPEC'N IF NECESSARY     * C2044400
*********************************************************************** C2044500
         SPACE 1                                                        C2044600
*        THIS ROUTINE FORCES THE 'ZZ' ATTRIBUTE FOR PACKED DECIMAL,     C2044700
*         BINARY AND FLOATING POINT OUTPUT FIELDS TO PREVENT ZERO       C2044800
*         VALUES BEING OVERRIDDEN WITH BLANKS.                          C2044900
         SPACE 1                                                        C2045000
BLCDFRCZ SUBIN ,                   ENTER ROUTINE                        C2045100
         TRT   BLDFLDF,FRCZTABL    TEST IF OUTPUT FORMAT REQUIRES 'ZZ'  C2045200
         BE    *+8                 BR IF 'ZZ' NOT REQUIRED              C2045300
         OI    BLDSW,MLCDZZ        FORCE 'ZZ' PRINT MODIFIER SPECIFIC'N C2045400
        SUBOUT ,                   RETURN TO CALLER                     C2045500
         EJECT                                                          C2045600
*********************************************************************** C2045700
*        GR10/11/12 PROCESSING:    PROCESS 'DETL' SPECIFICATION       * C2045800
*********************************************************************** C2045900
         SPACE 1                                                        C2046000
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2046100
BLCDDETL SUBIN ,                   ENTER ROUTINE                        C2046200
         CLC   GR10DETL,=C'DETL'   IS THIS A 'DETL' SPECIFICATION       C2046300
         BNE   *+8                 BR IF NOT                            C2046400
         OI    BLDSW2,MLCDDETL     FLAG THAT DETAIL VALUE ALWAYS REQD   C2046500
        SUBOUT ,                   RETURN TO CALLER                     C2046600
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2046700
         EJECT                                                          C2046800
*********************************************************************** C2046900
*        GR10/11/12 PROCESSING:    CONSTRUCT LCD                      * C2047000
*********************************************************************** C2047100
         SPACE 1                                                        C2047200
         USING GR10CARD,R6         ESTABLISH GR10 ADDRESSABILITY        C2047300
         USING LCDNTRY,R8          ESTABLISH LCD ADDRESSABILITY         C2047400
BLCDLCD  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C2047500
         SR    R2,R2               ZEROISE WORK REGISTER                C2047600
         TM    BLDSW,MLCDLIT+MLCDBL DOES THE C/C DEFINE A LITERAL VALUE C2047700
*                                  OR REQUIRE STORE FOR DUPLICATE FLD   C2047800
*                                  PRINT SUPPRESSION.                   C2047900
         BZ    BLCDLCD1            BR IF NOT A LITERAL OR STORE NOT REQ C2048000
         IC    R2,BLDFLDL          GET EXECUTABLE LENGTH OF LITERAL     C2048100
         LA    R2,1(,R2)           STEP UP TO TRUE LENGTH               C2048200
         BNO   BLCDLCD1            BR IF NOT BOTH 'LIT' AND 'BL'        C2048300
         LA    R2,0(R2,R2)         ACQUIRE 2ND EXTENSION FOR 'BL' STORE C2048400
BLCDLCD1 LA    R2,LCDLGTH(,R2)     ADD LENGTH OF BASIC LCD              C2048500
         GETCB LCD                 ACQUIRE AND CHAIN LCD                C2048600
         CHNCB LCD,SCE             SET POINTERS TO FACILITATE CHAINING  C2048700
         EJECT                                                          C2048800
*********************************************************************** C2048900
*        GR10/11/12 PROCESSING:    CONSTRUCT LCD ( CONT'D )           * C2049000
*********************************************************************** C2049100
         SPACE 1                                                        C2049200
         MVC   LCDFLDP,BLDFLDP     SET DISPLACEMENT OF OUTPUT FIELD     C2049300
         MVC   LCDFLDL,BLDFLDL     SET EXECATABLE LENGTH OF OUTPUT FIEL C2049400
         MVC   LCDFLDF,BLDFLDF     SET FORMAT OF OUTPUT FIELD           C2049500
         MVC   LCDNDEC,BLDNDEC     SET NO. OF DECIMALS REQUIRED IN OUTP C2049600
         MVC   LCDLINE,BLDLINE     SET INTERNAL REP'N OF 'LINE+'        C2049700
         MVC   LCDIOLB,GR10IOLB    SET LABEL OF REQUIRED OCB            C2049800
         MVC   LCDLABL,BLDLABL     SET LABEL OF OUTPUT FIELD            C2049900
         MVC   LCDKEYN,BLDKEYN     SET KEY NUMBER ( GR12 ONLY )         C2050000
         MVC   LCDSW,BLDSW         SET FLAG BYTE                        C2050100
         MVC   LCDSW2,BLDSW2       SET 2ND FLAG BYTE                    C2050200
         MVC   LCDID,BLDID         SET C/B FLAG OR NULLIFY IT           C2050300
         TM    LCDSW,MLCDLIT       DOES THIS LCD DEFINE A LITERAL       C2050400
         BZ    *+12                BR IF NOT A LITERAL                  C2050500
         IC    R15,LCDFLDL         GET EXECUTABLE LENGTH OF LITERAL     C2050600
         EX    R15,LCDLITMV        MOVE LITERAL TO LCD                  C2050700
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         C2050800
         DROP  R8                  DROP LCD ADDRESSABILITY              C2050900
         DROP  R6                  DROP GR10 ADDRESSABILITY             C2051000
         EJECT                                                          C2051100
*********************************************************************** C2051200
*        CONVERT BINARY POSITION TO EBCDIC                            * C2051300
*********************************************************************** C2051400
         SPACE 1                                                        C2051500
CONVPOS  SUBIN ,                   ENTER ROUTINE                        C2051600
         MVC   $WRKSNGL,BLDMASK    MOVE EDIT MASK TO WORK AREA          C2051700
         CVD   R1,$WRKDUBL         CONVERT VALUETO DECIMAL              C2051800
         ED    $WRKSNGL,$WRKDUBL+6 RETURN TO CALLER                     C2051900
        SUBOUT ,                   RETIRN TO CALLER                     C2052000
         TITLE 'PARROTC2 - EXECUTED INSTRUCTIONS'                       C2052100
*********************************************************************** C2052200
*        EXECUTED INSTRUCTIONS                                        * C2052300
*********************************************************************** C2052400
         SPACE 1                                                        C2052500
LCDLITMV MVC   LCDLIT-LCDNTRY(0,R8),GR10LIT-GR10CARD(R6) ADD LITERAL    C2052600
         TITLE 'PARROTC2 - MISCELLANEOUS CONSTANTS AND FIELDS'          C2052700
BLDMASK  DC    X'40212020'         MASK TO CONVERT POSN TO EBCDIC       C2052800
BLDSTART EQU   *                   START OF TEMPORARY FIELDS            C2052900
BLDFLDP  DS    H                   DISPLACEMENT OF OUTPUT FIELD         C2053000
BLDFLDL  DS    CL1                 LENGTH OF OUTPUT FIELD               C2053100
BLDFLDF  DS    CL1                 FORMAT OF OUTPUT FIELD               C2053200
BLDNDEC  DS    CL1                 NO OF DECIMALS REQUIRED IN OUTPUT    C2053300
BLDLINE  DS    CL1                 INTERNAL REP'N OF 'LINE+'            C2053400
BLDSW    DC    X'00'               SWITCH BYTE                          C2053500
BLDSW2   DC    X'00'               2ND SWITCH BYTE                      C2053600
BLDKEYN  DS    CL1                 KEY LEVEL TO WHICH GR12 APPLIES      C2053700
BLDLABL  DS    CL2                 LABEL OF OUTPUT FIELD                C2053800
BLDID    DS    CL1                 C/B IDENTIFIER OR NULLIFIER          C2053900
BLDEND   EQU   *                   END OF TEMPORARY FIELDS              C2054000
         SPACE 1                                                        C2054100
LINE01   DC    C'PTC2-001-W  COLUMN 5 OF THE PRECEDING CARD IS INVALIDL*C2054200
               Y NON-BLANK - IGNORED'                                   C2054300
LINE02   DC    C'PTC2-002-E  THE PRECEDING CONTROL CARD WILL BE IGNORED*C2054400
                BECAUSE OF THE ERROR(S) DESCRIBED ABOVE'                C2054500
LINE03   DC    C'PTC2-003-W  COLUMNS 19-20 CONTAIN AN UNKNOWN PRINT MODIC2054600
               IFICATION REQUEST - IGNORED'                             C2054700
LINE04   DC    C'PTC2-004-W  UNKNOWN ''LINE+'' REQUEST: ''LINE+1'' FORCEC2054800
               ED'                                                      C2054900
LINE05   DC    C'PTC2-005-E  UNKNOWN OUTPUT FORMAT SPECIFICATION'       C2055000
LINE06   DC    C'PTC2-006-E  ILLEGAL SYNTAX IN FIELD NAME ( COLUMNS 13-1C2055100
               17 )'                                                    C2055200
LINE07   DC    C'PTC2-007-E  START POSITION OF OUTPUT IS BEYOND THE END*C2055300
                POSITION: EXTREMELY DIFFICULT TO ACHIEVE'               C2055400
LINE09   DC    C'PTC2-009-W  NUMBER OF DECIMALS (COLUMN 18) MAY NOT BE *C2055500
               SUPPLIED FOR A LITERAL SPECIFICATION: IGNORED'           C2055600
LINE12   DC    C'PTC2-012-W  KEY LEVEL 0 INVALID IN GR10 CARD: TREATED *C2055700
               AS UNQUALIFIED GR10'                                     C2055800
         TITLE 'PARROTC2 - OUTPUT FORMAT CONVERSION TABLE'              C2055900
*        THIS CONVERSION TABLE IS USED TO CONVERT THE OUTPUT FORMAT     C2056000
*         SPECIFIED IN THE CONTROL CARD INTO THE EQUIVALENT INTERNAL    C2056100
*         REPRESENTATION OF THAT FORMAT.  NOTE THAT THIS INTERNAL       C2056200
*         REPRESENTATION IS USED FOR INDEXED BRANCHES.                  C2056300
         SPACE 1                                                        C2056400
FMATABLE EQU   *                   OUTPUT FORMAT CONVERSION TABLE       C2056500
FMATABNO EQU   (FMATABND-FMATABLE)/2 NO OF TABLE ELEMENTS               C2056600
         DC    C'X',AL1(FLCDCHAR)  CHARACTER                            C2056700
         DC    C'H',AL1(FLCDHEX)   HEXADECIMAL                          C2056800
         DC    C'B',AL1(FLCDBIT)   BIT STRING                           C2056900
         DC    C'9',AL1(FLCDNUM9)  DIPLAY NUMERIC WITH LEADING ZEROES   C2057000
         DC    C'Z',AL1(FLCDNUMZ)  DISPLAY NUMERIC WITHOUT LEADING ZERO C2057100
         DC    C',',AL1(FLCDNUME)  EDITED DISPALY NUMERIC               C2057200
         DC    C'$',AL1(FLCDNUM$)  EDITED DISPLAY NUMERIC WITH FLOATING C2057300
         DC    C'P',AL1(FLCDPD)    PACKED DECIMAL                       C2057400
         DC    C'1',AL1(FLCDBIN)   BINARY                               C2057500
         DC    C'D',AL1(FLCDDISP)  SIGNED DISPLAY NUMERIC               C2057600
         DC    C'F',AL1(FLCDFP)    FLOATING POINT                       C2057700
FMATABND EQU   *                   END OF OUTPUT FORMAT CONVERSION TABL C2057800
         SPACE 5                                                        C2057900
*        TRANSLATE TABLE USED TO DETERMINE WHICH OUTPUT FORMATS REQUIRE C2058000
*         THE 'ZZ' PRINT MODIFIER ATTRIBUTE TO BE FORCED.               C2058100
         SPACE 1                                                        C2058200
FRCZTABL DC    20AL1(0)            BASIC TABLE ( NO 'ZZ' FORCED )       C2058300
         ORG   FRCZTABL+FLCDPD     ORG TO TABLE ENTRY REQUIRING 'ZZ'    C2058400
         DC    AL1(1)              INDICATE 'ZZ' REQUIRED               C2058500
         ORG   FRCZTABL+FLCDBIN    ORG TO TABLE ENTRY REQUIRING 'ZZ'    C2058600
         DC    AL1(1)              INDICATE 'ZZ' REQUIRED               C2058700
         ORG   FRCZTABL+FLCDFP     ORG TO TABLE ENTRY REQUIRING 'ZZ'    C2058800
         DC    AL1(1)              INDICATE 'ZZ' REQUIRED               C2058900
         ORG                                                            C2059000
         TITLE 'PARROTC2 - CONVERSION TABLE FOR ''LINE+'' REQUESTS'     C2059100
*        THIS CONVERSION TABLE IS USED TO CONVERT A 'LINE+' REQUEST     C2059200
*         INTO THE EQUIVALENT MACHINE CONTROL CHARACTER                 C2059300
         SPACE 1                                                        C2059400
LINETABL EQU   *                   START OF TABLE                       C2059500
LINETABN EQU   (LINETABE-LINETABL)/2 NO OF ELEMENTS IN TABLE            C2059600
         DC    C'0',AL1(SPCE0AFT)  PRINT, NO SPACE                      C2059700
         DC    C'1',AL1(SPCE1AFT)  PRINT THAN 1 SPACE                   C2059800
         DC    C'2',AL1(SPCE2AFT)  PRINT THEN TWO SPACES                C2059900
         DC    C'3',AL1(SPCE3AFT)  PRINT THEN 3 SPACES                  C2060000
         DC    C'E',AL1(PAGESKIP)  NO PRINT, SKIP TO NEX PAGE           C2060100
LINETABE EQU   *                   END OF TABLE                         C2060200
         SPACE 5                                                        C2060300
        SCSEND C2                  TERMINATE CSET PARROTC2              C2060400
         END                                                            C2060500
