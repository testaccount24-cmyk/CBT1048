         TITLE 'PARROTC3 - GR13 CONTROL CARD PROCESSING'                C3000100
         MACRO                                                          C3000200
         $CB   &PRINT=OFF          MACRO TO GENERATE DSECTS             C3000300
         PUSH  PRINT               PRESERVE PRINT OPTIONS               C3000400
         PRINT &PRINT              SET PRINT OPTIONS FOR DSECTS         C3000500
               EJECT                                                    C3000600
         CBPREFIX ,                CONTROL BLOCK PREFIX                 C3000700
               EJECT                                                    C3000800
         CCB   ,                   CENTRAL CONTROL BLOCK                C3000900
               EJECT                                                    C3001000
         CSECTID ,                 CSECT IDENTIFICATION PREFIX          C3001100
               EJECT                                                    C3001200
         DEFOPCD DSECT=YES         OPERATION/FUNCTION DESCRIPTOR        C3001300
               EJECT                                                    C3001400
         EQUATES ,                 GENERATE STANDARD EQUATES            C3001500
               EJECT                                                    C3001600
         FDB   ,                   FIELD DEFINITION BLOCK               C3001700
               EJECT                                                    C3001800
         GR13  ,                   GR08 CONTROL CARD                    C3001900
               EJECT                                                    C3002000
         LIB   ,                   LABEL IDENTIFCIATION BLOCK           C3002100
               EJECT                                                    C3002200
         PCB   ,                   PROCESSING CONTROL BLOCK             C3002300
               EJECT                                                    C3002400
         SCE   ,                   SELECTION CRITERION ELEMENT          C3002500
               EJECT                                                    C3002600
         SPD   ,                   STORAGE POOL DEFINITION              C3002700
         POP   PRINT               RESTORE ORIGINAL PRINT OPTIONS       C3002800
         MEND                                                           C3002900
         SPACE 4                                                        C3003000
PARROTC3  CSECT                                                         C3003100
         $CB   ,                   GENERATE DSECTS AND EQUATES          C3003200
         EJECT                                                          C3003300
     SCSECT C3                                                          C3003400
         USING PARROTC3,R12         ESTABLISH PARROTC3 ADDRESSABILITY   C3003500
         USING CCBNTRY,R13         ESTABLISH CCB ADDRESSABILITY         C3003600
         SPACE 1                                                        C3003700
         ENTRY CCGR13              PROCESS GR13 CONTROL CARDS           C3003800
         ENTRY PARENDC3            END IF CSECT                         C3003900
         EJECT                                                          C3004000
*********************************************************************** C3004100
*        PROCESS GR13 CONTROL CARDS                                   * C3004200
*********************************************************************** C3004300
         SPACE 1                                                        C3004400
         USING GR13CARD,R6         ESTABLISH C/C ADDRESSABILITY         C3004500
         USING PCBNTRY,R8          ESTABLISH C/B ADDRESSABILITY         C3004600
         USING OPCDNTRY,R7         ESTABLISH OPCD DEF'N ADDRESSABILITY  C3004700
CCGR13   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C3004800
         CBAL  R4,VERIFOPC         ENSURE OP-CODE IS ON TABLE           C3004900
         BNE   CCGR13X             EXIT IF UNKNOWN OPERATION CODE       C3005000
         CBAL  R4,VERIFLDS         ENSURE THAT MANDATORY FIELDS PRESENT C3005100
         BNE   CCGR13X             EXIT IF MANDATORY FIELDS MISSING     C3005200
         CBAL  R4,BLDPCB           ACQUIRE AND INITIALISE PCB           C3005300
         LA    R7,3                SET TO LOOP THROUGH 3 TEST BUCKETS   C3005400
         LA    R10,GR13TST1        POINT AT FIRST TEST BUCKET           C3005500
CCGR1301 CBAL  R4,BLDSCE           CONSTRUCT SCE IF NECESSARY           C3005600
         LA    R10,GR13TST2-GR13TST1(,R10) INCREMENT TO NEXT BUCKET     C3005700
         BCT   R7,CCGR1301         LOOP BACK IF ANY BUCKETS LEFT        C3005800
CCGR13X SUBOUT RESTORE=R4          RESTOE AND RETURN                    C3005900
         DROP  R7                  DROP OPCD DEF'N ADDRESSABILITY       C3006000
         DROP  R8                  DROP PCB ADDRESSABILITY              C3006100
         DROP  R6                  DROP GR13 ADDRESSABILITY             C3006200
         EJECT                                                          C3006300
*********************************************************************** C3006400
*        GR13 PROCESSING:          LOCATE OPERATION CODE IN TABLE     * C3006500
*********************************************************************** C3006600
         SPACE 1                                                        C3006700
*        THIS ROUTINE VERIFIES THAT THE SUPPLIED OPERATION CODE IS ON   C3006800
*         THA TABLE OF ACCEPTABLE OPERATION CODES.                      C3006900
         SPACE 1                                                        C3007000
*        UPON RETURN, THE COND CODE IS SET THUS :-                      C3007100
*         BE  - OPERATION CODE VALID: REG 7 CONTAINS ADDR OF DEF'N      C3007200
*         BNE - OPERATION CODE NOT ON TABLE                             C3007300
         SPACE 1                                                        C3007400
         USING GR13CARD,R6         ESTABLISH C/C ADDRESSABILITY         C3007500
         USING OPCDNTRY,R7         ESTABLISH OPCD DEF'N ADDRESSABILITY  C3007600
VERIFOPC SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C3007700
         LA    R15,OPCDNO          SET TO LOOP THROUGH TABLE ELEMENTS   C3007800
         LA    R7,OPCDTABL         POINT REG 7 AT FIRST OPCD DEFINITION C3007900
VERIFOP1 CLC   GR13OPCD,OPCDOPCD   IS THIS THE CORRECT ENTRY            C3008000
         BE    VERIFOPX            GO TO EXIT IF YES                    C3008100
         LA    R7,OPCDLGTH(,R7)    INCREMENT TO NEXT TABLE ENTRY        C3008200
         BCT   R15,VERIFOP1        LOOP BACK IF ANY ENTRIES LEFT        C3008300
         LINE  LINE01,ELEVEL=YES   WARN OF UNKNOWN OPERATION CODE       C3008400
         CLI   *,Y                 SET COND CODE TO SHOW ERROR          C3008500
         B     *+8                 BYPASS RESETTING COND CODE           C3008600
VERIFOPX CLI   *,X                 SET COND CODE TO SHOW OPCD VALID     C3008700
        SUBOUT RESTORE=R4          RESTOE AND RETURN                    C3008800
         DROP  R7                  DROP OPCD DEF'N ADDRESSABILITY       C3008900
         DROP  R6                  DROP GR13 ADDRESSABILITY             C3009000
         EJECT                                                          C3009100
*********************************************************************** C3009200
*        GR13 PROCESSING:          ENSURE THAT MANDATORY FLDS PRESENT * C3009300
*********************************************************************** C3009400
         SPACE 1                                                        C3009500
*        THIS ROUTINE VERIFIES THAT THE MANDATORY GR13 FIELDS ARE       C3009600
*         PRESENT, USING THE OPERATION CODE DEFINITION TO DECIDE        C3009700
*         WHAT FIELDS ARE MANDATORY FOR THE SUPPLIED OPERATION CODE.    C3009800
         SPACE 1                                                        C3009900
*        UPON RETURN, THE COND CODE IS SET THUS :-                      C3010000
*         BE  - ALL MANDATORY FIELDS ARE PRESENT                        C3010100
*         BNE - AT LEAST ONE MANDATORY FIELD IS MISSING                 C3010200
         SPACE 1                                                        C3010300
         USING GR13CARD,R6         ESTABLISH C/C ADDRESSABILITY         C3010400
         USING OPCDNTRY,R7         ESTABLISH OPCD DEF'N ADDRESSABILITY  C3010500
VERIFLDS SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C3010600
         MVI   VERIFLDT,C'X'       ASSUME THAT CARD IS GOOD             C3010700
         SPACE 1                                                        C3010800
         TM    OPCDFLG1,F1OPMOD    IS OP-CODE MODIFIER MANDATORY        C3010900
         BZ    VERIFLD1            BR IF NOT TO NEXT TEST               C3011000
         CLC   GR13OPCM,$SPACES    IS MODIFIER SUPPLIED                 C3011100
         BNE   VERIFLD1            BR IF MODIFIER SUPPLIED              C3011200
         LINE  LINE02,ELEVEL=YES   TELL OF ERROR                        C3011300
         MVI   VERIFLDT,C'Y'       SET ERROR FLAG FOR LATER TESTING     C3011400
         SPACE 1                                                        C3011500
VERIFLD1 TM    OPCDFLG1,F1OPCARD   IS LABEL FIELD MANDATORY             C3011600
         BZ    VERIFLD2            BR IF NOT TO NEXT TEST               C3011700
         CLC   GR13LABL,$SPACES    IS FIELD LABEL SUPPLIED              C3011800
         BNE   VERIFLD2            BR IF MODIFIER SUPPLIED              C3011900
         LINE  LINE03,ELEVEL=YES   TELL OF ERROR                        C3012000
         MVI   VERIFLDT,C'Y'       SET ERROR FLAG FOR LATER TESTING     C3012100
         SPACE 1                                                        C3012200
VERIFLD2 TM    OPCDFLG1,F1OPIND    IS INDICATOR MANDATORY               C3012300
         BZ    VERIFLD3            BR IF NOT TO NEXT TEST               C3012400
         CLC   GR13IND,$SPACES     IS INDICATOR SUPPLIED                C3012500
         BNE   VERIFLD3            BR IF MODIFIER SUPPLIED              C3012600
         LINE  LINE04,ELEVEL=YES   TELL OF ERROR                        C3012700
         MVI   VERIFLDT,C'Y'       SET ERROR FLAG FOR LATER TESTING     C3012800
         SPACE 1                                                        C3012900
VERIFLD3 EQU   *                   ADD FURTHER TESTS                    C3013000
         SPACE 1                                                        C3013100
         CLI   VERIFLDT,C'X'       SET COND CODE FOR CALLER             C3013200
        SUBOUT RESTORE=R4          RESTOE AND RETURN                    C3013300
         DROP  R7                  DROP OPCD DEF'N ADDRESSABILITY       C3013400
         DROP  R6                  DROP GR13 ADDRESSABILITY             C3013500
         EJECT                                                          C3013600
*********************************************************************** C3013700
*        GR13 PROCESSING:          CONSTRUCT AND INITIALISE PCB       * C3013800
*********************************************************************** C3013900
         SPACE 1                                                        C3014000
         USING GR13CARD,R6         ESTABLISH C/C ADDRESSABILITY         C3014100
         USING PCBNTRY,R8          ESTABLISH C/B ADDRESSABILITY         C3014200
         USING OPCDNTRY,R7         ESTABLISH OPCD DEF'N ADDRESSABILITY  C3014300
BLDPCB   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C3014400
         CBAL  R4,GETPCB           ACQUIRE AND CHAIN PCB                C3014500
         MVC   PCBLABL,GR13LABL    SET FIELD / CC LABEL IN PCB          C3014600
         MVC   PCBTYPE,GR13PTYP    SET PROCESSING ( I.E. PHASE ) TYPE   C3014700
         MVC   PCBCBID,OPCDCBID    SET ID OF ASSOCIATED C/B ( IF ANY )  C3014800
         MVC   PCBOPCD,GR13OPCD    RECORD EBCDIC OPERATION CODE         C3014900
         MVC   PCBOPCM,GR13OPCM    SET OPERATION CODE MODIFIER          C3015000
         MVC   PCBFUNC,OPCDICDE    SET INTERNAL FORM OF OPERATION CODE  C3015100
         SPACE 1                                                        C3015200
         TM    OPCDFLG1,F1OPLIT    IS A LITERAL MANDATORY               C3015300
         BO    BLDPCB1             BR IF YES                            C3015400
         CLC   GR13LIT,$SPACES     WAS A NON-MANDATORY LITERAL SUPPLIED C3015500
         BE    BLDPCB2             BR IF NOT SUPPLIED                   C3015600
BLDPCB1  MVC   PCBLIT,GR13LIT      SET LITERAL IN PCB EXTENSION         C3015700
         OI    PCBSW,MPCBLIT       FLAG PCB THAT LITERAL EXISTS         C3015800
         SPACE 1                                                        C3015900
BLDPCB2  TM    OPCDFLG1,F1OPCARD   IS THERE AN ASSOCIATED CONTROL CARD  C3016000
         BNE   *+8                 BR IF NOT                            C3016100
         OI    PCBSW,MPCBCBXX      FLAG PCB THAT C/B ADDR MUST BE FOUND C3016200
         SPACE 1                                                        C3016300
         CLC   GR13IND,$SPACES     WAS AN INIDCATOR SUPPLIED            C3016400
         BE    BLDPCB3             BR IF NO INIDCATOR SUPPLIED          C3016500
         LA    R15,GR13IND         POINT WORK REG AT INDICATOR SPEC'N   C3016600
         CBAL  R4,CONVIND          CONVERT IND SPEC TO INTERNAL FORM    C3016700
         STH   R15,PCBINDD         PRESERVE DISPLACEMENT OF IND BYTE    C3016800
         STC   R14,PCBINDM         PRESERVE BIT MASK FOR APPROPRIATE BI C3016900
         OI    PCBSW,MPCBIND       FLAG PCB THAT INDICATOR EXISTS       C3017000
         SPACE 1                                                        C3017100
BLDPCB3  EQU   *                   FURTHER INITIALISATION               C3017200
        SUBOUT RESTORE=R4          RESTOE AND RETURN                    C3017300
         DROP  R7                  DROP OPCD DEF'N ADDRESSABILITY       C3017400
         DROP  R8                  DROP PCB ADDRESSABILITY              C3017500
         DROP  R6                  DROP GR13 ADDRESSABILITY             C3017600
         EJECT                                                          C3017700
*********************************************************************** C3017800
*        GR13 PROCESSING:          ACQUIRE AND CHAIN PCB              * C3017900
*********************************************************************** C3018000
         SPACE 1                                                        C3018100
         USING GR13CARD,R6         ESTABLISH C/C ADDRESSABILITY         C3018200
         USING PCBNTRY,R8          ESTABLISH C/B ADDRESSABILITY         C3018300
         USING OPCDNTRY,R7         ESTABLISH OPCD DEF'N ADDRESSABILITY  C3018400
GETPCB   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              C3018500
         LA    R2,PCBLGTH          STE PCB LENGTH IN REG 2              C3018600
         TM    OPCDFLG1,F1OPLIT    IS A LITERAL MANDATORY               C3018700
         BZ    GETPCB1             BR IF LITERAL NOT MANDATORY          C3018800
         LA    R2,L'PCBLIT(,R2)    ADD LENGTH OF PCB LITERAL EXTENSION  C3018900
         B     GETPCB2             GO ACQUIRE CORE                      C3019000
GETPCB1  CLC   GR13LIT,$SPACES     WAS NON-MANDATORY LITERAL SUPPLIED   C3019100
         BNE   *+8                 BR IF NOT SUPPLIED                   C3019200
         LA    R2,L'PCBLIT(,R2)    ADD LENGTH OF PCB LITERAL EXTENSION  C3019300
GETPCB2  GETCB PCB                 ACQUIRE AND CHAIN PCB                C3019400
         CHNCB PCB,SCE             SET POINTERS TO FACILITATE CHAINING  C3019500
         OI    PCBSW,MPCBFUNC      FLAG THAT PCB IS SPECIAL FUNCTION    C3019600
        SUBOUT RESTORE=R4          RESTOE AND RETURN                    C3019700
         DROP  R7                  DROP OPCD DEF'N ADDRESSABILITY       C3019800
         DROP  R8                  DROP PCB ADDRESSABILITY              C3019900
         DROP  R6                  DROP GR13 ADDRESSABILITY             C3020000
         EJECT                                                          C3020100
*********************************************************************** C3020200
*        CONVERT BINARY POSITION TO EBCDIC                            * C3020300
*********************************************************************** C3020400
         SPACE 1                                                        C3020500
CONVPOS  SUBIN ,                   ENTER ROUTINE                        C3020600
         MVC   $WRKSNGL,BLDMASK    MOVE EDIT MASK TO WORK AREA          C3020700
         CVD   R1,$WRKDUBL         CONVERT VALUETO DECIMAL              C3020800
         ED    $WRKSNGL,$WRKDUBL+6 RETURN TO CALLER                     C3020900
        SUBOUT ,                   RETIRN TO CALLER                     C3021000
         TITLE 'PARROTC3 - EXECUTABLE INSTRUCTIONS'                     C3021100
*********************************************************************** C3021200
*        EXECUTABLE INSTRUCTIONS                                      * C3021300
*********************************************************************** C3021400
         SPACE 1                                                        C3021500
         TITLE 'PARROTC3 - MISCELLANEOUS CONSTANTS AND FIELDS'          C3021600
BLDMASK  DC    X'40212020'         MASK TO CONVERT POSN TO EBCDIC       C3021700
VERIFLDT DC    C'X'                USED TO SET COND CODE BY 'VERIFLDS'  C3021800
         TITLE 'PARROTC3 - ERROR MESSAGES'                              C3021900
LINE01   DC    C'PTC3-001-E  COLUMNS 5-6 SPECIFY AN UNKNOWN OPERATION C*C3022000
               ODE: CONTROL CARD IGNORED'                               C3022100
LINE02   DC    C'PTC3-002-E  OPERATION CODE MODIFIER ( COLUMN 7 ) IS MANC3022200
               NDATORY FOR THIS OPERATION CODE: CONTROL CARD IGNORED'   C3022300
LINE03   DC    C'PTC3-030-E  FIELD LABEL ( COLUMNS 8-9 ) ARE MANDATORY FC3022400
               FOR THIS OPERATION CODE: CONTROL CARD IGNORED'           C3022500
LINE04   DC    C'PTC3-004-E  AN INDICATOR SPECIFICATION ( COLUMNS 11-12*C3022600
                ) IS MANDATORY FOR THIS OPERATION CODE: CONTROL CARD IGNC3022700
               NORED'                                                   C3022800
         TITLE 'PARROTC3 - FUNCTION/OPERATION CODE DEFINITIONS'         C3022900
OPCDTABL EQU   *                   START OF OP-CODE TABLE               C3023000
         SPACE 1                                                        C3023100
 DEFOPCD OPCD=$1,ICODE=01                                               C3023200
 DEFOPCD OPCD=$2,ICODE=02                                               C3023300
 DEFOPCD OPCD=AB,ICODE=03                                               C3023400
 DEFOPCD OPCD=C3,ICODE=04,CBID=MFD,CARD=GR33                            C3023500
 DEFOPCD OPCD=DW,ICODE=05,CBID=SAB,CARD=GR40                            C3023600
 DEFOPCD OPCD=D8,ICODE=06,CBID=DCE,CARD=GR35                            C3023700
 DEFOPCD OPCD=E2,ICODE=07,CBID=LIB,CARD=GR04,LIT=Y                      C3023800
 DEFOPCD OPCD=E3,ICODE=08,CBID=LIB,CARD=GR04,LIT=Y                      C3023900
 DEFOPCD  OPCD=I,ICODE=09,CBID=ISR,CARD=GR20                            C3024000
 DEFOPCD OPCD=L7,ICODE=10,CBID=LIB,CARD=GR04                            C3024100
 DEFOPCD OPCD=L8,ICODE=11,CBID=LIB,CARD=GR04                            C3024200
 DEFOPCD OPCD=L9,ICODE=12,CBID=CCE,CARD=GR32                            C3024300
 DEFOPCD OPCD=OF,ICODE=13,IND=Y                                         C3024400
 DEFOPCD OPCD=ON,ICODE=14,IND=Y                                         C3024500
 DEFOPCD OPCD=R5,ICODE=15,CBID=UEB,CARD=GR39                            C3024600
 DEFOPCD  OPCD=S,ICODE=16,CBID=SAB,CARD=GR40                            C3024700
         SPACE 1                                                        C3024800
OPCDNO   EQU   (*-OPCDTABL)/OPCDLGTH NUMBER OF ELEMENTS IN TABLE        C3024900
         SPACE 5                                                        C3025000
        SCSEND C3                  TERMINATE CSET PARROTC3              C3025100
         END                                                            C3025200
