         TITLE 'PARROTA2 - CONTROL CARD PROCESSING - PART 1'            A2000100
         MACRO                                                          A2000200
         $CB   &PRINT=OFF          MACRO TO GENERATE DSECTS             A2000300
         PUSH  PRINT               PRESERVE PRINT OPTIONS               A2000400
         PRINT &PRINT              SET PRINT OPTIONS FOR DSECTS         A2000500
               EJECT                                                    A2000600
         CBPREFIX ,                CONTROL BLOCK PREFIX                 A2000700
               EJECT                                                    A2000800
         CCB   ,                   CENTRAL CONTROL BLOCK                A2000900
               EJECT                                                    A2001000
         CSECTID ,                 CSECT IDENTIFICATION PREFIX          A2001100
               EJECT                                                    A2001200
         EQUATES ,                 GENERATE STANDARD EQUATES            A2001300
               EJECT                                                    A2001400
         FDB   ,                   FIELD DEFINITION BLOCK               A2001500
               EJECT                                                    A2001600
         GR00                      GR00 CONTROL CARD                    A2001700
               EJECT                                                    A2001800
         GR01  ,                   GR01 CONTROL CARD                    A2001900
               EJECT                                                    A2002000
         GR02  ,                   GR02 CONTROL CARD                    A2002100
               EJECT                                                    A2002200
         HDG   ,                   HEADING LINE STORAGE AREA            A2002300
               EJECT                                                    A2002400
         OCB   ,                   OUTPUT CONTROL BLOCK                 A2002500
               EJECT                                                    A2002600
         PARROTT DSECT=YES         CONVERSION TABLES                    A2002700
               EJECT                                                    A2002800
         RST   ,                   RECORD SELECTION TEST DEF'N          A2002900
               EJECT                                                    A2003000
         SCBUCKET ,                SELECTION CRITERION ' BUCKET '       A2003100
               EJECT                                                    A2003200
         SCE   ,                   SELECTION CRITERION ELEMENT          A2003300
               EJECT                                                    A2003400
         SPD   ,                   STORAGE POOL DEFINITION              A2003500
         POP   PRINT               RESTORE ORIGINAL PRINT OPTIONS       A2003600
         MEND                                                           A2003700
         EJECT                                                          A2003800
PARROTA2  CSECT                                                         A2003900
         $CB   ,                   GENERATE DSECTS AND EQUATES          A2004000
         EJECT                                                          A2004100
     SCSECT A2                                                          A2004200
         USING PARROTA2,R12         ESTABLISH PARROTA2 ADDRESSABILITY   A2004300
         USING CCBNTRY,R13         ESTABLISH CCB ADDRESSABILITY         A2004400
         SPACE 1                                                        A2004500
         ENTRY CCGR00              PROCESS GR00 CONTROL CARDS           A2004600
         ENTRY CCGR01              PROCESS GR01 CONTROL CARDS           A2004700
         ENTRY CCGR02              PROCESS GR02 CONTROL CARDS           A2004800
         ENTRY PARENDA2            END IF CSECT                         A2004900
         TITLE 'PARROTA2 - PROCESS GR00 CONTROL CARDS'                  A2005000
*********************************************************************** A2005100
*        PROCESS GR00 CONTROL CARDS                                   * A2005200
*********************************************************************** A2005300
         SPACE 1                                                        A2005400
*        GR00 CONTROL CARDS DEFINE ADDITIONAL OUTPUT DD STATEMENTS      A2005500
         SPACE 1                                                        A2005600
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2005700
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2005800
CCGR00   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2005900
         CBAL  R4,BLDSCAN          TEST IF OCB FOR THIS LABEL ALREADY E A2006000
         BE    CCGR0001            BR IF OCB ALREADY EXITSTS            A2006100
         CBAL  R4,BLDOCB           CONSTRUCT AND CHAIN A NEW OCB        A2006200
CCGR0001 TBLANK GR00UNUS           ENSURE THAT UNUSED COLUMNS ARE BLANK A2006300
         BE    CCGR0002            BR IF UNUSED COLUMNS ARE BLANK       A2006400
         LINE  LINE02              WARN OF POTENTIALLY MISPLACED DATA   A2006500
CCGR0002 CBAL  R4,BLDDDN           PROCESS DD-NAME                      A2006600
         CBAL  R4,BLDREPT          PROCESS REPORT SPECIFICATION         A2006700
         CBAL  R4,BLDDETL          PROCESS DETAIL SPECIFICATION         A2006800
         CBAL  R4,BLD2080          PROCESS TWENTY80 SPECIFICATION       A2006900
         CBAL  R4,BLDLCNT          PROCESS LINES/PAGE SPECIFICATION     A2007000
         CBAL  R4,BLDLOGO          PROCESS LOGO NAME SPECIFICATION      A2007100
         CBAL  R4,BLDRID           PROCESS REPORT IDENTIFIER SPEC'N     A2007200
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         A2007300
         DROP  R8                  DROP OCB ADDRESSABILITY              A2007400
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2007500
         EJECT                                                          A2007600
*********************************************************************** A2007700
*        PROCESS GR00 CARDS:       TEST IF OCB ALREADY EXISTS         * A2007800
*********************************************************************** A2007900
         SPACE 1                                                        A2008000
*        THIS ROUTINE SCANS THE OCB CHAIN TO DETERMINE IF AN OCB ALREAD A2008100
*         EXISTS WITH THIS LABEL.  IF IT DOES, THEN THE INFORMATION IN  A2008200
*         THE GR00 CARD WILL BE MERGED WITH ANY PREVIOUSLY ENTERED.     A2008300
         SPACE 1                                                        A2008400
*        UPON EXIT :-                                                   A2008500
*         R8  - OCB ADDRESS ( IF MATCH FOUND )                          A2008600
         SPACE 1                                                        A2008700
*        UPON RETURN, THE COND CODE IS SET THUS :-                      A2008800
*         BE  - OCB FOUND WITH THIS LABEL                               A2008900
*         BNE - NO OCB FOUND                                            A2009000
         SPACE 1                                                        A2009100
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2009200
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2009300
BLDSCAN  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2009400
         LTA   R8,$OCBOCB          GET ADDRESS OF 1ST OCB ( 'PRINT' )   A2009500
         B     *+8                 BYPASS FORWARD CHAINING              A2009600
OCBSCAN1 LTA   R8,OCBOCB,OCBSCANY  GET ADDRESS OF NEXT OCB              A2009700
         CLC   OCBLABL,GR00LABL    DOES OCB HAVE SAME LABEL AS GR00 CAR A2009800
         BNE   OCBSCAN1            LOOP BACK FOR NEXT OCB IF NOT        A2009900
         LINE  LINE01              WARN THAT PREVIOUS SPEC WILL BE OVER A2010000
         L     R2,OCBIOSW          SET DCBSW ADDRESS IN REGISTER 2      A2010100
         B     OCBSCANX            GO TO EXIT SHOWING MATCH FOUND       A2010200
OCBSCANY CLI   *,Y                 SET COND CODE TO SHWO NO MATCH       A2010300
         B     *+8                 BYPASS RESETTIND COND CODE           A2010400
OCBSCANX CLI   *,X                 SET COND CODE TO SHOW MATCH FOUND    A2010500
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         A2010600
         DROP  R8                  DROP OCB ADDRESSABILITY              A2010700
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2010800
         EJECT                                                          A2010900
*********************************************************************** A2011000
*        PROCESS GR00 CARDS:       CONSTRUCT AND CHAIN NEW OCB        * A2011100
*********************************************************************** A2011200
         SPACE 1                                                        A2011300
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2011400
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2011500
BLDOCB   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2011600
         LA    R2,OCBLGTH+IOPSLGTH SET LENGTH OF OCB + DCBSW IN REG 2   A2011700
         GETCB OCB                 ACQUIRE AND CHAIN OCB                A2011800
         CHNCB OCB,HDG             SET POINTERS TO FACILITATE CHAINING  A2011900
         MVC   OCBLABL,GR00LABL    SET LABEL IN OCB                     A2012000
         LA    R2,OCBLGTH(,R8)     POINT R2 PAST OCB AT DCBSW           A2012100
         ST    R2,OCBIOSW          PRESERVE DCBSW ADDRESS IN OCB        A2012200
         MVI   IOPSEOD(R2),C'X'    INITIALISE DCBSW EOD INDICATOR       A2012300
         MVC   IOPSISI(2,R2),$SPACES INITIALISE DCBSW ISAM ERROR FLAGS  A2012400
         ZAP   IOPSRCNT(4,R2),$ZERO INITIALISE DCBSW RECORD COUNT       A2012500
         ZAP   OCBPCNT,$ZERO       INITIALISE OCB PAGE COUNT            A2012600
         ZAP   OCBLPGE,$ZERO       INITILAISE OCB LINES/PAGE SPEC       A2012700
         ZAP   OCBLCNT,$ZERO       ZEROISE LINE-COUNT STORE             A2012800
         MVC   OCBRID,$SPACES      BLANK OUT OPTIONAL REPORT ID         A2012900
         MVI   OCBPCC,SPCE1AFT     SET TO DEFAULT SINGLE SPACING        A2013000
         MVC   OCBLOGO,=CL8'PARROT' SET DEFAULT LOGO NAME               A2013100
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         A2013200
         DROP  R8                  DROP OCB ADDRESSABILITY              A2013300
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2013400
         EJECT                                                          A2013500
*********************************************************************** A2013600
*        PROCESS GR00 CARDS:       PROCESS DDNAME SPECIFICATION       * A2013700
*********************************************************************** A2013800
         SPACE 1                                                        A2013900
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2014000
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2014100
BLDDDN   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2014200
         CLC   GR00DDN,$SPACES     IS A DDNAME SUPPLIED                 A2014300
         BE    BLDDDN1             BR IF DDNAME NOT SUPPLIED            A2014400
         MVC   IOPSDDN(8,R2),GR00DDN SET DDNAME IN DCBSW                A2014500
         B     BLDDDNX             GO TO ROUTINE EXIT                   A2014600
BLDDDN1  CLI   IOPSDDN(R2),X'00'   DOES DDNAME ALREADY EXIST            A2014700
         BNE   BLDDDNX             EXIT IF DDNAME ALREADY EXISTS        A2014800
         LINE  LINE03              WARN THAT DDNAME OMITTED             A2014900
         MVI   OCBID,NULLCB        NULLIFY OCB TO PREVENT SILLY PROCESS A2015000
BLDDDNX SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         A2015100
         DROP  R8                  DROP OCB ADDRESSABILITY              A2015200
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2015300
         EJECT                                                          A2015400
*********************************************************************** A2015500
*        PROCESS GR00 CARDS:       PROCESS 'REPORT' FLAG SPECIFICATION* A2015600
*********************************************************************** A2015700
         SPACE 1                                                        A2015800
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2015900
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2016000
BLDREPT  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2016100
         OI    OCBSW,MOCBREPT      ASSUME DEFAULT OF 'REPORT'           A2016200
         CLI   GR00REPT,C' '       IS REPORT FLAG OMITTED               A2016300
         BE    BLDREPTX            GO TO EXIT AND TAKE DEFAULT          A2016400
         CLI   GR00REPT,C'Y'       IS FLAG ON                           A2016500
         BE    BLDREPTX            GOT TO EXIT IF YES.                  A2016600
         CLI   GR00REPT,C'N'       IS FLAG SET OFF                      A2016700
         BNE   BLDREPT2            BR IF NOT OFF                        A2016800
         NI    OCBSW,FF-MOCBREPT   SET OFF REPORT FLAG IN OCB           A2016900
         B     BLDREPTX            GO TO EXIT                           A2017000
BLDREPT2 LINE  LINE04              WARN OF UNKNOWN SPECIFICATION        A2017100
BLDREPTX SUBOUT RESTORE=R4         RESTORE AND RETURN TO CALLER         A2017200
         DROP  R8                  DROP OCB ADDRESSABILITY              A2017300
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2017400
         EJECT                                                          A2017500
*********************************************************************** A2017600
*        PROCESS GR00 CARDS:       PROCESS 'DETAIL' SPECIFICATION     * A2017700
*********************************************************************** A2017800
         SPACE 1                                                        A2017900
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2018000
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2018100
BLDDETL  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2018200
         CLI   GR00DETL,C' '       IS DETAIL SPECIFICATION OMITTED      A2018300
         BE    BLDDETLX            GO TO EXIT IF OMITTED                A2018400
         CLI   GR00DETL,C'Y'       IS DETAIL FLAG ON                    A2018500
         BNE   BLDDETL1            BR IF FLAG NOT ON                    A2018600
         OI    OCBSW,MOCBDETL      FLAG THAT DETAIL REQUIRED            A2018700
         B     BLDDETLX            GO TO EXIT                           A2018800
BLDDETL1 CLI   GR00DETL,C'N'       TEST IF DETAIL FLAG IS OFF           A2018900
         BNE   BLDDETL2            BR IF FLAG NOT OFF                   A2019000
         NI    OCBSW,FF-MOCBDETL   SET OFF DETAIL ERQUIRED FLAG IN OCB  A2019100
         B     BLDDETLX            GO TO EXIT                           A2019200
BLDDETL2 LINE  LINE05              WARN OF UNKNOWN SPECIFICATION        A2019300
BLDDETLX SUBOUT RESTORE=R4         RESTORE AND RETURN TO CALLER         A2019400
         DROP  R8                  DROP OCB ADDRESSABILITY              A2019500
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2019600
         EJECT                                                          A2019700
*********************************************************************** A2019800
*        PROCESS GR00 CARDS:       PROCESS TWENTY80 SPECIFICATION     * A2019900
*********************************************************************** A2020000
         SPACE 1                                                        A2020100
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2020200
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2020300
BLD2080  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2020400
         CLI   GR002080,C' '       IS THE SPECIFICATION BLANK           A2020500
         BE    BLD2080X            EXIT IF NO SPECIFICATION             A2020600
         CLI   GR002080,C'Y'       IS THIS A TWENTY80 REPORT D/S        A2020700
         BNE   BLD20801            BR IF NOT A TWENTY80 REPORT SPEC'N   A2020800
         OI    OCBSW,MOCBREPT+MOCB2080 FLAG THAT THIS IS 20/80 REPORT   A2020900
         B     BLD2080X            GO TO EXIT                           A2021000
BLD20801 CLI   GR002080,C'N'       IS THIS NOT A TWENTY80 REPORT D/S    A2021100
         BNE   BLD20802            BR IF NOT 'N' SPEC'N                 A2021200
         NI    OCBSW,FF-MOCB2080   SET OFF TWENTY80 REPORT FLAG         A2021300
         B     BLD2080X            GO TO ROUTINE EXIT                   A2021400
BLD20802 LINE  LINE07              TELL OF UNKNOWN SPECIFICATION        A2021500
BLD2080X SUBOUT RESTORE=R4         PRESERVE RETURN ADDRESS              A2021600
         DROP  R8                  DROP OCB ADDRESSABILITY              A2021700
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2021800
         EJECT                                                          A2021900
*********************************************************************** A2022000
*        PROCESS GR00 CARDS:       PROCESS LINES/PAGE SPECIFICATION   * A2022100
*********************************************************************** A2022200
         SPACE 1                                                        A2022300
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2022400
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2022500
BLDLCNT  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2022600
       NUMERIC GR00LCNT            ENSURE THAT SPECIFICATION IS NUMERIC A2022700
         BH    BLDLCNTE            GO TELL OF ERROR IF NOT NUMERIC      A2022800
         ZAP   OCBLPGE,$PVALUE     SET LINES/PAGE IN OCB                A2022900
         B     BLDLCNTX            GO TO EXIT                           A2023000
BLDLCNTE LINE  LINE06              TELL OF NON.NUMERIC LINES/PAGE SPEC  A2023100
BLDLCNTX SUBOUT RESTORE=R4         RESTORE AND RETURN TO CALLER         A2023200
         DROP  R8                  DROP OCB ADDRESSABILITY              A2023300
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2023400
         EJECT                                                          A2023500
*********************************************************************** A2023600
*        PROCESS GR00 CARDS:       PROCESS LOGO SPECIFICATION         * A2023700
*********************************************************************** A2023800
         SPACE 1                                                        A2023900
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2024000
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2024100
BLDLOGO  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2024200
         CLC   GR00LOGO,$SPACES    TEST OF LOGO NAME SUPPLIED           A2024300
         BE    *+10                BR IF NO LOGO NAME                   A2024400
         MVC   OCBLOGO,GR00LOGO    SET LOGO NAME IN OCB                 A2024500
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         A2024600
         DROP  R8                  DROP OCB ADDRESSABILITY              A2024700
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2024800
         EJECT                                                          A2024900
*********************************************************************** A2025000
*        PROCESS GR00 CARDS:       PROCESS REPORT ID SPEC'N           * A2025100
*********************************************************************** A2025200
         SPACE 1                                                        A2025300
         USING GR00CARD,R6         ESTABLISH GR00 ADDRESSABILITY        A2025400
         USING OCBNTRY,R8          ESTABLISH OCB ADDRESSABILITY         A2025500
BLDRID   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2025600
         CLC   GR00RID,$SPACES     TEST IF REPORT IDENTIFIER SUPPLIED   A2025700
         BE    *+10                BR IF NO LOGO NAME                   A2025800
         MVC   OCBRID,GR00RID      SET REPORT ID IN THE OCB             A2025900
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         A2026000
         DROP  R8                  DROP OCB ADDRESSABILITY              A2026100
         DROP  R6                  DROP GR00 ADDRESSABILITY             A2026200
         TITLE 'PARROTA2 - PROCESS GR01 CONTROL CARDS'                  A2026300
*********************************************************************** A2026400
*        PROCESS GR01 CONTROL CARDS                                   * A2026500
*********************************************************************** A2026600
         SPACE 1                                                        A2026700
*        THIS ROUTINE CONSTRUCTS HEADING LINES FROM GR01 CONTROL CARDS. A2026800
*         THE FIRST CARD IS USED TO CONSTRUCT THE FIRST LINE, WHILE     A2026900
*         ALL OTHER LINES ARE COMPOSED OF TWO CARDS CONCATENATED        A2027000
*         TOGETHER.  ONE PROBLEM IS THAT FAYERS USED ASA CONTROL CHARS  A2027100
*         WHILE I AM USING MACHINE CONTROL CHARS, SO WHEN I EVALUATE    A2027200
*         LINE SPACING REQUESTS I MUST SET THE MACHINE CONTROL CHAR IN  A2027300
*         THE PRECEDING LINE AND NOT THE CURRENT ONE.                   A2027400
         SPACE 1                                                        A2027500
         USING HDGNTRY,R8          ESTABLISH HDG ADDRESSABILITY         A2027600
         USING OCBNTRY,R7          ESTABLISH OCB ADDRESSABILITY         A2027700
         USING GR01CARD,R6         ESTABLISH GR01 ADDRESSABILITY        A2027800
CCGR01   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2027900
         L     R7,$PTROCB          GET ADDRESS OF CURRENT OCB           A2028000
         L     R8,OCBHDG           GET ADDRESS OF 1ST HEADING C/B       A2028100
         LTRR  R8                  PURIFY AND TEST IF ONE EXISTS        A2028200
         BNZ   CCGR0101            BR IF 1ST HEADING AREA EXISTS        A2028300
         CBAL  R4,GETHDG           ACQUIRE HDG FOR 1ST HEADING LINE     A2028400
         MVC   HDGCC+20(75),GR01LNE1 MOVE 1ST HEADING LINE TO STORE     A2028500
         OI    HDGSW,MHDGHD1       FLAG THAT THIS IS THE 1ST LINE       A2028600
         LA    R10,OCBPCC          POINT AT CARRIAGE CONTROL FLD IN OCB A2028700
         CBAL  R4,GETHCC           SET MACHINE CONTROL CHAR AS PER GR01 A2028800
         NI    HDGSW,FF-MHDG1CRD   SET OFF FLAG SHOWING PARTLY FILLED   A2028900
         B     CCGR01X             GO TO EXIT                           A2029000
         EJECT                                                          A2029100
*********************************************************************** A2029200
*        PROCESS GR01 CONTROL CARDS ( CONT'D )                        * A2029300
*********************************************************************** A2029400
         SPACE 1                                                        A2029500
CCGR0101 LA    R10,HDGCC           POINT R10 AT CARR CONTROL IN THIS HD A2029600
         NC    HDGHDG+1(3),HDGHDG+1 IS THIS THE LAST IN THE CHAIN       A2029700
         BZ    *+12                BR IF LAST IN CHAIN                  A2029800
         L     R8,HDGHDG           GET ADDRESS OF NEXT HDG              A2029900
         B     CCGR0101            LOOP BACK TO TEST FOR END OF CHAIN   A2030000
         TM    HDGSW,MHDGHD1       IS THIS THE 1ST HEADING LINE         A2030100
         BO    CCGR0102            BR IF 1ST HEADING LINE AREA          A2030200
         TM    HDGSW,MHDG1CRD      HAS ONLY 1 CARD BEEN STORED          A2030300
         BZ    CCGR0102            BR IF NOT: LINE IS COMPLETE          A2030400
         NI    HDGSW,FF-MHDG1CRD   SET OFF FLAG SHOWING LINE INCOMPLETE A2030500
         MVC   HDGCARD2,GR01LNE1   MOVE DATA FRON 2ND CARD TO LINE      A2030600
         B     CCGR01X             TO TO EXIT                           A2030700
CCGR0102 CBAL  R4,GETHDG           ACQUIRE AND INITIALISE NEX HDG       A2030800
         OI    HDGSW,MHDG1CRD      FLAG THAT ONLY 1 CARD ADDED TO LINE  A2030900
         MVC   HDGCARD1,GR01LNE1   MOVE DATA FROM 1ST CARD TO LINE      A2031000
         CBAL  R4,GETHCC           SET CARRIAGE CONTROL CHAR            A2031100
CCGR01X SUBOUT RESTORE=R4          RESTORE AND RETURN TO MAINLINE       A2031200
         DROP  R6                  DROP GR01 ADDRESSABILITY             A2031300
         DROP  R7                  DROP OCB ADDRESSABILITY              A2031400
         DROP  R8                  DROP HDG ADDRESSABILITY              A2031500
         EJECT                                                          A2031600
*********************************************************************** A2031700
*        PROCESS GR01 CARDS:       ACQUIRE AND CHAIN HDG              * A2031800
*********************************************************************** A2031900
         SPACE 1                                                        A2032000
         USING HDGNTRY,R8          ESTABLISH HSG ADDRESSABILITY         A2032100
         USING OCBNTRY,R7          ESTABLISH OCB ADDRESSABILITY         A2032200
GETHDG   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2032300
         LA    R2,HDGLGTH          SET LENGTH OFHDG IN WORK REGISTER    A2032400
         GETCB HDG                 ACQUIRE AND CHAIN HDG                A2032500
         MVI   HDGCC,C' '          INSERT LEADING BLANK ...             A2032600
         MVC   HDGCC+1(132),HDGCC   ... AND PROPAGATE THROUGH           A2032700
         MVI   HDGCC,SPCE2AFT      SET DEFAULT CARRIAGE CONTROL CHAR    A2032800
         LH    R1,OCBHCNT          GET COUNT OF HEADING LINES           A2032900
         LA    R1,1(,R1)           INCREMENT LINE COUNT                 A2033000
         STH   R1,OCBHCNT          PRESERVE UPDATED HEADING LINE COUNT  A2033100
         OI    HDGSW,MHDG1CRD      FLAG THAT ONLY 1 CARD INSERTED       A2033200
        SUBOUT RESTORE=R4          RESTORE AND RETURN TO CALLER         A2033300
         DROP  R7                  DROP OCB ADDRESSABILITY              A2033400
         DROP  R8                  DROP HDG ADDRESSABILITY              A2033500
         EJECT                                                          A2033600
*********************************************************************** A2033700
*        PROCESS GR01 CARDS: CONVERT CARRIAGE CONTROL CHAR TO INTERNAL* A2033800
*********************************************************************** A2033900
         SPACE 1                                                        A2034000
*        THIS ROUTINE CONVERTS THE LINE SPACING SPECIFICATION IN THE    A2034100
*         GR01 CONTROL CARD INTO THE EQUVIALENT MACHINE CONTROL CHAR.   A2034200
         SPACE 1                                                        A2034300
*        UPON ENTRY, R10 POINTS AT THE DESTINATION FIELD                A2034400
         SPACE 1                                                        A2034500
         USING GR01CARD,R6         ESTABLISH GR06 ADDRESSABILITY        A2034600
GETHCC   SUBIN ,                   ENETR ROUTINE                        A2034700
         MVI   0(R10),SPCE1AFT     SET DEFAULT VALUE OF SINGLE SPACING  A2034800
         CLI   GR01CC,C'3'         IS THIS TRIPLKE SPACING REQUEST      A2034900
         BNE   *+8                 BR IF NOT                            A2035000
         MVI   0(R10),SPCE3AFT     SET TRIPLE SPACE CONTROL CHAR        A2035100
         CLI   GR01CC,C'2'         IS THIS DOUBLE SPACE REQUEST         A2035200
         BNE   *+8                 BR IF NOT                            A2035300
         MVI   0(R10),SPCE2AFT     SET DOUBLE SPACE CONTROL CHAR        A2035400
         CLI   GR01CC,C'0'         IS THIS SPACE SUPPRESS REQUEST       A2035500
         BNE   *+8                 BR IF NOT                            A2035600
         MVI   0(R10),SPCE0AFT     SET PRINT-NO-SPACE CONTROL CHARACTER A2035700
        SUBOUT ,                   RETURN TO CALLER                     A2035800
         DROP  R6                  DROP GR01 ADDRESSABILITY             A2035900
         EJECT                                                          A2036000
*********************************************************************** A2036100
*        PROCESS GR02 CONTROL CARDS                                   * A2036200
*********************************************************************** A2036300
         SPACE 1                                                        A2036400
         USING GR02CARD,R6         ESTABLISH C/C ADDRESSABILITY         A2036500
         USING RSTNTRY,R8          ESTABLISH C/B ADDRESSABILITY         A2036600
CCGR02   SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              A2036700
         LA    R10,GR02TST2        POINT AT SECOND BUCKET               A2036800
         LA    R7,3                SET TO LOOP THROUGH 3 BUCKETS        A2036900
         CLC   GR02POS,=C'AND'     IS THIS A TEST CONTINUATION CARD     A2037000
         BE    CCGR0201            BR IF YES TO CHAIN SCE'S             A2037100
         LA    R2,RSTLGTH          SET C/B LENGTH IN R2                 A2037200
         GETCB RST                 ACQUIRE AND CHAIN C/B                A2037300
         CHNCB RST,SCE             SET DUMMY 1ST POINTERS               A2037400
         ZAP   RSTRCNT,$ZERO       ZEROISE RECORD COUNT                 A2037500
         LA    R10,GR02TST1        POINT R10 AT 1ST BUCKET OF DATA      A2037600
         LA    R7,4                SET TO LOOP THROUGH 4 BUCKETS        A2037700
CCGR0201 CBAL  R4,BLDSCE           CONSTRUCT AND CHAIN SCE              A2037800
         LA    R10,GR02TST2-GR02TST1(,R10) INCREMENT TO NEXT BUCKET     A2037900
         BCT   R7,CCGR0201         LOOP BACK IF ANY BUCKETS LEFT        A2038000
        SUBOUT RESTORE=R4          RETORE AND RETURN TO MAIN-LINE       A2038100
         DROP  R6,R8               DROP C/C, C/B ADDRESSABILITY         A2038200
         EJECT                                                          A2038300
*********************************************************************** A2038400
*        CONVERT POSITION TO EBCDIC FOR ERROR MESSAGE                 * A2038500
*********************************************************************** A2038600
         SPACE 1                                                        A2038700
*        ON ENTRY, R1 CONTAINS THE POSITION TO BE CONVERTED             A2038800
         SPACE 1                                                        A2038900
CONVPOS  SUBIN ,                   ENTER ROUTINE                        A2039000
         MVC   $WRKSNGL,BLDMASK    MOVE EDIT MASK TO WORK AREA          A2039100
         CVD   R1,$WRKDUBL         CONVERT VALUETO DECIMAL              A2039200
         ED    $WRKSNGL,$WRKDUBL+6 RETURN TO CALLER                     A2039300
        SUBOUT ,                   RETIRN TO CALLER                     A2039400
         TITLE 'PARROTA2 - MISCELLANEOUS CONSTANTS AND FIELDS'          A2039500
BLDMASK  DC    X'40212020'         MASK TO CONVERT POSN TO EBCDIC       A2039600
         TITLE 'PARROTA2 - ERROR MESSAGES'                              A2039700
LINE01   DC    C'PTA2-001-W  THIS GR00 CARD HAS THE SAME LABEL AS A PRE*A2039800
               VIOUS ONE:  THE INFORMATION HEREIN WILL OVERRIDE PREVIOUSA2039900
               S SPECIFCIATIONS'                                        A2040000
LINE02   DC    C'PTA2-002-W  COLUMNS 36-80 ARE UNUSED, AND SHOULD BE BLAA2040100
               ANK: POTENTIALLY MISPLACED DATA'                         A2040200
LINE03   DC    C'PTA2-003-W  NO DDNAME SUPPLIED: SPECIFICATION WILL BE IA2040300
               IGNORED'                                                 A2040400
LINE04   DC    C'PTA2-004-W  COLUMN 15 CONTAINS AN UNKNOWN REPORT SPECIFA2040500
               FICATION: IGNORED'                                       A2040600
LINE05   DC    C'PTA2-005-W  COLUMN 16 CONTAINS AN UNKNOWN DETAIL SPECIFA2040700
               FICATION: IGNORED'                                       A2040800
LINE06   DC    C'PTA2-006-W  COLUMNS 18-19 NOT NUMERIC: LINES/PAGE SPECIA2040900
               IFCIATION IGNORED'                                       A2041000
LINE07   DC    C'PTA2-007-W  COLUMN 17 CONTAINS AN UNKNOWN TWENTY80 REPOA2041100
               ORT SPECIFICATION: IGNORED'                              A2041200
         SPACE 5                                                        A2041300
        SCSEND A2                  TERMINATE CSET PARROTA2              A2041400
         END                                                            A2041500
