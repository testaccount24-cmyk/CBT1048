         TITLE 'BUDGIE3I - INTERNAL MACROS'                             I 000010
         MACRO                                                          I 000020
         $CB   &PRINT=OFF                                               I 000030
         PUSH PRINT                PRESERVE CURRENT PRINT STATUS        I 000040
         PRINT &PRINT              ESTABLISH NEW PRINT STATUS           I 000050
               TITLE 'CB13 - CB13 CONTROL BLOCK'                        I 000060
         CB13  ,                   GENERATE DSECT                       I 000070
               TITLE 'CCB  - CENTRAL CONTROL BLOCK'                     I 000080
         CCB   ,                   GENERATE DSECT                       I 000090
               TITLE '     - BATCH BALANCING REPORT LINES'              I 000100
      DETAILBB ,                   GENERATE DSECTS                      I 000110
               TITLE '     - GENERAL EQUATES'                           I 000120
       EQUATES ,                   GENERATE EQUATES                     I 000130
         POP   PRINT               RESTORE ORIGINAL PRINT STATUS        I 000140
         MEND                                                           I 000150
         SPACE 5                                                        I 000160
BUDGIE3I CSECT                                                          I 000170
         $CB   ,                   GENERATE DSECTS ABD EQUATES          I 000180
         TITLE 'BUDGIE3I - TESTS AND OPERATIONS'                        I 000190
        SCSECT I                   DEFINE START OF CSECT BUDGIE3I       I 000200
         ENTRY TESTSL3             SELECT AND PERFORM REQUESTED ROUTINE I 000210
         SPACE 1                                                        I 000220
         USING DETAILBB,R7         ESTABLISH B/B REPORT ADDRESSABILITY  I 000230
         TITLE 'BUDGIE3I - SELECT APPROPRIATE ROUTINE'                  I 000240
*********************************************************************** I 000250
*        SELECT APPROPRIATE ROUTINE                                   * I 000260
*********************************************************************** I 000270
         SPACE 1                                                        I 000280
*        REGISTERS ARE NOW SET THUS :-                                  I 000290
*              R15 - END OF FIELD TO BE TESTED                          I 000300
*              R14 - START OF FIELD TO BE TESTED                        I 000310
*              R1  - EXECUTABLE LEBGTH                                  I 000320
*              R2  - ADDRESS OF ASSOC C/B ( WHERE RELEVANT )            I 000330
*              R4  - TEST CODE * 4 ( INDEX )                            I 000340
*              R6  - POINTS AT FIELD IN RECORD                          I 000350
*              R8  - ADDRESS OF CURRENT CB13                            I 000360
*              R10 - ADDRESS OF CURRENT CB02                            I 000370
         SPACE 1                                                        I 000380
TESTSL3  SUBIN ,                   ENTER ROUTINE                        I 000390
         SH    R4,=H'200'          DECREMENT INDEX: 1ST 50 TESTS DONE   I 000400
         B     TESTBR(R4)          SELECT APPROPRIATE ROUTINE           I 000410
TESTBR   EQU   *                   BASE FOR INDEXED BTANCH              I 000420
         B     TESTE2              TEST TYPE E2 - SUPPORTED             I 000430
         B     TESTE3              TEST TYPE E3 - SUPPORTED             I 000440
         B     TESTE4              TEST TYPE E4 - SUPPORTED             I 000450
         B     LOGICER             TEST TYPE E5 - UNSUPPORTED           I 000460
         B     TESTE6              TEST TYPE E6 - SUPPORTED             I 000470
         B     LOGICER             TEST TYPE E7 - UNSUPPORTED           I 000480
         B     LOGICER             TEST TYPE E8 - UNSUPPORTED           I 000490
         B     LOGICER             TEST TYPE E9 - UNSUPPORTED           I 000500
         B     LOGICER             TEST TYPE F1 - UNSUPPORTED           I 000510
         B     LOGICER             TEST TYPE F2 - UNSUPPORTED           I 000520
         B     LOGICER             TEST TYPE F3 - UNSUPPORTED           I 000530
         B     LOGICER             TEST TYPE F4 - UNSUPPORTED           I 000540
         B     LOGICER             TEST TYPE F5 - UNSUPPORTED           I 000550
         B     LOGICER             TEST TYPE F6 - UNSUPPORTED           I 000560
         B     LOGICER             TEST TYPE M1 - UNSUPPORTED           I 000570
         B     LOGICER             TEST TYPE M2 - UNALLOCATED           I 000580
         B     LOGICER             TEST TYPE M3 - UNALLOCATED           I 000590
         B     LOGICER             TEST TYPE M4 - UNALLOCATED           I 000600
         B     LOGICER             TEST TYPE M5 - UNALLOCATED           I 000610
         B     LOGICER             TEST TYPE M6 - UNALLOCATED           I 000620
         B     LOGICER             TEST TYPE M7 - UNALLOCATED           I 000630
         B     LOGICER             TEST TYPE M8 - UNALLOCATED           I 000640
         B     LOGICER             TEST TYPE M9 - UNALLOCATED           I 000650
         B     LOGICER             TEST TYPE US - SUPPORTED IN BUDGIE3G I 000660
         B     LOGICER             TEST TYPE UZ - SUPPORTED IN BUDGIE3G I 000670
         EJECT                                                          I 000680
*********************************************************************** I 000690
*        RETURN TO BUDGIE3G FOR TEST TERMINATION PROCESSING           * I 000700
*********************************************************************** I 000710
         SPACE 1                                                        I 000720
TESTEXIT CBAL  ,TESTRTN            PREPARE TO RETURN TO CALLER          I 000730
         SPACE 5                                                        I 000740
*********************************************************************** I 000750
*        ABEND IF GAIN CONTROL FOR TEST PERFORMED IN BUDGIE3G         * I 000760
*********************************************************************** I 000770
         SPACE 1                                                        I 000780
LOGICER  ABEND 80,FULL             SHOULD NEVER HAPPEN                  I 000790
         TITLE 'BUDGIE3I - TEST ROUTINES'                               I 000800
**********************************************************************  I 000810
*        E2 - PROPAGATE CHARACTER STRING                              * I 000820
**********************************************************************  I 000830
         SPACE 1                                                        I 000840
         USING CB13NTRY,R8         ESTABLISG CB13 ADDRESSABILITY        I 000850
TESTE2   SUBIN ,                   ENTER ROUTINE                        I 000860
         MVC   0(1,R6),CB13CBID    MOVE IN 1ST CHARCATER                I 000870
         LTR   R1,R1               TEST IF FIELD ONLY 1 BYTE LONG       I 000880
         BZ    TESTE2X             EXIT IF 1 BYTE                       I 000890
         MVC   1(1,R6),CB13CBID+1  MOVE IN 2ND CHARACTER                I 000900
         BCTR  R1,0                DECREMENT FOR EXECUTE                I 000910
         LTR   R1,R1               TEST IF FIELD ONLY 2 BYTES LONG      I 000920
         BZ    TESTE2X             BR IF FIELD LENGTH WAS 2             I 000930
         BCTR  R1,0                DECREMENT FOR EXECUTE AGAIN          I 000940
         EX    R1,TESTE2MV         PROPAGATE CHARACTERS                 I 000950
TESTE2X SUBOUT RETURN=TESTEXIT                                          I 000960
         SPACE 1                                                        I 000970
TESTE2MV MVC   2(0,R6),0(R6)       EXECUTED 2-CHR PROPAGATION           I 000980
         SPACE 1                                                        I 000990
         DROP  R8                  DROP CB13 ADDRESSABILITY             I 001000
         SPACE 5                                                        I 001010
**********************************************************************  I 001020
*        E3 - MOVE 'ERROR MESSGAE' CONTENTS TO FIELD                  * I 001030
**********************************************************************  I 001040
         SPACE 1                                                        I 001050
         USING CB13NTRY,R8         ESTABLISH CB13 ADDRESSABILITY        I 001060
TESTE3   SUBIN ,                   ENTR ROUTINE                         I 001070
         EX    R1,TESTE3MV         MOVE LITERAL TO FIELD                I 001080
        SUBOUT RETURN=TESTEXIT                                          I 001090
         SPACE 1                                                        I 001100
TESTE3MV MVC   0(0,R6),CB13MESS    EXECUTED MOVE OF LITERAL             I 001110
         SPACE 1                                                        I 001120
         DROP  R8                  DROP CB13 ADDERSSABILITY             I 001130
         EJECT                                                          I 001140
**********************************************************************  I 001150
*        E4 - REMOVE ALL EMBEDDED BLANKS AND LEFT-JUSTIFY             * I 001160
**********************************************************************  I 001170
         SPACE 1                                                        I 001180
TESTE4   SUBIN ,                   ENTER ROUTINE                        I 001190
         EX    R1,TESTCLC          IS FIELD ALL SPACES ALREADY          I 001200
         BE    TESTE4X             EXIT IF SPACES                       I 001210
         LTR   R1,R1               TEST IF FIELD LENGTH IS 1            I 001220
         BZ    TESTE4X             EXIT IF FIELD LENGH3 1               I 001230
         LR    R14,R6              POINT R14 AT START OF FIELD          I 001240
         AR    R14,R1              POINT R14 AT END OF FIELD            I 001250
         LR    R15,R1              SET R15 TO EXECUTABLE LENGTH         I 001260
         BCTR  R15,0               DECREMENT BY 1 FOR LEFT ADJUSTM&NT   I 001270
TESTE4L1 EX    R1,TESTE4SP         TEST IF REMAINDER ALL SPACES         I 001280
         BE    TESTE4X             EXIT IF YES                          I 001290
         CLI   0(R6),C' '          TEST IF THIS BYTE IS BLANK           I 001300
         BNE   TESTE4L2            BR IF NOT BLANK                      I 001310
         EX    R15,TESTLJST        LEFT JUSTIFY OVER BLANK              I 001320
         MVI   0(R14),C' '         INSERT TRAILING BLANK                I 001330
         B     TESTE4L1            GO BACK TO SEE IF STILL BLANK        I 001340
TESTE4L2 LA    R6,1(,R6)           POINT AT NEXT POSITION               I 001350
         BCTR  R15,0               DECREMENT R15 FOR NEXT TIME THROUGH  I 001360
         BCT   R1,TESTE4L1         DECREMENT R1 FOR EXECUTE             I 001370
*                                   FALL THROUGH IF ALL GONE            I 001380
TESTE4X SUBOUT RETURN=TESTEXIT     PREPARE TO RETRUN TO CALLER          I 001390
         EJECT                                                          I 001400
*********************************************************************** I 001410
*        E6 - DETERMINE IF FIELD CONTAINS SPECIFIC CHARACTER          * I 001420
*********************************************************************** I 001430
         SPACE 1                                                        I 001440
*        THIS TEST TYPE EXAMINES THE FIELD UNDER TEST, AND INDICATES    I 001450
*         AN ERROR CONDITION IF ANY POSITION OF THE FIELD CONTAINS THE  I 001460
*         CHARACTER THAT IS IN THE FIRST BYTE OF THE TEST 'LABEL'.      I 001470
         SPACE 1                                                        I 001480
         USING CB13NTRY,R8         ESTABLISH CB13 ADDRESSABILITY        I 001490
TESTE6   SUBIN ,                   ENETR ROUTINE                        I 001500
        BBMESS E6                  SET UP DEFAULT ERROR MESSAGE         I 001510
         MVI   E6TRTTAB,X'00'      MOVE LOWVALUES TO FIRST BYTE OF TABL I 001520
         MVC   E6TRTTAB+1(L'E6TRTTAB-1),E6TRTTAB ... AND PROPAGATE      I 001530
         SR    R4,R4               ZEROISE WORK REGISTER                I 001540
         IC    R4,CB13CBID         RETRIEVE CHARACTER FOR WHICH TO TEST I 001550
         A     R4,=A(E6TRTTAB)     POINT AT EQUIV POSN IN TRT TABLE     I 001560
         MVC   0(1,R4),CB13CBID    STORE CHARACTER IN TRT TABLE         I 001570
         EX    R1,E6TRT            TEST FIELD TO SEE IF IT CONTAINS CHA I 001580
         BZ    *+8                 BR IF CHARACTER NOT FOUND            I 001590
         MVI   $TSTTSTX,C'Y'       SHOW THAT TEST NOT SATISFIED         I 001600
        SUBOUT RETURN=TESTEXIT     PREPARE TO RETURN TO CALLER          I 001610
         DROP  R8                  DROP CB13 ADDRESSABILITY             I 001620
         TITLE 'BUDGIE3I - EXECUTED INSTRUCTIONS'                       I 001630
*********************************************************************** I 001640
*        EXECUTED INSTRUCTIONS                                        * I 001650
*********************************************************************** I 001660
         SPACE 1                                                        I 001670
TESTE4SP CLC   0(0,R6),$SPACES     TEST IF REMAINDER IS BLANK           I 001680
TESTLJST MVC   0(0,R6),1(R6)       LEFT JUSTIFY FIELD BY 1 BYTE         I 001690
TESTCLC  CLC   $TSTAREA(0),$SPACES COMPARE FIELD WITH SPACES            I 001700
E6TRT    TRT   0(0,R6),E6TRTTAB    TEST IF SPECIFIC CHARACTER PRESENT   I 001710
         TITLE 'MISCELLANEOUS CONSTANTS AND FIELDS'                     I 001720
BBMESSE6 DC    C'FIELD COLS   -   INVALID (E6)'                         I 001730
         SPACE 1                                                        I 001740
E6TRTTAB DS    0CL256              TRT TABLE FOR TEST TYPE E6           I 001750
         DC    256X'00'            TRT TABLE FOR TEST TYPE E6           I 001760
         TITLE 'BUDGIE3I - MISCELLANEOUS CONSTANTS AND FIELDS'          I 001770
         SPACE 1                                                        I 001780
        SCSEND I                   TERMINATE CSECT BUDGIE3I             I 001790
         END                                                            I 001800
         SPACE 1                                                        I 001810
         REPRO                                                          I 001820
 IDENTIFY BUDGIE3I('TESTS AND OPERATIONS')                              I 001830
         END                                                            I 001840
