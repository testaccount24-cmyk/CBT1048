         TITLE 'BUDGIE3P - INTERNAL MACROS'                             P 000010
         MACRO                                                          P 000020
         $CB   &PRINT=OFF                                               P 000030
         PUSH PRINT                PRESERVE CURRENT PRINT STATUS        P 000040
         PRINT &PRINT              ESTABLISH NEW PRINT STATUS           P 000050
               TITLE 'CB13 - CB13 CONTROL BLOCK'                        P 000060
         CB13  ,                   GENERATE DSECT                       P 000070
               TITLE 'CB33 - CB33 CONTROL BLOCK'                        P 000080
         CB33  ,                   GENERATE DSECT                       P 000090
               TITLE 'CCB  - CENTRAL CONTROL BLOCK'                     P 000100
         CCB   ,                   GENERATE DSECT                       P 000110
               TITLE '     - BATCH BALANCING REPORT LINES'              P 000120
      DETAILBB ,                   GENERATE DSECTS                      P 000130
               TITLE '     - GENERAL EQUATES'                           P 000140
       EQUATES ,                   GENERATE EQUATES                     P 000150
         POP   PRINT               RESTORE ORIGINAL PRINT STATUS        P 000160
         MEND                                                           P 000170
         SPACE 5                                                        P 000180
BUDGIE3P CSECT                                                          P 000190
         $CB   ,                   GENERATE DSECTS AND EQUATES          P 000200
         TITLE 'BUDGIE3P - PARTIAL KEY MATCHING'                        P 000210
        SCSECT P                   DEFINE CSECT BUDGIE3P                P 000220
         ENTRY CTESTE1             PARTIAL KEY MATCHING                 P 000230
         ENTRY UT493MBB            'MUST BE BLANK' CHAR (ALTERED BY PAR P 000240
         TITLE 'BUDGIE3P - PARTIAL KEY MATCHING'                        P 000250
*********************************************************************** P 000260
*        E1 - PARTIAL KEY MATCH                                       * P 000270
*********************************************************************** P 000280
         SPACE 1                                                        P 000290
         USING CB33NTRY,R2         ESTABLISH CB33 ADDRESSAILITY         P 000300
         USING CB13NTRY,R8         ESTABLISH CB13 ADDRESSABILITY        P 000310
         USING DETAILBB,R7         ESTABLISH B/B REPORT ADDRESSABILITY  P 000320
         SPACE 1                                                        P 000330
*        THIS CLEVER LITTLE ROUTINE ( EXTRACTED FROM THE SUBROUTINE     P 000340
*         UT493 ), COMPARES A TEST KEY WITH THE NON-BLANK PAORTIONS OF  P 000350
*         A MASTER KEY.  THE ROUTINE PERFROMS THIS FUNCTION AT THE FIEL P 000360
*         LEVEL, AND NOT BYTE BY BYTE.  IT ACCOMPLISHES THIS BY :-      P 000370
*              . SET NON-BLANK PORTION OF PARTIAL KEY TO X'FF', AND SET P 000380
*                BLANKS TO X'00'                                        P 000390
*              . SET TO LOW VALUES THE PARTS OF THE KEY UNDER TEST THAT P 000400
*                CORRESPOND TO BLANKS IN THE MASTER PARTIAL KEY         P 000410
*              . RESTORE THE NONBLANK PORTIONS OF THE MASTER KEY,       P 000420
*                LEAVING THE BLANK PARTS SET TO X'00'                   P 000430
*              . ALTER THE 'MUST BE BLANK' CHARACTER IN THE MASTER KEY  P 000440
*                TO BLANKS                                              P 000450
*              . COMPARE THE MODIFIED KEYS.  IN EACH KEY, THE POSNS     P 000460
*                CRRESPONDING TO BLANKS IN THE PARTIAL KEY ARE X'00', A P 000470
*                WILL GIVE EQUALITY REGARDLESS OF ORIGINAL CONTENTS.    P 000480
*        NOTE THAT BOTH THE ORIGINAL KEYS HAVE BEEN LEFT UNSULLIED.     P 000490
*         ALL CHANGES HAVE BEEN TO THE WORK AREAS.                      P 000500
         SPACE 1                                                        P 000510
         EJECT                                                          P 000520
*********************************************************************** P 000530
*        E1 - PARTIAL KEY MATCHINH ( CONT'D )                         * P 000540
*********************************************************************** P 000550
         SPACE 1                                                        P 000560
CTESTE1  SUBIN SAVE=R4             PRESERVE RETURN ADDRESS              P 000570
         OC    CB33POS2(2),CB33POS2 TEST IF 'TO' FIELD SUPPLIED         P 000580
         BZ    TESTE101            IF NOT, ASSUME SSPECIFIED IN MS13    P 000590
         SR    R6,R6               ZEROISE WORK REGISTER                P 000600
         IC    R6,CB33POS2         RETIREVE POSN OF FIELD TO BE TESTED  P 000610
         BCTR  R6,0                DECREMENT POSN TO DISPLACEMENT       P 000620
         A     R6,SYSUT1+IOPSRECA  POINT R6 AT FIELD TO BE TESTED       P 000630
         SR    R1,R1               ZEROISE WORK REGISTER                P 000640
         IC    R1,CB33LEN2         GET LENGTH OF FIELD                  P 000650
         BCTR  R1,0                DRECEMENT FOR EXECUTE                P 000660
         EX    R1,TESTMVC          MOVE FIELD TO 'TESTAREA'             P 000670
TESTE101 BBMESS 17                 SET UP DEFAULT MESSAGE               P 000680
         SR    R15,R15             ZEROISE WORK REGISTER                P 000690
         IC    R15,UT493MBB        PICK UP 'MUST BE BLANK' CHARACTER    P 000700
         IC    R14,$SPACES         SET X'40' IN WORK REG                P 000710
         STC   R14,E1TABLE1(R15)   MODIFY TABLE FOR 'MUST BE BLANK'     P 000720
         LR    R15,R1              SET EXECUTABLE LENGTH IN R15         P 000730
         MVC   $TSTAREA+50(50),CB33LIT MOVE PARTIAL KEY TO WORK AREA    P 000740
         EX    R15,E1TRTEST        TRANSLATE PARTIAL KEY TO X'FF' BAR B P 000750
         EX    R15,E1NCMAST        SET TEST KET BL TO X'00'             P 000760
         EX    R15,E1NCTEST        RESTORE PARTIAL KEY WITH BL SET X'00 P 000770
         EX    R15,E1TRTAB         SET 'MBB' CHAR TO BLANK              P 000780
         SR    R1,R1               ZEROISE WORK REGISTER                P 000790
         IC    R1,CB33TYPE         SET COMPARE TYPE                     P 000800
         EX    R15,E1COMPAR        COMPARE NONBLANK PARTS OF KEYS       P 000810
         CBAL  R4,COMPARE          TEST RESULT OF COMPARE               P 000820
         BE    *+8                 B IF EQUAL                           P 000830
         MVI   $TSTTSTX,C'Y'       INDICATE KEYS NOT EQUAL              P 000840
        SUBOUT RESTORE=R4          RESTORE AND RETURN                   P 000850
         EJECT                                                          P 000860
*********************************************************************** P 000870
*        TRANSLATE TABLES FOR THE PRTIAL KEY MATCH                    * P 000880
*********************************************************************** P 000890
         SPACE 1                                                        P 000900
E1TABLE1 DS    0C                  TABLE TO LATER 'MUST BE BLANK' TO BL P 000910
         DC    X'000102030405060708090A0B0C0D0E0F'                      P 000920
         DC    X'101112131415161718191A1B1C1D1E1F'                      P 000930
         DC    X'202122232425262728292A2B2C2D2E2F'                      P 000940
         DC    X'303132333435363738393A3B3C3D3E3F'                      P 000950
         DC    X'404142434445464748494A4B4C4D4E4F'                      P 000960
         DC    X'505152535455565758595A5B5C5D5E5F'                      P 000970
         DC    X'606162636465666768696A6B6C6D6E6F'                      P 000980
         DC    X'707172737475767778797A7B7C7D7E7F'                      P 000990
         DC    X'808182838485868788898A8B8C8D8E8F'                      P 001000
         DC    X'909192939495969798999A9B9C9D9E9F'                      P 001010
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                      P 001020
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                      P 001030
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                      P 001040
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                      P 001050
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                      P 001060
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                      P 001070
         SPACE 5                                                        P 001080
E1TABLE2 DS    0C                  USED TO NULLIFY BLANKS               P 001090
         DC    64X'FF'             EVERYTHING EXCEPT ...                P 001100
         DC    X'00'                ... BLANK ...                       P 001110
         DC    191X'FF'             ... IS HIGH VALUES                  P 001120
         SPACE 5                                                        P 001130
*********************************************************************** P 001140
*        EXECUTED INSTRUCTIONS                                        * P 001150
*********************************************************************** P 001160
         SPACE 1                                                        P 001170
E1TRTEST TR    $TSTAREA+50(0),E1TABLE2 TRANSLATE PARTIAL KEY BL TO X'00 P 001180
E1NCMAST NC    $TSTAREA(0),$TSTAREA+50 TURM OFF UNTESTED BITS           P 001190
E1NCTEST NC    $TSTAREA+50(0),CB33LIT TURN OFF BL IN PARTIAL KEY        P 001200
E1TRTAB  TR    $TSTAREA+50(0),E1TABLE1 TRANSLATE MBB CHAR TO BLANK      P 001210
E1COMPAR CLC   $TSTAREA(0),$TSTAREA+50 COMPARE MODIFIED KEYS            P 001220
         SPACE 1                                                        P 001230
TESTMVC  MVC   $TSTAREA(0),0(R6)   MOVE FIELD TO TST AREA               P 001240
         TITLE 'BUDGIE3P - MISCELLANEOUS CONSTANTS AND FIELDS'          P 001250
BBMESS17 DC    C'FIELD COLS   -   FAILED TEST'                          P 001260
UT493MBB DC    C'^'                'MUST BE BLANK' CHARACTER            P 001270
         SPACE 1                                                        P 001280
         DROP  R2,R7,R8            DROP ADDRESSABILITY                  P 001290
         SPACE 5                                                        P 001300
        SCSEND P                   TERMINATE CSECT BUDGIE3P             P 001310
         END                                                            P 001320
         SPACE 1                                                        P 001330
         REPRO                                                          P 001340
 IDENTIFY BUDGIE3P('PARTIAL KEY MATCHING')                              P 001350
         END                                                            P 001360
