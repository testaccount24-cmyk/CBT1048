//JEASPF08 JOB (1330,5),'J ABBEY',CLASS=N,NOTIFY=JEA
//SPFLIB08 EXEC LIBRARYN,FILE=TESTLIB,BLKSIZE=800
-OPT NOLIST,EXEC
-DLM SPFLIB08,SPF8
-ADD SPFLIB08,SPF8
-PGMR JEABBEY
-DESC SPF/LIBRARYN INTERFACE - DDN SET UP
         TITLE 'SET UP DDNAME ALLOCATION ENVIRONMENT FOR LIBRARYN'
***********************************************************************
*                     S P F L I B 0 8                                 *
*                                                                     *
*            THIS ROUTINE IS A COMMON SERVICE UTILITY FOR THE         *
*        MONSANTO SPF/LIBRARYN INTERFACE ROUTINE. IS IS INVOKED TO    *
*        ALLOCATE ALL THE ANCILLARY FILE SUCH AS SYSIN AND            *
*        SYSPRINT READY FOR PROCESSING BY THE LIBRARYN MAIN           *
*        PROGRAM ITSELF.                                              *
*            ON ENTRY TO THIS ROUTINE, REGISTER 1 CONTAINS THE        *
*        ADDRESS OF THE COMMON DATA AREA.                             *
*            THE ROUTINE OPERATES BY PERFORMING THE FOLLOWING         *
*        THE FOLLOWING STEPS:                                         *
*                                                                     *
*        1)    FREE THE DDNAMES 'SYSIN' AND  'SYSPRINT'               *
*              IN CASE THEY ARE ALREADY ALLOCATED.                    *
*        2)    ALLOCATE SYSPRINT TO A SYSOUT FILE.                    *
*        3)    ALLOCATE SYSIN TO A TEMPORARY WORK FILE READY TO       *
*              RECEIVE THE LIBRARYN CONTROL STREAM.                   *
*        4)    ALLOCATE 'OSJOB' AND 'LIST' TO SYSOUT IN CASE THEY     *
*              ARE NEEDED.                                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        PROGRAM ENTRY HOUSEKEEPING
***********************************************************************
         SPACE 2
SPFLIB08 ZCSECT
         LR    REG10,REG1          ADDRESS OF COMMON WORK AREA
         USING DATAAREA,REG10      ADDRESSABILITY FOR IT
         XC    ALLCCODE,ALLCCODE   CLEAR THE RETURN CODE
         EJECT
***********************************************************************
*        FREE THE DDNAMES 'SYSIN' AND  'SYSPRINT' IN CASE ALLOCATED
***********************************************************************
         SPACE 2
         LINK  EP=UT501,PARAM=(SYSNJCL,DFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)
         LINK  EP=UT501,PARAM=(SYSPJCL,DFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)
         EJECT
***********************************************************************
*        ALLOCATE THE DDNAMES AS REQUIRED FOR LIBRARYN
***********************************************************************
         SPACE 2
         LINK  EP=UT501,PARAM=(SYSNJCL,AFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)
         CLI   RETCODE,0           WAS ALLOCATION SUCCESSFUL ?
         BNE   BADALLOC            NO. ALLOCATION FAILED
         OI    WRKFLAG3,SYSNALLC   SIGNAL THAT SYSIN IS ALLOCATED
         LINK  EP=UT501,PARAM=(SYSPJCL,AFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)
         CLI   RETCODE,0           WAS ALLOCATION SUCCESSFUL ?
         BNE   BADALLOC            NO. ALLOCATION FAILED
         EJECT
***********************************************************************
*        TRY TO ALLOCATE 'OSJOB' AND 'LIST' IN CASE THEY ARE NEEDED
***********************************************************************
         SPACE 2
         LINK  EP=UT501,PARAM=(OSJBJCL,DFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)     FREE FIRST JUST IN CASE
         LINK  EP=UT501,PARAM=(LISTJCL,DFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)     FREE FIRST JUST IN CASE
         LINK  EP=UT501,PARAM=(OSJBJCL,AFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)     AND ALLOCATE TO SYSOUT
         LINK  EP=UT501,PARAM=(LISTJCL,AFUNCT,RETCODE,X1,X2),VL=1,     X
               MF=(E,CALLLIST)     AND ALLOCATE TO SYSOUT
         EJECT
***********************************************************************
*        RETURN TO THE CALLER OF THIS ROUTINE
***********************************************************************
         SPACE 2
PGMEXIT  DS    0H
         LH    REG15,ALLCCODE      LOAD THE RETURN CODE
         ZRETURN RC=(15)           RETURN TO CALLER
         EJECT
***********************************************************************
*        ERROR ROUTINES
***********************************************************************
         SPACE 2
BADALLOC DS    0H
         LA    REG15,4             DATA SET IS UNAVAILABLE
         STH   REG15,ALLCCODE      SAVE IN RETURN CODE AREA
         B     PGMEXIT             RETURN TO CALLER
         EJECT
***********************************************************************
*        CONSTANTS AND WORK AREAS
***********************************************************************
         SPACE 2
*
*        THESE STATEMENTS ARE HERE AND NOT IN THE COMMON AREA BECAUSE
*        THEY ARE FIXED CONSTANTS, NOT CHANGED BY THIS ROUTINE AND NOT
*        NEEDED BY ANY OTHER ROUTINE
*
OSJBJCL  DC    AL2(OSJBEND-*)      MAX LENGTH OF THE DATA
         DC    C'//OSJOB DD SYSOUT=A  '
OSJBEND  EQU   *
LISTJCL  DC    AL2(LISTEND-*)      MAX LENGTH OF THE DATA
         DC    C'//LIST DD SYSOUT=A   '
LISTEND  EQU   *
         LTORG
         DATAAREA
         END
-EMOD
-END
//SPFLIB08 EXEC ASMFCL,L=H,LOPT=RENT
//SYSLIB DD DSN=JEA.MACLIB,DISP=SHR
// DD DSN=SYS1.MACLIB,DISP=SHR
// DD DSN=SYS2.MACLIB,DISP=SHR
// DD DSN=SYS1.AMODGEN,DISP=SHR
//SYSIN DD DSN=&&OSJOB,DISP=(OLD,DELETE)
//LKED.SYSLMOD DD DSN=JEA.LOADLIB,DISP=SHR
 INCLUDE SYSLMOD(SPFLIB01)
 ENTRY SPFLIB01
 NAME SPFLIB01(R)
/*
