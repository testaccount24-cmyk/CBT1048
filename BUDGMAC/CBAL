         MACRO                                                          PM006550
&NAME    CBAL  &R,&RTN,&LOOP=YES,&NESTED=NO                             PM006560
         GBLC  &CBAL                                                    PM006570
         LCLC  &T                                                       PM006580
.*                                                                      PM006590
.*       ESTABLISH LINKAGE TO SUB-ROUTINE                               PM006600
.*                                                                      PM006610
.*       &R    - RETURN REGISTER ( MAY BE OMITTED )                     PM006620
.*       &RTN  - ROUTINE NAME                                           PM006630
.*       &LOOP=YES - SET GLOBAL SYMOLIC '&CBAL' TO 'Y' TO TRIGGER       PM006640
.*                   DETECTION OF SUBROUTINES WITH POTENTIAL LOOPS      PM006650
.*       &LOOP=NO  - DO NOT SET GLOBAL SYMBOL '&CBAL'                   PM006660
.*       &NESTED=NO - USE NORMAL CBAL REGISTER PRESERVATION TECHNIQUES  PM006670
.*       &NESTED=YES - PRESERVE BASE REGISTER WITHIN CODE               PM006680
.*                                                                      PM006690
.*       CBAL TECHNOLOGY PRECLUDES ISSUING A CBAL TO A ROUTINE THAT IN  PM006700
.*        TURN CBALS TO THE ORIGINAL ROUTINE, IF THE ROUTINES ARE IN    PM006710
.*        DIFFERENT CSECTS, BECAUSE WHEN THE ORIGINAL ROUTINE RETURNS   PM006720
.*        CONTROL TO THE ROUTINE THAT INVOKED IT, THE WROND BASE        PM006730
.*        REGISTER WILL BE RESTORED.  SUCH A SEQUENCE IS UNUSUAL - IT   PM006740
.*        HAS TAKEN ME THREE YEARS TO ENCOUNTER IT - BUT IT CAN HAPPEN. PM006750
.*        (VIZ 'WRITEP', A WRITE ROUTINE THAT CAN INVOKE A HEADING RTN  PM006760
.*        WHICH CAN INVOKE A PROCESSING ROUTINE WHICH CAN IN TUNR       PM006770
.*        INVOKE WRITEP).  TO CATER FOR THIS EVENTUALITY, 'NESTED=YES'  PM006780
.*        CAUSES THE BASE REGISTER TO BE PRESERVED WITHIN THE MACRO     PM006790
.*        EXPANSION, AND THUS ENSURING THE CORRECT VALUE IS RESTORED.   PM006800
.*                                                                      PM006810
         AIF   ('&R' EQ '').X00                                         PM006820
         AIF   ('&LOOP' EQ 'NO').X00                                    PM006830
&CBAL    SETC  'Y'                                                      PM006840
.X00     ANOP                                                           PM006850
&T       SETC  T'&RTN                                                   PM006860
         AIF   (T'&RTN EQ 'U').A01 BR IF ROUTINE UNDEFINED              PM006870
         AIF   (T'&RTN EQ 'T').A01 BRANCH IF ROUTINE EXTRN              PM006880
         AIF   (T'&RTN EQ '$').A01 BRANCH IF ROUTINE WXTRN              PM006890
.*                                                                      PM006900
.*       ASSUME THAT THE ROUTINE EXISTS IN THIS CSECT                   PM006910
.*                                                                      PM006920
         AIF   ('&R' NE '').A00                                         PM006930
&NAME    B     &RTN                GO TO REQUESTED ROUTINE              PM006940
         MEXIT                                                          PM006950
.A00     ANOP                                                           PM006960
&NAME    BAL   &R,&RTN             PERFORM REQUESTED ROUTINE            PM006970
         MEXIT                                                          PM006980
.A01     ANOP                                                           PM006990
.*                                                                      PM007000
.*       ROUTINE IS IN ANOTHER CSECT SO ESTABLISH LINKAGE VIA CCB       PM007010
.*                                                                      PM007020
&NAME    LA    R9,$&RTN            POINT AT RTN DEFINITION IN CCB       PM007030
         AIF   ('&R' NE '').A03                                         PM007040
         B     $CBALINK            BR TO LINKAGE ROUTINE                PM007050
         AGO   .A04                                                     PM007060
.A03     ANOP                                                           PM007070
         AIF   ('&NESTED' NE 'YES').N01                                 PM007080
         CNOP  0,4                 ENSURE ALIGNMENT                     PM007090
         ST    R12,*+16            PRESERVE BASE REGISTER IN S/A        PM007100
.N01     ANOP                                                           PM007110
         BAL   R4,$CBALINK         BR TO LINKAGE ROUTINE                PM007120
.A04     ANOP                                                           PM007130
         AIF   ('&SYSECT' EQ 'PARROT').A02                              PM007140
         AIF   ('&SYSECT' EQ 'BUDGIE3').A02                             PM007150
         AIF   ('&NESTED' NE 'YES').N02                                 PM007160
         L     R12,8(R4)           RESTORE BASE REGISTER FROM S/A       PM007170
         B     *+8                 BRANCH OVER REGISTER SAVE AREA       PM007180
         DS    F                   SAVE AREA FOR BASE REGISTER          PM007190
         AGO   .A02                                                     PM007200
.N02     ANOP                                                           PM007210
         L     R12,$&RTN.+4        RESTORE SUB-CSECT BASE REGISTER      PM007220
.A02     ANOP                                                           PM007230
         MEND                                                           PM007240
