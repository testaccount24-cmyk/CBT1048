         MACRO                                                          PD014250
&NAME    PARROTT &DSECT=YES,&CSECT=NO                                   PD014260
         AIF   ('&DSECT' EQ 'YES' AND '&CSECT' EQ 'YES').ERR01          PD014270
         AIF   ('&DSECT' EQ 'NO' AND '&CSECT' EQ 'NO').ERR01            PD014280
         AIF   ('&CSECT' NE 'YES').A01                                  PD014290
         SCSECT T                  GENERATE CSECT IDENTIFIER            PD014300
         DROP  R12                 DROP STANDARD ADDRESSABILITY         PD014310
         AGO   .A02                                                     PD014320
.A01     ANOP                                                           PD014330
PARROTT  DSECT                                                          PD014340
         VERSID T                  GENERATE CSECT IDENTIFIER            PD014350
.A02     ANOP                                                           PD014360
         USING PARROTT,R9          ESTABLISH ADDRESSABILITY             PD014370
         USING CCBNTRY,R13         ESTABLISH CCB ADDRESSABILITY         PD014380
         TITLE 'PARROTT - CONVERSION TABLE FOR TEST TYPES'              PD014390
*        THIS TABLE IS USED TO CONVERT RELATIONSHIP TESTS SPECIFIED     PD014400
*         IN SELECTION CRITERIA BUCKETS ( IN GR02 AND GR04 CARDS )      PD014410
*         INTO THE EQUIVALENT INTERNAL NOTATION ( WHICH IS A VALUE      PD014420
*         USED FOR TRIGGERING INDEXED BRANCHES ).                       PD014430
         SPACE 5                                                        PD014440
TESTTAB  EQU   *                   TABLE OF EBCDIC AND FLAG TEST TYPES  PD014450
         DC    CL2'EQ',AL1(EQ)     EQ - EQUAL TO                        PD014460
         DC    CL2'NE',AL1(NE)     NE - NOT EQUAL TO                    PD014470
         DC    CL2'LT',AL1(LT)     LT - LESS THAN                       PD014480
         DC    CL2'LE',AL1(LE)     LE - LESS THAN OR EQUAL TO           PD014490
         DC    CL2'GT',AL1(GT)     GT - GREATER THAN                    PD014500
         DC    CL2'GE',AL1(GE)     GE - GREATER THAN OR EQUAL TO        PD014510
         DC    CL2'ON',AL1(ON)     ON - SPECIFIED INDICATORS ON         PD014520
         DC    CL2'OF',AL1(OFF)    OF - SPECIFIED INDICATORS OFF        PD014530
         DC    X'FFFFFF'           END OF TABLE                         PD014540
TESTTABL EQU   3                   LENGTH OF EACH TABLE ELEMENT         PD014550
TESTTABN EQU   (*-TESTTAB)/TESTTABL NO OF ELEMENTS                      PD014560
         TITLE 'PARROTT - CONVERSION TABLE OF FIELD TYPES'              PD014570
*        THIS TABLE IS USED TO CONVERT THE FIELD TYPE SPECIFICATION     PD014580
*         ( IN GR02, GR03, GR04 CONTROL CARDS ) INTO THE EQUIVALENT     PD014590
*         INTERNAL VALUE ( WHICH IS A VALUE USED TO TRIGGER INDEXED     PD014600
*         BRANCHES ).                                                   PD014610
         SPACE 5                                                        PD014620
TYPETAB  EQU   *                   CONVERSION TABLE OF FIELD TYPES      PD014630
         DC    CL1'CHAR',AL1(CHAR) CHAR - CHARACTER                     PD014640
         DC    CL1'   ',AL1(CHAR)  BL   - DEFAULT TO CHARACTER          PD014650
         DC    CL1'NUM',AL1(NUM)   NUM  - DISPLAY NUMERIC               PD014660
         DC    CL1'PD',AL1(PD)     PD   - PACKED DECIMAL                PD014670
         DC    CL1'XHEX',AL1(HEX)  HEX  - HEXADECIMAL STRING            PD014680
         DC    CL1'BIT',AL1(BIT)   BIT  - BIT STRING                    PD014690
         DC    CL1'MASK',AL1(MASK) MASK - PARTIAL MASK                  PD014700
         DC    CL1'1-C',AL1(BIN)   BIN  - BINARY VALUE ( COMP-1 )       PD014710
         DC    CL1'FLOAT',AL1(FLOAT) FLOAT - FLOATING POINT VALUE       PD014720
         DC    CL1'STR',AL1(STRING) STR - CHAR STRING TO OCCUR IN FLD   PD014730
         DC    CL1'IND',AL1(IND)        - INDICATOR                     PD014740
         DC    CL1'GUESS',AL1(FREEFORM) - FREE FORMAT DISPLAY NUMERIC   PD014750
         DC    CL1'UNUSED',AL1(12)      - RESERVED FOR FUTURE USE       PD014760
         DC    X'FFFF'             END OF TABLE                         PD014770
TYPETABL EQU   2                   LENGTH OF TABLE ELEMENTS             PD014780
TYPETABN EQU   (*-TYPETAB)/TYPETABL NO OF ELEMENTS IN TABLE             PD014790
         TITLE 'PARROTT - TRANSLATE TABLES FOR FIELD VALIDATION'        PD014800
*        THESE TABLES ARE USED BY VARIOUS ROUTINES TO DETERMINE THE     PD014810
*         VALIDITY OF, OR FORMAT OF, FIELDS.  THE MAIN TABLE MERELY     PD014820
*         TRANSLATES THE VARIOUS TYPES OF CHARACTERS INTO SPECIFIC      PD014830
*         CODES, WHILE THE SMALL TABLES ARE USED TO TRANSLATE AND TEST  PD014840
*         THE RESULT OF THE FIRST TRANSLATION TO DETERMINE THE TYPES    PD014850
*         OF CHARACTERS THAT THE SOURCE FIELD CONTAINED.  THUS, MANY    PD014860
*         TESTS CAN BE MADE WITH ONE 'TR' AND ONE 'TRT', OBVIATING      PD014870
*         THE NEED FOR ITERATIVE MULTIPLE TESTS.                        PD014880
         SPACE 1                                                        PD014890
*        NOTE THAT THIS CSECT IS UNIQUE, IN THAT ITS ADDRESSABILITY IS  PD014900
*         ESTABLISHED VIA R9 AND NOT R12.  THIS IS SO THAT OTHER        PD014910
*         CSECTS, MAY EXECUTE THE INSTRUCTIONS HEREIN WHILE             PD014920
*         MAINTAINING THEIR OWN ADDRESSABILTIY.                         PD014930
         EJECT                                                          PD014940
*        THE SPECIFIC TRANLATION CODES ASSIGNED ARE:-                   PD014950
*              X'01' : 0 - 9                                            PD014960
*              X'02' : A - R                                            PD014970
*              X'03' : S - Z                                            PD014980
*              X'04' : SPECIAL CHARACTERS                               PD014990
*              X'05' : INVALID MULTI-PUNCHES                            PD015000
*              X'06' : 12-0 & 11-0 MULTI-PUNCHES                        PD015010
*              X'07' : BLANK                                            PD015020
         SPACE 1                                                        PD015030
TTABLE   DS    0CL256              TRANSLATION TABLE FOR TESTS          PD015040
         DC    64X'05'                                                  PD015050
         DC    X'07'               BLANK                                PD015060
         DC    9X'05'                                                   PD015070
         DC    7X'04'              SPECIAL:- H.<(+|&                    PD015080
         DC    9X'05'                                                   PD015090
         DC    8X'04'              SPECIAL:- !$*);^-/                   PD015100
         DC    9X'05'                                                   PD015110
         DC    5X'04'              SPECIAL:- ,%_>?                      PD015120
         DC    10X'05'                                                  PD015130
         DC    6X'04'              SPECIAL:- :#@.="                     PD015140
         DC    64X'05'                                                  PD015150
         DC    X'06'               12-0 PUNCH                           PD015160
         DC    9X'02'              ALPHA:-   ABCDEFGHI                  PD015170
         DC    6X'05'                                                   PD015180
         DC    X'06'               11-0 PUNCH                           PD015190
         DC    9X'02'              ALPHA:-   JKLMNOPQR                  PD015200
         DC    8X'05'                                                   PD015210
         DC    8X'03'              ALPHA:-   STUVWXYZ                   PD015220
         DC    6X'05'                                                   PD015230
         DC    10X'01'             NUMERIC:- 0123456789                 PD015240
         DC    6X'05'                                                   PD015250
         SPACE 1                                                        PD015260
TTABLEL1 DC    X'0101000001010100' ALLOW A-Z, BLANK                     PD015270
TTABLEL2 DC    X'0100000001010100' ALLOW A-Z, 0-9, BLANK                PD015280
TTABLEL3 DC    X'0100000000010100' ALLOW A-Z, 0-9, BLANK, SPECIAL       PD015290
TTABLENN DC    X'0100010101010101' ALLOW 0-9                            PD015300
TTABLEN  DC    X'0100010101010100' ALLOW 0-9, BLANK                     PD015310
TTABLES  DC    X'0100000101010000' ALLOW 0-9, A-R, BLANK, 12-0, 11-0    PD015320
TTABLEBL DC    X'0000000000000001' LOCATE FIRST BLANK                   PD015330
TTABLENO DC    X'0001000000000000' LOCATE FIRST 0-9                     PD015340
TTABLENS DC    X'0001010000000100' LOCATE FIRST 0-9, A-R, 11-0, 12-0    PD015350
         EJECT                                                          PD015360
*********************************************************************** PD015370
*        EXECUTABLE INSTRUCTIONS                                      * PD015380
*********************************************************************** PD015390
         SPACE 1                                                        PD015400
TESTTR   TR    $TSTAREA(0),TTABLE  TRANSLATE AGAINST MAIN TABLE         PD015410
TESTTRL1 TRT   $TSTAREA(0),TTABLEL1 ACCEPT A-Z AND BLANK                PD015420
TESTTRL2 TRT   $TSTAREA(0),TTABLEL2 ACCEPT A-Z, BLANK, 0-9              PD015430
TESTTRL3 TRT   $TSTAREA(0),TTABLEL3 ACCEPT A-Z,0-9, BLANK, SPECIAL CHA  PD015440
TESTTRNN TRT   $TSTAREA(0),TTABLENN ACCEPT 0-9                          PD015450
TESTTRN  TRT   $TSTAREA(0),TTABLEN  ACCEPT 0-9, BLANK                   PD015460
TESTTRS  TRT   $TSTAREA(0),TTABLES  ACCEPT 0-9, BLANK, A-R, 11-0, 12-0  PD015470
TESTTRNS TRT   $TSTAREA(0),TTABLENS LOCATE 1ST 0-9, A-R, 11-0, 12-0     PD015480
TESTTRNO TRT   $TSTAREA(0),TTABLENO LOCATE 1ST 0-9                      PD015490
TESTTRBL TRT   0(0,R14),TTABLEBL    LOCATE 1ST BLANK                    PD015500
         SPACE 5                                                        PD015510
*********************************************************************** PD015520
*        EXECUTABLE INSTRUCTIONS FOR PARTIAL KEY MATCHING ROUTINE     * PD015530
*********************************************************************** PD015540
         SPACE 1                                                        PD015550
E1TRTEST TR    $TSTAREA+50(0),E1TABLE2 TRANSLATE PARTIAL KEY BL TO '00' PD015560
E1NCMAST NC    $TSTAREA(0),$TSTAREA+50 TURN OFF UNTESTED BYTES          PD015570
E1NCTEST NC    $TSTAREA+50(0),0(R14) RESTORE PARTIAL KEY WITH BL='00'   PD015580
E1TRTAB  TR    $TSTAREA+50(0),E1TABLE1 TRANSLATE 'MBB' CHAR TO BLANK    PD015590
E1COMPAR CLC   $TSTAREA(0),$TSTAREA+50 COMPARE NON-BLANK PARTS OF KEYS  PD015600
E1MVCKEY MVC   $TSTAREA+50(0),0(R14) MOVE PARTIAL KEY TO WORK AREA      PD015610
E1MVCFLD MVC   $TSTAREA(0),0(R6)   MOVE FIELD UNDER TEST TO TEST AREA   PD015620
         SPACE 5                                                        PD015630
*********************************************************************** PD015640
*        EXECUTABLE INSTRUCTIONS FOR HEX STRING VALIDATION/CONVERSION * PD015650
*********************************************************************** PD015660
         SPACE 1                                                        PD015670
HEXVALID TRT   0(0,R14),HEXTAB1    ENSURE ONLY VALID HEX CHARACTERS     PD015680
HEXCONV  TR    0(0,R14),HEXTAB2    CONVERT 'A' TO X'FA' ETC             PD015690
         TITLE 'PARROTT - TABLES FOR PARTIAL KEY MATCHING'              PD015700
*        THESE TABLES ARE USED BR THE PARTIAL KEY MATCHING ROUTINE.     PD015710
         SPACE 5                                                        PD015720
*********************************************************************** PD015730
*        ALTER 'MUST BE BLANK' CHARACTER TO BLANK                     * PD015740
*********************************************************************** PD015750
         SPACE 1                                                        PD015760
E1TABLE1 DS    0CL256                                                   PD015770
         DC    X'000102030405060708090A0B0C0D0E0F'                      PD015780
         DC    X'101112131415161718191A1B1C1D1E1F'                      PD015790
         DC    X'202122232425262728292A2B2C2D2E2F'                      PD015800
         DC    X'303132333435363738393A3B3C3D3E3F'                      PD015810
         DC    X'404142434445464748494A4B4C4D4E4F'                      PD015820
         DC    X'505152535455565758595A5B5C5D5E5F'                      PD015830
         DC    X'606162636465666768696A6B6C6D6E6F'                      PD015840
         DC    X'707172737475767778797A7B7C7D7E7F'                      PD015850
         DC    X'808182838485868788898A8B8C8D8E8F'                      PD015860
         DC    X'909192939495969798999A9B9C9D9E9F'                      PD015870
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                      PD015880
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                      PD015890
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                      PD015900
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                      PD015910
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                      PD015920
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                      PD015930
         SPACE 5                                                        PD015940
*********************************************************************** PD015950
*        NULLIFY BLANKS                                               * PD015960
*********************************************************************** PD015970
         SPACE 1                                                        PD015980
E1TABLE2 DS    0CL256                                                   PD015990
         DC    64X'FF'             EVERYTHING EXCEPT ...                PD016000
         DC    X'00'                ... BLANK ...                       PD016010
         DC    191X'FF'             ... IS HIGH VALUES                  PD016020
         TITLE 'PARROTT - TRANSLATE TABLE FOR SCANNING FOR CHARACTERS'  PD016030
*********************************************************************** PD016040
*        TABLE FOR SCANNING A FIELD TO LOCATE A STRING                * PD016050
*********************************************************************** PD016060
         SPACE 1                                                        PD016070
*        THIS TABLE IS USED BY A NUMBER OF ROUTINES TO SCAN FOR A       PD016080
*         FIELD FOR A SPECIFIC CHARACTER.  THE TABLE IS THUS SUBJECT TO PD016090
*         MODIICATION, IN THAT EACH TIME IT IS USED, THE USER WILL :-   PD016100
*              . SET TABLE TO ALL LOW VALUES                            PD016110
*              . SET THE POSITION CORRESPONDING TO THE SEARCH CHARACTER PD016120
*                TO A VALUE OTHER THAN X'00'                            PD016130
*        THE USER CAN THEN USE 'TRT' TO LOCATE THE DESIRED CHARACTER.   PD016140
         SPACE 5                                                        PD016150
STRTAB   DC    256X'00'            TABLE FOR 'TRT'                      PD016160
         TITLE 'PARROTT - TABLES FOR HEX STRING VALIDATION/CONVERSION'  PD016170
*********************************************************************** PD016180
*        TABLES FOR HEX STRING VALIDATION/CONVERSION                  * PD016190
*********************************************************************** PD016200
         SPACE 1                                                        PD016210
*        THESE TABLES ARE USED FOR VALIDATING AND CONVERTING HEX        PD016220
*         STRING LITERALS.  THE FIRST TABLE IS USED TO ENSURE THAT THE  PD016230
*         LITERAL CONTAINS ONLY VALID ( CHARACTER REPRESENTATION OF )   PD016240
*         HEXADECIMAL CHARACTERS, WHILE THE SECOND TABLE CONVERTS       PD016250
*         'A'-'F' INTO X'FA'-X'FF' SO THAT A PACK OPERATION WILL ALTER  PD016260
*         THE STRING INTO A TRUE HEX LITERAL.                           PD016270
         SPACE 5                                                        PD016280
HEXTAB1  DC    256X'01'            VALIDATE HEX CHARACTERS              PD016290
         ORG   HEXTAB1+C'A'                                             PD016300
         DC    6X'00'              A - F                                PD016310
         ORG   HEXTAB1+C'0'                                             PD016320
         DC    10X'00'             0 - 9                                PD016330
         ORG                                                            PD016340
         SPACE 5                                                        PD016350
HEXTAB2  DC    256AL1(*-HEXTAB2)   CONVERT 'A' TO X'FA' ETC             PD016360
         ORG   HEXTAB2+C' '        BLANK                                PD016370
         DC    X'00'               CONVERT PADDING BLANKS TO PADDING 00 PD016380
         ORG   HEXTAB2+C'A'                                             PD016390
         DC    X'FAFBFCFDFEFF'     A - F INTO X'FA' - X'FF'             PD016400
         ORG                                                            PD016410
         TITLE 'PARROTT - TABLE FOR LOCATING 1ST NON-BLANK CHARACTER'   PD016420
*        THIS TABLE CAN BE USED BY 'TRT' TO LOCATE THE 1ST NON-BLANK    PD016430
*         CHARACTER IN A FIELD.                                         PD016440
         SPACE 5                                                        PD016450
NONBLTAB DC    256AL1(*-NONBLTAB)  BASIC TABLE                          PD016460
         ORG   NONBLTAB+X'00'      POINT AT LOW-VALUES POSITION         PD016470
         DC    X'01'               ALTER SO AS TO FAIL 'TRT'            PD016480
         ORG   NONBLTAB+C' '       POINT AT BLANK                       PD016490
         DC    X'00'               ALTER TO PASS 'TRT'                  PD016500
         ORG                                                            PD016510
         TITLE 'PARROTT - TABLE TO TRANSLATE BLANKS TO ZEROES'          PD016520
         SPACE 5                                                        PD016530
BLTOZERO DC    256AL1(*-BLTOZERO)  BASIC TRANSLATE TABLE                PD016540
         ORG   BLTOZERO+C' '       ORG TO BLANK                         PD016550
         DC    C'0'                CONVERT BLANK TO ZERO                PD016560
         ORG                                                            PD016570
         TITLE 'PARROTT - TRANSLATE TABLE FOR MATHEMATICAL OPERATIONS'  PD016580
*        THIS TABLE IS USED BY THE MATHEMATICAL ROUTINES TO LOCATE      PD016590
*         THE 1ST NON-ZERO BYTE IN A PACKED DECIMAL FIELD.  IT IS ALSO  PD016600
*         USED TO VALIDATE PACKED DECIMAL FIELDS.                       PD016610
         SPACE 5                                                        PD016620
PDTABLE  EQU   *                                                        PD016630
         DC    X'00'               '00'                                 PD016640
         DC    9X'02',6X'03'       '01' - '09', '0A' - '0F'             PD016650
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016660
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016670
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016680
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016690
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016700
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016710
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016720
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016730
         DC    10X'01',6X'03'      'N0' - 'N9', 'NA' -'NF'              PD016740
         DC    96X'04'             'A0' - 'FF'                          PD016750
         SPACE 5                                                        PD016760
*        THE SIGNIFICANCE OF THE FUNCTION BYTES IS:-                    PD016770
         SPACE 1                                                        PD016780
*        '00' - SOURCE BYTE CONTAINS LOW VALUES                         PD016790
*        '01' - 1ST HALF OF SOURCE BYTE CONTAINS DIGIT (NN)             PD016800
*        '02' - 2ND HALF OFSOURCE BYTE CONTAINS DIGIT (0N)              PD016810
*        '03' - SOURCE BYTE CONTAINS DIGIT AND SIGN                     PD016820
*        '04' - 1ST HALF OF SOURCE BYTE CONTAINS A - F (INVALID)        PD016830
         DROP  R9                  DROP ADDRESSABILITY                  PD016840
         MEXIT                                                          PD016850
.ERR01   MNOTE 16,'MUTUALLY EXCLUSIVE KEYWORD SPECIFICATION'            PD016860
         MEXIT                                                          PD016870
         MEND                                                           PD016880
