         MACRO                                                          PM013430
&NAME    DEFOPCD &OPCD=,&MOD=N,&IND=N,&LIT=N,                          *PM013440
               &DSECT=NO,&CARD=NONE,&DTYP=D,&ICODE=,&DFLDT=N,&DFLDL=8, *PM013450
               &CBID=                                                   PM013460
.*                                                                      PM013470
.*       OPCD  - OPERATION CODE ( E.G. I, DW, C3 )                      PM013480
.*       ICODE - INTERNAL CODE EQUIVALENT OF OPERATION CODE             PM013490
.*       CBID  - ID OF ASSOCIATED CBXX CONTROL BLOCK                    PM013500
.*       MOD   - MODIFIER REQUIRED FLAG ( Y / N )                       PM013510
.*       IND   - INDICATOR SPECIFICATION REQUIRED FLAG ( Y / N )        PM013520
.*       LIT   - LITERAL REQUIRED ( Y / N )                             PM013530
.*                                                                      PM013540
.*       DSECT - REQUEST FOR DSECT GENERATION ( YES / NO )              PM013550
.*       CARD  - LABEL OF ASSOCIATED GRXX CONTROL CARD                  PM013560
.*       DTYP  - DEFAULT PROCESSING TYPE ( E.G. N, A, T )               PM013570
.*       DFLDT - DEFAULT DESTINATION FIELD TYPE                         PM013580
.*       DFLDL - DEFAULT DESTINATION FIELD LENGTH                       PM013590
.*                                                                      PM013600
         LCLC  &FLAG1,&FLAG2,&ID                                        PM013610
         LCLC  &CURFLD                                                  PM013620
         LCLA  &NFLDS                                                   PM013630
         AIF   ('&DSECT' EQ 'YES').DSECT                                PM013640
         AIF   ('&OPCD' EQ '').ERR01                                    PM013650
         AIF   ('&ICODE' EQ '').ERR04                                   PM013660
&ID      SETC  '0'                                                      PM013670
         AIF   ('&CBID' EQ '').B00                                      PM013680
&ID      SETC  'FLAG&CBID'                                              PM013690
.B00     ANOP                                                           PM013700
&FLAG1   SETC  '0'                                                      PM013710
&FLAG2   SETC  '0'                                                      PM013720
         AIF   ('&MOD' EQ 'N').A03                                      PM013730
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPMOD'                                   PM013740
.A03     AIF   ('&IND' EQ 'N').A04                                      PM013750
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPIND'                                   PM013760
.A04     AIF   ('&LIT' EQ 'N').A05                                      PM013770
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPLIT'                                   PM013780
.A05     AIF   ('&CARD' EQ 'NONE').A06                                  PM013790
&FLAG1   SETC  '&FLAG1'.'+'.'F1OPCARD'                                  PM013800
.A06     ANOP                                                           PM013810
.*       INSERT TEST FOR BIT 6 WHEN IT IS ASSIGNED                      PM013820
.A07     ANOP                                                           PM013830
&NAME    DC    CL2'&OPCD',AL1(&ID),CL4'&CARD',AL1(&ICODE),CL1'&DTYP'    PM013840
         DC    CL1'&DFLDT',AL1(&DFLDL),AL1(&FLAG1),AL1(&FLAG2)          PM013850
         MEXIT                                                          PM013860
.DSECT   ANOP                                                           PM013870
*********************************************************************** PM013880
*        OPERATION/FUNCTION CODE DESCRIPTOR                           * PM013890
*********************************************************************** PM013900
         SPACE 1                                                        PM013910
OPCDNTRY DSECT                                                          PM013920
OPCDOPCD DS    CL2                 OPERATION/FUNCTION CODE              PM013930
OPCDCBID DS    CL1                 ID OF ASSOCIATED CONTROL BLOCK       PM013940
OPCDCARD DS    CL4                 ID OF ASSOCIATED CONTROL CARD        PM013950
OPCDICDE DS    CL1                 INTERNAL REPRESENTATION OF FUNCTION  PM013960
OPCDDTYP DS    CL1                 DEFAULT PROCESSING TYPE ( $PROCTYP ) PM013970
OPCDFLDT DS    CL1                 DEFAULT DESTINATION FIELD TYPE       PM013980
OPCDFLDL DS    CL1                 DEFAULT FIELD LENGTH                 PM013990
OPCDFLG1 DS    CL1                 FLAG BYTE 1                          PM014000
OPCDFLG2 DS    CL1                 FLAG BYTE 2                          PM014010
OPCDLGTH EQU   *-OPCDNTRY          LENGTH OF DSECT                      PM014020
         SPACE 5                                                        PM014030
*********************************************************************** PM014040
*        EQUATES FOR FLAG BYTE 1                                      * PM014050
*********************************************************************** PM014060
         SPACE 1                                                        PM014070
F1OPMOD  EQU   1                   OP-CODE MODIFIER REQUIRED            PM014080
F1OPIND  EQU   2                   INDICATOR SPECIFICATION REQUIRED     PM014090
F1OPLIT  EQU   4                   LITERAL VALUE REQUIRED               PM014100
F1OPCARD EQU   8                   LABEL OF ASSOCIATED CARD REQUIRED    PM014110
         SPACE 5                                                        PM014120
*********************************************************************** PM014130
*        EQUATES FOR FLAG BYTE 2                                      * PM014140
*********************************************************************** PM014150
         SPACE 1                                                        PM014160
         MEXIT                                                          PM014170
.ERRO1   MNOTE 16,'''OPCD='' KEYWORD IS MANDATORY'                      PM014180
         MEXIT                                                          PM014190
.ERR04   MNOTE 16,'''ICODE='' KEYWORD IS MANDATORY'                     PM014200
         MEXIT                                                          PM014210
         MEND                                                           PM014220
